&НаСервере
Функция ЗагрузитьКлассификатор()
		
	ПараметрыОбработки = Новый Структура("Классификаторы,НомерСтроки", Объект.Классификаторы.Выгрузить(), 1);
	
	АдресХранилища = ПоместитьВоВременноеХранилище(Неопределено, УникальныйИдентификатор);
	ЗаданиеВыполнено = Ложь;	
	
	ОбъектОбр = РеквизитФормыВЗначение("Объект");
	
	ОбъектОбр.ЗаписатьКлассификаторы(ПараметрыОбработки.НомерСтроки);
	ПроставитьАмортизационныеГруппыПодчиненным();
	
	Возврат Истина;

КонецФункции

&НаСервере
Функция ПолучитьРодителяСНепустойГруппой(ОКОФ)
	Элемент = ОКОФ;
	
	Пока Истина Цикл
		Элемент = Элемент.Родитель;
		Если Не ЗначениеЗаполнено(Элемент) Тогда 
			Прервать;
		КонецЕсли;
		
		Записи = РегистрыСведений.АмортизационныеГруппыОКОФ.СоздатьНаборЗаписей();
		Записи.Отбор.ОКОФ.Установить(Элемент);
		Записи.Прочитать();
		Если Записи.Количество() = 0 Тогда 
			Продолжить;
		КонецЕсли;
		
		Возврат Записи;
	КонецЦикла;
	
	Возврат Неопределено;
КонецФункции

&НаСервере
Процедура ПроставитьАмортизационныеГруппыПодчиненным()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ОбщероссийскийКлассификаторОсновныхФондов.Ссылка КАК ОКОФ
	|ИЗ
	|	Справочник.ОбщероссийскийКлассификаторОсновныхФондов КАК ОбщероссийскийКлассификаторОсновныхФондов
	|ГДЕ
	|	НЕ ОбщероссийскийКлассификаторОсновныхФондов.ЭтоГруппа
	|	И НЕ ОбщероссийскийКлассификаторОсновныхФондов.Ссылка В
	|				(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|					АмортизационныеГруппыОКОФ.ОКОФ
	|				ИЗ
	|					РегистрСведений.АмортизационныеГруппыОКОФ КАК АмортизационныеГруппыОКОФ)";
	
	Результат = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ОКОФ");
	
	Для Каждого ОКОФ Из Результат Цикл
		Записи = ПолучитьРодителяСНепустойГруппой(ОКОФ);
		
		Если ЗначениеЗаполнено(Записи) Тогда
			НовыеЗаписи = РегистрыСведений.АмортизационныеГруппыОКОФ.СоздатьНаборЗаписей();
			НовыеЗаписи.Отбор.ОКОФ.Установить(ОКОФ);
			НовыеЗаписи.Прочитать();
			Для Каждого Запись Из Записи Цикл 
				Элемент = НовыеЗаписи.Добавить();
				ЗаполнитьЗначенияСвойств(Элемент, Запись);
				Элемент.ОКОФ = ОКОФ;
			КонецЦикла;
			НовыеЗаписи.Записать();
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура ПрочитатьПомещенныйФайлНаСервере(Знач ПомещенныйФайл)
	
	Перем ДиспетчерОбновлений;
	
	ДиспетчерОбновлений = РеквизитФормыВЗначение("Объект");
	ДиспетчерОбновлений.ДобавитьНовыйКлассификатор(ПомещенныйФайл.Имя, ПомещенныйФайл.Хранение,УникальныйИдентификатор);
		
	ЗначениеВРеквизитФормы(ДиспетчерОбновлений, "Объект");
	
КонецПроцедуры

&НаКлиенте
Процедура ПрочитатьВыбранныйФайл()
	
	ОписаниеПереданногоФайла = Новый Структура("Имя, Хранение");
	ОписаниеПереданногоФайла.Имя = ИмяФайла;
	ОписаниеПереданногоФайла.Хранение = АдресВременногоХранилища;
	
	ПрочитатьПомещенныйФайлНаСервере(ОписаниеПереданногоФайла);
	
	Если Объект.Загружать Тогда
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаКлассификатор;
	Иначе
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаПредупреждение;
	КонецЕсли;	
		
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьИПрочитатьФайлЗавершение(Результат, АдресФайла, ВыбранноеИмяФайла, ДополнительныеПараметры) Экспорт
	
	Если Результат Тогда
		ШаблонСообщения = НСтр("ru = 'Выполняется считывание данных классификатора Амортизационные группы ОКОФ из файла'");
		Состояние(ШаблонСообщения);
		АдресВременногоХранилища = АдресФайла;
		ИмяФайла = ВыбранноеИмяФайла;
		ПрочитатьВыбранныйФайл();
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ОткрытьИПрочитатьФайл(Команда)
	
	АдресВременногоХранилища = Неопределено;
	
	ОчиститьСообщения();
	Попытка
		Оповещение = Новый ОписаниеОповещения("ОткрытьИПрочитатьФайлЗавершение", ЭтотОбъект);
		НачатьПомещениеФайла(Оповещение, АдресВременногоХранилища, ИмяФайла, Истина, УникальныйИдентификатор);
	Исключение
		ШаблонСообщения = НСтр("ru = 'При чтении файла возникла ошибка
									 |%1'");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения, 
			КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		ПоказатьПредупреждение( , ТекстСообщения, , НСтр("ru = 'Ошибка'"));
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьДанные(Команда)
	
	Если НЕ Объект.Загружать Тогда
		Возврат;
	КонецЕсли;	
	
	ЗагрузитьКлассификатор();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	xmlnsXML                = "http://www.w3.org/XML/1998/namespace";
	xmlnsV8XDTO             = "http://v8.1c.ru/8.1/xdto";
	xmlnsОписанияТиповКлассификаторов = "urn:uuid:101fa9a0-ea86-11dc-afc4-0002a5d5c51b";
	xmlnsКлассификатор      = "urn:uuid:be515360-d4a7-11dc-8abf-0002a5d5c51b";

КонецПроцедуры



