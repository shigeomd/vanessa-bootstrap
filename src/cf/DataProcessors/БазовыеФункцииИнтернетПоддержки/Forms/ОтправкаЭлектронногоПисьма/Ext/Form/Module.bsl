
// Хранение контекста взаимодействия с сервисом
&НаКлиенте
Перем КонтекстВзаимодействия Экспорт;

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	// Сформировать шаблон письма в зависимости от переданных праметров
	СформироватьПисьмо(Параметры);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ИнтернетПоддержкаПользователейКлиент.ОбработатьОткрытиеФормы(КонтекстВзаимодействия, ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Отправить(Команда)
	
	// Проверка заполнения полей
	Если ПустаяСтрока(ЭлектроннаяПочта) Тогда
		СообщениеПользователю = Новый СообщениеПользователю;
		СообщениеПользователю.Текст = НСтр("ru = 'Поле ""E-mail для обратной связи"" не заполнено.'");
		СообщениеПользователю.Поле  = "ЭлектроннаяПочта";
		СообщениеПользователю.Сообщить();
		Возврат;
	КонецЕсли;
	
	Если ПустаяСтрока(Тема) Тогда
		СообщениеПользователю = Новый СообщениеПользователю;
		СообщениеПользователю.Текст = НСтр("ru = 'Поле ""Тема сообщения"" не заполнено.'");
		СообщениеПользователю.Поле  = "Тема";
		СообщениеПользователю.Сообщить();
		Возврат;
	КонецЕсли;
	
	Если ПустаяСтрока(Сообщение) Тогда
		СообщениеПользователю = Новый СообщениеПользователю;
		СообщениеПользователю.Текст = НСтр("ru = 'Не заполнен тест письма.'");
		СообщениеПользователю.Поле  = "Сообщение";
		СообщениеПользователю.Сообщить();
		Возврат;
	КонецЕсли;
	
	ПараметрыПисьма = Новый Структура("ОтКого, Тема, Сообщение", ЭлектроннаяПочта, Тема, Сообщение);
	Если НЕ ПустаяСтрока(УсловноеИмяПолучателя) Тогда
		ПараметрыПисьма.Вставить("УсловноеИмяПолучателя", УсловноеИмяПолучателя);
	КонецЕсли;
	
	Состояние(НСтр("ru = 'Отправка'")
		,
		,
		НСтр("ru = 'Выполняется отправка электронного письма в службу тех. поддержки.'"),
		БиблиотекаКартинок.ИнтернетПоддержкаПользователейОтправкаПисьма);
	
	РезультатОтправки = ИнтернетПоддержкаПользователейКлиент.ОтправитьЭлектронноеСообщениеСлужбеТехПоддержки(
		ПараметрыПисьма,
		КонтекстВзаимодействия);
	
	Состояние();
	
	Если НЕ РезультатОтправки Тогда
		ПоказатьПредупреждение(,
			НСтр("ru = 'При отправке письма произошла ошибка.
			|Подробнее см. в журнале регистрации.'"));
	Иначе
		Закрыть();
		ПоказатьПредупреждение(, НСтр("ru = 'Сообщение успешно отправлено.'"));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Выполняет заполнение шаблона письма в зависимости от значения параметров.
//
// Параметры:
// - Параметры (ДанныеФормыСтруктура, Структура) - параметры формирования шаблона.
//
&НаСервере
Процедура СформироватьПисьмо(Параметры)
	
	Тема = Параметры.Тема;
	
	Сообщение = СтрЗаменить(
		Параметры.ТекстСообщения,
		"%ТехническиеПараметры%",
		ТекстТехническихПараметров(Параметры.ПриНачалеРаботыСистемы));
	
	Если ПустаяСтрока(Параметры.Кому) Тогда
		EMailДляОтправки = "webits-info@1c.ru";
	Иначе
		EMailДляОтправки = Параметры.Кому;
	КонецЕсли;
	
	Элементы.ПояснениеКЗаголовку.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Письмо будет отправлено в техподдержку пользователей на адрес %1'"),
		EMailДляОтправки);
	
	Если НЕ ПустаяСтрока(Параметры.ОтКого) Тогда
		ЭлектроннаяПочта = Параметры.ОтКого;
	КонецЕсли;
	
	УсловноеИмяПолучателя = Параметры.УсловноеИмяПолучателя;
	
	Если ПустаяСтрока(Тема) Тогда
		Тема = НСтр("ru = '<Укажите тему сообщения>'");
	КонецЕсли;
	
	Если ПустаяСтрока(Сообщение) Тогда
		Сообщение = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = '<Заполните содержимое письма>,
				|
				|Логин: %1
				|С уважением, .'"),
			Параметры.Логин);
	КонецЕсли;
	
КонецПроцедуры

// Формирует шаблон текста технических параметров.
//
// Возвращаемое значение - Строка - шаблон технических параметров.
//
&НаСервере
Функция ТекстТехническихПараметров(ПриНачалеРаботыСистемы)
	
	СисИнфо = Новый СистемнаяИнформация;
	
	Если ПриНачалеРаботыСистемы Тогда
		МестоВызоваСервиса = НСтр("ru = 'автоматический'");
	Иначе
		МестоВызоваСервиса = НСтр("ru = 'ручной'");
	КонецЕсли;
	
	ТехническиеПараметры = НСтр("ru = 'Технические параметры подключения:
		|(нужны для воспроизведения описанной проблемы)
		|
		|- имя конфигурации: %1,
		|- номер версии конфигурации: %2,
		|- номер версии платформы: %3,
		|- версия библиотеки Интернет-поддержки: %4,
		|- язык пользователя: %5,
		|- вид приложения: управляемый,
		|- вызов сервиса: %6.'")
		+ Символы.ПС;
	
	ТехническиеПараметры = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		ТехническиеПараметры,
		Строка(ИнтернетПоддержкаПользователейКлиентСервер.ИмяКонфигурации()),
		Строка(ИнтернетПоддержкаПользователейКлиентСервер.ВерсияКонфигурации()),
		Строка(СисИнфо.ВерсияПриложения),
		ИнтернетПоддержкаПользователейКлиентСервер.ВерсияБиблиотеки(),
		ТекущийКодЛокализации(),
		МестоВызоваСервиса);
	
	Возврат ТехническиеПараметры;
	
КонецФункции

#КонецОбласти
