
// Хранение контекста взаимодействия с сервисом
&НаКлиенте
Перем КонтекстВзаимодействия Экспорт;

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	// Заполнение формы необходимыми параметрами.
	ЗаполнитьФорму();
	
	Если ТекущийВариантИнтерфейсаКлиентскогоПриложения() = ВариантИнтерфейсаКлиентскогоПриложения.Такси Тогда
		Элементы.ГруппаПоясненияЗаголовкаПинКод.Отображение = ОтображениеОбычнойГруппы.Нет;
		Элементы.ГруппаЗаполненияКонтентаПинКод.Отображение = ОтображениеОбычнойГруппы.Нет;
		Элементы.ГруппаКартинкиКонтентаПинКод.Отображение   = ОтображениеОбычнойГруппы.Нет;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ИнтернетПоддержкаПользователейКлиент.ОбработатьОткрытиеФормы(КонтекстВзаимодействия, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии()
	
	Если НЕ ПрограммноеЗакрытие Тогда
		ИнтернетПоддержкаПользователейКлиент.ЗавершитьБизнесПроцесс(КонтекстВзаимодействия);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НадписьВыходаПользователяПинКодНажатие(Элемент)
	
	ИнтернетПоддержкаПользователейКлиент.ОбработатьВыходПользователя(КонтекстВзаимодействия, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПояснениеЗаголовкаПинКодОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылка, СтандартнаяОбработка)
	
	Если НавигационнаяСсылка = "TechSupport" Тогда
		СтандартнаяОбработка = Ложь;
		ИнтернетПоддержкаПользователейКлиент.ОткрытьДиалогОтправкиЭлектронногоПисьма(
			КонтекстВзаимодействия,
			ПараметрыСообщенияВТехПоддержку());
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Назад(Команда)
	
	ПараметрыЗапроса = Новый Массив;
	ПараметрыЗапроса.Добавить(Новый Структура("Имя, Значение", "backRegistration", "true"));
	
	ИнтернетПоддержкаПользователейКлиент.ОбработкаКомандСервиса(КонтекстВзаимодействия, ЭтотОбъект, ПараметрыЗапроса);
	
КонецПроцедуры

&НаКлиенте
Процедура ОКПинКод(Команда)
	
	Если НЕ ЗаполнениеПолейКорректно() Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыЗапроса = Новый Массив;
	ПараметрыЗапроса.Добавить(Новый Структура("Имя, Значение", "regnumber", РегистрационныйНомерПинКод));
	ПараметрыЗапроса.Добавить(Новый Структура("Имя, Значение", "pincode", СтрЗаменить(ПинКод, "-", "")));
	
	ИнтернетПоддержкаПользователейКлиент.ОбработкаКомандСервиса(КонтекстВзаимодействия, ЭтотОбъект, ПараметрыЗапроса);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Выполняет начальное заполнение полей фомры
&НаСервере
Процедура ЗаполнитьФорму()
	
	ЗаголовокПользователя = НСтр("ru = 'Логин:'") + " " + Параметры.login;
	
	Элементы.НадписьЛогинаПользователяПинКод.Заголовок = ЗаголовокПользователя;
	РегистрационныйНомерПинКод = Параметры.regNumber;
	ПинКод                     = Параметры.pincode;
	
КонецПроцедуры

// Выполняет проверку заполненности полей РегНомер и ПинКод
//
// Возвращаемое значение: Булево. Истина - поля заполнены Некорректно,
//		Ложь - в противном случае.
//
&НаКлиенте
Функция ЗаполнениеПолейКорректно()
	
	Результат = Истина;
	
	Если ПустаяСтрока(РегистрационныйНомерПинКод) Тогда
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru = 'Не заполнено поле ""Регистрационный номер""'");
		Сообщение.Поле  = "РегистрационныйНомерПинКод";
		Сообщение.Сообщить();
		
		Результат = Ложь;
		
	КонецЕсли;
	
	Если ПустаяСтрока(ПинКод) Тогда
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru = 'Не заполнено поле ""Пинкод""'");
		Сообщение.Поле  = "ПинКод";
		Сообщение.Сообщить();
		
		Результат = Ложь;
		
	КонецЕсли;
	
	ПинКодСтр = СтрЗаменить(СтрЗаменить(ПинКод, "-", ""), " ", "");
	Если СтрДлина(ПинКодСтр) <> 16 Тогда
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru = 'Пин-код должен состоять из 16 цифр.'");
		Сообщение.Поле  = "ПинКод";
		Сообщение.Сообщить();
		
		Результат = Ложь;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Функция ПараметрыСообщенияВТехПоддержку()
	
	Результат = Новый Структура;
	Результат.Вставить("Тема", НСтр("ru = 'Интернет-поддержка. Регистрация продукта.'"));
	
	ЛогинПользователя = ИнтернетПоддержкаПользователейКлиентСервер.ЗначениеСессионногоПараметра(
		КонтекстВзаимодействия.КСКонтекст,
		"login");
	
	ТекстСообщения = НСтр("ru = 'Здравствуйте!
		|У меня не получается зарегистрировать программный продукт
		|для подключения Интернет-поддержки. Прошу помочь разобраться с проблемой.
		|
		|Логин: %1.
		|Регистрационный номер: %2.
		|Пинкод: %3.
		|
		|%ТехническиеПараметры%
		|-----------------------------------------------
		|С уважением, .'");
	
	ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		ТекстСообщения,
		ЛогинПользователя,
		РегистрационныйНомерПинКод,
		ПинКод);
	
	Результат.Вставить("ТекстСообщения", ТекстСообщения);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти
