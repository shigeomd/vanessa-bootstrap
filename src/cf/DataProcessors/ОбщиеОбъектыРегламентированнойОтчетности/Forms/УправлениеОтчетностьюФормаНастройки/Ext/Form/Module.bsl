&НаКлиенте
Перем КонтекстЭДОКлиент;

// ПриСозданииНаСервере()
//
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
				
	ПоказыватьПустыеДокументыВыгрузки = Параметры.мОтображатьПустые;
	ФильтроватьВидыОтчетовПоПериоду   = Параметры.мФильтроватьВидыОтчетовПоПериоду;
	
	мЗадаватьВопросПриЗакрытии = Истина;
	
	Если НЕ Параметры.ДоступенМеханизмПечатиРеглОтчетностиСДвухмернымШтрихкодомPDF417 Тогда
		Элементы.НастройкиПечатиМашиночитаемыхФорм.Видимость = Ложь;
	КонецЕсли;
		
КонецПроцедуры // ПриСозданииНаСервере()

&НаКлиенте
Процедура УправлениеЭУПанельУстановкаКомпонентыПечати()
	
	ВебБраузер = РегламентированнаяОтчетностьКлиент.ВебБраузер();
	
	Если ЗначениеЗаполнено(ВебБраузер)
	   И НЕ ВебБраузер = "MSIE" Тогда
		
		Элементы.КомпонентаПечати.Видимость = Ложь;
		Элементы.НастройкиШаблонов.Видимость = Ложь;
		Элементы.НастройкиПечатиМашиночитаемыхФорм.Заголовок = "";
		
		Возврат;
		
	КонецЕсли;	
	
	Если РегламентированнаяОтчетностьКлиент.КомпонентаПечатиМашиночитаемыхФормДоступна() Тогда
		Элементы.КомпонентаПечати.ТекущаяСтраница = Элементы.КомпонентаПечати.ПодчиненныеЭлементы.ПереустановкаКомпонентыПечати;
	Иначе
		Элементы.КомпонентаПечати.ТекущаяСтраница = Элементы.КомпонентаПечати.ПодчиненныеЭлементы.УстановкаКомпонентыПечати;
	КонецЕсли;

КонецПроцедуры

// ПриОткрытии()
//
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УправлениеЭУПанельУстановкаКомпонентыПечати();
		
КонецПроцедуры // ПриОткрытии()

// ПередЗакрытием()
//
&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	Если Модифицированность И мЗадаватьВопросПриЗакрытии Тогда
		Отказ = Истина;
		ОписаниеОповещения = Новый ОписаниеОповещения("ПередЗакрытиемЗавершение", ЭтотОбъект);
		ПоказатьВопрос(ОписаниеОповещения, НСтр("ru='Сохранить настройки?'"), РежимДиалогаВопрос.ДаНетОтмена);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытиемЗавершение(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		Если СохранитьНастройки() Тогда
			мЗадаватьВопросПриЗакрытии = Ложь;
			Закрыть(Истина);
		КонецЕсли;
	ИначеЕсли Ответ = КодВозвратаДиалога.Нет Тогда
		мЗадаватьВопросПриЗакрытии = Ложь;
		Закрыть();
	КонецЕсли;

КонецПроцедуры // ПередЗакрытием()

// СохранитьНастройки()
//
&НаКлиенте
Функция СохранитьНастройки()
	
	ВладелецФормы.мОтображатьПустые = ПоказыватьПустыеДокументыВыгрузки;
	ВладелецФормы.мФильтроватьВидыОтчетовПоПериоду = ФильтроватьВидыОтчетовПоПериоду;
		
	Возврат Истина;
	
КонецФункции // СохранитьНастройки()


// СохранитьИЗакрыть()
//
&НаКлиенте
Процедура СохранитьИЗакрыть(Команда)
	
	Если НЕ СохранитьНастройки() Тогда
		Возврат;
	КонецЕсли;
	
	мЗадаватьВопросПриЗакрытии = Ложь;
	Закрыть(Истина);
	
КонецПроцедуры // СохранитьИЗакрыть()

// КомпонентаПечатиНадписьЗдесьНажатие()
//
&НаКлиенте
Процедура КомпонентаПечатиНадписьЗдесьНажатие(Элемент)
	
	КаталогКомпонентыМЧБ = "";
	
	#Если ВебКлиент Тогда
		
		Если НЕ ПодключитьРасширениеРаботыСФайлами() Тогда
			ДополнительныеПараметры = Новый Структура("КаталогКомпонентыМЧБ", КаталогКомпонентыМЧБ);
			ОписаниеОповещения = Новый ОписаниеОповещения("КомпонентаПечатиНадписьЗдесьНажатиеЗавершение", ЭтотОбъект, ДополнительныеПараметры);
			НачатьУстановкуРасширенияРаботыСФайлами(ОписаниеОповещения);
		Иначе
			УстановитьКомпонентуМЧБ(КаталогКомпонентыМЧБ);
		КонецЕсли;
		
	#Иначе
		
		ДистрибутивКомпонентыПечатиМашиночитаемыхФорм = Новый СписокЗначений;
		
		РегламентированнаяОтчетностьВызовСервера.ПолучитьДистрибутивКомпонентыПечатиМашиночитаемыхФорм(ДистрибутивКомпонентыПечатиМашиночитаемыхФорм);
		
		РегламентированнаяОтчетностьКлиент.УстановитьКомпонентуПечатиМашиночитаемыхФорм(Истина, ДистрибутивКомпонентыПечатиМашиночитаемыхФорм, КаталогКомпонентыМЧБ);
		УправлениеЭУПанельУстановкаКомпонентыПечати();
		
	#КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура КомпонентаПечатиНадписьЗдесьНажатиеЗавершение(ДополнительныеПараметры) Экспорт
	
	КаталогКомпонентыМЧБ = ДополнительныеПараметры.КаталогКомпонентыМЧБ;
	
	Если НЕ ПодключитьРасширениеРаботыСФайлами() Тогда
		ПоказатьПредупреждение(,НСтр("ru='Не удалось подключить расширение работы с файлами!
			|Установка компоненты печати машиночитаемых форм невозможна.'"));
	Иначе
		УстановитьКомпонентуМЧБ(КаталогКомпонентыМЧБ);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура УстановитьКомпонентуМЧБ(КаталогКомпонентыМЧБ)
	
	ДиалогВыбораКаталога = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
	
	ДиалогВыбораКаталога.Заголовок = "Выберите каталог для сохранения дистрибутива компоненты печати машиночитаемых форм.";
	
	Если ДиалогВыбораКаталога.Выбрать() Тогда
		КаталогКомпонентыМЧБ = ДиалогВыбораКаталога.Каталог;
	КонецЕсли;
	
	Если КаталогКомпонентыМЧБ = Неопределено
		ИЛИ ПустаяСтрока(КаталогКомпонентыМЧБ)
		ИЛИ КаталогКомпонентыМЧБ = Ложь Тогда	
		Возврат;
	КонецЕсли;
	
	КаталогКомпонентыМЧБ = КаталогКомпонентыМЧБ + "\";
	ДистрибутивКомпонентыПечатиМашиночитаемыхФорм = Новый СписокЗначений;
	
	РегламентированнаяОтчетностьВызовСервера.ПолучитьДистрибутивКомпонентыПечатиМашиночитаемыхФорм(ДистрибутивКомпонентыПечатиМашиночитаемыхФорм);
	
	РегламентированнаяОтчетностьКлиент.УстановитьКомпонентуПечатиМашиночитаемыхФорм(Истина, ДистрибутивКомпонентыПечатиМашиночитаемыхФорм, КаталогКомпонентыМЧБ);
	УправлениеЭУПанельУстановкаКомпонентыПечати();

КонецПроцедуры // КомпонентаПечатиНадписьЗдесьНажатие()

// НадписьЗдесьНажатие()
//
&НаКлиенте
Процедура НадписьЗдесьНажатие(Элемент)
	
	ОткрытьФорму("РегистрСведений.ШаблоныПечатиМашиночитаемыхФорм.Форма.ФормаСписка");
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьЗдесьНастройкиДОСФНСНажатие(Элемент)
	
	Если КонтекстЭДОКлиент = Неопределено Тогда 
		//// инициализируем контекст ЭДО - модуль обработки
		ОписаниеОповещения = Новый ОписаниеОповещения("НадписьЗдесьНастройкиДОСФНСНажатиеЗавершение", ЭтотОбъект);
		ДокументооборотСКОКлиент.ПолучитьКонтекстЭДО(ОписаниеОповещения);
	Иначе
		ОткрытьФорму(КонтекстЭДОКлиент.ПутьКОбъекту + ".Форма.Настройки");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьЗдесьНастройкиДОСФНСНажатиеЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	КонтекстЭДОКлиент = Результат.КонтекстЭДО;
	Если КонтекстЭДОКлиент = Неопределено Тогда 
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Результат.ТекстОшибки);
	Иначе
		ОткрытьФорму(КонтекстЭДОКлиент.ПутьКОбъекту + ".Форма.Настройки");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьЗдесьНастройкиШаблоновЭВФНажатие(Элемент)
	
	ОткрытьФорму("РегистрСведений.ШаблоныЭВФОтчетовСтатистики.ФормаСписка");
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьЗдесьНастройкиФорматаВыгрузкиОтчетовСтатистикиНажатие(Элемент)
	
	Если ЭтаФорма.ВладелецФормы.Организация.Количество() > 0 Тогда
		Организация = ЭтаФорма.ВладелецФормы.Организация[0].Значение;
	КонецЕсли;
	
	РегламентированнаяОтчетностьКлиент.ПоказатьФормуНастроекФорматаВыгрузкиСтатистическойОтчетностиВЭлектронномВиде(Организация);
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьЗдесьНастройкиМеханизмаОнлайнСервисовРОНажатие(Элемент)
	
	ОткрытьФорму("Обработка.ОнлайнСервисыРегламентированнойОтчетности.Форма.НастройкиМеханизмаОнлайнСервисов");
	
КонецПроцедуры
