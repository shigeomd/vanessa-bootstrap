&НаКлиенте
Перем КонтекстЭДОКлиент;

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТекущаяДатаСервер = ТекущаяДатаСеанса();
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ТекстСообщения = "";
	КонтекстЭДОСервер = ДокументооборотСКОВызовСервера.ПолучитьОбработкуЭДО(ТекстСообщения);
	Если КонтекстЭДОСервер = Неопределено Тогда
		Отказ = Истина;
	КонецЕсли;
	
	Организация = Параметры.Организация;
	УчетнаяЗаписьДокументооборота = КонтекстЭДОСервер.УчетнаяЗаписьОрганизации(Организация);
	Попытка
		СпецоператорСвязи = УчетнаяЗаписьДокументооборота.СпецоператорСвязи;
	Исключение
		Отказ = Истина;
		Возврат;
	КонецПопытки;
	ПоддерживаетсяОтправкаВторичногоЗаявления = ЭлектронныйДокументооборотСКонтролирующимиОрганамиВызовСервера.ПолучитьПараметрСпецоператора(СпецоператорСвязи, "ПоддерживаетсяОтправкаВторичногоЗаявления") = "Истина";
	
	Если НЕ ПоддерживаетсяОтправкаВторичногоЗаявления Тогда
		Отказ = Истина;
	КонецЕсли;
	
	// Создаем таблицу с одной строкой.
	// Только при такой структуре данных мы сможем воспользоваться существующей функцией из КонтекстЭДОСервер.
	ИзменившиесяРеквизитыОрганизации = ИзменившиесяРеквизитыОрганизаций.Добавить();
	ИзменившиесяРеквизитыОрганизации.Организация = Организация;
	ИзменившиесяРеквизитыОрганизации.УчетнаяЗаписьДокументооборота = УчетнаяЗаписьДокументооборота;
	
	// Заполняем значения в колонках СписокИзменившихсяРеквизитов и ПредставлениеИзменившихсяРеквизитов
	КонтекстЭДОСервер.ДобавитьВТаблицуСписокИзменившихсяРеквизитовПодключенияК1СОтчетности(ИзменившиесяРеквизитыОрганизации);
	// Определяем, нужно ли показывать напоминание или нет
	КонтекстЭДОСервер.ОставитьТолькоИзменившиесяРеквизитыТребующиеНапоминания(ИзменившиесяРеквизитыОрганизаций, ТекущаяДатаСеанса());
	
	// Проверяем необходимость показа предупреждения
	Если ИзменившиесяРеквизитыОрганизаций.Количество() = 0 Тогда
		Отказ = Истина;
	Иначе
		Элементы.ПредупреждениеОбИзмененииПараметров.Заголовок = НСтр("ru = 'Изменились следующие настройки подключения: '") 
			+ ИзменившиесяРеквизитыОрганизаций[0].ПредставлениеИзменившихсяРеквизитов; 
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПриОткрытииЗавершение", ЭтотОбъект);
	
	ДокументооборотСКОКлиент.ПолучитьКонтекстЭДО(ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытииЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	КонтекстЭДОКлиент = Результат.КонтекстЭДО;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ОтправитьЗаявление(Команда)
	
	// Открываем мастер
	ПользовательЗахотелОтправитьЗаявление = Истина;
	ПараметрыФормы = Новый Структура("Организация", Организация);
	ОткрытьФорму(КонтекстЭДОКлиент.ПутьКОбъекту + ".Форма.МастерФормированияЗаявкиНаИзменениеПараметровПодключения",ПараметрыФормы, ,Организация);

	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура НапомнитьПозже(Команда)
	
	НапомнитьПользователюПозже();
	
КонецПроцедуры

&НаСервере
Процедура СохранитьДатуНапоминанияДляИзменившихсяРеквизитов(ДатаНапоминания)
	
	ТекстСообщения = "";
	КонтекстЭДОСервер = ДокументооборотСКОВызовСервера.ПолучитьОбработкуЭДО(ТекстСообщения);
	КонтекстЭДОСервер.СохранитьДатуНапоминанияДляИзменившихсяРеквизитов(ИзменившиесяРеквизитыОрганизаций, ДатаНапоминания);
	
КонецПроцедуры


&НаКлиенте
Процедура НапомнитьПользователюПозже()
	
	ДатаНапоминания = НачалоДня(ТекущаяДатаСервер) + 24 * 60 * 60;
	СохранитьДатуНапоминанияДляИзменившихсяРеквизитов(ДатаНапоминания);
	
	Если ЭтаФорма.Открыта() Тогда
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии()
	
	НапомнитьПользователюПозже();
	
КонецПроцедуры


