
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТипИсточника = ТипЗнч(Параметры.ИсточникСсылка);
	
	Если ТипИсточника = Тип("СправочникСсылка.ОтправкиРПН") Тогда
		HTMLТекст = Параметры.ИсточникСсылка.Протокол.Получить();
	Иначе //РегламентированныйОтчет или ЭлектронноеПредставление
		КонтекстЭДО = ДокументооборотСКОВызовСервера.ПолучитьОбработкуЭДО();
		ПоследняяОтправкаСсылка = КонтекстЭДО.ПолучитьПоследнююОтправкуОтчетаВРПН(Параметры.ИсточникСсылка);
		Если ПоследняяОтправкаСсылка<> Неопределено Тогда
			HTMLТекст = ПоследняяОтправкаСсылка.Протокол.Получить();
		КонецЕсли;
	КонецЕсли;
	
	Если Параметры.ИсточникСсылка.СтатусОтправки = Перечисления.СтатусыОтправки.Сдан Тогда
		Заголовок = НСтр("ru = 'Протокол о сдаче'");
	Иначе
		Заголовок = НСтр("ru = 'Протокол ошибок'");
	КонецЕсли;
	
	Элементы.КнопкаПечать.Видимость = Параметры.Свойство("ПечатьВозможна") И Параметры.ПечатьВозможна = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура Печать(Команда)
	
	Элементы.HTMLТекст.Документ.execCommand("Print");
	
КонецПроцедуры

&НаКлиенте
Процедура HTMLТекстПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	
	Ссылка = Неопределено;
	Попытка
		Ссылка = СокрЛП(нрег(ДанныеСобытия.Element.innerText));
		Если Лев(Ссылка, 7) <> "http://" И Лев(Ссылка, 8) <> "https://" Тогда
			Ссылка = Неопределено;
		КонецЕсли;
	Исключение
	КонецПопытки;
	
	Если ЗначениеЗаполнено(Ссылка) Тогда
		ПерейтиПоНавигационнойСсылке(Ссылка);
	КонецЕсли;
	
КонецПроцедуры
