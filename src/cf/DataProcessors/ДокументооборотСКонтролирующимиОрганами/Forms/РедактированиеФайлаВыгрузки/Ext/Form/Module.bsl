
&НаКлиенте
Функция Проверить()
	
	Если ПустаяСтрока(ИмяФайла) Тогда
		ПоказатьПредупреждение(, "Имя файла не задано!");
		ТекущийЭлемент = Элементы.ИмяФайла;
		Возврат Ложь;
	КонецЕсли;
	
	Если ПустаяСтрока(Текст) Тогда
		ПоказатьПредупреждение(, "Текст выгрузки пуст!");
		ТекущийЭлемент = Элементы.Текст;
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

&НаКлиенте
Процедура КомандаОК(Команда)
	
	Если НЕ Проверить() Тогда
		Возврат;
	КонецЕсли;
	СпрашиватьПриЗакрытии = Ложь;
	РезультатРедактирования.Ответ = Истина;
	РезультатРедактирования.ИмяФайла = ИмяФайла;
	РезультатРедактирования.Текст = Текст;
	Закрыть(РезультатРедактирования);

КонецПроцедуры

&НаКлиенте
Процедура КомандаЗакрыть(Команда)
	
	РезультатРедактирования.Ответ = Ложь;
	Закрыть(РезультатРедактирования);
	
КонецПроцедуры

&НаКлиенте
Процедура ИмяФайлаПриИзменении(Элемент)
	
	ОбновитьЗаголовок();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗаголовок()
	
	Заголовок = ?(ПустаяСтрока(ИмяФайла), "<имя файла не задано>", СокрЛП(ИмяФайла)) + " - редактирование";
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбновитьЗаголовок();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если СпрашиватьПриЗакрытии Тогда
		Отказ = Истина;
		Если Модифицированность Тогда
			ОписаниеОповещения = Новый ОписаниеОповещения("ПередЗакрытиемЗавершение", ЭтотОбъект);
			ПоказатьВопрос(ОписаниеОповещения, "Данные были изменены. Сохранить изменения?", РежимДиалогаВопрос.ДаНетОтмена);
		Иначе
			СпрашиватьПриЗакрытии = Ложь;
			РезультатРедактирования.Ответ = Ложь;
			Закрыть(РезультатРедактирования);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытиемЗавершение(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		Если НЕ Проверить() Тогда
			СпрашиватьПриЗакрытии = Истина;
		Иначе
			СпрашиватьПриЗакрытии = Ложь;
			Закрыть(Истина);
		КонецЕсли;
	ИначеЕсли Ответ = КодВозвратаДиалога.Нет Тогда
		СпрашиватьПриЗакрытии = Ложь;
		РезультатРедактирования.Ответ = Ложь;
		Закрыть(РезультатРедактирования);
	Иначе
		Отказ = Истина;
		СпрашиватьПриЗакрытии = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	СпрашиватьПриЗакрытии = Истина;
	РезультатРедактирования = Новый Структура("Ответ,ИмяФайла,Текст", Ложь, "", "");
	ИмяФайла = Параметры.ИмяФайла;
	Текст = Параметры.Текст;
	
КонецПроцедуры
