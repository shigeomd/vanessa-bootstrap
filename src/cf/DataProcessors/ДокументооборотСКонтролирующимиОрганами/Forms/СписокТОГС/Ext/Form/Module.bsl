&НаКлиенте
Перем КонтекстЭДОКлиент;


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТекущийКодТОГС 	= Параметры.ТекущийКодТОГС;
	СтарыйКодТОГС   = ТекущийКодТОГС; 
	Спецоператор 	= Параметры.Спецоператор;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПриОткрытииЗавершение", ЭтотОбъект);
	
	ДокументооборотСКОКлиент.ПолучитьКонтекстЭДО(ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытииЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	КонтекстЭДОКлиент = Результат.КонтекстЭДО;
		
	Макет = КонтекстЭДОКлиент.ПолучитьМакетОбработки("КодыТОГС");
	ИмяКолонкиСпецоператора = ЭлектронныйДокументооборотСКонтролирующимиОрганамиВызовСервера.ПолучитьПараметрСпецоператора(Спецоператор, "СпецоператорСвязи");
	
	// Определяем номера колонок 
	КолонкаСКодом 			=  Макет.Область("Код").Лево;
	КолонкаСНаименованием 	=  Макет.Область("Наименование").Лево;
	КолонкаСРегионом 		=  Макет.Область("Регион").Лево;
	КолонкаСГородом 		=  Макет.Область("Город").Лево;
	КолонкаСоСпецоператором =  Макет.Область(ИмяКолонкиСпецоператора).Лево;
	
	ТаблицаТОГС.Очистить();
	
	// Заполняем таблицу ТОГС
	Для НомерСтроки = 2 По Макет.ВысотаТаблицы Цикл
		
		ТОГСПоддерживается = (СокрЛП(Макет.Область(НомерСтроки, КолонкаСоСпецоператором).Текст) <> "x");
		
		Если ТОГСПоддерживается Тогда 
			НоваяСтрокаТаблицыТОГС 				= ТаблицаТОГС.Добавить();
			НоваяСтрокаТаблицыТОГС.Код 			= СокрЛП(Макет.Область(НомерСтроки, КолонкаСКодом).Текст);
			НоваяСтрокаТаблицыТОГС.Наименование = СокрЛП(Макет.Область(НомерСтроки, КолонкаСНаименованием).Текст);
			НоваяСтрокаТаблицыТОГС.Регион		= СокрЛП(Макет.Область(НомерСтроки, КолонкаСРегионом).Текст);
			НоваяСтрокаТаблицыТОГС.Город 		= СокрЛП(Макет.Область(НомерСтроки, КолонкаСГородом).Текст);
		КонецЕсли;	
			
	КонецЦикла;
	
	СпозиционироватьсяНаНужнойСтрокеТаблицыТОГС();	 

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЦЫ ФОРМЫ <ИМЯ ТАБЛИЦЫ ФОРМЫ>
&НаКлиенте
Процедура ТаблицаТОГСВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ВыбратьСтроку();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ
&НаКлиенте
Процедура Выбрать(Команда)
	
	ВыбратьСтроку();
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть(СтарыйКодТОГС);
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
&НаКлиенте
Процедура ВыбратьСтроку()
	
	ВыбранныйКодТОГС = Неопределено;
	
	ВыбранныйКодТОГС = Элементы.ТаблицаТОГС.ТекущиеДанные.Код;
	Если ВыбранныйКодТОГС = "" Тогда
		ВыбранныйКодТОГС = "00-00";
	КонецЕсли;
	
	Закрыть(ВыбранныйКодТОГС);
	
КонецПроцедуры

&НаСервере
Процедура СпозиционироватьсяНаНужнойСтрокеТаблицыТОГС()
	
	МассивНайденныхСтрок = ТаблицаТОГС.НайтиСтроки(Новый Структура("Код",ТекущийКодТОГС));
	
	Для каждого СтрокаМассива Из МассивНайденныхСтрок Цикл
		Элементы.ТаблицаТОГС.ТекущаяСтрока = СтрокаМассива.ПолучитьИдентификатор();				
	КонецЦикла; 		
	
КонецПроцедуры











