
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТекстСообщения = "";
	КонтекстЭДОСервер = ДокументооборотСКОВызовСервера.ПолучитьОбработкуЭДО(ТекстСообщения);
	Если КонтекстЭДОСервер = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	СведенияОбОтчете = КонтекстЭДОСервер.СведенияПоОтправляемымОбъектам(Параметры.СсылкаНаОтчет);
	
	ВариантОтчета = Строка(СведенияОбОтчете.ВариантОтчета);
	Если ВариантОтчета = "П" Тогда
		ВариантОтчета = НСтр("ru = 'первичный'");
	ИначеЕсли Лев(ВариантОтчета, 2) = "К/" Тогда
		ВариантОтчета = НСтр("ru = 'корректирующий'") + " / " + Сред(ВариантОтчета, 3);
	КонецЕсли;
	
	НаименованиеИВариантОтчета = Строка(СведенияОбОтчете.Наименование) + 
		?(НЕ ПустаяСтрока(ВариантОтчета), " (", "") + ВариантОтчета + ?(НЕ ПустаяСтрока(ВариантОтчета), ")", "");
	НаименованиеИВариантОтчетаВтораяСтрока = "";
	МаксимальнаяДлинаСтроки = 45;
	Если СтрДлина(НаименованиеИВариантОтчета) > МаксимальнаяДлинаСтроки Тогда
		Если СтрДлина(НаименованиеИВариантОтчета) > МаксимальнаяДлинаСтроки * 2 Тогда
			ПозицияВСтроке = Цел(СтрДлина(НаименованиеИВариантОтчета) / 2);
		Иначе
			ПозицияВСтроке = МаксимальнаяДлинаСтроки;
		КонецЕсли;
		Пока ПозицияВСтроке > 1 Цикл
			Если Сред(НаименованиеИВариантОтчета, ПозицияВСтроке, 1) = " " Тогда
				НаименованиеИВариантОтчетаВтораяСтрока = Сред(НаименованиеИВариантОтчета, ПозицияВСтроке + 1);
				НаименованиеИВариантОтчета = Лев(НаименованиеИВариантОтчета, ПозицияВСтроке - 1);
				Прервать;
			КонецЕсли;
			ПозицияВСтроке = ПозицияВСтроке - 1;
		КонецЦикла;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(НаименованиеИВариантОтчетаВтораяСтрока) Тогда
		Элементы.ДекорацияОтчет.Заголовок = НСтр("ru = 'Отчет'") + ":
			| ";
		Элементы.НаименованиеИВариантОтчета.Заголовок = НаименованиеИВариантОтчета + "
			|" + НаименованиеИВариантОтчетаВтораяСтрока;
		КлючСохраненияПоложенияОкна = "НаименованиеИВариантОтчетаВДвеСтроки";
	Иначе
		Элементы.ДекорацияОтчет.Заголовок = НСтр("ru = 'Отчет'") + ":";
		Элементы.НаименованиеИВариантОтчета.Заголовок = НаименованиеИВариантОтчета;
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(СведенияОбОтчете.ПредставлениеПериода) Тогда
		Элементы.ДекорацияДатаСоздания.Видимость = Истина;
		Элементы.ДатаСоздания.Видимость = Истина;
		Элементы.ДекорацияПериод.Видимость = Ложь;
		Элементы.ПредставлениеПериода.Видимость = Ложь;
		Элементы.ДатаСоздания.Заголовок = Формат(СведенияОбОтчете.Дата, "ДФ=dd.MM.yyyy");
	Иначе
		Элементы.ПредставлениеПериода.Заголовок = Строка(СведенияОбОтчете.ПредставлениеПериода);
	КонецЕсли;
	Элементы.НаименованиеОрганизации.Заголовок = Строка(СведенияОбОтчете.Организация.Наименование);
	Элементы.ПредставлениеКонтролирующегоОргана.Заголовок = Строка(СведенияОбОтчете.ПредставлениеКонтролирующегоОргана);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодтверждениеКорректностиЗаполненияПриИзменении(Элемент)
	
	Элементы.Отправить.Доступность = ПодтверждениеКорректностиЗаполнения;
	
КонецПроцедуры

&НаКлиенте
Процедура Отправить(Команда)
	
	Закрыть(КодВозвратаДиалога.ОК);
	
КонецПроцедуры
