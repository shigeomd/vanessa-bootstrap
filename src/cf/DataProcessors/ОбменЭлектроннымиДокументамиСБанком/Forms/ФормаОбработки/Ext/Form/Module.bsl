
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ИспользуетсяОбменЭДСБанками = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьЗначениеФункциональнойОпции(
																				"ИспользоватьОбменЭДСБанками");
	Если НЕ ИспользуетсяОбменЭДСБанками Тогда
		ТекстСообщения = ЭлектронныеДокументыСлужебныйВызовСервера.ТекстСообщенияОНеобходимостиНастройкиСистемы(
																				"РаботаСБанками");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , , , Отказ);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ЗаполнитьПараметры();
	Элементы.ВыполнитьОбменСБанком.Доступность = ЗначениеЗаполнено(Объект.НастройкаЭДО);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ОбновитьСостояниеЭД" Тогда
		Элементы.ВыпискиБанка.Обновить();
	ИначеЕсли ИмяСобытия = "ПолученыВыписки" Тогда
		Элементы.ВыпискиБанка.Обновить();
		ПоследнийЭД = ПоследнийЭД(Объект.НастройкаЭДО);
		Если ЗначениеЗаполнено(ПоследнийЭД) ТОгда
			Элементы.ВыпискиБанка.ТекущаяСтрока = ПоследнийЭД;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НастройкаЭДОПриИзменении(Элемент)
	
	ЗаполнитьПараметры();
	Модифицированность = Ложь;
	Элементы.ВыполнитьОбменСБанком.Доступность = ЗначениеЗаполнено(Объект.НастройкаЭДО);
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодПриИзменении(Элемент)
	
	Модифицированность = Ложь;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗапроситьВыписку(Команда)
	
	Если Не ЗначениеЗаполнено(Период) Тогда
		СообщениеТекст = Нстр("ru = 'Необходимо выбрать период запроса'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СообщениеТекст, , "Период");
		Возврат;
	КонецЕсли;
	
	ЭлектронныеДокументыКлиент.ПолучитьВыпискуБанка(
		Объект.НастройкаЭДО, Период.ДатаНачала, Период.ДатаОкончания, Элементы.ВыпискиБанка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыВыписки

&НаКлиенте
Процедура ВыпискиБанкаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		Элементы.ВыпискиБанка.ТекущаяСтрока = ВыбранноеЗначение;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ЗаполнитьПараметры()
	
	Если ЗначениеЗаполнено(Объект.НастройкаЭДО) Тогда
		РеквизитыНастройкиЭДО = РеквизитыНастройкиЭДО(Объект.НастройкаЭДО);
		Объект.Банк         = РеквизитыНастройкиЭДО.Контрагент;
		Объект.Организация  = РеквизитыНастройкиЭДО.Организация;
		ПрограммаБанка      = РеквизитыНастройкиЭДО.ПрограммаБанка;
	Иначе
		Объект.Банк        = ПредопределенноеЗначение("Справочник.Организации.ПустаяСсылка");
		Объект.Организация = ПредопределенноеЗначение("Справочник.Организации.ПустаяСсылка");
		ПрограммаБанка     = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.ПустаяСсылка");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.НастройкаЭДО) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
								ВыпискиБанка, "НастройкаЭДО", Объект.НастройкаЭДО);
	Иначе
		ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбораДинамическогоСписка(ВыпискиБанка, "НастройкаЭДО");
	КонецЕсли;

	Элементы.ГруппаДоступа.Доступность = ЗначениеЗаполнено(Объект.НастройкаЭДО);

	
КонецПроцедуры

&НаСервереБезКонтекста
Функция РеквизитыНастройкиЭДО(НастройкаЭДО)
	
	Возврат ОбщегоНазначения.ЗначенияРеквизитовОбъекта(НастройкаЭДО, "Организация, Контрагент, ПрограммаБанка");
	
КонецФункции

&НаСервереБезКонтекста
Функция ПоследнийЭД(НастройкаЭДО)
	
	Запрос = новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ЭДПрисоединенныеФайлы.Ссылка КАК ЭлектронныйДокумент
	|ИЗ
	|	Справочник.ЭДПрисоединенныеФайлы КАК ЭДПрисоединенныеФайлы
	|ГДЕ
	|	ЭДПрисоединенныеФайлы.СоглашениеЭД = &НастройкаЭДО
	|	И (ЭДПрисоединенныеФайлы.ВидЭД = ЗНАЧЕНИЕ(Перечисление.ВидыЭД.ЗапросВыписки)
	|			ИЛИ ЭДПрисоединенныеФайлы.ВидЭД = ЗНАЧЕНИЕ(Перечисление.ВидыЭД.ВыпискаБанка))
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаСоздания УБЫВ";
	Запрос.УстановитьПараметр("НастройкаЭДО", НастройкаЭДО);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.ЭлектронныйДокумент;
	КонецЕсли
	
КонецФункции

#КонецОбласти

