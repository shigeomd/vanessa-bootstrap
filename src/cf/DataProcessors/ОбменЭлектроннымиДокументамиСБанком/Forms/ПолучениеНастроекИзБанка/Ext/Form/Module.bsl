#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
		
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.СпособАутентификации = "ПоСертификату" Тогда
		Для Каждого Элемент Из Параметры.СписокВыбораСертификатов Цикл
			Элементы.Сертификат.СписокВыбора.Добавить(Элемент.Значение, Элемент.Представление);
		КонецЦикла;
		Элементы.СтраницыСертификатИлиПользователь.ТекущаяСтраница = Элементы.СтраницаСертификат;
	ИначеЕсли Параметры.СпособАутентификации = "ПоЛогинуИлиСертификату" Тогда
		Для Каждого Элемент Из Параметры.СписокВыбораСертификатов Цикл
			Элементы.СертификатСмешанный.СписокВыбора.Добавить(Элемент.Значение, Элемент.Представление);
		КонецЦикла;
		Элементы.СтраницыСертификатИлиПользователь.ТекущаяСтраница = Элементы.СтраницаСмешанный;
	КонецЕсли;
	
	Элементы.СтраницыСертификатИлиПользователь.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
	Элементы.СтраницыСмешанногоРежима.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	СтруктураВозврата = Новый Структура;
	СтруктураВозврата.Вставить("Пароль", Пароль);
	
	Если Элементы.СтраницыСертификатИлиПользователь.ТекущаяСтраница = Элементы.СтраницаПользователь Тогда
		Если НЕ ЗначениеЗаполнено(Пользователь) Тогда
			ТекстСообщения = НСтр("ru = 'Укажите имя пользователя'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "Пользователь");
			Возврат;
		КонецЕсли;
		СтруктураВозврата.Вставить("Логин", Пользователь);
	ИначеЕсли Элементы.СтраницыСертификатИлиПользователь.ТекущаяСтраница = Элементы.СтраницаСертификат Тогда
		Если НЕ ЗначениеЗаполнено(Сертификат) Тогда
			ТекстСообщения = НСтр("ru = 'Выберите сертификат из установленных на компьютере'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "Сертификат");
			Возврат;
		КонецЕсли;
		СтруктураВозврата.Вставить("Отпечаток", Сертификат);
	ИначеЕсли Элементы.СтраницыСертификатИлиПользователь.ТекущаяСтраница = Элементы.СтраницаСмешанный Тогда
		Если СпособАутентификации Тогда
			Если НЕ ЗначениеЗаполнено(Пользователь) Тогда
				ТекстСообщения = НСтр("ru = 'Укажите имя пользователя'");
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "Пользователь");
				Возврат;
			КонецЕсли;
			СтруктураВозврата.Вставить("Логин", Пользователь);
		Иначе
			Если НЕ ЗначениеЗаполнено(Сертификат) Тогда
				ТекстСообщения = НСтр("ru = 'Выберите сертификат из установленных на компьютере'");
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "Сертификат");
				Возврат;
			КонецЕсли;
			СтруктураВозврата.Вставить("Отпечаток", Сертификат);
		КонецЕсли;
	КонецЕсли;
	
	Закрыть(СтруктураВозврата);
	
КонецПроцедуры

&НаКлиенте
Процедура СпособАутентификацииПриИзменении(Элемент)
	
	Если СпособАутентификации Тогда
		Элементы.СтраницыСмешанногоРежима.ТекущаяСтраница = Элементы.СтраницаПользовательСмешанный;
	Иначе
		Элементы.СтраницыСмешанногоРежима.ТекущаяСтраница = Элементы.СтраницаСертификатСмешанный;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти