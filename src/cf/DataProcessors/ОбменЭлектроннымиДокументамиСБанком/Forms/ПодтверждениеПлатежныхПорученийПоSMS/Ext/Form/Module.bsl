#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	ЭлектронныйДокумент = Параметры.ЭлектронныйДокумент;
	
	ДопИнформацияПоЭД = ЭлектронныеДокументыСлужебный.ПолучитьДанныеФайла(ЭлектронныйДокумент);
	Если НЕ (ДопИнформацияПоЭД.Свойство("СсылкаНаДвоичныеДанныеФайла")
				И ЗначениеЗаполнено(ДопИнформацияПоЭД.СсылкаНаДвоичныеДанныеФайла)) Тогда
			
		Отказ = Истина;
		Возврат;
	КонецЕсли;
		
	ДанныеЭД = ПолучитьИзВременногоХранилища(ДопИнформацияПоЭД.СсылкаНаДвоичныеДанныеФайла);

	ИмяФайла = ПолучитьИмяВременногоФайла("xml");
			
	Если ИмяФайла = Неопределено Тогда
		ТекстОшибки = НСтр("ru = 'Не удалось прочитать электронный документ. Проверьте настройку рабочего каталога'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ДанныеЭД.Записать(ИмяФайла);
	
	СтруктураДанных = ЭлектронныеДокументыВнутренний.СформироватьДеревоРазбора(
									ИмяФайла, ЭлектронныйДокумент.НаправлениеЭД);
	УдалитьФайлы(ИмяФайла);
	Если СтруктураДанных = Неопределено Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	СуммаЧислом = ЭлектронныеДокументыВнутренний.ПолучитьЗначениеРеквизитаСтрокиДереваРазбора(
						СтруктураДанных.ДеревоРазбора, СтруктураДанных.СтрокаОбъекта, "Сумма");
	Сумма = Формат(СуммаЧислом, "ЧДЦ=2") + " руб.";
	Получатель = ЭлектронныеДокументыВнутренний.ПолучитьЗначениеРеквизитаСтрокиДереваРазбора(
					СтруктураДанных.ДеревоРазбора, СтруктураДанных.СтрокаОбъекта, "ПолучательНаименование");
	БИК = ЭлектронныеДокументыВнутренний.ПолучитьЗначениеРеквизитаСтрокиДереваРазбора(
					СтруктураДанных.ДеревоРазбора, СтруктураДанных.СтрокаОбъекта, "ПолучательБИКБанка");
	НомерСчета = ЭлектронныеДокументыВнутренний.ПолучитьЗначениеРеквизитаСтрокиДереваРазбора(
					СтруктураДанных.ДеревоРазбора, СтруктураДанных.СтрокаОбъекта, "ПолучательРасчСчет");
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	ИдентификаторСессии = Параметры.Сессия.Идентификатор;
	
	Если Параметры.ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.ОбменЧерезДопОбработку") Тогда
		ПодключаемыйМодуль = ЭлектронныеДокументыСлужебныйКлиент.ВнешнийПодключаемыйМодульЧерезДополнительнуюОбработку(Параметры.СоглашениеЭД);
	Иначе
		ПараметрыПодсистемыОбменСБанками = ПараметрыПриложения["ОбменСБанками.Параметры"];
		ПодключаемыйМодуль = ?(ТипЗнч(ПараметрыПодсистемыОбменСБанками) = Тип("Соответствие"),
			ПараметрыПодсистемыОбменСБанками.Получить(ЭлектронныеДокументыСлужебныйКлиент.ИмяКомпонентыiBank2()), Неопределено);
	КонецЕсли;
	
	Попытка
		Если Параметры.ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.ОбменЧерезДопОбработку") Тогда
			ПодключаемыйМодуль.ОтправитьКодПодтвержденияПоSMS(Параметры.Сертификат, Параметры.Сессия);
		Иначе
			СессияXML = ЭлектронныеДокументыСлужебныйКлиент.СериализованныеДанные(Параметры.Сессия);
			ПодключаемыйМодуль.ОтправитьКодПодтвержденияПоSMS(СессияXML);
		КонецЕсли;
	Исключение
		ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ШаблонОшибки = НСтр("ru = 'Ошибка отправки кода подтверждения платежного поручения.
									|Код ошибки: %1
									|%2'");
		Если Параметры.ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.ОбменЧерезДопОбработку") Тогда
			ДеталиОшибки = ПодключаемыйМодуль.ДеталиОшибки();
		Иначе
			ДеталиОшибки = ЭлектронныеДокументыСлужебныйКлиент.ИнформацияОбОшибкеiBank2();
		КонецЕсли;
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							ШаблонОшибки, ДеталиОшибки.Код, ДеталиОшибки.Сообщение);
		Операция = НСтр("ru = 'Отправка кода подтверждения платежного поручения'");
		ЭлектронныеДокументыСлужебныйВызовСервера.ОбработатьИсключениеПоЭДНаСервере(
							Операция, ПодробноеПредставлениеОшибки, ТекстСообщения, 1);
		Отказ = Истина;
	КонецПопытки;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ЭлектронныйДокументВладелецФайлаНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЭлектронныйДокумент", ЭлектронныйДокумент);
	ОткрытьФорму("Обработка.ЭлектронныеДокументы.Форма.ФормаЗагрузкиПросмотраЭД", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаПодтвердить(Команда)

	Если ПустаяСтрока(Пароль) Тогда
		ТекстСообщения = НСтр("ru = 'Для продолжения необходимо указать код подтверждения.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "Пароль");
		Возврат;
	КонецЕсли;

	Закрыть(Пароль);

КонецПроцедуры

#КонецОбласти
