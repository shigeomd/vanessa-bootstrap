&НаКлиенте
Перем ВнешнийПодключаемыйМодуль;

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого Хранилище Из Параметры.Хранилища Цикл
		Элементы.ИдентификаторХранилища.СписокВыбора.Добавить(Хранилище);
	КонецЦикла;
	
	Если Параметры.Хранилища.Количество() = 1 Тогда
		Элементы.ИдентификаторХранилища.Доступность = Ложь;
		ТекущийЭлемент = Элементы.ПИН;
	КонецЕсли;
	
	ПрограммаБанка = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры.СоглашениеЭД, "ПрограммаБанка");
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	Если ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.ОбменЧерезДопОбработку") Тогда
		ВнешнийПодключаемыйМодуль = ЭлектронныеДокументыСлужебныйКлиент.ВнешнийПодключаемыйМодульЧерезДополнительнуюОбработку(
																										Параметры.СоглашениеЭД);
		Если ВнешнийПодключаемыйМодуль = Неопределено Тогда
			Отказ = Истина;
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	УстановитьДоступностьЭлементов();
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ХранилищеПриИзменении(Элемент)

	УстановитьДоступностьЭлементов();
	ПИН = "";
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Готово(Команда)
	
	Если Не ЗначениеЗаполнено(ИдентификаторХранилища) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не выбрано хранилище'"), , "ИдентификаторХранилища");
		Возврат;
	КонецЕсли;
	
	Если Элементы.ПИН.Доступность Тогда
		Если ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.ОбменЧерезДопОбработку") Тогда
			ПинКодУстановлен = ЭлектронныеДокументыСлужебныйКлиент.УстановитьPINКодХранилищаЧерезДополнительнуюОбработку(
																ВнешнийПодключаемыйМодуль, ИдентификаторХранилища, ПИН)
		Иначе
			ПинКодУстановлен = ЭлектронныеДокументыСлужебныйКлиент.УстановитьPINКодХранилищаiBank2(ИдентификаторХранилища, ПИН);
		КонецЕсли;
	Иначе
		ПинКодУстановлен = Истина;
	КонецЕсли;
	
	Если ПинКодУстановлен Тогда
		Закрыть(ИдентификаторХранилища);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УстановитьДоступностьЭлементов()

	Если ЗначениеЗаполнено(ИдентификаторХранилища) Тогда
	
		Если ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.ОбменЧерезДопОбработку") Тогда
			Элементы.ПИН.Доступность = ЭлектронныеДокументыСлужебныйКлиент.ТребуетсяУстановкаPINКодаХранилищаЧерезДополнительнуюОбработку(ВнешнийПодключаемыйМодуль, ИдентификаторХранилища) = Истина
				И НЕ ЭлектронныеДокументыСлужебныйКлиент.УстановленPINКодХранилищаЧерезДополнительнуюОбработку(ВнешнийПодключаемыйМодуль, ИдентификаторХранилища) = Истина;
		Иначе
			Элементы.ПИН.Доступность = ЭлектронныеДокументыСлужебныйКлиент.НеобходимоУстановитьPINКодХранилищаiBank2(ИдентификаторХранилища) = Истина;
		КонецЕсли;
	
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

