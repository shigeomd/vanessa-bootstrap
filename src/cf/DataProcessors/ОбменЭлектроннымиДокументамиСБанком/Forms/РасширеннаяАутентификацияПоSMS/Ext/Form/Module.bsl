&НаКлиенте
Перем ВнешнийПодключаемыйМодуль;

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	ПрограммаБанка = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры.НастройкаЭДО, "ПрограммаБанка");
	
	Если ПрограммаБанка = Перечисления.ПрограммыБанка.АсинхронныйОбмен ИЛИ ПрограммаБанка = Неопределено Тогда
		Если НЕ ЗначениеЗаполнено(Параметры.Телефон) Тогда
			Элементы.Телефон.Видимость = Ложь;
		КонецЕсли;
		Телефон = Параметры.Телефон;
		ИдентификаторСессии = Параметры.ИдентификаторСессии;
	Иначе
		Элементы.Телефон.Видимость = Ложь;
	КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.ОбменЧерезДопОбработку")
		ИЛИ ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.iBank2") Тогда
		
		ОтправитьSMS();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Готово(Команда)
	
	Если ПустаяСтрока(Пароль) Тогда
		ТекстСообщения = НСтр("ru = 'Для продолжения укажите одноразовый пароль.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "Пароль");
		Возврат;
	КонецЕсли;
	
	Если ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.ОбменЧерезДопОбработку")
		ИЛИ ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.iBank2") Тогда

		ДанныеРасширеннойАутентификации = Новый Структура("Способ, Сессия, Пароль");
		ДанныеРасширеннойАутентификации.Способ = "SMS";
		ДанныеРасширеннойАутентификации.Сессия = Параметры.Сессия;
		ДанныеРасширеннойАутентификации.Пароль = Пароль;

		Попытка
			Если ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.ОбменЧерезДопОбработку") Тогда
				ВнешнийПодключаемыйМодуль.РасширеннаяАутентификация(Параметры.Сертификат, ДанныеРасширеннойАутентификации);
			Иначе
				ДанныеРасширеннойАутентификацииXML = ЭлектронныеДокументыСлужебныйКлиент.СериализованныеДанные(ДанныеРасширеннойАутентификации);
				ВнешнийПодключаемыйМодуль.РасширеннаяАутентификация(ДанныеРасширеннойАутентификацииXML);
			КонецЕсли;
			Закрыть(Истина);
		Исключение
			ШаблонОшибки = НСтр("ru = 'Ошибка SMS аутентификации.
										|Код ошибки: %1
										|%2'");
			Если ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.ОбменЧерезДопОбработку") Тогда
				ДеталиОшибки = ВнешнийПодключаемыйМодуль.ДеталиОшибки();
			Иначе
				ДеталиОшибки = ЭлектронныеДокументыСлужебныйКлиент.ИнформацияОбОшибкеiBank2();
			КонецЕсли;
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
								ШаблонОшибки, ДеталиОшибки.Код, ДеталиОшибки.Сообщение);
			Операция = НСтр("ru = 'SMS аутентификация'");
			ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			ЭлектронныеДокументыСлужебныйВызовСервера.ОбработатьИсключениеПоЭДНаСервере(
								Операция, ПодробноеПредставлениеОшибки, ТекстСообщения, 1);
			Элементы.ФормаКнопкаГотово.Доступность = Ложь;
		КонецПопытки;
		
	Иначе
		
		Закрыть(Пароль);
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОтправитьSMS()
	
	ИдентификаторСессии = Параметры.Сессия.Идентификатор;

	Если ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.ОбменЧерезДопОбработку") Тогда
		ВнешнийПодключаемыйМодуль = ЭлектронныеДокументыСлужебныйКлиент.ВнешнийПодключаемыйМодульЧерезДополнительнуюОбработку(Параметры.НастройкаЭДО);
	Иначе
		ПараметрыПодсистемыОбменСБанками = ПараметрыПриложения["ОбменСБанками.Параметры"];
		ВнешнийПодключаемыйМодуль = ?(ТипЗнч(ПараметрыПодсистемыОбменСБанками) = Тип("Соответствие"),
			ПараметрыПодсистемыОбменСБанками.Получить(ЭлектронныеДокументыСлужебныйКлиент.ИмяКомпонентыiBank2()), Неопределено);
	КонецЕсли;
		
	Попытка
		Если ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.ОбменЧерезДопОбработку") Тогда
			ВнешнийПодключаемыйМодуль.ОтправитьОдноразовыйПарольРасширеннойАутентификацииПоSMS(
														Параметры.Сертификат, Параметры.Сессия);
		Иначе
			СессияXML = ЭлектронныеДокументыСлужебныйКлиент.СериализованныеДанные(Параметры.Сессия);
			ВнешнийПодключаемыйМодуль.ОтправитьПарольРасширеннойАутентификацииПоSMS(СессияXML);
		КонецЕсли;
	Исключение
		ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ШаблонОшибки = НСтр("ru = 'Ошибка запуска аутентификации по SMS.
									|Код ошибки: %1
									|%2'");
		Если ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.ОбменЧерезДопОбработку") Тогда
			ДеталиОшибки = ВнешнийПодключаемыйМодуль.ДеталиОшибки();
		Иначе
			ДеталиОшибки = ЭлектронныеДокументыСлужебныйКлиент.ИнформацияОбОшибкеiBank2();
		КонецЕсли;
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							ШаблонОшибки, ДеталиОшибки.Код, ДеталиОшибки.Сообщение);
		Операция = НСтр("ru = 'Установка соединения'");
		ЭлектронныеДокументыСлужебныйВызовСервера.ОбработатьИсключениеПоЭДНаСервере(
			Операция, ПодробноеПредставлениеОшибки, ТекстСообщения, 1);
		Элементы.ФормаКнопкаГотово.Видимость = Ложь;
	КонецПопытки;

КонецПроцедуры

#КонецОбласти
