#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	// Значения реквизитов формы
	Если Константы.ИнтерфейсТакси.Получить() Тогда
		ВариантИнтерфейса = "ИнтерфейсТакси";
	ИначеЕсли Константы.ИнтерфейсВерсии82.Получить() Тогда
		ВариантИнтерфейса = "ИнтерфейсВерсии82";
	ИначеЕсли Константы.ИнтерфейсВерсии77.Получить() Тогда
		ВариантИнтерфейса = "ИнтерфейсВерсии77";
	Иначе
		ВариантИнтерфейса = "";
	КонецЕсли;
	ВариантИнтерфейсаДоИзменения = ВариантИнтерфейса;
	Если ВариантИнтерфейса = "" Тогда
		ВариантИнтерфейса = "ИнтерфейсТакси";
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВариантИнтерфейсаПриИзменении(Элемент)

	Если ВариантИнтерфейсаДоИзменения <> ВариантИнтерфейса Тогда
		Элементы.ГруппаПерезапуск.ТекущаяСтраница = Элементы.ГруппаПерезапускКнопка;
	Иначе
		Элементы.ГруппаПерезапуск.ТекущаяСтраница = Элементы.ГруппаПерезапускПустаяСтраница;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Перезапустить(Команда)
	
	УстановитьИнтерфейсНаСервере(ВариантИнтерфейса);
	
	ЗавершитьРаботуСистемы(Ложь, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

////////////////////////////////////////////////////////////////////////////////
// Сервер

&НаСервереБезКонтекста
Процедура УстановитьИнтерфейсНаСервере(ВариантИнтерфейса)

	ОбщегоНазначенияБП.УстановитьРежимКомандногоИнтерфейса(ВариантИнтерфейса);
	УстановитьПривилегированныйРежим(Истина);
	Константы.РежимИнтерфейсаУстановленАвтоматически.Установить(Ложь);
	ХранилищеОбщихНастроек.Сохранить(ВРег("ДатаСменыИнтерфейса"),,Дата('00010101'),,);
	
КонецПроцедуры

#КонецОбласти
