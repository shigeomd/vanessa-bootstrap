////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ
//

&НаКлиенте
Процедура ОбработкаВыбораЗначения()
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураВыбора = Новый Структура;
	СтруктураВыбора.Вставить("Документ", ТекущиеДанные.Документ);
	СтруктураВыбора.Вставить("Договор", ТекущиеДанные.ДоговорКонтрагента);
	
	ОповеститьОВыборе(СтруктураВыбора);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНД ФОРМЫ
//

&НаКлиенте
Процедура Изменить(Команда)
	
	Если Элементы.Список.ТекущиеДанные <> Неопределено Тогда 
		Документ = Элементы.Список.ТекущиеДанные.Документ;
		ПоказатьЗначение( , Документ);	
	КонецЕсли;
			
КонецПроцедуры

&НаКлиенте
Процедура Выбрать(Команда)
	
	ОбработкаВыбораЗначения();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СОБЫТИЯ ФОРМЫ
//

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТипыОснований = Документы.ТипВсеСсылки();
	
	ЭтоВыданныйСчетФактура = Ложь;
	Если Параметры.ЗначенияЗаполнения.ТипСчетаФактуры = "Выданный" Тогда 
		ЭтоВыданныйСчетФактура = Истина;	
		ТипыОснований = УчетНДСПереопределяемый.ПолучитьСписокТиповПоВидуСчетаФактурыВыставленного(Параметры.ЗначенияЗаполнения.ВидСчетаФактуры, Параметры.ЗначенияЗаполнения.Исправление)
	ИначеЕсли Параметры.ЗначенияЗаполнения.ТипСчетаФактуры = "Полученный" Тогда
		ТипыОснований = УчетНДСПереопределяемый.ПолучитьСписокТиповПоВидуСчетаФактурыПолученного(Параметры.ЗначенияЗаполнения.ВидСчетаФактуры);
	КонецЕсли;
	
	Запрос = Новый Запрос;
	
	ОтборОрганизация	= Параметры.Отбор.Организация;
	ОтборКонтрагент		= Параметры.Отбор.Контрагент;
	
	Запрос.УстановитьПараметр("Организация",	ОтборОрганизация);
	Запрос.УстановитьПараметр("Контрагент",		ОтборКонтрагент);
	Запрос.УстановитьПараметр("ТипыОснований",	ТипыОснований);
	Запрос.УстановитьПараметр("Выданный",		ЭтоВыданныйСчетФактура);
	Запрос.УстановитьПараметр("ТекущийСчетФактура",	Параметры.ЗначенияЗаполнения.СчетФактура);
	
	Запрос.Текст = ТекстЗапросаСтрокиОснований();
	
	СтрокиОснований.Загрузить(Запрос.Выполнить().Выгрузить());
	
	ТекущийДокумент = Параметры.Документ;
	ТекущийДоговор 	= Параметры.Договор;
	
КонецПроцедуры

&НаСервере
Функция ТекстЗапросаСтрокиОснований()
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ДокументыБезСчетаФактуры.ВидДокумента,
	|	ДокументыБезСчетаФактуры.Дата КАК Дата,
	|	ДокументыБезСчетаФактуры.Номер КАК Номер,
	|	ДокументыБезСчетаФактуры.Организация,
	|	ДокументыБезСчетаФактуры.Контрагент,
	|	ДокументыБезСчетаФактуры.СуммаДокумента,
	|	ДокументыБезСчетаФактуры.ДоговорКонтрагента,
	|	ДокументыБезСчетаФактуры.ВалютаДокумента,
	|	ДокументыБезСчетаФактуры.Документ КАК Документ,
	|	ВЫБОР
	|		КОГДА ДокументыБезСчетаФактуры.Документ.РучнаяКорректировка = ИСТИНА
	|			ТОГДА ВЫБОР
	|					КОГДА ДокументыБезСчетаФактуры.Документ.ПометкаУдаления = ИСТИНА
	|						ТОГДА 10
	|					КОГДА ДокументыБезСчетаФактуры.Документ.Проведен = ЛОЖЬ
	|						ТОГДА 9
	|					ИНАЧЕ 8
	|				КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ДокументыБезСчетаФактуры.Документ.ПометкаУдаления = ИСТИНА
	|					ТОГДА 2
	|				КОГДА ДокументыБезСчетаФактуры.Документ.Проведен = ИСТИНА
	|					ТОГДА 1
	|				ИНАЧЕ 0
	|			КОНЕЦ
	|	КОНЕЦ КАК СостояниеДокумента
	|ИЗ
	|	(ВЫБРАТЬ
	|		Документы.ВидДокумента КАК ВидДокумента,
	|		СчетаФактуры.Ссылка КАК СчетФактура,
	|		Документы.Документ КАК Документ,
	|		Документы.Дата КАК Дата,
	|		Документы.Номер КАК Номер,
	|		Документы.Организация КАК Организация,
	|		Документы.Контрагент КАК Контрагент,
	|		Документы.СуммаДокумента КАК СуммаДокумента,
	|		Документы.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|		Документы.ВалютаДокумента КАК ВалютаДокумента
	|	ИЗ
	|		(ВЫБРАТЬ
	|			ПриходныйКассовыйОрдерРасшифровкаПлатежа.Ссылка КАК Документ,
	|			МАКСИМУМ(""Поступление наличных"") КАК ВидДокумента,
	|			ПриходныйКассовыйОрдерРасшифровкаПлатежа.Ссылка.Дата КАК Дата,
	|			ПриходныйКассовыйОрдерРасшифровкаПлатежа.Ссылка.Номер КАК Номер,
	|			ПриходныйКассовыйОрдерРасшифровкаПлатежа.Ссылка.Организация КАК Организация,
	|			ПриходныйКассовыйОрдерРасшифровкаПлатежа.Ссылка.Контрагент КАК Контрагент,
	|			СУММА(ПриходныйКассовыйОрдерРасшифровкаПлатежа.СуммаПлатежа) КАК СуммаДокумента,
	|			ПриходныйКассовыйОрдерРасшифровкаПлатежа.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|			ПриходныйКассовыйОрдерРасшифровкаПлатежа.Ссылка.ВалютаДокумента КАК ВалютаДокумента
	|		ИЗ
	|			Документ.ПриходныйКассовыйОрдер.РасшифровкаПлатежа КАК ПриходныйКассовыйОрдерРасшифровкаПлатежа
	|		
	|		СГРУППИРОВАТЬ ПО
	|			ПриходныйКассовыйОрдерРасшифровкаПлатежа.Ссылка,
	|			ПриходныйКассовыйОрдерРасшифровкаПлатежа.ДоговорКонтрагента,
	|			ПриходныйКассовыйОрдерРасшифровкаПлатежа.Ссылка.Дата,
	|			ПриходныйКассовыйОрдерРасшифровкаПлатежа.Ссылка.Номер,
	|			ПриходныйКассовыйОрдерРасшифровкаПлатежа.Ссылка.Организация,
	|			ПриходныйКассовыйОрдерРасшифровкаПлатежа.Ссылка.Контрагент,
	|			ПриходныйКассовыйОрдерРасшифровкаПлатежа.Ссылка.ВалютаДокумента
	|		
	|		ОБЪЕДИНИТЬ ВСЕ
	|		
	|		ВЫБРАТЬ
	|			РасходныйКассовыйОрдерРасшифровкаПлатежа.Ссылка,
	|			МАКСИМУМ(""Выдача наличных""),
	|			РасходныйКассовыйОрдерРасшифровкаПлатежа.Ссылка.Дата,
	|			РасходныйКассовыйОрдерРасшифровкаПлатежа.Ссылка.Номер,
	|			РасходныйКассовыйОрдерРасшифровкаПлатежа.Ссылка.Организация,
	|			РасходныйКассовыйОрдерРасшифровкаПлатежа.Ссылка.Контрагент,
	|			СУММА(РасходныйКассовыйОрдерРасшифровкаПлатежа.СуммаПлатежа),
	|			РасходныйКассовыйОрдерРасшифровкаПлатежа.ДоговорКонтрагента,
	|			РасходныйКассовыйОрдерРасшифровкаПлатежа.Ссылка.ВалютаДокумента
	|		ИЗ
	|			Документ.РасходныйКассовыйОрдер.РасшифровкаПлатежа КАК РасходныйКассовыйОрдерРасшифровкаПлатежа
	|		
	|		СГРУППИРОВАТЬ ПО
	|			РасходныйКассовыйОрдерРасшифровкаПлатежа.Ссылка,
	|			РасходныйКассовыйОрдерРасшифровкаПлатежа.ДоговорКонтрагента,
	|			РасходныйКассовыйОрдерРасшифровкаПлатежа.Ссылка.Дата,
	|			РасходныйКассовыйОрдерРасшифровкаПлатежа.Ссылка.Номер,
	|			РасходныйКассовыйОрдерРасшифровкаПлатежа.Ссылка.Организация,
	|			РасходныйКассовыйОрдерРасшифровкаПлатежа.Ссылка.Контрагент,
	|			РасходныйКассовыйОрдерРасшифровкаПлатежа.Ссылка.ВалютаДокумента
	|		
	|		ОБЪЕДИНИТЬ ВСЕ
	|		
	|		ВЫБРАТЬ
	|			ПоступлениеНаРасчетныйСчетРасшифровкаПлатежа.Ссылка,
	|			""Поступление на расчетный счет"",
	|			ПоступлениеНаРасчетныйСчетРасшифровкаПлатежа.Ссылка.Дата,
	|			ПоступлениеНаРасчетныйСчетРасшифровкаПлатежа.Ссылка.Номер,
	|			ПоступлениеНаРасчетныйСчетРасшифровкаПлатежа.Ссылка.Организация,
	|			ПоступлениеНаРасчетныйСчетРасшифровкаПлатежа.Ссылка.Контрагент,
	|			ПоступлениеНаРасчетныйСчетРасшифровкаПлатежа.СуммаПлатежа,
	|			ПоступлениеНаРасчетныйСчетРасшифровкаПлатежа.ДоговорКонтрагента,
	|			ПоступлениеНаРасчетныйСчетРасшифровкаПлатежа.Ссылка.ВалютаДокумента
	|		ИЗ
	|			Документ.ПоступлениеНаРасчетныйСчет.РасшифровкаПлатежа КАК ПоступлениеНаРасчетныйСчетРасшифровкаПлатежа
	|		
	|		ОБЪЕДИНИТЬ ВСЕ
	|		
	|		ВЫБРАТЬ
	|			СписаниеСРасчетногоСчетаРасшифровкаПлатежа.Ссылка,
	|			""Списание с расчетного счета"",
	|			СписаниеСРасчетногоСчетаРасшифровкаПлатежа.Ссылка.Дата,
	|			СписаниеСРасчетногоСчетаРасшифровкаПлатежа.Ссылка.Номер,
	|			СписаниеСРасчетногоСчетаРасшифровкаПлатежа.Ссылка.Организация,
	|			СписаниеСРасчетногоСчетаРасшифровкаПлатежа.Ссылка.Контрагент,
	|			СписаниеСРасчетногоСчетаРасшифровкаПлатежа.СуммаПлатежа,
	|			СписаниеСРасчетногоСчетаРасшифровкаПлатежа.ДоговорКонтрагента,
	|			СписаниеСРасчетногоСчетаРасшифровкаПлатежа.Ссылка.ВалютаДокумента
	|		ИЗ
	|			Документ.СписаниеСРасчетногоСчета.РасшифровкаПлатежа КАК СписаниеСРасчетногоСчетаРасшифровкаПлатежа
	|
	|		ОБЪЕДИНИТЬ ВСЕ
	|		
	|		ВЫБРАТЬ
	|			ОплатаПлатежнойКартойРасшифровкаПлатежа.Ссылка,
	|			""Оплата платежной картой"",
	|			ОплатаПлатежнойКартойРасшифровкаПлатежа.Ссылка.Дата,
	|			ОплатаПлатежнойКартойРасшифровкаПлатежа.Ссылка.Номер,
	|			ОплатаПлатежнойКартойРасшифровкаПлатежа.Ссылка.Организация,
	|			ОплатаПлатежнойКартойРасшифровкаПлатежа.Ссылка.Контрагент,
	|			ОплатаПлатежнойКартойРасшифровкаПлатежа.СуммаПлатежа,
	|			ОплатаПлатежнойКартойРасшифровкаПлатежа.ДоговорКонтрагента,
	|			ОплатаПлатежнойКартойРасшифровкаПлатежа.Ссылка.ВалютаДокумента
	|		ИЗ
	|			Документ.ОплатаПлатежнойКартой.РасшифровкаПлатежа КАК ОплатаПлатежнойКартойРасшифровкаПлатежа
	|		
	|		ОБЪЕДИНИТЬ ВСЕ
	|		
	|		ВЫБРАТЬ
	|			ДокументРасчетовСКонтрагентом.Ссылка,
	|			""Документ расчетов с контрагентами (ручной учет)"",
	|			ДокументРасчетовСКонтрагентом.Дата,
	|			ДокументРасчетовСКонтрагентом.Номер,
	|			ДокументРасчетовСКонтрагентом.Организация,
	|			ДокументРасчетовСКонтрагентом.Контрагент,
	|			ДокументРасчетовСКонтрагентом.СуммаДокумента,
	|			ДокументРасчетовСКонтрагентом.ДоговорКонтрагента,
	|			ДокументРасчетовСКонтрагентом.ВалютаДокумента
	|		ИЗ
	|			Документ.ДокументРасчетовСКонтрагентом КАК ДокументРасчетовСКонтрагентом
	|		
	|		ОБЪЕДИНИТЬ ВСЕ
	|		
	|		ВЫБРАТЬ
	|			ОтчетКомитентуОПродажах.Ссылка,
	|			""Отчет комитенту"",
	|			ОтчетКомитентуОПродажах.Дата,
	|			ОтчетКомитентуОПродажах.Номер,
	|			ОтчетКомитентуОПродажах.Организация,
	|			ОтчетКомитентуОПродажах.Контрагент,
	|			ОтчетКомитентуОПродажах.СуммаДокумента,
	|			ОтчетКомитентуОПродажах.ДоговорКонтрагента,
	|			ОтчетКомитентуОПродажах.ВалютаДокумента
	|		ИЗ
	|			Документ.ОтчетКомитентуОПродажах КАК ОтчетКомитентуОПродажах
	|		
	|		ОБЪЕДИНИТЬ ВСЕ
	|		
	|		ВЫБРАТЬ
	|			КорректировкаПоступления.Ссылка,
	|			""Корректировка поступления"",
	|			КорректировкаПоступления.Дата,
	|			КорректировкаПоступления.Номер,
	|			КорректировкаПоступления.Организация,
	|			КорректировкаПоступления.Контрагент,
	|			КорректировкаПоступления.СуммаДокумента,
	|			КорректировкаПоступления.ДоговорКонтрагента,
	|			КорректировкаПоступления.ВалютаДокумента
	|		ИЗ
	|			Документ.КорректировкаПоступления КАК КорректировкаПоступления
	|		
	|		ОБЪЕДИНИТЬ ВСЕ
	|		
	|		ВЫБРАТЬ
	|			ВозвратТоваровОтПокупателя.Ссылка,
	|			""Возврат товаров от покупателя"",
	|			ВозвратТоваровОтПокупателя.Дата,
	|			ВозвратТоваровОтПокупателя.Номер,
	|			ВозвратТоваровОтПокупателя.Организация,
	|			ВозвратТоваровОтПокупателя.Контрагент,
	|			ВозвратТоваровОтПокупателя.СуммаДокумента,
	|			ВозвратТоваровОтПокупателя.ДоговорКонтрагента,
	|			ВозвратТоваровОтПокупателя.ВалютаДокумента
	|		ИЗ
	|			Документ.ВозвратТоваровОтПокупателя КАК ВозвратТоваровОтПокупателя
	|		
	|		ОБЪЕДИНИТЬ ВСЕ
	|		
	|		ВЫБРАТЬ
	|			АвансовыйОтчетОплатаПоставщикам.Ссылка,
	|			МАКСИМУМ(""Авансовый отчет""),
	|			АвансовыйОтчетОплатаПоставщикам.Ссылка.Дата,
	|			АвансовыйОтчетОплатаПоставщикам.Ссылка.Номер,
	|			АвансовыйОтчетОплатаПоставщикам.Ссылка.Организация,
	|			АвансовыйОтчетОплатаПоставщикам.Контрагент,
	|			СУММА(АвансовыйОтчетОплатаПоставщикам.Сумма),
	|			АвансовыйОтчетОплатаПоставщикам.ДоговорКонтрагента,
	|			АвансовыйОтчетОплатаПоставщикам.Ссылка.ВалютаДокумента
	|		ИЗ
	|			Документ.АвансовыйОтчет.ОплатаПоставщикам КАК АвансовыйОтчетОплатаПоставщикам
	|		
	|		СГРУППИРОВАТЬ ПО
	|			АвансовыйОтчетОплатаПоставщикам.Контрагент,
	|			АвансовыйОтчетОплатаПоставщикам.ДоговорКонтрагента,
	|			АвансовыйОтчетОплатаПоставщикам.Ссылка,
	|			АвансовыйОтчетОплатаПоставщикам.Ссылка.Дата,
	|			АвансовыйОтчетОплатаПоставщикам.Ссылка.Номер,
	|			АвансовыйОтчетОплатаПоставщикам.Ссылка.Организация,
	|			АвансовыйОтчетОплатаПоставщикам.Ссылка.ВалютаДокумента
	|		
	|		ОБЪЕДИНИТЬ ВСЕ
	|		
	|		ВЫБРАТЬ
	|			КорректировкаДолгаДебиторскаяЗадолженность.Ссылка,
	|			""Корректировка долга"",
	|			КорректировкаДолгаДебиторскаяЗадолженность.Ссылка.Дата,
	|			КорректировкаДолгаДебиторскаяЗадолженность.Ссылка.Номер,
	|			КорректировкаДолгаДебиторскаяЗадолженность.Ссылка.Организация,
	|			КорректировкаДолгаДебиторскаяЗадолженность.Ссылка.КонтрагентДебитор,
	|			КорректировкаДолгаДебиторскаяЗадолженность.Сумма,
	|			КорректировкаДолгаДебиторскаяЗадолженность.ДоговорКонтрагента,
	|			КорректировкаДолгаДебиторскаяЗадолженность.Ссылка.ВалютаДокумента
	|		ИЗ
	|			Документ.КорректировкаДолга.ДебиторскаяЗадолженность КАК КорректировкаДолгаДебиторскаяЗадолженность
	|		ГДЕ
	|			НЕ &Выданный
	|		
	|		ОБЪЕДИНИТЬ ВСЕ
	|		
	|		ВЫБРАТЬ
	|			КорректировкаДолгаКредиторскаяЗадолженность.Ссылка,
	|			""Корректировка долга"",
	|			КорректировкаДолгаКредиторскаяЗадолженность.Ссылка.Дата,
	|			КорректировкаДолгаКредиторскаяЗадолженность.Ссылка.Номер,
	|			КорректировкаДолгаКредиторскаяЗадолженность.Ссылка.Организация,
	|			КорректировкаДолгаКредиторскаяЗадолженность.Ссылка.КонтрагентКредитор,
	|			КорректировкаДолгаКредиторскаяЗадолженность.Сумма,
	|			ВЫБОР
	|				КОГДА КорректировкаДолгаКредиторскаяЗадолженность.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКорректировкаДолга.ПереносЗадолженности)
	|					ТОГДА КорректировкаДолгаКредиторскаяЗадолженность.КорДоговорКонтрагента
	|				ИНАЧЕ КорректировкаДолгаКредиторскаяЗадолженность.ДоговорКонтрагента
	|			КОНЕЦ,
	|			КорректировкаДолгаКредиторскаяЗадолженность.Ссылка.ВалютаДокумента
	|		ИЗ
	|			Документ.КорректировкаДолга.КредиторскаяЗадолженность КАК КорректировкаДолгаКредиторскаяЗадолженность
	|		ГДЕ
	|			&Выданный
	|		
	|		ОБЪЕДИНИТЬ ВСЕ
	|		
	|		ВЫБРАТЬ
	|			КорректировкаРеализации.Ссылка,
	|			""Корректировка реализации"",
	|			КорректировкаРеализации.Дата,
	|			КорректировкаРеализации.Номер,
	|			КорректировкаРеализации.Организация,
	|			КорректировкаРеализации.Контрагент,
	|			КорректировкаРеализации.СуммаДокумента,
	|			КорректировкаРеализации.ДоговорКонтрагента,
	|			КорректировкаРеализации.ВалютаДокумента
	|		ИЗ
	|			Документ.КорректировкаРеализации КАК КорректировкаРеализации
	|		
	|		ОБЪЕДИНИТЬ ВСЕ
	|		
	|		ВЫБРАТЬ
	|			ОтчетКомиссионераОПродажах.Ссылка,
	|			""Отчет комиссионера (агента) о продажах"",
	|			ОтчетКомиссионераОПродажах.Дата,
	|			ОтчетКомиссионераОПродажах.Номер,
	|			ОтчетКомиссионераОПродажах.Организация,
	|			ОтчетКомиссионераОПродажах.Контрагент,
	|			ОтчетКомиссионераОПродажах.СуммаДокумента,
	|			ОтчетКомиссионераОПродажах.ДоговорКонтрагента,
	|			ОтчетКомиссионераОПродажах.ВалютаДокумента
	|		ИЗ
	|			Документ.ОтчетКомиссионераОПродажах КАК ОтчетКомиссионераОПродажах) КАК Документы
	|			ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|				СчетФактураВыданный.Ссылка КАК Ссылка,
	|				СчетФактураВыданный.Организация КАК Организация,
	|				СчетФактураВыданный.Контрагент КАК Контрагент,
	|				СчетФактураВыданный.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|				СчетФактураВыданный.ВалютаДокумента КАК ВалютаДокумента,
	|				СчетФактураВыданный.ДокументОснование КАК ДокументОснование
	|			ИЗ
	|				Документ.СчетФактураВыданный КАК СчетФактураВыданный
	|			ГДЕ
	|				СчетФактураВыданный.ПометкаУдаления = ЛОЖЬ
	|				И &Выданный
	|			
	|			ОБЪЕДИНИТЬ ВСЕ
	|			
	|			ВЫБРАТЬ
	|				СчетФактураПолученный.Ссылка,
	|				СчетФактураПолученный.Организация,
	|				СчетФактураПолученный.Контрагент,
	|				СчетФактураПолученный.ДоговорКонтрагента,
	|				СчетФактураПолученный.ВалютаДокумента,
	|				СчетФактураПолученный.ДокументОснование
	|			ИЗ
	|				Документ.СчетФактураПолученный КАК СчетФактураПолученный
	|			ГДЕ
	|				СчетФактураПолученный.ПометкаУдаления = ЛОЖЬ
	|				И НЕ &Выданный) КАК СчетаФактуры
	|			ПО Документы.Документ = СчетаФактуры.ДокументОснование
	|				И Документы.Контрагент = СчетаФактуры.Контрагент
	|				И Документы.ДоговорКонтрагента = СчетаФактуры.ДоговорКонтрагента
	|				И Документы.Документ = СчетаФактуры.ДокументОснование
	|	ГДЕ
	|		Документы.Организация = &Организация
	|		И Документы.Контрагент = &Контрагент) КАК ДокументыБезСчетаФактуры
	|ГДЕ
	|	(ТИПЗНАЧЕНИЯ(ДокументыБезСчетаФактуры.Документ) В (&ТипыОснований)
	|				И ДокументыБезСчетаФактуры.СчетФактура ЕСТЬ NULL 
	|			ИЛИ ДокументыБезСчетаФактуры.СчетФактура = &ТекущийСчетФактура)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата";
	
	Возврат ТекстЗапроса;
	
КонецФункции

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ОбработкаВыбораЗначения();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Отбор = Новый Структура();
	Отбор.Вставить("Документ", ТекущийДокумент);
	Отбор.Вставить("ДоговорКонтрагента", ТекущийДоговор);
	
	Строки = СтрокиОснований.НайтиСтроки(Отбор);
	
	Если Строки.Количество() <> 0  Тогда
		Элементы.Список.ТекущаяСтрока = Строки[0].ПолучитьИдентификатор();
	КонецЕсли;
	
КонецПроцедуры




