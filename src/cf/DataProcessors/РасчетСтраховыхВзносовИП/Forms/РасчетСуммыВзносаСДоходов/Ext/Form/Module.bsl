#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИмяВзноса = Параметры.ИмяВзноса;
	Всего     = Параметры.Всего;
	Уплачено  = Параметры.Уплачено;
	Сумма     = Параметры.Сумма;
	
	Период           = Параметры.Период;
	Организация      = Параметры.Организация;
	СтруктураДоходов = Параметры.СтруктураДоходов;
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры.СтруктураДоходов);
	ПредельнаяСуммаВзноса = Параметры.ПредельнаяСуммаВзноса;
	
	Заголовок = Параметры.Заголовок;
	
	УправлениеФормойНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	Если Модифицированность Тогда
		Отказ = Истина;
		ТекстВопроса = НСтр("ru = 'Данные были изменены. Сохранить изменения?'");
		Оповещение = Новый ОписаниеОповещения("ВопросПередЗакрытиемЗавершение", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросПередЗакрытиемЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ОповеститьОВыбореИЗакрыть();
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		Модифицированность = Ложь;
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СуммаПриИзменении(Элемент)
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КнигаУчетаДоходовРасходовИП(Команда)
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("РежимРасшифровки", Истина);
	СтруктураПараметров.Вставить("Организация",      Организация);
	СтруктураПараметров.Вставить("НачалоПериода",    НачалоГода(Период));
	СтруктураПараметров.Вставить("КонецПериода",     КонецКвартала(Период));
	
	ОткрытьФорму("Отчет.КнигаУчетаДоходовИРасходовПредпринимателя.Форма", СтруктураПараметров, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура КнигаУчетаДоходовРасходовУСН(Команда)
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("РежимРасшифровки", Истина);
	СтруктураПараметров.Вставить("Организация",      Организация);
	СтруктураПараметров.Вставить("НачалоПериода",    НачалоГода(Период));
	СтруктураПараметров.Вставить("КонецПериода",     КонецКвартала(Период));
	
	ОткрытьФорму("Отчет.КнигаУчетаДоходовИРасходов.Форма", СтруктураПараметров, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекларацииЕНВД(Команда)
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("Организация",   Организация);
	СтруктураПараметров.Вставить("НачалоПериода", НачалоГода(Период));
	СтруктураПараметров.Вставить("КонецПериода",  КонецГода(Период));
	СтруктураПараметров.Вставить("ОтчетнаяФормаПредставление", "ЕНВД");
	
	УстановитьНастройкиФормыСпискаРегламентированныхОтчетов(СтруктураПараметров);
	
	ОткрытьФорму("Документ.РегламентированныйОтчет.ФормаСписка", , ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура Патенты(Команда)
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("Отбор", Новый Структура("Владелец", Организация));
	СтруктураПараметров.Вставить("Период", Период);
	
	ОткрытьФорму("Справочник.Патенты.ФормаСписка", СтруктураПараметров, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура Обновить(Команда)
	
	ЗаполнитьСтраховыеВзносыНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ОК(Команда)
	
	ОповеститьОВыбореИЗакрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УправлениеФормойНаСервере()
	
	ВсегоДоходов = СуммаДоходаИП + СуммаДоходаУСН + ВмененныйДоход + ПотенциальноВозможныйДоход;
	
	ДанныеДляРасчетаСтраховыхВзносовСДоходов = УчетСтраховыхВзносовИП.ДанныеДляРасчетаСтраховыхВзносовСДоходов(Организация, Период);
	
	ДекорацияФормулаРасчета = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("=  ( %1 - %2 ) x %3",
		Формат(ВсегоДоходов, "Л=ru; ЧЦ=15; ЧДЦ=2; ЧН=0,00"),
		Формат(ДанныеДляРасчетаСтраховыхВзносовСДоходов.ПорогДоходов, "Л=ru; ЧЦ=8; ЧДЦ=2; ЧН=0,00"),
		Формат(ДанныеДляРасчетаСтраховыхВзносовСДоходов.ПроцентВзносов, "ЧЦ=1; ЧДЦ=; ЧН=0")+ "%");
	
	Элементы.ДекорацияФормулаРасчета.Ширина   = СтрДлина(ДекорацияФормулаРасчета);
	
	// Суммы доходов по видам деятельности
	
	НачалоПериода = НачалоГода(Период);
	КонецПериода  = КонецГода(Период);
	
	Если УчетнаяПолитика.Существует(Организация, КонецПериода) Тогда
		ПлательщикНДФЛ       = УчетнаяПолитика.ПлательщикНДФЛЗаПериод(Организация, НачалоПериода, КонецПериода);
		ПрименяетсяУСН       = УчетнаяПолитика.ПрименяетсяУСНЗаПериод(Организация, НачалоПериода, КонецПериода);
		ПрименяетсяУСНПатент = УчетнаяПолитика.ПрименяетсяУСНПатентЗаПериод(Организация, НачалоПериода, КонецПериода);
		ПлательщикЕНВД       = УчетнаяПолитика.ПлательщикЕНВДЗаПериод(Организация, НачалоПериода, КонецПериода);
	Иначе
		День = 24 * 60 * 60;
		ПлательщикНДФЛ       = УчетнаяПолитика.ПлательщикНДФЛ(Организация, КонецПериода + День);
		ПрименяетсяУСН       = УчетнаяПолитика.ПрименяетсяУСН(Организация, КонецПериода + День);
		ПрименяетсяУСНПатент = УчетнаяПолитика.ПрименяетсяУСНПатент(Организация, КонецПериода + День);
		ПлательщикЕНВД       = УчетнаяПолитика.ПлательщикЕНВД(Организация, КонецПериода + День);
	КонецЕсли;
	
	Элементы.Всего.Подсказка = ?(ПредельнаяСуммаВзноса, НСтр("ru = ' (предельная сумма взноса)'"), "");
	Элементы.ДекорацияФормулаРасчета.Видимость = Не ПредельнаяСуммаВзноса;
	
	КоличествоВидовДеятельности = ПлательщикНДФЛ + ПрименяетсяУСН + ПлательщикЕНВД + ПрименяетсяУСНПатент;
	
	Если КоличествоВидовДеятельности > 0 Тогда
		
		Если КоличествоВидовДеятельности = 1 Тогда
			
			Элементы.ВсегоДоходов.Видимость       = Ложь;
			Элементы.ДекорацияВТомЧисле.Видимость = Ложь;
			
			Элементы.СуммаДоходаИП.Заголовок              = НСтр("ru = 'Сумма дохода'");
			Элементы.СуммаДоходаУСН.Заголовок             = НСтр("ru = 'Сумма дохода'");
			Элементы.ВмененныйДоход.Заголовок             = НСтр("ru = 'Вмененный доход'");
			Элементы.ПотенциальноВозможныйДоход.Заголовок = НСтр("ru = 'Потенциальный доход'");
			
			Элементы.СуммаДоходаИП.Ширина = 15;
			Элементы.СуммаДоходаУСН.Ширина = 15;
			Элементы.ПотенциальноВозможныйДоход.Ширина = 15;
			Элементы.ВмененныйДоход.Ширина = 15;
			
		Иначе
			
			Элементы.ВсегоДоходов.Видимость       = Истина;
			Элементы.ДекорацияВТомЧисле.Видимость = Истина;
			
			Элементы.СуммаДоходаИП.Заголовок              = НСтр("ru = 'ОСНО'");
			Элементы.СуммаДоходаУСН.Заголовок             = НСтр("ru = 'УСН'");
			Элементы.ПотенциальноВозможныйДоход.Заголовок = НСтр("ru = 'ПСН'");
			Элементы.ВмененныйДоход.Заголовок             = НСтр("ru = 'ЕНВД'");
			
			Элементы.СуммаДоходаИП.Ширина = 18;
			Элементы.СуммаДоходаУСН.Ширина = 18;
			Элементы.ПотенциальноВозможныйДоход.Ширина = 18;
			Элементы.ВмененныйДоход.Ширина = 18;
			
		КонецЕсли;
		
		Элементы.СуммаДоходаИП.Видимость               = ПлательщикНДФЛ;
		Элементы.КнигаУчетаДоходовРасходовИП.Видимость = ПлательщикНДФЛ;
		Элементы.КнигаУчетаДоходовРасходовИП.Заголовок = 
			НСтр("ru = 'Книга учета доходов и расходов'") + ?(ПрименяетсяУСН, НСтр("ru = ' (ИП)'"), "");
		
		Элементы.СуммаДоходаУСН.Видимость               = ПрименяетсяУСН;
		Элементы.КнигаУчетаДоходовРасходовУСН.Видимость = ПрименяетсяУСН;
		Элементы.КнигаУчетаДоходовРасходовУСН.Заголовок = 
			НСтр("ru = 'Книга учета доходов и расходов'") + ?(ПлательщикНДФЛ, НСтр("ru = ' (УСН)'"), "");
		
		Элементы.ВмененныйДоход.Видимость = ПлательщикЕНВД;
		Элементы.ДекларацииЕНВД.Видимость = ПлательщикЕНВД;
		
		Элементы.ПотенциальноВозможныйДоход.Видимость = ПрименяетсяУСНПатент;
		Элементы.Патенты.Видимость                    = ПрименяетсяУСНПатент;
		
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Форма.Остаток = Форма.Всего - Форма.Уплачено - Форма.Сумма;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСтраховыеВзносыНаСервере()
	
	Периодичность = УчетСтраховыхВзносовИП.ПериодичностьУплатыФиксированныхСтраховыхВзносов(Организация, Период);
	
	// Доходы по видам деятельности
	СтруктураДоходов = УчетСтраховыхВзносовИП.СтруктураДоходовПоВидамДеятельности(
		Организация, НачалоГода(Период), КонецКвартала(Период));
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, СтруктураДоходов);
	
	// Страховые взносы, исчисленные с суммы доходов
	СтраховыеВзносыСДоходовКУплате = УчетСтраховыхВзносовИП.СтраховыеВзносыСДоходовКУплате(
		Организация, Период, Периодичность, СтруктураДоходов, Ложь);
	
	ПредельнаяСуммаВзноса = СтраховыеВзносыСДоходовКУплате.ПредельнаяСуммаВзноса;
	Всего                 = СтраховыеВзносыСДоходовКУплате.СуммаВзносаПФРсДоходовВсего;
	Уплачено              = СтраховыеВзносыСДоходовКУплате.СуммаВзносаПФРсДоходовУплачено;
	Сумма                 = СтраховыеВзносыСДоходовКУплате.СуммаВзносаПФРсДоходов;
	
	УправлениеФормойНаСервере();
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура УстановитьНастройкиФормыСпискаРегламентированныхОтчетов(Параметры)
	
	НастройкиФильтров = ХранилищеНастроекДанныхФорм.Загрузить("Документ.РегламентированныйОтчет.Форма.ФормаСписка", "НастройкиФильтров");
	
	Если НастройкиФильтров = Неопределено Тогда
		НастройкиФильтров = Новый Структура;
	КонецЕсли;
	
	НастройкиФильтров.Вставить("Организация",   Параметры.Организация);
	НастройкиФильтров.Вставить("ДатаНач",       Параметры.НачалоПериода);
	НастройкиФильтров.Вставить("ДатаКон",       Параметры.КонецПериода);
	НастройкиФильтров.Вставить("Периодичность", "Произвольный");
	НастройкиФильтров.Вставить("НалоговыйОрганЗначение", "    ");
	
	ОтчетнаяФормаЗначение = Справочники.РегламентированныеОтчеты.НайтиПоНаименованию(Параметры.ОтчетнаяФормаПредставление, Истина);
	Если ЗначениеЗаполнено(ОтчетнаяФормаЗначение) Тогда
		НастройкиФильтров.Вставить("ОтчетнаяФормаПредставление", Параметры.ОтчетнаяФормаПредставление);
		НастройкиФильтров.Вставить("ОтчетнаяФормаЗначение", ОтчетнаяФормаЗначение);
	КонецЕсли;
	
	ХранилищеНастроекДанныхФорм.Сохранить("Документ.РегламентированныйОтчет.Форма.ФормаСписка", "НастройкиФильтров", НастройкиФильтров);
	
КонецПроцедуры

&НаКлиенте
Процедура ОповеститьОВыбореИЗакрыть()
	
	ЗначениеВыбора = Новый Структура;
	ЗначениеВыбора.Вставить(ИмяВзноса, Сумма);
	ЗначениеВыбора.Вставить("СтруктураДоходов", СтруктураДоходов);
	
	Модифицированность = Ложь;
	ОповеститьОВыборе(Новый Структура(ИмяВзноса, Сумма));
	
КонецПроцедуры

#КонецОбласти