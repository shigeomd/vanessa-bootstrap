#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("ВариантЗагрузкиЭД") Тогда
		Объект.ВариантЗагрузкиЭД = Параметры.ВариантЗагрузкиЭД;
	Иначе
		Отказ = Истина;
		ТекстПредупреждения = НСтр("ru='Обработка не предназначена для непосредственного использования.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстПредупреждения);
	КонецЕсли;
	
	Объект.ГлубинаПоиска = Параметры.ГлубинаПоиска;
	СсылкаКонтрагента    = Параметры.СсылкаКонтрагента;
	
	Если Объект.ВариантЗагрузкиЭД = Перечисления.ВариантыЗагрузкиЭД.СчетНаОплату Тогда
		Заголовок = Нстр("ru='Загрузка реквизитов из счета на оплату'");
	ИначеЕсли Объект.ВариантЗагрузкиЭД = Перечисления.ВариантыЗагрузкиЭД.РеализацияТоваровИУслуг Тогда
		Заголовок = Нстр("ru='Загрузка реквизитов из реализации товаров и услуг'");
		//Элементы.Загрузить.КнопкаПоУмолчанию = Истина;
	Иначе
		Заголовок = Нстр("ru='Заполнение реквизитов контрагента из электронной почты'");
		//Элементы.Заполнить.КнопкаПоУмолчанию = Истина;
	КонецЕсли;
	
	СистемнаяУчетнаяЗапись = Справочники.УчетныеЗаписиЭлектроннойПочты.СистемнаяУчетнаяЗаписьЭлектроннойПочты;
	
	Если УчетнаяЗаписьНастроенаНаПолучениеПочты(СистемнаяУчетнаяЗапись) Тогда
		УчетнаяЗапись = СистемнаяУчетнаяЗапись;
	КонецЕсли;
	
	Если Объект.ВариантЗагрузкиЭД <> Перечисления.ВариантыЗагрузкиЭД.Карточка Тогда
		СтраницаСДанными = "СтраницаДанныеЭлектронныхДокументов";
	Иначе
		СтраницаСДанными = "СтраницаДанныеКарточек";
	КонецЕсли;
	
	Параметры.Свойство("ИдентификаторФормыВладельца", ИдентификаторФормыВладельца);
	
КонецПроцедуры

&НаСервере
Процедура ПередЗагрузкойДанныхИзНастроекНаСервере(Настройки)
	
	СистемнаяУчетнаяЗапись = Справочники.УчетныеЗаписиЭлектроннойПочты.СистемнаяУчетнаяЗаписьЭлектроннойПочты;
	
	Если НЕ ЗначениеЗаполнено(Настройки.Получить("УчетнаяЗапись")) И
			УчетнаяЗаписьНастроенаНаПолучениеПочты(СистемнаяУчетнаяЗапись) Тогда
		Настройки.Вставить("УчетнаяЗапись", СистемнаяУчетнаяЗапись);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	ПрочитатьПисьмаНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если НЕ ЗначениеЗаполнено(ИдентификаторФоновогоЗадания) Тогда
		ПрочитатьПисьмаНаСервере();
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ИдентификаторФоновогоЗадания) Тогда
		Если Объект.ВариантЗагрузкиЭД = ПредопределенноеЗначение("Перечисление.ВариантыЗагрузкиЭД.СчетНаОплату") Тогда
			КлючеваяОперация = "ЗагрузкаСчетаНаОплатуИзЭлектроннойПочты";
		ИначеЕсли Объект.ВариантЗагрузкиЭД = ПредопределенноеЗначение("Перечисление.ВариантыЗагрузкиЭД.РеализацияТоваровИУслуг") Тогда
			КлючеваяОперация = "ЗагрузкаРеализацииТоваровИУслугИзЭлектроннойПочты";
		ИначеЕсли Объект.ВариантЗагрузкиЭД = ПредопределенноеЗначение("Перечисление.ВариантыЗагрузкиЭД.Карточка") Тогда
			КлючеваяОперация = "ЗагрузкаКарточкиКонтрагентаИзЭлектроннойПочты";
		КонецЕсли;
		
		ОценкаПроизводительностиКлиентСервер.НачатьЗамерВремени(КлючеваяОперация);
		
		ПодключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеЗагрузкиДанных", 1, Истина);
	Иначе
		ТекущаяСтраница = СтраницаСДанными;
		УправлениеФормой(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура УчетнаяЗаписьПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(УчетнаяЗапись) Тогда
		ПрочитатьПисьма();
	Иначе
		Объект.ДанныеКарточек.Очистить();
		Объект.ДанныеЭлектронныхДокументов.Очистить();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ГлубинаПоискаПриИзменении(Элемент)
	
	ПрочитатьПисьма();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДанныеКарточек

&НаКлиенте
Процедура ДанныеКарточекВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	СтруктураРеквизитов = Элемент.ТекущиеДанные.СтруктураРеквизитов;
	ОбработатьВыборЭлектронногоДокумента(СтруктураРеквизитов);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДанныеЭлектронныхДокументов

&НаКлиенте
Процедура ДанныеЭлектронныхДокументовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Поле.Имя = "ДанныеЭлектронныхДокументовДокумент" Тогда
		ПоказатьТабличныйДокумент();
		Возврат;
	КонецЕсли;
	
	СтруктураРеквизитов = Элемент.ТекущиеДанные.СтруктураРеквизитов;
	ОбработатьВыборЭлектронногоДокумента(СтруктураРеквизитов);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Заполнить(Команда)
	
	Если НЕ ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ТекущийЭлемент) = Тип("ТаблицаФормы") Тогда
		Если ТекущийЭлемент.Имя = "ДанныеКарточек" Тогда
			СтруктураРеквизитов = Элементы.ДанныеКарточек.ТекущиеДанные.СтруктураРеквизитов;
			ОбработатьВыборЭлектронногоДокумента(СтруктураРеквизитов);
		ИначеЕсли ТекущийЭлемент.Имя = "ДанныеЭлектронныхДокументов" Тогда
			СтруктураРеквизитов = Элементы.ДанныеЭлектронныхДокументов.ТекущиеДанные.СтруктураРеквизитов;
			ОбработатьВыборЭлектронногоДокумента(СтруктураРеквизитов);
		КонецЕсли;
	Иначе
		СообщениеПользователю = НСтр("ru='Выберите документ для загрузки'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СообщениеПользователю);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузкаДанныхОтмена(Команда)
	
	ЗагрузкаДанныхОтменаНаСервере(ИдентификаторФоновогоЗадания);
	ТекущаяСтраница = СтраницаСДанными;
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПоказатьТабличныйДокумент()
		
	ТекущиеДанные = Элементы.ДанныеЭлектронныхДокументов.ТекущиеДанные;	
	Если ТекущиеДанные.РасширениеФайла = "xlsx" Тогда
				
		Если ОбщегоНазначенияКлиентСервер.ЭтоВебКлиент() Тогда
			
			ИмяВременногоФайла = ТекущиеДанные.Документ + "." + ТекущиеДанные.РасширениеФайла;
			ПолучитьФайл(ТекущиеДанные.ФайлТабличногоДокумента, ИмяВременногоФайла, Истина);			
			
		Иначе
			
			#Если Не ВебКлиент Тогда
			
				ИмяВременногоФайла = ПолучитьИмяВременногоФайла(ТекущиеДанные.РасширениеФайла);
				ПолучитьФайл(ТекущиеДанные.ФайлТабличногоДокумента, ИмяВременногоФайла, Ложь);							
				Если ТекущиеДанные.РасширениеФайла = "xlsx" Тогда
					
					ЗапуститьПриложение(ИмяВременногоФайла);
					
				КонецЕсли;
			
			#КонецЕсли
			
		КонецЕсли;
				
	Иначе
	
		ТабличныйДокумент = ПолучитьИзВременногоХранилища(ТекущиеДанные.ТабличныйДокумент);
		ТабличныйДокумент.Защита = Истина;
		ТабличныйДокумент.Показать(ТекущиеДанные.Документ);
	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьВыборЭлектронногоДокумента(СтруктураРеквизитовЭД)
	
	ЕстьОшибки = Ложь;
	
	Если Объект.ВариантЗагрузкиЭД = ПредопределенноеЗначение("Перечисление.ВариантыЗагрузкиЭД.Карточка") Тогда
		Закрыть(СтруктураРеквизитовЭД);
	ИначеЕсли Объект.ВариантЗагрузкиЭД = ПредопределенноеЗначение("Перечисление.ВариантыЗагрузкиЭД.СчетНаОплату") Тогда
		ПодготовитьРеквизитыСчета(СтруктураРеквизитовЭД, ЕстьОшибки);
		Если НЕ ЕстьОшибки Тогда
			Закрыть(СтруктураРеквизитовЭД);
		КонецЕсли;
	ИначеЕсли Объект.ВариантЗагрузкиЭД = ПредопределенноеЗначение("Перечисление.ВариантыЗагрузкиЭД.РеализацияТоваровИУслуг") Тогда
		
		ТекущиеДанные = Элементы.ДанныеЭлектронныхДокументов.ТекущиеДанные;
		Если ЗначениеЗаполнено(ТекущиеДанные.ТекстОшибки) Тогда
			
			Сообщить(ТекущиеДанные.ТекстОшибки);
			
			Возврат;
			
		КонецЕсли;		
		ИдентификаторСтроки = Элементы.ДанныеЭлектронныхДокументов.ТекущаяСтрока;		
		СтруктураЭД = ПодготовитьСтруктуруЭД(ИдентификаторСтроки);		
		Закрыть(СтруктураЭД);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПодготовитьСтруктуруЭД(ИдентификаторСтроки)
	
	ТекущиеДанные = Объект.ДанныеЭлектронныхДокументов.НайтиПоИдентификатору(ИдентификаторСтроки);
	ДанныеДокумента = ПолучитьИзВременногоХранилища(ТекущиеДанные.СтруктураРеквизитов);
	ИдентификаторФормы = ?(ЗначениеЗаполнено(ИдентификаторФормыВладельца), ИдентификаторФормыВладельца, УникальныйИдентификатор);
	СтруктураЭД = ЭлектронныеДокументыБП.ПолучитьСтруктуруЭД(ДанныеДокумента, ИдентификаторФормы, ТекущиеДанные.ФайлДанных, ТекущиеДанные.Наименование);
	
	// ищем данные счета-фактуры
	Отбор = Новый Структура("УИД", ТекущиеДанные.СчетФактураУИД);
	НайденныеСтроки = Объект.ДанныеЭлектронныхДокументов.НайтиСтроки(Отбор);
	Если НайденныеСтроки.Количество() <> 0 Тогда
		
		ДанныеСФ = НайденныеСтроки[0].СтруктураРеквизитов;
		СтруктураДанныхСФ = ПолучитьИзВременногоХранилища(ДанныеСФ);		
		Если ТипЗнч(СтруктураДанныхСФ) = Тип("Структура") Тогда
			
			СтруктураЭД.Вставить("ДанныеСФ", ПоместитьВоВременноеХранилище(СтруктураДанныхСФ, ИдентификаторФормы));
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если Не ПустаяСтрока(ТекущиеДанные.НомерСчФ) Тогда
		
		СтруктураЭД.Вставить("НомерСФ" , ТекущиеДанные.НомерСчФ);
		СтруктураЭД.Вставить("ДатаСФ" , ТекущиеДанные.ДатаСчФ);
		
	КонецЕсли;
	
	Возврат СтруктураЭД;
	
КонецФункции

&НаСервереБезКонтекста
Функция УчетнаяЗаписьНастроенаНаПолучениеПочты(Знач УчетнаяЗапись)
	
	Возврат ЗначениеЗаполнено(УчетнаяЗапись) И УчетнаяЗапись.ПолучитьОбъект() <> Неопределено 
		И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(УчетнаяЗапись, "ИспользоватьДляПолучения");
	
КонецФункции

&НаКлиенте
Процедура ПрочитатьПисьма()
	
	Если НЕ УчетнаяЗаписьНастроенаНаПолучениеПочты(УчетнаяЗапись) Тогда
		ТекстСообщения = НСтр("ru = 'Для работы требуется настройка учетной записи электронной почты на получение сообщений.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,
			УчетнаяЗапись,
			"ИспользоватьДляПолучения");
		Возврат;
	КонецЕсли;
	
	Объект.ДанныеКарточек.Очистить();
	Объект.ДанныеЭлектронныхДокументов.Очистить();
	
	ПрочитатьПисьмаНаСервере();
	
	ПодключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеЗагрузкиДанных", 1, Истина);
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьПисьмаНаСервере()
	
	Если НЕ УчетнаяЗаписьНастроенаНаПолучениеПочты(УчетнаяЗапись) Тогда
		Возврат;
	КонецЕсли;
	
	Если Объект.ВариантЗагрузкиЭД = ПредопределенноеЗначение("Перечисление.ВариантыЗагрузкиЭД.Карточка") Тогда
		ПрочитатьПисьмаСКарточками();
	Иначе
		ПрочитатьПисьмаСДокументами();
	КонецЕсли;
	
	ТекущаяСтраница = "СтраницаВыполняетсяЗагрузкаДанных";
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьПисьмаСКарточками()
	
	ИмяЗадания = НСтр("ru = 'Чтение карточек контрагентов'");
	
	НастройкиЧтенияПочты = Новый Структура;
	НастройкиЧтенияПочты.Вставить("ГлубинаПоиска", Объект.ГлубинаПоиска);
	НастройкиЧтенияПочты.Вставить("УчетнаяЗапись", УчетнаяЗапись);
	
	Результат = ДлительныеОперации.ЗапуститьВыполнениеВФоне(Новый УникальныйИдентификатор,
					"Обработки.ПрямойОбменЭД.ПрочитатьКарточкиИзЭлектроннойПочтыАсинхронно",
					НастройкиЧтенияПочты,
					ИмяЗадания);
	
	ИдентификаторФоновогоЗадания = Результат.ИдентификаторЗадания;
	АдресРезультата = Результат.АдресХранилища;
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьПисьмаСДокументами()
	
	ИмяЗадания = НСтр("ru='Чтение электронных документов'");
	
	ОтборСтрок = Новый Структура;
	ОтборСтрок.Вставить("Показывать", Истина);
	Элементы.ДанныеЭлектронныхДокументов.ОтборСтрок = Новый ФиксированнаяСтруктура(ОтборСтрок);
	
	НастройкиЧтенияПочты = Новый Структура;
	НастройкиЧтенияПочты.Вставить("ГлубинаПоиска"          , Объект.ГлубинаПоиска);
	НастройкиЧтенияПочты.Вставить("УчетнаяЗапись"          , УчетнаяЗапись);
	НастройкиЧтенияПочты.Вставить("ВариантзагрузкиЭД"      , Объект.ВариантЗагрузкиЭД);
	НастройкиЧтенияПочты.Вставить("УникальныйИдентификатор", УникальныйИдентификатор);
	
	Результат = ДлительныеОперации.ЗапуститьВыполнениеВФоне(Новый УникальныйИдентификатор,
					"Обработки.ПрямойОбменЭД.ПрочитатьДокументыИзЭлектроннойПочтыАсинхронно",
					НастройкиЧтенияПочты,
					ИмяЗадания);
	
	ИдентификаторФоновогоЗадания = Результат.ИдентификаторЗадания;
	АдресРезультата = Результат.АдресХранилища;
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьПрочитанныеДанные()
	
	ПрочитанныеДанные = ПолучитьИзВременногоХранилища(АдресРезультата);
	Если ТипЗнч(ПрочитанныеДанные) = Тип("ТаблицаЗначений") И ПрочитанныеДанные.Количество() > 0 Тогда
		Если Объект.ВариантЗагрузкиЭД = Перечисления.ВариантыЗагрузкиЭД.Карточка Тогда
			Для Каждого ЭлементДанных Из ПрочитанныеДанные Цикл
				СтрокаДанныхКарточки = Объект.ДанныеКарточек.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаДанныхКарточки, ЭлементДанных.ДанныеКарточки);
				СтрокаДанныхКарточки.СтруктураРеквизитов = ЭлементДанных.ДанныеКарточки;
			КонецЦикла;
			ПроверитьДублиИННКонтрагентов(Объект.ДанныеКарточек, СсылкаКонтрагента);
		ИначеЕсли Объект.ВариантЗагрузкиЭД = Перечисления.ВариантыЗагрузкиЭД.СчетНаОплату Тогда
			Для Каждого ЭлементДанных Из ПрочитанныеДанные Цикл
				СтрокаДанных = Объект.ДанныеЭлектронныхДокументов.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаДанных, ЭлементДанных);
				ПредставлениеДокумента = НСтр("ru='Счет %1 от %2'");
				ПредставлениеДокумента = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПредставлениеДокумента,
					ЭлементДанных.НомерСчета,
					Формат(ЭлементДанных.ДатаСчета, "ДЛФ=D"));
				СтрокаДанных.Документ = ПредставлениеДокумента;
				СтрокаДанных.ТабличныйДокумент = ПоместитьВоВременноеХранилище(ЭлементДанных.ТабличныйДокумент,
					УникальныйИдентификатор);
				СтрокаДанных.СтруктураРеквизитов = ЭлементДанных.ДанныеДокумента;
			КонецЦикла;
		ИначеЕсли Объект.ВариантЗагрузкиЭД = Перечисления.ВариантыЗагрузкиЭД.РеализацияТоваровИУслуг Тогда
			Для Каждого ЭлементДанных Из ПрочитанныеДанные Цикл				
				СтрокаДанных = Объект.ДанныеЭлектронныхДокументов.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаДанных, ЭлементДанных);
				СтрокаДанных.ТабличныйДокумент = ПоместитьВоВременноеХранилище(ЭлементДанных.ТабличныйДокумент,
					УникальныйИдентификатор);
				СтрокаДанных.СтруктураРеквизитов = ПоместитьВоВременноеХранилище(ЭлементДанных.ДанныеДокумента,
					УникальныйИдентификатор);
				СтрокаДанных.ФайлДанных = ПоместитьВоВременноеХранилище(ЭлементДанных.ФайлДанных,
					УникальныйИдентификатор);
				СтрокаДанных.ФайлТабличногоДокумента = ПоместитьВоВременноеХранилище(ЭлементДанных.ФайлТабличногоДокумента,
					УникальныйИдентификатор);	
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ПроверитьДублиИННКонтрагентов(ДанныеКарточек, СсылкаКонтрагента)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ИННКонтрагентов"  , ОбщегоНазначения.ВыгрузитьКолонку(ДанныеКарточек, "ИНН", Истина));
	Запрос.УстановитьПараметр("СсылкаКонтрагента", СсылкаКонтрагента);
	
	Запрос.Текст = "ВЫБРАТЬ
	|	Контрагенты.ИНН КАК ИННКонтрагентаВБазе
	|ИЗ
	|	Справочник.Контрагенты КАК Контрагенты
	|ГДЕ
	|	Контрагенты.ИНН В(&ИННКонтрагентов)
	|	И НЕ Контрагенты.Ссылка = &СсылкаКонтрагента";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	ДублиКонтрагентов = РезультатЗапроса.Выгрузить();
	
	Отбор = Новый Структура;
	
	Для Каждого Контрагент Из ДублиКонтрагентов Цикл
		Отбор.Вставить("ИНН", Контрагент.ИННКонтрагентаВБазе);
		КарточкиКонтрагентовСДублемПоИНН = ДанныеКарточек.НайтиСтроки(Отбор);
		Для Каждого КарточкаКонтрагентаСДублемПоИНН Из КарточкиКонтрагентовСДублемПоИНН Цикл
			КарточкаКонтрагентаСДублемПоИНН.ЕстьДубльПоИНН = Истина;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ПодготовитьРеквизитыСчета(СтруктураРеквизитов, ЕстьОшибки)
	
	Обработки.ПрямойОбменЭД.ДополнитьСтруктуруРеквизитовСчета(СтруктураРеквизитов, ЕстьОшибки);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗаданиеВыполнено(Знач ИдентификаторФоновогоЗадания)
	
	Возврат Обработки.ПрямойОбменЭД.ЗаданиеВыполнено(ИдентификаторФоновогоЗадания);
	
КонецФункции

&НаКлиенте
Процедура Подключаемый_ПроверитьВыполнениеЗагрузкиДанных()
	
	Если НЕ ЗначениеЗаполнено(ИдентификаторФоновогоЗадания) Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗаданиеВыполнено(ИдентификаторФоновогоЗадания) Тогда
		ТекущаяСтраница = СтраницаСДанными;
		ЗагрузитьПрочитанныеДанные();
		УправлениеФормой(ЭтотОбъект);
	Иначе
		ПодключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеЗагрузкиДанных", 1, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Объект   = Форма.Объект;
	Элементы = Форма.Элементы;
	
	Если Элементы.СтраницыФормы.ТекущаяСтраница.Имя <> Форма.ТекущаяСтраница Тогда
		Элементы.СтраницыФормы.ТекущаяСтраница = Элементы[Форма.ТекущаяСтраница];
	КонецЕсли;
	
	Если Объект.ВариантЗагрузкиЭД <> ПредопределенноеЗначение("Перечисление.ВариантыЗагрузкиЭД.Карточка") Тогда
		ИмяТабличнойЧасти = "ДанныеЭлектронныхДокументов";
		Элементы.Загрузить.КнопкаПоУмолчанию = Истина;
	Иначе
		ИмяТабличнойЧасти = "ДанныеКарточек";
		Элементы.Заполнить.КнопкаПоУмолчанию = Истина;
	КонецЕсли;
	
	Если Объект[ИмяТабличнойЧасти].Количество() > 0 Тогда
		Форма.ТекущийЭлемент = Элементы[ИмяТабличнойЧасти];
	КонецЕсли;
	
	Если Форма.ТекущаяСтраница = "СтраницаВыполняетсяЗагрузкаДанных" Тогда
		Элементы.ЗагрузкаДанныхОтмена.Видимость = Истина;
		Элементы.ЗагрузкаДанныхОтмена.КнопкаПоУмолчанию = Истина;
	Иначе
		Элементы.ЗагрузкаДанныхОтмена.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗагрузкаДанныхОтменаНаСервере(Знач ИдентификаторФоновогоЗадания)
	
	Если ЗначениеЗаполнено(ИдентификаторФоновогоЗадания) Тогда
		ДлительныеОперации.ОтменитьВыполнениеЗадания(ИдентификаторФоновогоЗадания);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

