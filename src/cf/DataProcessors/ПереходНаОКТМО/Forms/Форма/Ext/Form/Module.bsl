////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ РольДоступна(Метаданные.Роли.ПолныеПрава) 
		И НЕ РольДоступна(Метаданные.Роли.ДобавлениеИзменениеНастроекБухгалтерии) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ТаблицаРегистрацийВНалоговомОрганеСНезаполеннымКодомПоОКТМО = Обработки.ПереходНаОКТМО.ПолучитьТаблицуРегистрацийВНалоговомОрганеСНезаполеннымКодомПоОКТМО();
	
	Если ТаблицаРегистрацийВНалоговомОрганеСНезаполеннымКодомПоОКТМО.Количество() > 0 Тогда 
		
		Список.Загрузить(ТаблицаРегистрацийВНалоговомОрганеСНезаполеннымКодомПоОКТМО);
		
	Иначе
		
		Если Параметры.Свойство("НачалоРаботыСистемы") И Параметры.НачалоРаботыСистемы = Истина Тогда
			Отказ = Истина;
			Возврат;
		КонецЕсли;
		
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаОповещение;
		
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ПодробнееНаИТС(Команда)
	
	ПерейтиПоНавигационнойСсылке("http://its.1c.ru/db/metod81#content:5518:1");
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьИзменения(Команда)
	
	Если СохранитьИзмененияНаСервере() = "ВсеКодыОбновлены" Тогда
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПропуститьЭтотШаг(Команда)
	
	Закрыть();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере
Функция СохранитьИзмененияНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ПустаяСтрока", "");
	Запрос.УстановитьПараметр("ТаблицаСоответствияКодов", Список.Выгрузить());
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаСоответствияКодов.Организация,
	|	ТаблицаСоответствияКодов.РегистрацияВНалоговомОргане,
	|	ТаблицаСоответствияКодов.КодПоОКАТО,
	|	ТаблицаСоответствияКодов.КодПоОКТМО
	|ПОМЕСТИТЬ ВТТаблицаСоответствияКодов
	|ИЗ
	|	&ТаблицаСоответствияКодов КАК ТаблицаСоответствияКодов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаСоответствияКодов.РегистрацияВНалоговомОргане КАК Ссылка,
	|	ТаблицаСоответствияКодов.КодПоОКТМО
	|ИЗ
	|	ВТТаблицаСоответствияКодов КАК ТаблицаСоответствияКодов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.РегистрацииВНалоговомОргане КАК СправочникРегистрацииВНалоговомОргане
	|		ПО ТаблицаСоответствияКодов.РегистрацияВНалоговомОргане = СправочникРегистрацииВНалоговомОргане.Ссылка
	|ГДЕ
	|	СправочникРегистрацииВНалоговомОргане.КодПоОКТМО = &ПустаяСтрока
	|	И ТаблицаСоответствияКодов.КодПоОКТМО <> &ПустаяСтрока";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Пока Выборка.Следующий() Цикл
		РегистрацияВНалоговомОргане = Выборка.Ссылка.ПолучитьОбъект();
		РегистрацияВНалоговомОргане.ОбменДанными.Загрузка = Истина;
		ЗаполнитьЗначенияСвойств(РегистрацияВНалоговомОргане, Выборка);
		РегистрацияВНалоговомОргане.Записать();
	КонецЦикла;
	
	ТаблицаРегистрацийВНалоговомОрганеСНезаполеннымКодомПоОКТМО = Обработки.ПереходНаОКТМО.ПолучитьТаблицуРегистрацийВНалоговомОрганеСНезаполеннымКодомПоОКТМО();
	
	Если ТаблицаРегистрацийВНалоговомОрганеСНезаполеннымКодомПоОКТМО.Количество() = 0 Тогда
		Возврат "ВсеКодыОбновлены";
	Иначе
		Список.Загрузить(ТаблицаРегистрацийВНалоговомОрганеСНезаполеннымКодомПоОКТМО);
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

