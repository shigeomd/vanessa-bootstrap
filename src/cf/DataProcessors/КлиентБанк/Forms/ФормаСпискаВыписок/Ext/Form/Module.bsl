
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПодготовитьФормуНаСервере();	
	
КонецПроцедуры

&НаСервере
Процедура ПодготовитьФормуНаСервере()

	ШаблонСтроки = НСтр("ru = 'Выписки банка с %1 по %2'");
	Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСтроки, 
	Формат(Параметры.НачПериода, "ДФ=dd.MM.yyyy"), Формат(Параметры.КонПериода, "ДФ=dd.MM.yyyy"));
	Список.Параметры.УстановитьЗначениеПараметра("МассивСсылок", Параметры.ВыпискиБанка.ВыгрузитьЗначения());	
	
КонецПроцедуры // ПодготовитьФормуНаСервере()

&НаСервереБезКонтекста
Функция ФайлДанныхЭД(СсылкаНаЭД)
			
	ДопИнформацияПоЭД = ЭлектронныеДокументыСлужебный.ПолучитьДанныеФайла(СсылкаНаЭД, СсылкаНаЭД.УникальныйИдентификатор(), Истина);
	Идентификатор = ДопИнформацияПоЭД.Наименование;	
	
	Если ДопИнформацияПоЭД.Свойство("СсылкаНаДвоичныеДанныеФайла")
		И ЗначениеЗаполнено(ДопИнформацияПоЭД.СсылкаНаДвоичныеДанныеФайла) Тогда
		
		ДанныеЭД = ПолучитьИзВременногоХранилища(ДопИнформацияПоЭД.СсылкаНаДвоичныеДанныеФайла);
		
		Если ЗначениеЗаполнено(ДопИнформацияПоЭД.Расширение) Тогда
			ИмяФайла = ЭлектронныеДокументыСлужебный.ТекущееИмяВременногоФайла(ДопИнформацияПоЭД.Расширение);
		Иначе
			ИмяФайла = ЭлектронныеДокументыСлужебный.ТекущееИмяВременногоФайла("xml");
		КонецЕсли;
		
		Если ИмяФайла = Неопределено Тогда
			ТекстОшибки = НСтр("ru = 'Не удалось просмотреть электронный документ. Проверьте настройку рабочего каталога'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
			Возврат Неопределено;
		КонецЕсли;
		
		ВыборкаЭДДопДанных = ЭлектронныеДокументыСлужебный.ВыборкаДопДанныеЭД(СсылкаНаЭД);
		Если ВыборкаЭДДопДанных.Следующий() Тогда
			ДопДанныеЭД = ЭлектронныеДокументыСлужебный.ПолучитьДанныеФайла(ВыборкаЭДДопДанных.Ссылка,
				ВыборкаЭДДопДанных.Ссылка.УникальныйИдентификатор(), Истина);
			СсылкаНаДвоичныеДанныеФайла = "";
			Если ДопДанныеЭД.Свойство("СсылкаНаДвоичныеДанныеФайла", СсылкаНаДвоичныеДанныеФайла)
				И ЗначениеЗаполнено(СсылкаНаДвоичныеДанныеФайла) Тогда
				ДанныеДопФайла = ПолучитьИзВременногоХранилища(СсылкаНаДвоичныеДанныеФайла);
			
				Если ЗначениеЗаполнено(ДопДанныеЭД.Расширение) Тогда
					ИмяФайлаДопДанных = ЭлектронныеДокументыСлужебный.ТекущееИмяВременногоФайла(ДопДанныеЭД.Расширение);
				Иначе
					ИмяФайлаДопДанных = ЭлектронныеДокументыСлужебный.ТекущееИмяВременногоФайла("xml");
				КонецЕсли;
			
				Если ИмяФайлаДопДанных = Неопределено Тогда
					ТекстОшибки = НСтр("ru = 'Не удалось получить доп. данные электронного документа. Проверьте настройку рабочего каталога'");
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
					Возврат Неопределено;
				КонецЕсли;
				ДанныеДопФайла.Записать(ИмяФайлаДопДанных);
			КонецЕсли;
		КонецЕсли;
		
		ДанныеЭД.Записать(ИмяФайла);
		
		ИмяФайлаДанных = ИмяФайла;
		ТабличныйДокумент = ЭлектронныеДокументыВнутренний.СформироватьПечатнуюФормуЭД(
																			ИмяФайла,
																			СсылкаНаЭД.НаправлениеЭД,
																			СсылкаНаЭД.УникальныйИдентификатор(),
																			Неопределено,
																			СсылкаНаЭД.УникальныйИдентификатор());
			
		Если ТипЗнч(ТабличныйДокумент)=Тип("ТабличныйДокумент") Тогда
			
			Возврат ТабличныйДокумент;
			
		Иначе
			
			Возврат Неопределено;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Неопределено;
			
КонецФункции

&НаКлиенте
Процедура ВыпискиБанкаПриАктивизацииСтроки(Элемент)
	
	ТабличныйДокумент = ФайлДанныхЭД(Элементы.ВыпискиБанка.ТекущиеДанные.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьЭД(Команда)
	
	ПоказатьЗначение(, Элементы.ВыпискиБанка.ТекущиеДанные.Ссылка);
	
КонецПроцедуры
