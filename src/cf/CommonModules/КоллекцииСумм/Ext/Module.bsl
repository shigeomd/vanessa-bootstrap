#Область ОписаниеМодуля
// Коллекции сумм предназначены для хранения некоторых наборов сумм.
// Над коллекциями и наборами можно выполнять различные арифметические действия.
//
// В коллекции сумм может содержаться произвольное количество наборов.
// Наборы именованные. К разным наборам могут применять разные правила обработки.
// Пример такого набора - "НалоговыйУчет" или "ЭлементыЗатрат".
//
// Наборы могут иметь фиксированный состав (перечеь сумм) или переменный.
//
// Суммы из наборов с фиксированным составом идентифицируются по имени суммы и хранятся в свойствах коллекции.
// Поэтому не допускаются одинаковые имена сумм в разных наборах.
//
// Суммы из наборов с переменном составом идентифицируются порядковым номером.
// Такие наборы хранятся в свойстве имя которого - имя набора.
// Поэтому не допускаются имена наборов идентичные именам сумм.
//
// Набор с переменным составом хранится в виде коллекции, 
// каждый элемент которой содержит порядковый номер и значение суммы 
// (в текущей реализации - ТаблицаЗначений с двумя колонками).
// Поэтому наборы такого вида уместно применять, когда набор сумм достаточно разреженный.
//
// Пример коллекции сумм:
// - Набор "БухгалтерскийУчет" с фиксированным составом - содержит 1 свойство "Сумма"
// - Набор "РазницыПоНалогуНаПрибыль" с фиксированным составом - содержит 2 свойства "СуммаПР", "СуммаВР"
// - Набор "ЭлементыЗатрат" с переменным составом
//
// Такая коллекция будет содержать свойства "Сумма", "СуммаПР", "СуммаВР" и "ЭлементыЗатрат"
// При этом первые три значения будут содержать тип Число, а третье - ТаблицаЗначений
//
// Коллекции сумм могут быть созданы конструктором, а могут иметь другой тип. 
// Если коллекция не создана конструктором:
// 1. Состав, тип и содержание свойств должны быть такими же, как при создании коллекции конструктором
// 2. имена свойств могут отличаться от тех, что заданы в описании.
//    Для "перевода" описание такой коллекции дополняется соответствием фактических имен свойств (полей) "каноническим", т.е. заданным в описании наборов.
//
// Примером коллекции, не созданной конструктором может быть выборка из запроса.
// Такая выборка может содержать несколько наборов полей для разных коллекций.
// Например: "Приход, ПриходПР, ПриходВР, Расход, РасходПР, РасходВР".
// Для того, чтобы с такой коллекцией можно было работать так же, как и с коллекцией, созданной в соответствии с примером выше,
// следует задать такое соответствие:
// 		СоответствиеИмен.Вставить("Сумма",   "Приход");
// 		СоответствиеИмен.Вставить("СуммаПР", "ПриходПР");
// 		СоответствиеИмен.Вставить("СуммаВР", "ПриходВР");
// 		СоответствиеИмен.Вставить("Сумма",   "Расход");
// 		СоответствиеИмен.Вставить("СуммаПР", "РасходПР");
// 		СоответствиеИмен.Вставить("СуммаВР", "РасходВР");
//
// Во всех случаях под соответствием коллекции описанию понимается наличие в коллекции свойств,
// предусмотренных описанием, и имеющих подходящий тип.
// При этом коллекции могут иметь и другие поля, в том числе, соответствовать и более 
// "широкому" описанию, включать не описанные наборы сумм.
// 
#КонецОбласти

#Область ОписаниеКоллекции

// Конструктор описания коллекции
// 
// Пример:
//  Описание = НовыйОписаниеКоллекцииСумм();
//  ДобавитьНаборСумм(Описание, "БухгалтерскийНалоговыйУчет", "Сумма,СуммаНУ");
//  ДобавитьНаборСумм(Описание, "ЭлементыЗатрат");
//
Функция НовыйОписаниеКоллекцииСумм() Экспорт
	
	ОписаниеКоллекции = Новый Структура;
	ОписаниеКоллекции.Вставить("НаборыСумм",                 Новый Структура);
	ОписаниеКоллекции.Вставить("ЗначенияПоУмолчанию",        Новый Структура);
	ОписаниеКоллекции.Вставить("ИменаВсехСвойствСтрокой",    "");
	// Фиксированные наборы
	ОписаниеКоллекции.Вставить("ИменаСвойств",               Новый Массив);
	ОписаниеКоллекции.Вставить("ИменаСвойствСтрокой",        "");
	// Переменные наборы
	ОписаниеКоллекции.Вставить("ИменаПеременныхНаборовСумм", Новый Массив);
	
	Возврат ОписаниеКоллекции;
	
КонецФункции

// Добавляет в описание коллекции сумм описание набора.
//
// Параметры:
//  ОписаниеКоллекции - описание, сконструированное функцией НовыйОписаниеКоллекцииСумм()
//
//  ИмяНабора         - Строка
//
//  СоставНабора      - Строка - для набора с фиксированным составом: имена свойств коллекции, разделенные запятыми
//                    - Неопределено - указывает, что состав набора переменный
//
Процедура ДобавитьНаборСумм(ОписаниеКоллекции, ИмяНабора, СоставНабора = Неопределено) Экспорт
	
	ОписаниеНабора = Новый Структура;
	ОписаниеНабора.Вставить("ФиксированныйСостав", Ложь);
	
	Если Не ПустаяСтрока(ОписаниеКоллекции.ИменаВсехСвойствСтрокой) Тогда
		ОписаниеКоллекции.ИменаВсехСвойствСтрокой = ОписаниеКоллекции.ИменаВсехСвойствСтрокой + ",";
	КонецЕсли;
	
	Если СоставНабора = Неопределено Тогда
		// Переменный состав
		
		ОписаниеКоллекции.ИменаПеременныхНаборовСумм.Добавить(ИмяНабора);
		ОписаниеКоллекции.ЗначенияПоУмолчанию.Вставить(ИмяНабора, НовыйПеременныйНаборСумм());
		ОписаниеКоллекции.ИменаВсехСвойствСтрокой = ОписаниеКоллекции.ИменаВсехСвойствСтрокой + ИмяНабора;
		
	Иначе // Фиксированный состав
		
		
		ИменаСвойств = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(СоставНабора, ",", Ложь, Истина);
		Для Каждого ИмяСвойства Из ИменаСвойств Цикл
			ОписаниеКоллекции.ИменаСвойств.Добавить(ИмяСвойства);
			ОписаниеКоллекции.ЗначенияПоУмолчанию.Вставить(ИмяСвойства, 0);
		КонецЦикла;
		
		ОписаниеКоллекции.ИменаВсехСвойствСтрокой = ОписаниеКоллекции.ИменаВсехСвойствСтрокой + СоставНабора;
		
		Если ПустаяСтрока(ОписаниеКоллекции.ИменаСвойствСтрокой) Тогда
			ОписаниеКоллекции.ИменаСвойствСтрокой = СоставНабора;
		Иначе
			ОписаниеКоллекции.ИменаСвойствСтрокой = ОписаниеКоллекции.ИменаСвойствСтрокой + "," + СоставНабора;
		КонецЕсли;
		
		ОписаниеНабора.ФиксированныйСостав = Истина;
		ОписаниеНабора.Вставить("ИменаСвойств",        ИменаСвойств);
		ОписаниеНабора.Вставить("ИменаСвойствСтрокой", СоставНабора);
		
		
	КонецЕсли;
	
	ОписаниеКоллекции.НаборыСумм.Вставить(ИмяНабора, ОписаниеНабора);
	
КонецПроцедуры

// Проверяет наличие набора в описании коллекции сумм.
//
// Параметры:
//  ОписаниеКоллекции - описание, сконструированное функцией НовыйОписаниеКоллекцииСумм()
//
//  ИмяНабора         - Строка
//
// Возвращаемое значение:
//  Булево
//
Функция ЕстьНаборСумм(ОписаниеКоллекции, ИмяНабора) Экспорт
	
	Возврат ОписаниеКоллекции.НаборыСумм.Свойство(ИмяНабора);
	
КонецФункции

// Определяет перечень свойств фиксированных наборов коллекции сумм.
//
// Параметры:
//  ОписаниеКоллекции - описание, сконструированное функцией НовыйОписаниеКоллекцииСумм()
//
// Возвращаемое значение:
//  Строка - перечень имен свойств, разделенных запятыми
//
Функция ИменаСвойствСтрокой(ОписаниеКоллекции) Экспорт
	
	Возврат ОписаниеКоллекции.ИменаСвойствСтрокой;
	
КонецФункции

// Определяет перечень свойств фиксированных наборов коллекции сумм.
//
// Параметры:
//  ОписаниеКоллекции - описание, сконструированное функцией НовыйОписаниеКоллекцииСумм()
//
// Возвращаемое значение:
//  Массив - элементы массива - строки, имена свойств
//
Функция ИменаСвойств(ОписаниеСумм) Экспорт
	
	Возврат ОписаниеСумм.ИменаСвойств;
	
КонецФункции

#КонецОбласти

#Область СозданиеКоллекции

// Конструктор коллекции сумм.
//
// Параметры:
//  ОписаниеКоллекции - описание, сконструированное функцией НовыйОписаниеКоллекцииСумм()
//
Функция НовыйКоллекцияСумм(ОписаниеКоллекции) Экспорт
	
	Если ПустаяСтрока(ОписаниеКоллекции.ИменаВсехСвойствСтрокой) Тогда
		Возврат Новый Структура;
	КонецЕсли;
	
	// Создадим элементы для фиксированных наборов
	Коллекция = Новый Структура(ОписаниеКоллекции.ИменаВсехСвойствСтрокой);
	Заполнить(Коллекция, ОписаниеКоллекции.ЗначенияПоУмолчанию, ОписаниеКоллекции);
		
	Возврат Коллекция;
	
КонецФункции

// Заполняет коллекцию сумм на основании другой коллекции сумм.
//
// Параметры:
//  Коллекция         - Заполняемая коллекция - должна соответствовать ОписаниеКоллекций
//
//  Основание         - Коллекция сумм - должна соответствовать ОписаниеКоллекций
//
//  ОписаниеКоллекций - описание, сконструированное функцией НовыйОписаниеКоллекцииСумм().
//
//  СоответствиеИмен  - Соответствие - (необязательный) правила соответствия имен свойств коллекции-основания и заполняемой коллекции.
//                      В ключе элемента соответствия - имя свойства заполняемой коллекции,
//                      в значении - имя свойства коллекции-основания.
//
Процедура Заполнить(Коллекция, Основание, ОписаниеКоллекций, СоответствиеИмен = Неопределено) Экспорт
	
	// Фиксированные наборы
	Если СоответствиеИмен = Неопределено Тогда
		ЗаполнитьЗначенияСвойств(Коллекция, Основание, ОписаниеКоллекций.ИменаСвойствСтрокой);
	Иначе // С учетом соответствия имен
		Для Каждого ИмяСвойства Из ОписаниеКоллекций.ИменаСвойств Цикл
			ИмяСвойстваОснования = СоответствиеИмен[ИмяСвойства];
			Если ИмяСвойстваОснования = Неопределено Тогда
				Коллекция[ИмяСвойства] = Основание[ИмяСвойства];
			Иначе
				Коллекция[ИмяСвойства] = Основание[ИмяСвойстваОснования];
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	// Заполним переменные наборы
	Для Каждого ИмяНабора Из ОписаниеКоллекций.ИменаПеременныхНаборовСумм Цикл
		Коллекция[ИмяНабора] = Основание[ИмяНабора].Скопировать();
	КонецЦикла;
	
КонецПроцедуры

// Позволяет работать со строками таблицы значений как с коллекциями сумм.
// Перед первым изменением переменного набора сумм в каждой из строк,
// следует инициализировать этот набор - см. ИнициализироватьПеременныйНаборСумм()
//
// Параметры:
//  Результат         - ТаблицаЗначений - дополняемая таблица значений (коллекция коллекций сумм)
//
//  ОписаниеКоллекции - описание, сконструированное функцией НовыйОписаниеКоллекцииСумм().
//                      Таблица значений приводится к этому описанию.
//
Процедура ДополнитьТаблицуЗначенийКолонкамиСумм(Результат, ОписаниеКоллекции) Экспорт
	
	Для Каждого ИмяСвойства Из ОписаниеКоллекции.ИменаСвойств Цикл
		Результат.Колонки.Добавить(ИмяСвойства, УчетЗатрат.ТипСумма());
	КонецЦикла;
	
	Для Каждого ИмяНабора Из ОписаниеКоллекции.ИменаПеременныхНаборовСумм Цикл
		// При первом обращении на изменение следует инициализировать 
		// - см. ИнициализироватьПеременныйНаборСумм()
		Результат.Колонки.Добавить(ИмяНабора, Новый ОписаниеТипов("ТаблицаЗначений"));
	КонецЦикла;
	
КонецПроцедуры

Функция НовыйПеременныйНаборСумм()
	
	КоллекцияСумм = Новый ТаблицаЗначений;
	КоллекцияСумм.Колонки.Добавить("Индекс", УчетЗатрат.ТипИдентификатораВершины());
	КоллекцияСумм.Колонки.Добавить("Сумма",  УчетЗатрат.ТипСумма());
	КоллекцияСумм.Индексы.Добавить("Индекс");
	Возврат КоллекцияСумм;
	
КонецФункции

// Позволяет изменять переменный набор сумм, входящий в коллекцию, 
// созданную без использования конструктора НовыйКоллекцияСумм()
//
// Параметры:
//  Коллекция - Заполняемая коллекция - должна включать переменный набор сумм с именем ИмяНабора
//
//  ИмяНабора - Строка - имя инициализируемого набора
//
// Возвращаемое значение:
//  ТаблицаЗначений - переменный набор сумм
//
Функция ИнициализироватьПеременныйНаборСумм(Коллекция, ИмяНабора) Экспорт
	
	НаборСумм = Коллекция[ИмяНабора];
	Если НаборСумм.Колонки.Количество() = 0 Тогда
		Коллекция[ИмяНабора] = НовыйПеременныйНаборСумм();
	КонецЕсли;
	
	Возврат Коллекция[ИмяНабора];
	
КонецФункции

#КонецОбласти

#Область ОперацииНадКоллекциями

// Прибавляет к каждой из сумм, входящей в коллекцию А, соответствующую сумму, входящую в коллекцию Б.
// 
// Параметры:
//  КоллекцияА        - первое слагаемое, модифицируемая коллекция, должна соответствовать ОписаниеКоллекций
//
//  КоллекцияБ        - второе слагаемое, не изменяется, должна соответствовать ОписаниеКоллекций
//
//  ОписаниеКоллекций - описание, сконструированное функцией НовыйОписаниеКоллекцииСумм()
// 
Процедура Сложить(КоллекцияА, КоллекцияБ, ОписаниеКоллекций) Экспорт
	
	Для Каждого ИмяСвойства Из ОписаниеКоллекций.ИменаСвойств Цикл
		
		ЗначениеБ = КоллекцияБ[ИмяСвойства];
		Если ЗначениеБ <> 0 Тогда
			КоллекцияА[ИмяСвойства] = КоллекцияА[ИмяСвойства] + ЗначениеБ;
		КонецЕсли;
					
	КонецЦикла;
	
	Для Каждого ИмяНабора Из ОписаниеКоллекций.ИменаПеременныхНаборовСумм Цикл
				
		НаборСуммБ = КоллекцияБ[ИмяНабора];
		
		Если НаборСуммБ.Количество() > 0 Тогда
			
			НаборСуммА = ИнициализироватьПеременныйНаборСумм(КоллекцияА, ИмяНабора);
			
			Для Каждого СтрокаБ Из НаборСуммБ Цикл
				СтрокаА = НаборСуммА.Добавить();
				СтрокаА.Индекс = СтрокаБ.Индекс;
				СтрокаА.Сумма  = СтрокаБ.Сумма;
			КонецЦикла;
			
			НаборСуммА.Свернуть("Индекс", "Сумма");
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Вычитает из каждой из сумм, входящей в коллекцию А, соответствующую сумму, входящую в коллекцию Б.
// 
// Параметры:
//  КоллекцияА        - уменьшаемое, модифицируемая коллекция, должна соответствовать ОписаниеКоллекций
//
//  КоллекцияБ        - вычитаемое, не изменяется, должна соответствовать ОписаниеКоллекций
//
//  ОписаниеКоллекций - описание, сконструированное функцией НовыйОписаниеКоллекцииСумм()
// 
Процедура Вычесть(КоллекцияА, КоллекцияБ, ОписаниеКоллекций) Экспорт
	
	Для Каждого ИмяСвойства Из ОписаниеКоллекций.ИменаСвойств Цикл
		
		ЗначениеБ = КоллекцияБ[ИмяСвойства];
		Если ЗначениеБ <> 0 Тогда
			КоллекцияА[ИмяСвойства] = КоллекцияА[ИмяСвойства] - ЗначениеБ;
		КонецЕсли;
					
	КонецЦикла;
	
	Для Каждого ИмяНабора Из ОписаниеКоллекций.ИменаПеременныхНаборовСумм Цикл
				
		НаборСуммБ = КоллекцияБ[ИмяНабора];
		
		Если НаборСуммБ.Количество() > 0 Тогда
			
			НаборСуммА = ИнициализироватьПеременныйНаборСумм(КоллекцияА, ИмяНабора);
			
			Для Каждого СтрокаБ Из НаборСуммБ Цикл
				СтрокаА = НаборСуммА.Добавить();
				СтрокаА.Индекс = СтрокаБ.Индекс;
				СтрокаА.Сумма  = - СтрокаБ.Сумма;
			КонецЦикла;
			
			НаборСуммА.Свернуть("Индекс", "Сумма");
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Проверяет коллекцию сумм на тривиальность.
// 
// Параметры:
//  Коллекция - проверяемая коллекция сумм, должна соответствовать ОписаниеКоллекции
//
//  ОписаниеКоллекции - описание, сконструированное функцией НовыйОписаниеКоллекцииСумм()
//
// Возвращаемое значение:
//  Булево - Истина, если все суммы в коллекции не заполнены (равны нулю)
// 
Функция Пустая(Коллекция, ОписаниеКоллекции) Экспорт
	
	Для Каждого ИмяСвойства Из ОписаниеКоллекции.ИменаСвойств Цикл
		
		Если ЗначениеЗаполнено(Коллекция[ИмяСвойства]) Тогда
			Возврат Ложь;
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого ИмяНабора Из ОписаниеКоллекции.ИменаПеременныхНаборовСумм Цикл
		
		Для Каждого ЭлементНабораСумм Из Коллекция[ИмяНабора] Цикл
			
			Если ЗначениеЗаполнено(ЭлементНабораСумм.Сумма) Тогда
				Возврат Ложь;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
		
	Возврат Истина;
	
КонецФункции

// Обеспечивает, что все суммы фиксированных наборов коллекции Суммы 
// будут меньше или равны соответствующим суммам в коллекции Ограничения.
//
// Параметры:
//  Суммы        - контролируемая (модифицируемая) коллекция, должна соответствовать ОписаниеКоллекций
//
//  Ограничения  - не изменяется, должна соответствовать ОписаниеКоллекций
//
//  ОписаниеКоллекций - описание, сконструированное функцией НовыйОписаниеКоллекцииСумм()
// 
Процедура Ограничить(Суммы, Ограничения, ОписаниеКоллекций) Экспорт
	
	// Не умеет работать с переменными наборами
	
	Для Каждого ИмяСвойства Из ОписаниеКоллекций.ИменаСвойств Цикл
		
		Сумма       = Суммы[ИмяСвойства];
		Ограничение = Макс(0, Ограничения[ИмяСвойства]);
		
		Если Ограничение > Сумма Тогда
			// В пределах
			Продолжить;
		КонецЕсли;
		
		Суммы[ИмяСвойства] = Ограничение;
		
	КонецЦикла;
	
КонецПроцедуры

// Рассчитывает долю для каждого элемента коллекции сумм.
// Результат помещает в новую коллекцию.
//
// Параметры:
//  Коллекция         - коллекция сумм, доли которых надо рассчитать; должна соответствовать ОписаниеКоллекций
//
//  ОписаниеКоллекции - описание, сконструированное функцией НовыйОписаниеКоллекцииСумм()
//
//  Числитель         - Число
//
//  Знаменатель       - Число
//
// Возвращаемое значение:
//  Коллекция, сконструированная функцией НовыйКоллекцияСумм(), содержит доли от переданных сумм
// 
Функция Доля(Коллекция, ОписаниеКоллекции, Числитель, Знаменатель) Экспорт
	
 	Результат = НовыйКоллекцияСумм(ОписаниеКоллекции);
	
	Если Числитель = Знаменатель Или Знаменатель = 0 Тогда
		
		Заполнить(Результат, Коллекция, ОписаниеКоллекции);
		
		Возврат Результат;
		
	КонецЕсли;
	
	Для Каждого ИмяСвойства Из ОписаниеКоллекции.ИменаСвойств Цикл
		Результат[ИмяСвойства] = ДоляСуммы(Коллекция[ИмяСвойства], Числитель, Знаменатель);
	КонецЦикла;
	
	Для Каждого ИмяНабора Из ОписаниеКоллекции.ИменаПеременныхНаборовСумм Цикл
		НаборСумм = ИнициализироватьПеременныйНаборСумм(Результат, ИмяНабора);
		Для Каждого ЭлементНабораСумм Из Коллекция[ИмяНабора] Цикл
			ДобавитьВПеременныйНаборСумм(НаборСумм, ЭлементНабораСумм.Индекс, ЭлементНабораСумм.Сумма);
		КонецЦикла;
	КонецЦикла;
			
	Возврат Результат;
	
КонецФункции

Функция ДоляСуммы(Знач Сумма, Знач Числитель, Знач Знаменатель) Экспорт
	
	Если Числитель = Знаменатель Или Знаменатель = 0 Тогда
		Возврат Сумма;
	КонецЕсли;
	
	Цена  = Сумма / Знаменатель;
	Возврат Окр(Цена * Числитель, УчетЗатрат.РазрядностьДробнойЧасти());
	
КонецФункции

Функция ЭлементПеременногоНабораСумм(Коллекция, ИмяНабора, Индекс) Экспорт
	
	НаборСумм = ИнициализироватьПеременныйНаборСумм(Коллекция, ИмяНабора);
	
	Элемент = НаборСумм.Найти(Индекс, "Индекс");
	
	Если Элемент = Неопределено Тогда
		Элемент = НаборСумм.Добавить();
		Элемент.Индекс = Индекс;
	КонецЕсли;
	
	Возврат Элемент;
	
КонецФункции

Функция ДобавитьВПеременныйНаборСумм(НаборСумм, Индекс, Сумма) Экспорт
	
	// Допускается использовать только для первоначального заполнения коллекции уникальными значениями
	// Перед использованием следует инициализировать переменный набор
	НоваяСтрока = НаборСумм.Добавить();
	НоваяСтрока.Индекс = Индекс;
	НоваяСтрока.Сумма  = Сумма;
	
КонецФункции

Функция ИтогПеременногоНабораСумм(Коллекция, ИмяНабора) Экспорт
	
	НаборСумм = Коллекция[ИмяНабора];
	Если НаборСумм.Количество() = 0 Тогда
		Возврат 0;
	КонецЕсли;
	
	Возврат НаборСумм.Итог("Сумма");
	
КонецФункции

#КонецОбласти
