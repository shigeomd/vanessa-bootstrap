
Функция ПолучитьДанныеПоСчетам(ПараметрыСтр) Экспорт

	Параметры = ЗначениеИзСтрокиВнутр(ПараметрыСтр);
	
	СписСчетов = Новый Массив; 
	
	Если ЗначениеЗаполнено(Параметры.СчетКод)  Тогда
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	Хозрасчетный.Ссылка
		|ИЗ
		|	ПланСчетов.Хозрасчетный КАК Хозрасчетный
		|ГДЕ
		|	Хозрасчетный.Ссылка В ИЕРАРХИИ
		|			(ВЫБРАТЬ 
		|				ХЗ.Ссылка
		|			ИЗ
		|				ПланСчетов.Хозрасчетный КАК ХЗ
		|			ГДЕ
		|				ХЗ.Код В (&МассивКодов))");
		
		Запрос.Параметры.Вставить("МассивКодов", СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(СтрЗаменить(Параметры.СчетКод, " ", "")));
		
		СписСчетов = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
		
	КонецЕсли; 
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ОстаткиИОбороты.Счет.Код КАК СчетКод,
	|	ОстаткиИОбороты.СуммаНачальныйОстатокДт КАК СальдоНачДт,
	|	ОстаткиИОбороты.СуммаНачальныйОстатокКт КАК СальдоНачКт,
	|	ОстаткиИОбороты.СуммаОборотДт КАК ОборотДт,
	|	ОстаткиИОбороты.СуммаОборотКт КАК ОборотКт,
	|	ОстаткиИОбороты.СуммаКонечныйОстатокДт КАК СальдоКонДт,
	|	ОстаткиИОбороты.СуммаКонечныйОстатокКт КАК СальдоКонКт
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.ОстаткиИОбороты(&ДатаНачала, &ДатаОкончания, , , Счет = &Счет, , ) КАК ОстаткиИОбороты
	|
	|УПОРЯДОЧИТЬ ПО
	|	СчетКод");
	
	Запрос.Параметры.Вставить("ДатаНачала", Параметры.ДатаНачала);
	Запрос.Параметры.Вставить("ДатаОкончания", Параметры.ДатаОкончания);
	
	Если СписСчетов.Количество() = 1 Тогда
		
		Запрос.Параметры.Вставить("Счет", СписСчетов[0]);
		
	ИначеЕсли СписСчетов.Количество() > 1 Тогда 
		
		Запрос.Параметры.Вставить("Счет", СписСчетов);
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Счет = &Счет", "Счет В (&Счет)"); 
		
	Иначе
		Если ЗначениеЗаполнено(Параметры.СчетКод) Тогда
			Запрос.Параметры.Вставить("Счет", ПланыСчетов.Хозрасчетный.ПустаяСсылка());
		Иначе
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "Счет = &Счет", ""); 
		КонецЕсли; 
	КонецЕсли; 
	
	Рез = Запрос.Выполнить().Выгрузить(); 
	
	Возврат ЗначениеВСтрокуВнутр(Рез);
	
КонецФункции
 