
Функция ИзменитьПорядокЭлементов(Ссылка, УстановленныеОтборы, ОтображениеСписком, Вверх) Экспорт
	
	Информация = РегламентированнаяОтчетность.ПолучитьИнформациюДляПеремещенияОМетаданных(Ссылка);
	
	// Для иерархических справочников может быть установлен отбор по родителю, если нет,
	// то способ отображения должен быть иерархический или в виде дерева
	Если Информация.ЕстьРодитель И ОтображениеСписком И Не УстановленныеОтборы.ЕстьОтборПоРодителю Тогда
		Возврат НСтр("ru = 'Перед перемещением необходимо установить отображение в виде дерева или иерархического списка!'");
	КонецЕсли;
	
	// Для подчиненных справочников должен быть установлен отбор по владельцу
	Если Информация.ЕстьВладелец И НЕ УстановленныеОтборы.ЕстьОтборПоВладельцу Тогда
		Возврат НСтр("ru = 'Перед перемещением необходимо установить отбор по владельцу!'");
	КонецЕсли;
	
	// Проверим, есть ли у выбранного объекта реквизит доп. упорядочивания
	Если Информация.ЕстьГруппы Тогда
		ЭтоГруппа = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "ЭтоГруппа");
		Если ЭтоГруппа И Не Информация.ДляГрупп Тогда
			// Это группа, но для группа порядок не назначается
			Возврат "";
		ИначеЕсли Не ЭтоГруппа И Не Информация.ДляЭлементов Тогда
			// Это элемент, но для элементов порядок не назначается
			Возврат "";
		КонецЕсли;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	масУсловия = Новый Массив;
	
	// Добавим условие по родителю
	Если Информация.ЕстьРодитель Тогда
		масУсловия.Добавить("Таблица.Родитель = &Родитель");
		Запрос.УстановитьПараметр("Родитель", ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "Родитель"));
	КонецЕсли;
	
	// Добавим условие по владельцу
	Если Информация.ЕстьВладелец Тогда
		масУсловия.Добавить("Таблица.Владелец = &Владелец");
		Запрос.УстановитьПараметр("Владелец", ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "Владелец"));
	КонецЕсли;
	
	// Добавим условие на группу
	Если Информация.ЕстьГруппы Тогда
		Если Информация.ДляГрупп И Не Информация.ДляЭлементов Тогда
			масУсловия.Добавить("Таблица.ЭтоГруппа");
		ИначеЕсли Не Информация.ДляГрупп И Информация.ДляЭлементов Тогда
			масУсловия.Добавить("НЕ Таблица.ЭтоГруппа");
		КонецЕсли;
	КонецЕсли;
	
	// Составим строку со всеми условиями
	СтрУсловия = "";
	СтрДобавка = "
	|ГДЕ
	|	";
	Для Каждого Условие Из масУсловия Цикл
		СтрУсловия = СтрУсловия + СтрДобавка + Условие;
		СтрДобавка = "
		|	И ";
	КонецЦикла;
	
	// Составим текст запроса
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Таблица.Ссылка,
	|	Таблица.Код КАК ПорядокСтарый,
	|	Таблица.Код КАК ПорядокНовый
	|ИЗ
	|	" + Ссылка.Метаданные().ПолноеИмя() + " КАК Таблица
	|" + СтрУсловия + "
	|
	|УПОРЯДОЧИТЬ ПО
	|	Код";
	
	
	Запрос.Текст = ТекстЗапроса;
	ТабЭлементы = Запрос.Выполнить().Выгрузить();
	
	Стр1 = ТабЭлементы.Найти(Ссылка, "Ссылка");
	Если Стр1 = Неопределено Тогда
		Возврат "";
	КонецЕсли;
	
	Смещение = ?(Вверх, -1, 1);
	Индекс1 = ТабЭлементы.Индекс(Стр1);
	Индекс2 = Индекс1 + Смещение;
	Если (Индекс2 < 0) ИЛИ (Индекс2 >= ТабЭлементы.Количество()) Тогда
		Возврат "";
	КонецЕсли;
	Стр2 = ТабЭлементы[Индекс2];
	
	Стр1.ПорядокНовый = Стр2.ПорядокСтарый;
	Стр2.ПорядокНовый = Стр1.ПорядокСтарый;
	
	ТабЭлементы.Сдвинуть(Стр1, Смещение);
	
	предПорядок = 0;
	НачатьТранзакцию();
	
	Для Каждого Стр Из ТабЭлементы Цикл
		
		Если Число(предПорядок) >= Число(Стр.ПорядокНовый) Тогда
			Стр.ПорядокНовый = предПорядок + 1;
		КонецЕсли;
		предПорядок = Стр.ПорядокНовый;
		
		Если Стр.ПорядокНовый <> Стр.ПорядокСтарый Тогда
			Объект = Стр.Ссылка.ПолучитьОбъект();
			ЗаблокироватьДанныеДляРедактирования(Объект.Ссылка);
			Объект.Код = Стр.ПорядокНовый;
			Объект.Записать();
		КонецЕсли;
		
	КонецЦикла;
	
	ЗафиксироватьТранзакцию();
	
	Возврат "";
	
КонецФункции

Функция ПравоДоступаКРегламентированномуОтчету(ИдентификаторОтчета) Экспорт
		
	Если Метаданные.Документы.Найти(ИдентификаторОтчета) <> Неопределено Тогда // это документ
		
		Если НЕ ПравоДоступа("Изменение", Метаданные.Документы[ИдентификаторОтчета]) Тогда
			Возврат Ложь;
		КонецЕсли;
		
	ИначеЕсли Метаданные.Отчеты.Найти(ИдентификаторОтчета) <> Неопределено Тогда // это отчет
		
		Если НЕ ПравоДоступа("Использование", Метаданные.Отчеты[ИдентификаторОтчета]) Тогда
			Возврат Ложь;
		КонецЕсли;
		
	Иначе
		
		Возврат Неопределено;
		
	КонецЕсли;
	
	Возврат Истина;
		
КонецФункции

Функция ЗаполнитьСведенияОбОрганизацииНаСервере(Форма, Знач СписокСведений, Дополнения = "", РазделДляВывода = "Титульный") Экспорт

	Если (Форма.СтруктураРеквизитовФормы.Организация  = Неопределено) Или (Форма.СтруктураРеквизитовФормы.Организация = РегламентированнаяОтчетностьКлиентСервер.ПустоеЗначениеТипа("СправочникСсылка.Организации")) Тогда
		Возврат Неопределено;
	КонецЕсли;

	// Теперь получаем данные из глобальной общей функции
	ОКВЭДВариант = 0; // Стандартный вариант, нигде не применяется.
	Если ТипЗнч(СписокСведений) = Тип("Строка") Тогда
		Если Найти(СписокСведений, "ОКВЭДКвадратикиТочкиРазделения") > 0 Тогда
			ОКВЭДВариант = 1; // Авансы земля 2006
			СписокСведений = СтрЗаменить(СписокСведений, "ОКВЭДКвадратикиТочкиРазделения", "ОКВЭД");
		КонецЕсли;
	КонецЕсли;

	ОргСведения = РегламентированнаяОтчетностьВызовСервера.ПолучитьСведенияОбОрганизации(Форма.СтруктураРеквизитовФормы.Организация, Форма.ДатаПодписи, СписокСведений);

	Результат = "";
	ПБОЮЛ = НЕ РегламентированнаяОтчетностьВызовСервера.ЭтоЮридическоеЛицо(Форма.СтруктураРеквизитовФормы.Организация);
	Титульный = Форма["ТабличныйДокумент"];

	// ЕСЛИ В ДЕКЛАРАЦИИ СТАРЫЙ ФОРМАТ "УПОЛНОМОЧЕННЫЙ ПРЕДСТАВИТЕЛЬ", ТОГДА ДАННЫЕ ПО ПРЕДСТАВИТЕЛЮ ПОЛУЧИМ В Т.Ч. ИЗ СПРАВОЧНИКА "РЕГИСТРАЦИИВНАЛОГОВОМОРГАНЕ".
	// Алкогольные декларации.
	Если (Титульный.Области.Найти("ИННУп1") <> Неопределено) Или
	     (Титульный.Области.Найти("ИННУпП1") <> Неопределено) Тогда
		// Если в запросе данных передали параметр КодНО, то код Инспекции берем из справочника оргазинаций,
		// иначе, с титульного листа.
		Если ОргСведения.Свойство("КодНО") Тогда
			КодНО = ОргСведения.КодНО;
		Иначе
			КодНО = РегламентированнаяОтчетностьКлиентСервер.ВернутьЗначениеПараметраСЛиста(Форма, "Титульный", "КодИМНС");
		КонецЕсли;
		
		Если ОргСведения.Свойство("КППЮЛ") Тогда
			КПП = ОргСведения.КППЮЛ;
		Иначе
			КПП = РегламентированнаяОтчетностьКлиентСервер.ВернутьЗначениеПараметраСЛиста(Форма, "Титульный", "КПП1");
		КонецЕсли;

		// Получи данные о представителе.
		ДанныеПредставителя = РегламентированнаяОтчетностьВызовСервера.ПолучитьСведенияОПредставителе(Форма.СтруктураРеквизитовФормы.Организация, Форма.ДатаПодписи, Ложь, КодНО, КПП);
		
		// "Сольем" данные в общую структуру.
		Если ОргСведения.Свойство("ФИОУпПред") Тогда
			ОргСведения.Вставить("ФИОУпПред", ДанныеПредставителя.ФИОУпПред);
		КонецЕсли;

		Если ОргСведения.Свойство("ИННУпПред") Тогда
			ОргСведения.Вставить("ИННУпПред", ДанныеПредставителя.ИННУпПред);
		КонецЕсли;

	КонецЕсли;

	ФИО = Неопределено;
	Если НЕ ОргСведения.Свойство("ФИО", ФИО) Тогда
		ФИО = Неопределено;
	КонецЕсли;

	НаимЮЛПол = Неопределено;
	Если НЕ ОргСведения.Свойство("НаимЮЛПол", НаимЮЛПол) Тогда
		НаимЮЛПол = Неопределено;
	КонецЕсли;

	ТипНП = Неопределено;
	Если ОргСведения.Свойство("ТипНП", ТипНП) Тогда
		Форма.УправлениеЛистамиНаСервере(ТипНП);
	КонецЕсли;

	Для Каждого ПолученныйРезультат Из ОргСведения Цикл

		Результат = ПолученныйРезультат.Значение;
		Параметр  = ПолученныйРезультат.Ключ;

		Если Параметр = "ИННРук" Тогда
			РегламентированнаяОтчетностьКлиентСервер.ПроставитьВКвадратыЗначения(Форма, "Титульный", "ИННРук", 12, ?(ПБОЮЛ, "            ", Результат));
		ИначеЕсли Параметр = "ИННБух" Тогда
			РегламентированнаяОтчетностьКлиентСервер.ПроставитьВКвадратыЗначения(Форма, "Титульный", "ИННГБ" , 12, ?(ПБОЮЛ, "            ", Результат));
		ИначеЕсли Параметр = "ИННУпПред" Тогда
			РегламентированнаяОтчетностьКлиентСервер.ПроставитьВКвадратыЗначения(Форма, "Титульный", "ИННУП" , 12, ?(ПБОЮЛ, "            ", Результат));
		ИначеЕсли Параметр = "ОКВЭД" Тогда
			// Используется как минимум в авансах по земельному налогу.
			Если ОКВЭДВариант = 1 Тогда

				Титульный.Области.ОКВЭД.Значение = Результат;

			КонецЕсли;

		ИначеЕсли Параметр = "НаимЮЛПол" Тогда
			Титульный.Области.ОргНазв.Значение = ?(ФИО = Неопределено, Результат, ?(ПБОЮЛ, ФИО, Результат));
		ИначеЕсли Параметр = "ТелОрганизации" Тогда
			Титульный.Области.ТелОрганизации.Значение = Результат;
		ИначеЕсли Параметр = "ИННЮЛ" Тогда
			Если ЗначениеЗаполнено(Результат) Тогда
				Результат = РегламентированнаяОтчетностьКлиентСервер.ДополнитьСтроку(Результат, 12, "0");
			Иначе
				Результат = РегламентированнаяОтчетностьКлиентСервер.ДополнитьСтроку(Результат, 12, " ");
			КонецЕсли;

		ИначеЕсли Параметр = "КППЮЛ" Тогда
			Результат = РегламентированнаяОтчетностьКлиентСервер.ДополнитьСтроку(Результат, 9, " ");
		ИначеЕсли Параметр = "ОГРН" Тогда
			Если Дополнения = "не предусмотрено для заполнения ФЛ" И ПБОЮЛ Тогда
				ОГРН = "             ";
				ОГРНКоордината = "ОГРН";
			Иначе
				ОГРНКоордината = ?(ТипНП = 3, "ОГРНИП", "ОГРН");
				ОГРН = РегламентированнаяОтчетностьКлиентСервер.ДополнитьСтроку(Результат, ?(ТипНП = 3, 15, 13), " ");
			КонецЕсли;

			РегламентированнаяОтчетностьКлиентСервер.ПроставитьВКвадратыЗначения(Форма, "Титульный", ОГРНКоордината, СтрДлина(ОГРН), ОГРН);
		ИначеЕсли Параметр = "КодНО" Тогда
			Результат = РегламентированнаяОтчетностьКлиентСервер.ДополнитьСтроку(Результат, 4, " ");
			РегламентированнаяОтчетностьКлиентСервер.ПроставитьВКвадратыЗначения(Форма, "Титульный", "КодИМНС", 4, Результат);
		ИначеЕсли Параметр = "НаимНО" Тогда
			Титульный.Области.ОргИМНС.Значение = Результат;
		ИначеЕсли Параметр = "ФИОРук" Тогда
			Титульный.Области.ОргДиректор.Значение  = ?(ПБОЮЛ, "", Результат);
		ИначеЕсли Параметр = "НаимОППол" Тогда
			Если ТипНП = 2 Тогда
				// Если данной организация - обособленное подразделение, то запишем ее название, а в ячейку
				// под название организации, запишем название головной организации.
				Титульный.Области.ОбПодрНазв.Значение = Результат;
				Титульный.Области.ОргНазв.Значение = ОргСведения.НаимГоловнОрг;
			Иначе
				// Если заданная организация не обособка, тогда очистим ее поле.
				Титульный.Области.ОбПодрНазв.Значение = "";
			КонецЕсли;
		ИначеЕсли Параметр = "ФИОБух" Тогда
			Титульный.Области.ОргБухгалтер.Значение = ?(ПБОЮЛ, "", Результат);
		ИначеЕсли Параметр = "ФИОУпПред" Тогда
			Титульный.Области.ОргУП.Значение = ?(ПБОЮЛ, "", Результат);
		ИначеЕсли Найти(Параметр, "ТитульныйЛистФИО_") > 0 Тогда
			Титульный.Области[Сред(Параметр, 18)].Значение = ФИО;
		ИначеЕсли Параметр = "НаимИОПол" Тогда
			Титульный.Области.ИноОргНазв.Значение = Результат;
		КонецЕсли;

		// Если дата подписи поменялась, то перепишем название в декл. только для ПБОЮЛов.
		Если (Параметр = "НаимЮЛПол" Или Параметр = "ФИО") И ПБОЮЛ Тогда
			Титульный.Области.ОргНазв.Значение = ?(ФИО = Неопределено, Результат, ?(ПБОЮЛ, ФИО, Результат));
		КонецЕсли;

	КонецЦикла;

	// Если на титульном листе найдена область ДокУпПред - докуент уполномоченного представителя, тогда получим данные о представителе.
	// В отчете должна быть процедура ПроставитьСведенияОПредставителеОрганизации().
	Если Титульный.Области.Найти("ДокУпПред") <> Неопределено Тогда
		
		Попытка
			
			Титульный.Области["КодИМНС"].Значение = ОргСведения.КодНО;
			Титульный.Области.КПП1.Значение    	  = ОргСведения.КППЮЛ;
			
			Форма.ПроставитьСведенияОПредставителеОрганизации();
			
		Исключение
			
		КонецПопытки;
		
	КонецЕсли;

	Форма.ЗаполнитьДатуВЯчейкахНаСервере();
	
	Возврат ОргСведения;

КонецФункции

Функция ПолучитьСведенияОбОрганизации(Знач Организация, Знач ДатаЗначения = Неопределено, Знач СписокПоказателей = Неопределено) Экспорт

	Если Организация = Неопределено ИЛИ Организация = РегламентированнаяОтчетностьКлиентСервер.ПустоеЗначениеТипа("СправочникСсылка.Организации") Тогда
		Возврат Новый Структура;
	КонецЕсли;
	
	Если ТипЗнч(СписокПоказателей) = Тип("Строка") Тогда
		
		СписокПоказателейСтрока = Строка(СписокПоказателей);
		СписокПоказателей = Новый СписокЗначений;
		ВхождениеЗапятой = Найти(СписокПоказателейСтрока, ",");
		Пока ВхождениеЗапятой <> 0 Цикл
			ИмяПоказателя = СокрЛП(Лев(СписокПоказателейСтрока, ВхождениеЗапятой - 1));
			СписокПоказателейСтрока = Сред(СписокПоказателейСтрока, ВхождениеЗапятой + 1);
			ВхождениеЗапятой = Найти(СписокПоказателейСтрока, ",");
			Если ПустаяСтрока(ИмяПоказателя) Тогда
				Продолжить;
			КонецЕсли;
			СписокПоказателей.Добавить("", ИмяПоказателя);
		КонецЦикла;
		ИмяПоказателя = СокрЛП(СписокПоказателейСтрока);
		Если НЕ ПустаяСтрока(ИмяПоказателя) Тогда
			СписокПоказателей.Добавить("", ИмяПоказателя);
		КонецЕсли;
		
	КонецЕсли;	
				
	Возврат РегламентированнаяОтчетностьПереопределяемый.ПолучитьСведенияОбОрганизации(Организация, ДатаЗначения, СписокПоказателей);

КонецФункции

Функция ПолучитьСведенияОПредставителе(ФормаОрганизация, ФормаДатаПодписи, Знач МожетБытьТолькоЮРЛицо = Неопределено, Знач ИФНС = Неопределено, КПП = Неопределено) Экспорт
	
	ДанныеКонтактнойИнформации = Новый Структура("ФИОУпПред, ИННУпПред, ТелУпПред, ВидУдЛичнУпПред, КодУдЛичнУпПред, СерияУдЛичнУпПред
	                                            |,НомерУдЛичнУпПред, ОрганВыданУдЛичнУпПред, ДатаУдЛичнУпПред, ДатаРождУпПред ,МестоРождУпПред
	                                            |,ПолУпПред, ИндексМЖУпПред, СубъектМЖУпПред, КодСубъектМЖУпПред, РайонМЖУпПред, ГородМЖУпПред
	                                            |,НПунктМЖУпПред, УлицаМЖУпПред, ДомМЖУпПред, КорпусМЖУпПред, КвартираМЖУпПред, ГраждУпПред");

	ДанныеКонтактнойИнформации = РегламентированнаяОтчетностьВызовСервера.ПолучитьСведенияОбОрганизации(ФормаОрганизация, ФормаДатаПодписи, "ФИОУпПред"
	                                                                                               + ",ИННУпПред"
	                                                                                               + ",ТелУпПред"
	                                                                                               + ",ВидУдЛичнУпПред"
	                                                                                               + ",КодУдЛичнУпПред"
	                                                                                               + ",СерияУдЛичнУпПред"
	                                                                                               + ",НомерУдЛичнУпПред"
	                                                                                               + ",ОрганВыданУдЛичнУпПред"
	                                                                                               + ",ДатаУдЛичнУпПред"
	                                                                                               + ",ДатаРождУпПред"
	                                                                                               + ",МестоРождУпПред"
	                                                                                               + ",ПолУпПред"
	                                                                                               + ",ИндексМЖУпПред"
	                                                                                               + ",СубъектМЖУпПред"
	                                                                                               + ",КодСубъектМЖУпПред"
	                                                                                               + ",РайонМЖУпПред"
	                                                                                               + ",ГородМЖУпПред"
	                                                                                               + ",НПунктМЖУпПред"
	                                                                                               + ",УлицаМЖУпПред"
	                                                                                               + ",ДомМЖУпПред"
	                                                                                               + ",КорпусМЖУпПред"
	                                                                                               + ",КвартираМЖУпПред"
	                                                                                               + ",ГраждУпПред");

	ДанныеКонтактнойИнформации.Вставить("ПредставительФЛ", Неопределено);
	Если НЕ ПустаяСтрока(ДанныеКонтактнойИнформации.ФИОУпПред) Тогда
		ДанныеКонтактнойИнформации.Вставить("ПредставительФЛ", Истина);
	КонецЕсли;
	ДанныеКонтактнойИнформации.Вставить("ДокументПредставителя", "");
    		
	// Получим данные о представителе из справочника Регистрации ИФНС.
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Владелец", ФормаОрганизация);
	Запрос.УстановитьПараметр("КодИФНС", ИФНС);
	Запрос.УстановитьПараметр("КПП", КПП);
	Запрос.УстановитьПараметр("ПометкаУдаления", Ложь);

	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	СправочникИФНС.Представитель,
	               |	СправочникИФНС.ДокументПредставителя
	               |ИЗ
	               |Справочник.РегистрацииВНалоговомОргане КАК СправочникИФНС
	               |ГДЕ
	               |СправочникИФНС.Владелец = &Владелец
	               | И СправочникИФНС.Код = &КодИФНС
				   | И СправочникИФНС.КПП = &КПП
                   | И СправочникИФНС.ПометкаУдаления = &ПометкаУдаления";
	СправочникИФНС = Запрос.Выполнить().Выгрузить();

	Если СправочникИФНС.Количество() = 0 Тогда
		Возврат ДанныеКонтактнойИнформации;
	КонецЕсли;

	Представитель = СправочникИФНС[0].Представитель;
	Если Представитель = Неопределено Тогда
		// В справочнике не заполнен представитель ни ФЛ ни ЮР.лицо
		Возврат ДанныеКонтактнойИнформации;
	КонецЕсли;

	Если РегламентированнаяОтчетность.ПредставительЯвляетсяФизЛицом(Представитель) Тогда
		
		// В случае, если представитель Физ. лицо, а в отчете только может быть Физ. лицо, тогда возвратим пустую структуру данных.
		Если МожетБытьТолькоЮРЛицо = Истина Тогда
			Возврат ДанныеКонтактнойИнформации;
		КонецЕсли;

		// Признак, что представитель ФИЗ. ЛИЦО.
		ДанныеКонтактнойИнформации.Вставить("ПредставительФЛ", Истина);

		// ФИО Уполномоченного представителя.
		ДанныеФЛ = РегламентированнаяОтчетность.ПолучитьФИОФизЛица(Представитель, ФормаДатаПодписи);
		ДанныеКонтактнойИнформации.Вставить("ФИОУпПред", СокрЛП(СокрЛП(ДанныеФЛ.Фамилия) + " " + СокрЛП(ДанныеФЛ.Имя) + " " + СокрЛП(ДанныеФЛ.Отчество)));

		// ИНН
		ДанныеКонтактнойИнформации.Вставить("ИННУпПред", Представитель.ИНН);

		// Телефон Уполномоченного представителя.

		ДомашнийТелефонФизЛица = ?(РегламентированнаяОтчетностьКлиентСервер.СвойствоОпределено(Справочники.ВидыКонтактнойИнформации, "ТелефонДомашнийФизическиеЛица"), Справочники.ВидыКонтактнойИнформации.ТелефонДомашнийФизическиеЛица, Справочники.ВидыКонтактнойИнформации.ТелефонРабочийФизическиеЛица);
		Данные = Новый Структура("Тип, Вид", Перечисления.ТипыКонтактнойИнформации.Телефон, ДомашнийТелефонФизЛица);
		Результат2 = Представитель.КонтактнаяИнформация.НайтиСтроки(Данные);
		ДанныеКонтактнойИнформации.Вставить("ТелУпПред", "");
		Если Результат2.Количество() > 0 Тогда
			ДанныеКонтактнойИнформации.Вставить("ТелУпПред", Результат2[0].Представление);
		КонецЕсли;

		Если Результат2.Количество() > 0 Тогда
			Значение = Результат2[0].Представление;
		Иначе
			Значение = "";
		КонецЕсли;

		// ПАСПОРТНЫЕ ДАННЫЕ ПРЕДСТАВИТЕЛЯ.

		УдЛичн = РегламентированнаяОтчетность.ПолучитьДокФизЛица(Представитель, ФормаДатаПодписи);
		ДанныеКонтактнойИнформации.Вставить("КодУдЛичнУпПред", "");
		ДанныеКонтактнойИнформации.Вставить("ВидУдЛичнУпПред", "");

		Если УдЛичн = Неопределено Тогда
			ДанныеКонтактнойИнформации.Вставить("КодУдЛичнУпПред", "");
			ДанныеКонтактнойИнформации.Вставить("ВидУдЛичнУпПред", "");
			ДанныеКонтактнойИнформации.Вставить("ОрганВыданУдЛичнУпПред", "");
			ДанныеКонтактнойИнформации.Вставить("СерияУдЛичнУпПред", "");
			ДанныеКонтактнойИнформации.Вставить("НомерУдЛичнУпПред", "");
			ДанныеКонтактнойИнформации.Вставить("ДатаУдЛичнУпПред", "");
		Иначе

			Если УдЛичн.ВидДокумента <> Справочники.ВидыДокументовФизическихЛиц.ПустаяСсылка() Тогда
				Если Метаданные.Справочники.ВидыДокументовФизическихЛиц.Реквизиты.Найти("КодМВД") <> Неопределено Тогда
					ДанныеКонтактнойИнформации.Вставить("КодУдЛичнУпПред", УдЛичн.ВидДокумента.КодМВД);
				Иначе
					ДанныеКонтактнойИнформации.Вставить("КодУдЛичнУпПред", "");
				КонецЕсли;
				ДанныеКонтактнойИнформации.Вставить("ВидУдЛичнУпПред", УдЛичн.ВидДокумента.Наименование);
			КонецЕсли;

			ДанныеКонтактнойИнформации.Вставить("ОрганВыданУдЛичнУпПред", УдЛичн.КемВыдан);
			ДанныеКонтактнойИнформации.Вставить("СерияУдЛичнУпПред", УдЛичн.Серия);
			ДанныеКонтактнойИнформации.Вставить("НомерУдЛичнУпПред", УдЛичн.Номер);
			ДанныеКонтактнойИнформации.Вставить("ДатаУдЛичнУпПред", УдЛичн.ДатаВыдачи);
		КонецЕсли;

		// ДАТА РОЖДЕНИЯ.
		ДанныеКонтактнойИнформации.Вставить("ДатаРождУпПред", Представитель.ДатаРождения);
		ДанныеКонтактнойИнформации.Вставить("МестоРождУпПред", Представитель.МестоРождения);
		
		// ПОЛ
		Если Метаданные.Перечисления.Найти("ПолФизическогоЛица") <> Неопределено Тогда

				
			Если Представитель.Пол = Перечисления.ПолФизическогоЛица.Женский Тогда
				Значение = 2;
			ИначеЕсли Представитель.Пол = Перечисления.ПолФизическогоЛица.Мужской Тогда
				Значение = 1;
			Иначе
				Значение = 0;
			КонецЕсли;
			
		Иначе
			Значение = 0;
		КонецЕсли;
		ДанныеКонтактнойИнформации.Вставить("ПолУпПред", Значение);

		// АДРЕСАНАЯ ЧАСТЬ
		Данные = Новый Структура("Тип, Вид", Перечисления.ТипыКонтактнойИнформации.Адрес, Справочники.ВидыКонтактнойИнформации.АдресМестаПроживанияФизическиеЛица);
		Результат = Представитель.КонтактнаяИнформация.НайтиСтроки(Данные);

		Если Результат.Количество() = 0 Тогда
			ДанныеКонтактнойИнформации.Вставить("ИндексМЖУпПред", "");
			ДанныеКонтактнойИнформации.Вставить("СубъектМЖУпПред", "");
			ДанныеКонтактнойИнформации.Вставить("КодСубъектМЖУпПред", "");
			ДанныеКонтактнойИнформации.Вставить("РайонМЖУпПред", "");
			ДанныеКонтактнойИнформации.Вставить("ГородМЖУпПред", "");
			ДанныеКонтактнойИнформации.Вставить("НПунктМЖУпПред", "");
			ДанныеКонтактнойИнформации.Вставить("УлицаМЖУпПред", "");
			ДанныеКонтактнойИнформации.Вставить("ДомМЖУпПред", "");
			ДанныеКонтактнойИнформации.Вставить("КорпусМЖУпПред", "");
			ДанныеКонтактнойИнформации.Вставить("КвартираМЖУпПред", "");
		Иначе
			
			СтруктураАдреса = Новый Структура("Индекс, Регион, Район, Город, НаселенныйПункт, Улица, Дом, Корпус, Квартира");
			
			РегламентированнаяОтчетностьВызовСервера.СформироватьАдрес(Результат[0].ЗначенияПолей, СтруктураАдреса);
									
			// ИНДЕКС М.Ж.
			ДанныеКонтактнойИнформации.Вставить("ИндексМЖУпПред", СтруктураАдреса.Индекс);
			ДанныеКонтактнойИнформации.Вставить("СубъектМЖУпПред", СтруктураАдреса.Регион);
			ДанныеКонтактнойИнформации.Вставить("КодСубъектМЖУпПред", Формат(РегламентированнаяОтчетностьВызовСервера.КодРегионаПоНазванию(СтруктураАдреса.Регион), "ЧЦ=2; ЧВН="));
			ДанныеКонтактнойИнформации.Вставить("РайонМЖУпПред", СтруктураАдреса.Район);
			ДанныеКонтактнойИнформации.Вставить("ГородМЖУпПред", СтруктураАдреса.Город);
			ДанныеКонтактнойИнформации.Вставить("НПунктМЖУпПред", СтруктураАдреса.НаселенныйПункт);
			ДанныеКонтактнойИнформации.Вставить("УлицаМЖУпПред", СтруктураАдреса.Улица);
			ДанныеКонтактнойИнформации.Вставить("ДомМЖУпПред", СтруктураАдреса.Дом);
			ДанныеКонтактнойИнформации.Вставить("КорпусМЖУпПред", СтруктураАдреса.Корпус);
			ДанныеКонтактнойИнформации.Вставить("КвартираМЖУпПред", СтруктураАдреса.Квартира);
		КонецЕсли;

		Возврат ДанныеКонтактнойИнформации;

	КонецЕсли; // Если представитель физ.лицо.

	Если НЕ РегламентированнаяОтчетность.ПредставительЯвляетсяФизЛицом(Представитель) И МожетБытьТолькоЮРЛицо = Ложь Тогда
		Возврат ДанныеКонтактнойИнформации;
	КонецЕсли;
	
КонецФункции

Функция ОпределитьСтраницуПанелиПоКодуПоказателяНаСервере(Форма, Знач КодПоказателя) Экспорт
	Перем ИмяСтраницы;

	Если РегламентированнаяОтчетностьКлиентСервер.СвойствоОпределено(Форма, "мПоляПоискаПоказателяВТСП") Тогда
		
		ПоляПоиска = Новый Массив;
		ПоляПоискаСтр = Форма.мПоляПоискаПоказателяВТСП;
		
		ЧислоСимволов = Найти(ПоляПоискаСтр, ",") - 1;
		
		Пока ЧислоСимволов > 0 Цикл
			
			ПоляПоиска.Добавить(СокрЛП(Лев(ПоляПоискаСтр, ЧислоСимволов)));
			
			ПоляПоискаСтр = Сред(ПоляПоискаСтр, ЧислоСимволов + 2);
			
			ЧислоСимволов = Найти(ПоляПоискаСтр, ",") - 1;
			
		КонецЦикла;
		
		Если ЗначениеЗаполнено(ПоляПоискаСтр) Тогда
			ПоляПоиска.Добавить(СокрЛП(ПоляПоискаСтр));
		КонецЕсли;
						
	Иначе
		
		ПоляПоиска = Новый Массив;
		ПоляПоиска.Добавить("КодПоказателяПоСоставу");
		ПоляПоиска.Добавить("КодПоказателяПоФорме");
		
	КонецЕсли;

	// В случае, если показатель многострочный (для импорта XML данных в зарплатных отчетах, тогда
	// определим его смысловую часть и найдем раздел.
	Если (Лев(КодПоказателя, 1) = "П") И (Найти(КодПоказателя, "_") = 14) Тогда
		КодПоказателя = Сред(КодПоказателя, 1, Найти(КодПоказателя, "_") - 1);
		// Временный обход для показателя по графе 10 раздела 3. В составе показателей
		// графы 10 нет, поэтому временно привяжем к разделу 3.
		Если КодПоказателя = "П000030001010" Тогда
			Возврат "Раздел3";
		КонецЕсли;
	КонецЕсли;

	Для Каждого ПолеПоиска Из ПоляПоиска Цикл
		
		НайденнаяСтрока = Форма.мТаблицаСоставПоказателей.НайтиСтроки(Новый Структура(ПолеПоиска, КодПоказателя));
		
		Если НайденнаяСтрока.Количество() > 0 Тогда
			ИмяСтраницы = НайденнаяСтрока[0].ИмяПоляТаблДокумента;
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ИмяСтраницы;

КонецФункции

Функция ПолучитьТаблицуВариантовЗаполненияНаСервере(Форма, ИмяСтраницыПанели, ПолучитьЭталонную = Ложь, ТипЗнчСуммы = Неопределено, Знач ИмяСекции = "") Экспорт
	Перем МассивВариантовЗаполненияСтраниц;
	Перем ТаблицаВариантыЗаполнения;
	Перем ТаблицаСтраницРаздела;

	Если Форма.мСтруктураВариантыЗаполнения.Свойство(ИмяСтраницыПанели, МассивВариантовЗаполненияСтраниц) Тогда
		
		МассивВариантовЗаполненияСтраниц = Форма["ТаблицаВариантыЗаполнения" + ИмяСтраницыПанели];

		Если НЕ ПолучитьЭталонную Тогда
			// получим варианты заполнения ячеек, заданные для активной страницы
			НомерТекущейСтраницы = 1;

			// Определим, присутствует ли многостраничность в декларации или нет.
			Попытка
				МногостраничностьЕсть = ?(Форма.мСтруктураМногостраничныхРазделов = Неопределено, Ложь, Истина);
			Исключение
				МногостраничностьЕсть = Ложь;
			КонецПопытки;

			Если МногостраничностьЕсть Тогда
				Если Форма.мСтруктураМногостраничныхРазделов.Свойство(ИмяСтраницыПанели, ТаблицаСтраницРаздела) Тогда
					
					ТаблицаСтраницРаздела = Форма[ТаблицаСтраницРаздела];
					
					// Раздел является многостраничным. Определим активную страницу раздела:
					СтрТаблицаСтраницы = ТаблицаСтраницРаздела.НайтиСтроки(Новый Структура("АктивнаяСтраница", Истина));

					Если СтрТаблицаСтраницы.Количество() > 0 Тогда
						НомерТекущейСтраницы = ТаблицаСтраницРаздела.Индекс(СтрТаблицаСтраницы[0]) + 1;
					КонецЕсли;
				КонецЕсли;

				Если МассивВариантовЗаполненияСтраниц.Количество() < НомерТекущейСтраницы Тогда
					Для Сч = МассивВариантовЗаполненияСтраниц.Количество() По НомерТекущейСтраницы Цикл
						Если ТипЗнч(МассивВариантовЗаполненияСтраниц[0].ТаблицаВариантовЗаполнения) = Тип("ДанныеФормыКоллекция") Тогда
						
							ТаблВариантов = МассивВариантовЗаполненияСтраниц.Добавить();	
							
							КопироватьДанныеФормы(МассивВариантовЗаполненияСтраниц[0].ТаблицаВариантовЗаполнения, ТаблВариантов.ТаблицаВариантовЗаполнения);
							
						Иначе
							МассивВариантовЗаполненияСтраниц.Добавить(МассивВариантовЗаполненияСтраниц[0].ТаблицаВариантовЗаполнения.Скопировать());
						КонецЕсли;
					КонецЦикла;
				КонецЕсли;

			КонецЕсли;
			
			ТаблицаВариантыЗаполнения = МассивВариантовЗаполненияСтраниц[НомерТекущейСтраницы - 1].ТаблицаВариантовЗаполнения;
		Иначе
			// Получим предустановленные (эталонные) варианты заполнения ячеек поля табличного
			// документа на заданной странице

			ОписаниеТиповСтрока15  = Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(15));
			ОписаниеТиповСтрока50  = Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(50));

			ОписаниеТиповСтрока100 = Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(100));

			ОписаниеТиповЧисло1    = Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(1));

			Если ТипЗнчСуммы = Неопределено Тогда
				ОписаниеТиповЧисло15   = Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2));
			Иначе
				ОписаниеТиповЧисло15 = ТипЗнчСуммы;
			КонецЕсли;

			ТаблВариантыЗаполнения = Новый ТаблицаЗначений;
			ТаблВариантыЗаполнения.Колонки.Добавить( "КодПоказателя",     ОписаниеТиповСтрока50  );
			ТаблВариантыЗаполнения.Колонки.Добавить( "ВариантЗаполнения", ОписаниеТиповЧисло1    );
			ТаблВариантыЗаполнения.Колонки.Добавить( "ЗначениеАвто",      ОписаниеТиповЧисло15   );
			ТаблВариантыЗаполнения.Колонки.Добавить( "Дельта",            ОписаниеТиповЧисло15   );
			ТаблВариантыЗаполнения.Колонки.Добавить( "Комментарий",       ОписаниеТиповСтрока100 );
			ТаблВариантыЗаполнения.Колонки.Добавить( "КодПоказателяПоСтруктуре",       ОписаниеТиповСтрока50 );

			// варианты заполнения определяем по макету СоставПоказателей
						
			//МакетСоставаПоказателей = Отчеты[Сред(Лев(Форма.ИмяФормы, Найти(Форма.ИмяФормы, ".Форма.") - 1), 7)].ПолучитьМакет(Форма.СтруктураРеквизитовФормы.мСоставПоказателей);
			МакетСоставаПоказателей = ОбъектОтчета(Форма.ИмяФормы).ПолучитьМакет(Форма.СтруктураРеквизитовФормы.мСоставПоказателей);

			ИмяСекции = ?(ЗначениеЗаполнено(ИмяСекции) И ТипЗнч(ИмяСекции) = Тип("Строка"), ИмяСекции, ИмяСтраницыПанели);
			
			ТекОбласть = МакетСоставаПоказателей.Области.Найти(ИмяСекции);
			Если ТекОбласть <> Неопределено Тогда
			
				Для Ном = ТекОбласть.Верх По ТекОбласть.Низ Цикл
					// Перебираем строки макета.
					// Код показателя (по составу показателей) определяется по первой колонке макета
					КодПоказателя = СокрП(МакетСоставаПоказателей.Область(Ном, 1).Текст);
					ИмяПоказателя = КодПоказателя;

					Если КодПоказателя = "===" Тогда         // признак конечной строки
						Прервать;
					КонецЕсли;

					Если Лев(КодПоказателя, 2) = "//" Тогда  // пропускаем комментарии
						Продолжить;
					КонецЕсли;

					// код показателя по форме отчете (имя ячейки в полях табличного документа формы)
					КодПоказателяПоФорме = СокрЛП(МакетСоставаПоказателей.Область(Ном, 2).Текст);
					// вариант заполнения ячейки определяется по колонке 6 макета
					//стрВариантЗаполнения = СокрЛП(МакетСоставаПоказателей.Область(Ном, 6).Текст);
					стрВариантЗаполнения = РегламентированнаяОтчетность.ПолучитьВариантЗаполненияПоказателяРегОтчета(МакетСоставаПоказателей, Ном);
					чВариантЗаполнения   = ? (ПустаяСтрока(стрВариантЗаполнения), 0, Число(стрВариантЗаполнения));

					КодПоказателяПоСтруктуре = СокрЛП(МакетСоставаПоказателей.Область(Ном, 9).Текст);

					Если чВариантЗаполнения <> 0 Тогда
						// Если в колонке 6 задан варианет заполнения показателя, 
						// то его значение может определяться по данным ИБ.
						// Дополняем таблицу значений вариантов заполнения
						НоваяСтрока = ТаблВариантыЗаполнения.Добавить();
						НоваяСтрока.КодПоказателя      = ? (Не ПустаяСтрока(КодПоказателяПоФорме), КодПоказателяПоФорме, КодПоказателя);
						НоваяСтрока.ВариантЗаполнения  = чВариантЗаполнения;
						НоваяСтрока.КодПоказателяПоСтруктуре = КодПоказателяПоСтруктуре;
					КонецЕсли;
				КонецЦикла;

			КонецЕсли;

			Если ТаблВариантыЗаполнения.Количество() > 0 Тогда
				ТаблицаВариантыЗаполнения = ТаблВариантыЗаполнения.Скопировать();
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

	Возврат ТаблицаВариантыЗаполнения;

КонецФункции

Функция ПолучитьНазваниеРегионаПоКоду(КодРегиона) Экспорт
    	
	Если НЕ ЗначениеЗаполнено(КодРегиона) Тогда
		Возврат "";
	КонецЕсли;

	Попытка
		ЧисловойКодРегиона = Число(КодРегиона);
	Исключение
		Возврат "";
	КонецПопытки;
	
	Возврат АдресныйКлассификатор.НаименованиеРегионаПоКоду(ЧисловойКодРегиона);

КонецФункции

Процедура ВставитьОбластьВТабличныйДокумент(ТекТабличноеПоле, НомерВерхЯчейкиОбласти, НомерНижнЯчейкиОбласти) Экспорт
	
	ВставляемаяОбласть = ТекТабличноеПоле.Область(НомерВерхЯчейкиОбласти, , НомерНижнЯчейкиОбласти);
	
	ТекТабличноеПоле.ВставитьОбласть(ВставляемаяОбласть, ,ТипСмещенияТабличногоДокумента.ПоВертикали);
		
КонецПроцедуры

Процедура УдалитьОбластьИзТабличногоДокумента(ТекТабличноеПоле, НомерВерхЯчейкиОбласти, НомерНижнЯчейкиОбласти) Экспорт
	
	УдаляемаяОбласть = ТекТабличноеПоле.Область(НомерВерхЯчейкиОбласти, , НомерНижнЯчейкиОбласти);
	
	ТекТабличноеПоле.УдалитьОбласть(УдаляемаяОбласть, ТипСмещенияТабличногоДокумента.ПоВертикали);
		
КонецПроцедуры

Процедура ВывестиРазделВТабличныйДокументНаСервере(Знач НастройкиМнгЧ, ИмяГруппы, Раздел, СтруктураГруппы, мСтруктураКолвоКолонокРазделов) Экспорт
	
	// реальное кол-во строк (колво заполненных строк)
	ТекущееКоличествоСтрокГруппы = СтруктураГруппы.Количество();
	
	// Режим восстановления многострочных блоков. Из процедуры ВосстановитьСохраненныеДанные.
	НастройкиМнгЧ = ДанныеФормыВЗначение(НастройкиМнгЧ, Тип("ТаблицаЗначений")).Скопировать();
	
	НазвОбласти = НастройкиМнгЧ.Найти(ИмяГруппы, "ИдГруппы").Область;
	
	НастройкиМнгЧ.Колонки.Добавить("НизМногострочнойЧасти");
	НастройкиМнгЧ.Колонки.Добавить("ВерхМногострочнойЧасти");
	
	// Определим координаты всех многострочных блоков, для последующего восстановления.
	Для каждого Зап Из НастройкиМнгЧ Цикл
		КоординатаНиз  = Раздел.Области[Зап.Область].Низ;
		КоординатаВерх = Раздел.Области[Зап.Область].Верх;
		Зап.НизМногострочнойЧасти    = КоординатаНиз;
		Зап.ВерхМногострочнойЧасти   = КоординатаВерх;
		// Координаты текущей области
		Если Зап.Область = НазвОбласти Тогда
			Низ           = КоординатаНиз;
			Верх          = КоординатаВерх;
		КонецЕсли;
	КонецЦикла;
	
	ВысотаСтроки = НастройкиМнгЧ.Найти(ИмяГруппы, "ИдГруппы").ВысотаСтроки;
		
	// Скопируем верхнюю часть документа, не включая многострочную часть.
	ТаблДок = Раздел.ПолучитьОбласть(1, , Верх - 1);
	// Скопируем подвал, все, что ниже области многострочного блока.
	Подвал = Раздел.ПолучитьОбласть(Низ + 1, , Раздел.ВысотаТаблицы);
			
	Строка = Раздел.ПолучитьОбласть(Верх, , Верх + ВысотаСтроки - 1);
	
	Если ВысотаСтроки > 1 Тогда
		ОбластьОпределениеВысотыСтрокиМногострочнойЧасти=Строка.Области.Найти("ОпределениеВысотыСтрокиМногострочнойЧасти");
		Если ОбластьОпределениеВысотыСтрокиМногострочнойЧасти<>Неопределено Тогда
			ОбластьОпределениеВысотыСтрокиМногострочнойЧасти.Имя = "";
		КонецЕсли;
	КонецЕсли;
	
	Если Строка.Области.Найти(НазвОбласти) <> Неопределено Тогда
		Строка.Области[НазвОбласти].Имя = "";
	КонецЕсли;
	
	// При превышении реального кол-ва строк над исходным кол-вом (по шаблону),
	// автоматически добавляем необходимое колво строк.
	
	Для НомНовойСтроки = 1 По ТекущееКоличествоСтрокГруппы Цикл
		// Переопределим именнованность.
		Строка1 = Строка.ПолучитьОбласть(1, , Строка.ВысотаТаблицы);
		НомСтр = Формат(НомНовойСтроки, "ЧГ=0");
		
		Для НомерГрафы = 1 По мСтруктураКолвоКолонокРазделов[ИмяГруппы] Цикл
			
			КолИмя = ИмяГруппы + Формат(НомерГрафы, "ЧЦ=2; ЧВН=");
			
			Если СтруктураГруппы[0].Свойство(КолИмя) Тогда
				
				Строка1.Области[КолИмя + "_1"].Имя = КолИмя + "_" + НомСтр;
				
			КонецЕсли;
			
		КонецЦикла;
		
		РегламентированнаяОтчетность.ЗаполнитьСтрокуДаннымиИзТаблицы(ДанныеФормыВЗначение(СтруктураГруппы, Тип("ТаблицаЗначений")), Строка1, СтруктураГруппы[НомНовойСтроки - 1], НомСтр);
		ТаблДок.Вывести(Строка1);
		
	КонецЦикла;
		
	ТаблДок.Вывести(Подвал);
	
	Раздел.Очистить();
	Раздел.Вывести(ТаблДок);
		
	// Восстановим области.
	Для каждого Зап Из НастройкиМнгЧ Цикл
		Раздел.Область("R" + Формат(Зап.НизМногострочнойЧасти, "ЧГ=0") + ":R" + Формат(Зап.ВерхМногострочнойЧасти, "ЧГ=0")).Имя = Зап.Область;
	КонецЦикла;
		
КонецПроцедуры

Процедура ПолучитьСведения(ОргСведения, ПБОЮЛ, Организация, ДатаПодписи, СписокСведений, ДанныеПредставителя, КодФНС, КПП) Экспорт
	
	ОргСведения = РегламентированнаяОтчетностьВызовСервера.ПолучитьСведенияОбОрганизации(Организация, ДатаПодписи, СписокСведений);
	
	// Если в запросе данных передали параметр КодНО, то код Инспекции берем из справочника оргазинаций,
	// иначе, с титульного листа.
	Если ОргСведения.Свойство("КодНО") Тогда
		КодНО = ОргСведения.КодНО;
	Иначе
		КодНО = КодФНС;
	КонецЕсли;
		
	// Получим данные о представителе.
	ДанныеПредставителя = РегламентированнаяОтчетностьВызовСервера.ПолучитьСведенияОПредставителе(Организация, ДатаПодписи, Ложь, КодНО, КПП);
	
	ПБОЮЛ = НЕ РегламентированнаяОтчетностьВызовСервера.ЭтоЮридическоеЛицо(Организация);
		
КонецПроцедуры

Функция КПППоКомбинацииОрганизацииИКодаИФНС(Организация, КодИФНС) Экспорт
	
	Если НЕ Метаданные.Перечисления.Найти("ЮридическоеФизическоеЛицо") = Неопределено Тогда
		
		Если Организация.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо Тогда
			Возврат "";
		КонецЕсли;
		
	КонецЕсли;
	
	Запрос = Новый Запрос("ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	                      |	РегистрацииВНалоговомОргане.КПП КАК КПП
	                      |ИЗ
	                      |	Справочник.РегистрацииВНалоговомОргане КАК РегистрацииВНалоговомОргане
	                      |ГДЕ
	                      |	РегистрацииВНалоговомОргане.Владелец = &Владелец
	                      |	И РегистрацииВНалоговомОргане.Код = &Код
						  |	И РегистрацииВНалоговомОргане.ПометкаУдаления = &ПометкаУдаления");
						  
	Запрос.УстановитьПараметр("ПометкаУдаления", Ложь);					  
	Запрос.УстановитьПараметр("Владелец", Организация);
	Запрос.УстановитьПараметр("Код", КодИФНС);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		
		Возврат СокрЛП(Выборка.КПП);
		
	Иначе
		
		СведенияОбОрганизации = РегламентированнаяОтчетностьВызовСервера.ПолучитьСведенияОбОрганизации(Организация, ТекущаяДатаСеанса(), "КППЮЛ");
		
		Если ЗначениеЗаполнено(СведенияОбОрганизации.КППЮЛ) Тогда
			
			Возврат СокрЛП(СведенияОбОрганизации.КППЮЛ);
			
		Иначе
			
			Сообщение = Новый СообщениеПользователю;
			
			Сообщение.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Невозможно определить КПП для организации %1 и ИФНС с кодом %2!'"), Организация, КодИФНС);
			
			Сообщение.Сообщить();
			
			Возврат Неопределено;
			
		КонецЕсли
		
	КонецЕсли;
	
КонецФункции

Функция ВывестиСведенияСлужебнойЧастиИОбОтправителе30(Текст, Организация, КПП = Неопределено, КодНалоговогоОргана, ВерсияФормата = "3.00") Экспорт
	
	Если НЕ РегламентированнаяОтчетность.ВывестиСведенияСлужебнойЧасти30(Текст, Организация, , , КПП, КодНалоговогоОргана, , ВерсияФормата)
	 ИЛИ НЕ РегламентированнаяОтчетность.ВывестиСведенияОбОтправителе30(Текст, Организация) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

Функция ЭтоЮридическоеЛицо(Организация) Экспорт
	
	Возврат РегламентированнаяОтчетностьПереопределяемый.ЭтоЮридическоеЛицо(Организация);
	
КонецФункции

Функция ИННСоответствуетТребованиямНаСервере(Знач ИНН, Знач ТипЛица) Экспорт

	ИНН = СокрЛП(ИНН);
	ДлинаИНН =  СтрДлина(ИНН);

	Если НЕ СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(ИНН) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если Метаданные.Перечисления.Найти("ЮридическоеФизическоеЛицо") = Неопределено Тогда
		ПроверяетсяИННФизЛица = ТипЛица;
	Иначе
		ПроверяетсяИННФизЛица = (ТипЛица = Истина ИЛИ ТипЛица = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо);
	КонецЕсли;
	
	Если ДлинаИНН = 10  И НЕ ПроверяетсяИННФизЛица Тогда

		КонтрольнаяСумма = 0;

		Для Н = 1 По 9 Цикл

			Если 	  Н = 1 Тогда
				Множитель = 2;
			ИначеЕсли Н = 2 Тогда
				Множитель = 4;
			ИначеЕсли Н = 3 Тогда
				Множитель = 10;
			ИначеЕсли Н = 4 Тогда
				Множитель = 3;
			ИначеЕсли Н = 5 Тогда
				Множитель = 5;
			ИначеЕсли Н = 6 Тогда
				Множитель = 9;
			ИначеЕсли Н = 7 Тогда
				Множитель = 4;
			ИначеЕсли Н = 8 Тогда
				Множитель = 6;
			ИначеЕсли Н = 9 Тогда
				Множитель = 8;
			КонецЕсли; 
			
			Цифра = Число(Сред(ИНН,Н,1));
			КонтрольнаяСумма = КонтрольнаяСумма + Цифра * Множитель;
			
		КонецЦикла; 
		
		КонтрольныйРазряд = (КонтрольнаяСумма %11) %10;
		
		Если КонтрольныйРазряд <> Число(Сред(ИНН,10,1)) Тогда
			Возврат Ложь;
		КонецЕсли; 
		
	ИначеЕсли ДлинаИНН =12 И ПроверяетсяИННФизЛица Тогда
		
		КонтрольнаяСумма11 = 0;
		КонтрольнаяСумма12 = 0;
		
		Для Н=1 По 11 Цикл
			
			// Расчет множителя для 11-го и 12-го разрядов
			Если Н = 1 Тогда
				Множитель11 = 7;
				Множитель12 = 3;
			ИначеЕсли Н = 2 Тогда
				Множитель11 = 2;
				Множитель12 = 7;
			ИначеЕсли Н = 3 Тогда
				Множитель11 = 4;
				Множитель12 = 2;
			ИначеЕсли Н = 4 Тогда
				Множитель11 = 10;
				Множитель12 = 4;
			ИначеЕсли Н = 5 Тогда
				Множитель11 = 3;
				Множитель12 = 10;
			ИначеЕсли Н = 6 Тогда
				Множитель11 = 5;
				Множитель12 = 3;
			ИначеЕсли Н = 7 Тогда
				Множитель11 = 9;
				Множитель12 = 5;
			ИначеЕсли Н = 8 Тогда
				Множитель11 = 4;
				Множитель12 = 9;
			ИначеЕсли Н = 9 Тогда
				Множитель11 = 6;
				Множитель12 = 4;
			ИначеЕсли Н = 10 Тогда
				Множитель11 = 8;
				Множитель12 = 6;
			ИначеЕсли Н = 11 Тогда
				Множитель11 = 0;
				Множитель12 = 8;
			КонецЕсли; 
			
			Цифра = Число(Сред(ИНН,Н,1));
			КонтрольнаяСумма11 = КонтрольнаяСумма11 + Цифра * Множитель11;
			КонтрольнаяСумма12 = КонтрольнаяСумма12 + Цифра * Множитель12;
			
		КонецЦикла; 
		
		КонтрольныйРазряд11 = (КонтрольнаяСумма11 %11) %10;
		КонтрольныйРазряд12 = (КонтрольнаяСумма12 %11) %10;
		
		Если КонтрольныйРазряд11 <> Число(Сред(ИНН,11,1))
			ИЛИ КонтрольныйРазряд12 <> Число(Сред(ИНН,12,1)) Тогда
			Возврат Ложь;
		КонецЕсли; 
		
	Иначе
		
		Возврат Ложь;
		
	КонецЕсли; 
	
	Возврат Истина;
	
КонецФункции

Процедура УстановитьСчетчикВыгруженныхФайлов(Организация, СчФайлов, ТипСчетчика = Неопределено, ПериодУстановки = Неопределено, ИФНС = "") Экспорт
	
	Если ТипСчетчика = "СчетчикФайловАлко" Тогда
		ТипСчетчика = Перечисления.ТипыСчетчиковВыгрузки.СчетчикФайловАлко;
	КонецЕсли;
		
	МенЗап = РегистрыСведений.СчетчикиВыгрузок.СоздатьМенеджерЗаписи();
	МенЗап.Период = НачалоГода(?(ПериодУстановки = Неопределено, ТекущаяДатаСеанса(), ПериодУстановки));
	МенЗап.Организация = Организация;
	МенЗап.Тип = ?(ТипСчетчика = Неопределено, Перечисления.ТипыСчетчиковВыгрузки.СчетчикФайлов, ТипСчетчика);
	МенЗап.ИФНС = ?(ТекущаяДатаСеанса() < '20070701000000', "", ИФНС);
	МенЗап.Значение = СчФайлов;
	МенЗап.Записать(Истина);
	
КонецПроцедуры

Функция КодРегионаПоНазванию(Название) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Название) Тогда
		Возврат "";
	КонецЕсли;
	
	Назв = СокрЛП(Название);
	ПервыйПробел = Найти(Назв, " ");
	Если ПервыйПробел <> 0 Тогда
		Назв = Лев(Назв, ПервыйПробел - 1);
	КонецЕсли;
	
	КодРегиона = АдресныйКлассификатор.КодРегионаПоНаименованию(Назв);
	
	Если ЗначениеЗаполнено(КодРегиона) Тогда
		Возврат Формат(КодРегиона, "ЧЦ=2; ЧВН=");
	КонецЕсли;
	
	МакетРегионы = Обработки.ОбщиеОбъектыРегламентированнойОтчетности.ПолучитьМакет("Регионы");
	НРегАдресРегион = НРег(Название);
	Для Инд = 1 По МакетРегионы.ВысотаТаблицы Цикл
		ТекРегион = СокрЛП(МакетРегионы.Область(Инд, 1, Инд, 1).Текст);
		Если Лев(НРегАдресРегион, СтрДлина(ТекРегион)) = НРег(ТекРегион) Тогда
			Возврат СокрЛП(МакетРегионы.Область(Инд, 2, Инд, 2).Текст);
		КонецЕсли;
	КонецЦикла;
	
	Возврат "";
	
КонецФункции

Функция ПолучитьШаблоныМашиночитаемыхФорм(Знач ИмяОтчета, ПрефиксИмениМакетаШаблона, КоличествоВыгруженныхШаблонов, ТочныеПараметрыШаблонаЗаданы, ПрефиксИмениШаблона, ДистрибутивКомпонентыПечатиМашиночитаемыхФорм) Экспорт
	
	ШаблоныМашиночитаемыхФорм = Новый СписокЗначений;
	
	ОбъектМетаданных = ОбъектОтчета(ИмяОтчета);
	МакетыОтчета = ОбъектМетаданных.Метаданные().Макеты;
	//ИмяОтчета = Сред(Лев(ИмяОтчета, Найти(ИмяОтчета, ".Форма.") - 1), 7);
	//
	//ОбъектМетаданных = Метаданные.Отчеты.Найти(ИмяОтчета);
	//
	//МакетыОтчета = ОбъектМетаданных.Макеты;
	
	Для Каждого МакетОтчета Из МакетыОтчета Цикл
		Если МакетОтчета.ТипМакета = Метаданные.СвойстваОбъектов.ТипМакета.ДвоичныеДанные
		И Лев(нрег(МакетОтчета.Имя), СтрДлина(ПрефиксИмениМакетаШаблона)) = нрег(ПрефиксИмениМакетаШаблона) Тогда
			СинонимМакета = СокрЛП(МакетОтчета.Синоним);
			ВхождениеПробела = Найти(СинонимМакета, " ");
			ИмяФайлаШаблона = ?(ВхождениеПробела = 0, СинонимМакета, Лев(СинонимМакета, СтрДлина(ВхождениеПробела - 1)));
			Попытка
				//ШаблоныМашиночитаемыхФорм.Добавить(Отчеты[ОбъектМетаданных.Имя].Создать().ПолучитьМакет(МакетОтчета.Имя), ИмяФайлаШаблона);
				ШаблоныМашиночитаемыхФорм.Добавить(ОбъектМетаданных.ПолучитьМакет(МакетОтчета.Имя), ИмяФайлаШаблона);
				КоличествоВыгруженныхШаблонов = КоличествоВыгруженныхШаблонов + 1;
			Исключение
								
				Сообщение = Новый СообщениеПользователю;
				
				Сообщение.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Не удалось получить шаблон машиночитаемой формы ""%1""!%2'"), ИмяФайлаШаблона, Символы.ПС + ОписаниеОшибки());
				
				Сообщение.Сообщить();
				
			КонецПопытки;
		КонецЕсли;
	КонецЦикла;
	
	// выгружаем внешние шаблоны
	Если ТочныеПараметрыШаблонаЗаданы Тогда
		РезультатЗапроса = РегламентированнаяОтчетность.ВыполнитьЗапросКРегиструШаблоновПечатиМашиночитаемыхФорм(ПрефиксИмениШаблона + "%");
		Если КоличествоВыгруженныхШаблонов = 0 И РезультатЗапроса.Пустой() Тогда
			РезультатЗапроса = РегламентированнаяОтчетность.ВыполнитьЗапросКРегиструШаблоновПечатиМашиночитаемыхФорм();
		КонецЕсли;
	Иначе
		РезультатЗапроса = РегламентированнаяОтчетность.ВыполнитьЗапросКРегиструШаблоновПечатиМашиночитаемыхФорм();
	КонецЕсли;
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			Попытка
				ШаблоныМашиночитаемыхФорм.Добавить(Выборка.Шаблон.Получить(), Выборка.ИмяФайлаШаблона);
			Исключение
				
				Сообщение = Новый СообщениеПользователю;
				
				Сообщение.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Не удалось получить шаблон машиночитаемой формы ""%1""!%2'"), Выборка.ИмяФайлаШаблона, Символы.ПС + ОписаниеОшибки());
				
				Сообщение.Сообщить();
				
			КонецПопытки;
		КонецЦикла;
	КонецЕсли;
		
	РегламентированнаяОтчетностьВызовСервера.ПолучитьДистрибутивКомпонентыПечатиМашиночитаемыхФорм(ДистрибутивКомпонентыПечатиМашиночитаемыхФорм);
	
	Возврат ШаблоныМашиночитаемыхФорм;
	
КонецФункции

Процедура ПолучитьДистрибутивКомпонентыПечатиМашиночитаемыхФорм(ДистрибутивКомпонентыПечатиМашиночитаемыхФорм) Экспорт
	
	МетаданныеМакета = Метаданные.Обработки.ОбщиеОбъектыРегламентированнойОтчетности.Макеты.ДистрибутивКомпонентыПечатиМашиночитаемыхФорм;
		
	ДистрибутивКомпонентыПечатиМашиночитаемыхФорм.Добавить(Обработки.ОбщиеОбъектыРегламентированнойОтчетности.ПолучитьМакет(МетаданныеМакета.Имя), МетаданныеМакета.Синоним);
	
КонецПроцедуры

Функция ПолучитьСсылкуНаФормуРеглОтчета(ТекДок, ПараметрыФормы) Экспорт
			
	ПараметрыФормы.Вставить("мДатаНачалаПериодаОтчета", ТекДок.ДатаНачала);
	ПараметрыФормы.Вставить("мСохраненныйДок", ТекДок.Ссылка);
	ПараметрыФормы.Вставить("мДатаКонцаПериодаОтчета", ТекДок.ДатаОкончания);
	ПараметрыФормы.Вставить("Организация", ТекДок.Организация);
	ПараметрыФормы.Вставить("мВыбраннаяФорма", ТекДок.ВыбраннаяФорма);
		
	Возврат Строка(РегламентированнаяОтчетность.ФормаРеглОтчета(ТекДок.ИсточникОтчета, ТекДок.ВыбраннаяФорма, , ТекДок.Ссылка)) + ".Форма." + Строка(ТекДок.ВыбраннаяФорма);
		
КонецФункции

Функция ВерсияФормыСовпадаетСВерсиейСохраненногоОтчета(Док, ВерсияФормы) Экспорт
	
	ВерсияФормыДанных = Док.ДанныеОтчета.Получить().ВерсияФормы;
	
	Если НЕ РегламентированнаяОтчетность.ПродолжитьПриНесоответствииВерсийФорм(ВерсияФормыДанных, ВерсияФормы) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

Функция ПолучитьПараметрыФормыИзСохраненногоОтчета(Док, ПредставлениеДокументаРеглОтч) Экспорт
	
	ПредставлениеДокументаРеглОтч = РегламентированнаяОтчетностьКлиентСервер.ПредставлениеДокументаРеглОтч(Док.Ссылка);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("мДатаНачалаПериодаОтчета", НачалоДня(Док.ДатаНачала));
	ПараметрыФормы.Вставить("мДатаКонцаПериодаОтчета",  КонецДня(Док.ДатаОкончания));
	ПараметрыФормы.Вставить("мСохраненныйДок",          Док.Ссылка);
	ПараметрыФормы.Вставить("мПериодичность",           Док.Периодичность);
	ПараметрыФормы.Вставить("мВыбраннаяФорма",          Док.ВыбраннаяФорма);
	ПараметрыФормы.Вставить("ИсточникОтчета",           Док.ИсточникОтчета);
	ПараметрыФормы.Вставить("Организация",              Док.Организация);
			
	Возврат ПараметрыФормы;
	
КонецФункции

Функция ПроверитьВозможностьВыгрузки(Доки) Экспорт
	
	Для Каждого Док Из Доки Цикл
		Если Док.Значение.Организация <> Доки.Получить(0).Значение.Организация Тогда
			Возврат 1;
		КонецЕсли;
		Если КонецДня(Док.Значение.ДатаОкончания) <> КонецДня(Доки.Получить(0).Значение.ДатаОкончания) Тогда
			Возврат 2;
		КонецЕсли;
	КонецЦикла;
		
	Возврат 0;
	
КонецФункции

Функция СформироватьСтруктуруПараметровФайлаВыгрузкиНаСервере(Адрес, ВерФормВыгрузки) Экспорт
	
	ВремФайл = ПолучитьИмяВременногоФайла();
	
	ТекстДок = ПолучитьИзВременногоХранилища(Адрес);
		
	ТекстДок.Записать(ВремФайл, ?(СтрЧислоВхождений(ВерФормВыгрузки, "Версия 2") > 0 ИЛИ СтрЧислоВхождений(ВерФормВыгрузки, "Версия 3") > 0, "cp866", "windows-1251"));
	
	Возврат РегламентированнаяОтчетностьКлиентСервер.СформироватьСтруктуруПараметровФайлаВыгрузки(ВремФайл);

КонецФункции

Функция РазделТекущейОбластиНаСервере(Форма, ТекущаяОбласть, ДлИмениОбласти = 13) Экспорт
	
	Если ТекущаяОбласть = Неопределено Тогда
		Возврат Неопределено; // отсутствует активная область табличного документа
	Иначе
		ИмяТекОбласти = ТекущаяОбласть.Имя;
		ПозицияНачалаИмениПараметра = Найти(ИмяТекОбласти, "П0");
		ИмяАктивногоРаздела = Сред(ИмяТекОбласти, ПозицияНачалаИмениПараметра, ДлИмениОбласти);
	КонецЕсли;
	
	Для Каждого ЭлементСтруктуры Из Форма.мСтруктураМногострочныхРазделов Цикл
		ИмяГруппы = ЭлементСтруктуры.Ключ;
		СтруктураГруппы = Форма[ЭлементСтруктуры.Значение];
		Если СтруктураГруппы[0].Свойство(ИмяАктивногоРаздела) Тогда
			Возврат ИмяГруппы;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции

Функция ПолучитьСведенияОПредставителеОрганизации(Организация, КодИФНС, КПП, ДатаПодписиОтчета)	Экспорт
	
	Результат = Новый Структура("ПредставительЮр, ПредставительФЛ, ДокументПредставителя, Фамилия, Имя, Отчество", "", "", "", "", "", "");
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Владелец", Организация);
	Запрос.УстановитьПараметр("КодИФНС", КодИФНС);
	Запрос.УстановитьПараметр("КПП", КПП);
	Запрос.УстановитьПараметр("ПометкаУдаления", Ложь);
	
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	СправочникИФНС.КПП,
	               |	СправочникИФНС.Код КАК КодНО,
	               |	СправочникИФНС.НаименованиеИФНС КАК Наименование,
	               |	СправочникИФНС.Представитель,
	               |	СправочникИФНС.ДокументПредставителя
	               |ИЗ
	               |Справочник.РегистрацииВНалоговомОргане КАК СправочникИФНС
	               |ГДЕ
	               |СправочникИФНС.Владелец = &Владелец
	               | И СправочникИФНС.Код = &КодИФНС
				   | И СправочникИФНС.КПП = &КПП
                   | И СправочникИФНС.ПометкаУдаления = &ПометкаУдаления";
	СправочникИФНС = Запрос.Выполнить().Выгрузить();
	Если СправочникИФНС.Количество() = 0 Тогда
		Возврат Результат;
	КонецЕсли;
	
	// В справочнике регистрации ИФНС может быть только информация по одной ИФНС!
	Представитель = СправочникИФНС[0].Представитель;
	Если НЕ РегламентированнаяОтчетность.ПредставительЯвляетсяФизЛицом(Представитель) Тогда
		Если ЗначениеЗаполнено(Представитель) Тогда
			Если Представитель.Метаданные().Реквизиты.Найти("НаименованиеПолное") = Неопределено Тогда
				Результат.Вставить("ПредставительЮр", Представитель.Наименование);	
			Иначе
				Результат.Вставить("ПредставительЮр", Представитель.НаименованиеПолное);
			КонецЕсли;
		КонецЕсли;
	ИначеЕсли ЗначениеЗаполнено(Представитель) Тогда
		ЗаполнитьЗначенияСвойств(Результат, РегламентированнаяОтчетность.ПолучитьФИОФизЛица(Представитель, ДатаПодписиОтчета));
		Результат.Вставить("ПредставительФЛ", СокрЛП(СокрЛП(Результат.Фамилия) + " " + СокрЛП(Результат.Имя) + " " + СокрЛП(Результат.Отчество)));
	КонецЕсли;
	Результат.Вставить("ДокументПредставителя", СправочникИФНС[0].ДокументПредставителя);
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьПоКодамСведенияОПредставителе(Организация, КодНО, КПП = Неопределено) Экспорт
	
	ТипПодписанта = "1";
	флПредставительЮрЛицо = Истина;
	НаименованиеОрганизацииПредставителя = "";
	ФИОПредставителя = "";
	ПредставительСсылка = Неопределено;
	ДокументПредставителя = "";
	               	
	Запрос = Новый Запрос;
	
	ТекстЗапроса = "ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	               |	РегистрацииВНалоговомОргане.Представитель,
	               |	РегистрацииВНалоговомОргане.УполномоченноеЛицоПредставителя,
	               |	РегистрацииВНалоговомОргане.ДокументПредставителя
	               |ИЗ
	               |	Справочник.РегистрацииВНалоговомОргане КАК РегистрацииВНалоговомОргане
	               |ГДЕ
	               |	(РегистрацииВНалоговомОргане.Владелец = &Организация
				   |			ИЛИ РегистрацииВНалоговомОргане.Владелец = &ГоловнаяОрганизация)
				   |	И РегистрацииВНалоговомОргане.Код = &КодНО
				   |	И НЕ РегистрацииВНалоговомОргане.ПометкаУдаления";
				   
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("КодНО", КодНО);
	Запрос.УстановитьПараметр("ГоловнаяОрганизация", РегламентированнаяОтчетность.ГоловнаяОрганизация(Организация));
		
	Если КПП <> Неопределено Тогда
		ТекстЗапроса = ТекстЗапроса + " И РегистрацииВНалоговомОргане.КПП = &КПП";
		Запрос.УстановитьПараметр("КПП", КПП);
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() И ЗначениеЗаполнено(Выборка.Представитель) Тогда
		ТипПодписанта = "2";
		ПредставительСсылка = Выборка.Представитель;
		ДокументПредставителя = Выборка.ДокументПредставителя;
		
		Если НЕ РегламентированнаяОтчетность.ПредставительЯвляетсяФизЛицом(Выборка.Представитель) Тогда
			флПредставительЮрЛицо = Истина;
			НаименованиеОрганизацииПредставителя = СокрЛП(ПредставительСсылка);
			ФИОПредставителя = СокрЛП(Выборка.УполномоченноеЛицоПредставителя);
		Иначе
			флПредставительЮрЛицо = Ложь;
			ФИОПредставителя = СокрЛП(ПредставительСсылка);
		КонецЕсли;
		
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("ТипПодписанта", ТипПодписанта);
	Результат.Вставить("флПредставительЮрЛицо", флПредставительЮрЛицо);
	Результат.Вставить("НаименованиеОрганизацииПредставителя", НаименованиеОрганизацииПредставителя);
	Результат.Вставить("ФИОПредставителя", ФИОПредставителя);
	Результат.Вставить("ПредставительСсылка", ПредставительСсылка);
	Результат.Вставить("ДокументПредставителя", ДокументПредставителя);
	
	Возврат Результат;						 
							 
КонецФункции

Функция ЗаменитьТекстРегионаНаКодРегиона(Знач АдресВФормате9зпт) Экспорт

	Параметры = Новый Массив;

	ПредыдущаяЗапятая = 0;
	Для Сч = 1 По СтрДлина(АдресВФормате9зпт) Цикл
		ТекСимв = Сред(АдресВФормате9зпт, Сч, 1);
		Если ТекСимв = "," Тогда
			Параметры.Добавить(Сред(АдресВФормате9зпт, ПредыдущаяЗапятая + 1, Сч - (ПредыдущаяЗапятая + 1)));
			ПредыдущаяЗапятая = Сч;
		КонецЕсли;
	КонецЦикла;

	Если ПредыдущаяЗапятая <> СтрДлина(АдресВФормате9зпт) Тогда
		Параметры.Добавить(Сред(АдресВФормате9зпт, ПредыдущаяЗапятая + 1, СтрДлина(АдресВФормате9зпт) - ПредыдущаяЗапятая));
	КонецЕсли;
	
	Для Сч = Параметры.Количество() + 1 По 13 Цикл
		Параметры.Добавить("");
	КонецЦикла;

	Регион = Параметры[2];

	Если НЕ СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(Регион) Тогда
		Регион = Формат(РегламентированнаяОтчетностьВызовСервера.КодРегионаПоНазванию(Регион), "ЧЦ=2; ЧН=; ЧВН=");
	КонецЕсли;
				
	Возврат Параметры[0] + "," + Параметры[1] + "," + Регион		+ "," + Параметры[3] + "," +
	        Параметры[4] + "," + Параметры[5] + "," + Параметры[6]	+ "," + Параметры[7] + "," +
	        Параметры[8] + "," + Параметры[9] + "," + Параметры[10] + "," + Параметры[11] + "," + Параметры[12];		

КонецФункции

Функция ДокументыСУчетомКритериевОтбораКалендарь(ИсточникОтчета, ДатаНачалаПериодаОтчета, ДатаКонцаПериодаОтчета, ОрганизацияОтбор, КодИФНС, Периодичность) Экспорт

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ИсточникОтчета", ИсточникОтчета);
	Запрос.УстановитьПараметр("Организация", ОрганизацияОтбор);
	Запрос.УстановитьПараметр("КодИМНС", КодИФНС);
	ДатаОконч = ?(НЕ ЗначениеЗаполнено(ДатаКонцаПериодаОтчета), '20291231', ДатаКонцаПериодаОтчета);
	Запрос.УстановитьПараметр("ДатаОкончания", НачалоДня(ДатаОконч));
	Запрос.УстановитьПараметр("ДатаОкончанияКон", КонецДня(ДатаОконч));
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	РегламентированныйОтчет.Ссылка
	               |ИЗ
	               |	Документ.РегламентированныйОтчет КАК РегламентированныйОтчет
	               |ГДЕ
	               |	РегламентированныйОтчет.ИсточникОтчета = &ИсточникОтчета
			   	   |	И РегламентированныйОтчет.Организация = &Организация";
	Если НЕ ПустаяСтрока(КодИФНС) Тогда
		Запрос.Текст = Запрос.Текст + "
	               |	И РегламентированныйОтчет.КодИМНС = &КодИМНС";
	КонецЕсли;
	Запрос.Текст = Запрос.Текст + "
				   |	И (РегламентированныйОтчет.ДатаОкончания МЕЖДУ &ДатаОкончания И &ДатаОкончанияКон)";
	
	ТаблРез = Запрос.Выполнить().Выгрузить();
	КолТаблРез = ТаблРез.Количество();
	Если КолТаблРез = 0 Тогда
		Возврат 0;
	Иначе
		Возврат КолТаблРез;
	КонецЕсли;
	
КонецФункции

Процедура ПроверитьДоступностьОтчета(ИсточникОтчета, ДатаНач, ДатаКон, Периодичность, ОрганизацияОтбор) Экспорт
	
	ТекОтчет = РегламентированнаяОтчетность.РеглОтчеты(ИсточникОтчета);
	Если ТекОтчет = Неопределено Тогда
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru = 'Не удалось открыть отчет!'");
		Сообщение.Сообщить();
		
		Возврат;
	КонецЕсли;
	
	ТекФорма = РегламентированнаяОтчетность.ФормаРеглОтчета(ИсточникОтчета);
	Если ТекФорма = Неопределено Тогда
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru = 'Не удалось открыть отчет!'");
		Сообщение.Сообщить();
		
		Возврат;
	КонецЕсли;

	Попытка
		ТекФорма.Организация =  ОрганизацияОтбор;
	Исключение
	КонецПопытки;

	Попытка
		ТекФорма.мДатаНачалаПериодаОтчета = ДатаНач;
		ТекФорма.мДатаКонцаПериодаОтчета = ДатаКон;
	Исключение
	КонецПопытки;

	Если РегламентированнаяОтчетностьКлиентСервер.СвойствоОпределено(ТекФорма, "мПериодичность") Тогда
		Если Периодичность <> Неопределено Тогда
			ТекФорма.мПериодичность = Периодичность;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Функция СформироватьНапоминанияПользователяКалендарьБухгалтера() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|Пользователь
	|, ПоследняяДатаДействия
	|, ИсточникОтчета
	|, ЗаПериодС
	|, ЗаПериодПо
	|, Организация
	|, ТипСообщения
	|, ФормаОтчета
	|, Периодичность
	|, Название
	|, Налогоплательщик
	|, ДобавитьКДате(ПоследняяДатаДействия, День, &ДниНапоминаний) Как СрокНачалаНапоминаний
	|, НарастающийИтог
	|, ДатаПоказа
	|ИЗ РегистрСведений.СобытияКалендаряБухгалтера
	|ГДЕ
	|Пользователь = &Пользователь
	|И &РабочаяДата >= ДобавитьКДате(ПоследняяДатаДействия, День, &ДниНапоминаний)
	|И ДатаПоказа <> &ДатаПоказа";

	КолВоДней = 5;

	ТекРабочаяДата = ТекущаяДатаСеанса();
	Запрос.УстановитьПараметр("Пользователь", Пользователи.ТекущийПользователь());
	Запрос.УстановитьПараметр("ДниНапоминаний", (-1) * КолВоДней);
	Запрос.УстановитьПараметр("РабочаяДата", ТекРабочаяДата);
	Запрос.УстановитьПараметр("ДатаПоказа", ТекРабочаяДата);
	ТаблРезультат = Запрос.Выполнить().Выгрузить();

	Если ТаблРезультат.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;

	Напоминания = Новый Массив;
	СпрРегОтчетыПустаяСсылка = Справочники.РегламентированныеОтчеты.ПустаяСсылка();
	Для каждого Зап Из ТаблРезультат Цикл
		Запись = РегистрыСведений.СобытияКалендаряБухгалтера.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(Запись, Зап);
		Запись.Прочитать();
		Если Запись.Выбран() Тогда
			Запись.ДатаПоказа = ТекРабочаяДата;
			Запись.Записать();
			Зап.ДатаПоказа = ТекРабочаяДата;
		КонецЕсли;
		
		Если Зап.ТипСообщения = Перечисления.СообщенияДляРегОтчетов.СдачаОтчета Тогда
			ОбъектНапоминание = Зап.ИсточникОтчета;
			НайденныйЭлемент = Справочники.РегламентированныеОтчеты.НайтиПоРеквизиту("ИсточникОтчета", ОбъектНапоминание);
			
			СкрытыеРегламентированныеОтчеты = РегистрыСведений.СкрытыеРегламентированныеОтчеты.СоздатьМенеджерЗаписи();
			СкрытыеРегламентированныеОтчеты.РегламентированныйОтчет = НайденныйЭлемент.Ссылка;
			СкрытыеРегламентированныеОтчеты.Прочитать();
			
			Если НайденныйЭлемент = СпрРегОтчетыПустаяСсылка
				Или СкрытыеРегламентированныеОтчеты.Выбран() Тогда
				Продолжить;
			КонецЕсли;
		КонецЕсли;

		СобытиеКалендаря = Новый Структура;
		СобытиеКалендаря.Вставить("Срок", Зап.ПоследняяДатаДействия);
		СобытиеКалендаря.Вставить("ИсточникОтчета", Зап.ИсточникОтчета);
		СобытиеКалендаря.Вставить("ЗаПериодС", Зап.ЗаПериодС);
		СобытиеКалендаря.Вставить("ЗаПериодПо", Зап.ЗаПериодПо);
		СобытиеКалендаря.Вставить("Организация", Зап.Организация);
		СобытиеКалендаря.Вставить("ТипСообщения", Зап.ТипСообщения);
		СобытиеКалендаря.Вставить("ФормаОтчета", Зап.ФормаОтчета);
		СобытиеКалендаря.Вставить("Периодичность", Зап.Периодичность);
		СобытиеКалендаря.Вставить("НазваниеОтчетаИлиНалога", Зап.Название);
		СобытиеКалендаря.Вставить("Налогоплательщик", Зап.Налогоплательщик);
		СобытиеКалендаря.Вставить("НарастающийИтог", Зап.НарастающийИтог);
		СобытиеКалендаря.Вставить("ДатаПоказа", Зап.ДатаПоказа);
		Напоминания.Добавить(СобытиеКалендаря);
	КонецЦикла;
	
	Возврат Напоминания;

КонецФункции

Функция ВерсияФорматаВыгрузки(ВыбраннаяФорма, ИсточникОтчета, НаДату = Неопределено) Экспорт

	Попытка
		
		Возврат РегламентированнаяОтчетность.РеглОтчеты(ИсточникОтчета).ВерсияФорматаВыгрузки(НаДату, ВыбраннаяФорма);
		
	Исключение
		
		Возврат Неопределено;
		
	КонецПопытки;

КонецФункции

Функция ИмяФормыРеглОтчетаДействующейВОтчетномПериоде(ИсточникОтчета, ДатаОкончания, СсылкаНаСохрРеглОтчет = Неопределено) Экспорт
	
	Если НЕ СсылкаНаСохрРеглОтчет = Неопределено Тогда
		
		Возврат СсылкаНаСохрРеглОтчет.ВыбраннаяФорма;
		
	КонецЕсли;
	
	ТаблицаФормОтчета = РегламентированнаяОтчетность.РеглОтчеты(ИсточникОтчета).ТаблицаФормОтчета();
	
	ФормаОтчета = Неопределено;
	
	Для Каждого Строка Из ТаблицаФормОтчета Цикл
		
		Если (Строка.ДатаНачалоДействия > КонецДня(ДатаОкончания)) ИЛИ
			((Строка.ДатаКонецДействия > '00010101000000') И (Строка.ДатаКонецДействия < НачалоДня(ДатаОкончания))) Тогда

			Продолжить;
			
		КонецЕсли;

		ФормаОтчета = Строка.ФормаОтчета;

		Возврат ФормаОтчета;
		
	КонецЦикла;

	// Если не удалось найти форму, соответствующую выбранному периоду,
	// то по умолчанию выдаем текущую (действующую) форму.
	//Если ФормаОтчета = Неопределено Тогда
	//	ФормаОтчета = ТаблицаФормОтчета[0].ФормаОтчета;
	//КонецЕсли;
	
	Возврат ФормаОтчета;
	
КонецФункции

Функция ФормаРеглОтчетаДействуетВОтчетномПериоде(ИсточникОтчета, ДатаОкончания, ИмяФормыОтчета) Экспорт
	
	Если НЕ (ЗначениеЗаполнено(ИсточникОтчета)
		И ЗначениеЗаполнено(ДатаОкончания)
		И ЗначениеЗаполнено(ИмяФормыОтчета)) Тогда
		
		Возврат Ложь;
		
	КонецЕсли;
	
	ТаблицаФормОтчета = РегламентированнаяОтчетность.РеглОтчеты(ИсточникОтчета).ТаблицаФормОтчета();
	
	Для Каждого Строка Из ТаблицаФормОтчета Цикл
		
		Если (Строка.ДатаНачалоДействия > КонецДня(ДатаОкончания))
		 ИЛИ ((Строка.ДатаКонецДействия > '00010101000000')
		   И (Строка.ДатаКонецДействия < НачалоДня(ДатаОкончания))) Тогда
			
			Продолжить;
			
		ИначеЕсли Строка.ФормаОтчета = ИмяФормыОтчета Тогда
			
			Возврат Истина;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

Процедура СформироватьАдрес(КонтактнаяИнформация, РоссийскийАдрес) Экспорт
	
	ЗначенияПолей = УправлениеКонтактнойИнформацией.ПредыдущийФорматКонтактнойИнформацииXML(КонтактнаяИнформация);
	
	Для Каждого Элемент Из РоссийскийАдрес Цикл
		РоссийскийАдрес[Элемент.Ключ] = "";
	КонецЦикла;
	
	Для НомСтр = 1 По СтрЧислоСтрок(ЗначенияПолей) Цикл
		
		Стр = СтрПолучитьСтроку(ЗначенияПолей, НомСтр);
		
		ПредставлениеСтр = Лев(Стр, Найти(Стр, "=") - 1);
		ЗначениеСтр = Сред(Стр, Найти(Стр, "=") + 1);
		
		Если ТипЗнч(РоссийскийАдрес) = Тип("Соответствие") И ЗначениеЗаполнено(ПредставлениеСтр) И НЕ РоссийскийАдрес.Получить(ПредставлениеСтр) = Неопределено Тогда
			РоссийскийАдрес[ПредставлениеСтр] = ЗначениеСтр;
		ИначеЕсли ТипЗнч(РоссийскийАдрес) = Тип("Структура") И ЗначениеЗаполнено(ПредставлениеСтр) И РоссийскийАдрес.Свойство(ПредставлениеСтр) Тогда
			РоссийскийАдрес[ПредставлениеСтр] = ЗначениеСтр;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ПроверитьВозможностьВыводаМашиночитаемойФормыЧерезВебСервис(АдресПараметровПроксиВоВременномХранилище = "", ПолучитьОпределенияВебСервиса = Истина, ЭтоВызовИзВебКлиента = Ложь) Экспорт
	
	ИспользоватьВебСервисДляФормированияМашиночитаемыхФорм = Ложь;
	ИспользоватьВебСервисТолькоВРежимеВебКлиента           = Ложь;
	АдресТочкиПодключенияВебСервиса = "";
	ИмяПользователяВебСервиса       = "";
	ПарольПользователяВебСервиса    = "";
	
	УстановитьПривилегированныйРежим(Истина);
	
	ИспользоватьВебСервисДляФормированияМашиночитаемыхФорм = Константы.ИспользоватьСервисФормированияМЧБсPDF417.Получить();
	ИспользоватьВебСервисТолькоВРежимеВебКлиента           = Константы.ИспользоватьСервисФормированияМЧБсPDF417ТолькоВРежимеВебКлиента.Получить();
	АдресТочкиПодключенияВебСервиса                        = Константы.АдресСервисаФормированияМЧБсPDF417.Получить();
	ИмяПользователяВебСервиса                              = Константы.ИмяПользователяСервисаФормированияМЧБсPDF417.Получить();
	ПарольПользователяВебСервиса                           = Константы.ПарольПользователяСервисаФормированияМЧБсPDF417.Получить();
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Если ИспользоватьВебСервисДляФормированияМашиночитаемыхФорм <> Истина Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если ИспользоватьВебСервисТолькоВРежимеВебКлиента = Истина И НЕ ЭтоВызовИзВебКлиента Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если НЕ ПолучитьОпределенияВебСервиса Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если Не ОбщегоНазначенияКлиентСервер.ПодсистемаСуществует("СтандартныеПодсистемы.РаботаВМоделиСервиса") Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru = 'Невозможно подключиться к веб-сервису формирования машиночитаемых бланков.'");
		Сообщение.Сообщить();
		Возврат Ложь;
	КонецЕсли;
	
	АдресТочкиПодключенияВебСервиса = СокрЛП(АдресТочкиПодключенияВебСервиса);
	Если ПустаяСтрока(АдресТочкиПодключенияВебСервиса) Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru='Не заполнен адрес точки подключения веб-сервиса!'");
		Сообщение.Сообщить();
		Возврат Ложь;
	КонецЕсли;
	
	ИмяВебСервиса = "CreateRepWithPDF417";
	
	АдресТочкиПодключенияВебСервиса = ?(Прав(АдресТочкиПодключенияВебСервиса, 1) = "/", Сред(АдресТочкиПодключенияВебСервиса, 1, СтрДлина(АдресТочкиПодключенияВебСервиса) - 1), АдресТочкиПодключенияВебСервиса);
	АдресВебСервиса = АдресТочкиПодключенияВебСервиса + "/ws/" + ИмяВебСервиса + "?wsdl";
	URIПространстваИмен = "http://www.1c.ru/LibraryRegulatoryReporting/1.0";
	Таймаут = 45;
	
	Попытка
		Прокси = ОбщегоНазначения.WSПрокси(АдресВебСервиса,
										   URIПространстваИмен,
										   ИмяВебСервиса, 
										   , 
										   ИмяПользователяВебСервиса, 
										   ПарольПользователяВебСервиса, 
										   Таймаут,
										   Истина);
	Исключение
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru='Не удалось получить подключение к веб-сервису!'");
		Сообщение.Сообщить();
		Возврат Ложь;
	КонецПопытки;
	
	ПараметрыПрокси = Новый Структура;
	ПараметрыПрокси.Вставить("АдресWSDL",           АдресВебСервиса);
	ПараметрыПрокси.Вставить("URIПространстваИмен", URIПространстваИмен);
	ПараметрыПрокси.Вставить("ИмяСервиса",          ИмяВебСервиса);
	ПараметрыПрокси.Вставить("ИмяТочкиПодключения", "");
	ПараметрыПрокси.Вставить("ИмяПользователя",     ИмяПользователяВебСервиса);
	ПараметрыПрокси.Вставить("Пароль",              ПарольПользователяВебСервиса);
	ПараметрыПрокси.Вставить("Таймаут",             Таймаут);
	
	АдресПараметровПроксиВоВременномХранилище = ПоместитьВоВременноеХранилище(Новый ФиксированнаяСтруктура(ПараметрыПрокси), Новый УникальныйИдентификатор());
	
	Возврат Истина;
	
КонецФункции

Функция ВывестиМашиночитаемуюФормуЧерезВебСервис(ИмяФормыОтчета, НаименованиеИФНС, ФайлВыгрузкиВоВременномХранилище, ПараметрыСформированногоОтчета, АдресПараметровПроксиВоВременномХранилище) Экспорт
	
	ПараметрыСформированногоОтчета = Новый Структура;
	ПараметрыСформированногоОтчета.Вставить("ИДСессии", "");
	ПараметрыСформированногоОтчета.Вставить("АдресОтчетаВоВременномХранилище");
	ПараметрыСформированногоОтчета.Вставить("ВремяОжиданияДоПолученияОтчета", 0);
	
	Попытка
		ПараметрыПрокси = ПолучитьИзВременногоХранилища(АдресПараметровПроксиВоВременномХранилище);
		
		Прокси = ОбщегоНазначения.WSПрокси(ПараметрыПрокси.АдресWSDL,
										   ПараметрыПрокси.URIПространстваИмен,
										   ПараметрыПрокси.ИмяСервиса, 
										   ПараметрыПрокси.ИмяТочкиПодключения, 
										   ПараметрыПрокси.ИмяПользователя, 
										   ПараметрыПрокси.Пароль, 
										   ПараметрыПрокси.Таймаут,
										   Ложь);
	Исключение
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru='Не удалось получить подключение к веб-сервису! Проверьте адрес точки подключения, имя, пароль или доступность веб-сервиса.'");
		Сообщение.Сообщить();
		Возврат Ложь;
	КонецПопытки;
	
	АдресФайлаВыгрузкиВоВременномХранилище = Неопределено;
	ИмяФайлаВыгрузки                       = Неопределено;
	КодировкаФайлаВыгрузки                 = Неопределено;
	ФайлВыгрузкиВоВременномХранилище.Свойство("АдресФайлаВыгрузкиВоВременномХранилище", АдресФайлаВыгрузкиВоВременномХранилище);
	ФайлВыгрузкиВоВременномХранилище.Свойство("ИмяФайлаВыгрузки",                       ИмяФайлаВыгрузки);
	ФайлВыгрузкиВоВременномХранилище.Свойство("КодировкаФайлаВыгрузки",                 КодировкаФайлаВыгрузки);
	
	Если НЕ ЭтоАдресВременногоХранилища(АдресФайлаВыгрузкиВоВременномХранилище) Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru='Не удалось получить файл выгрузки отчета на сервере!'");
		Сообщение.Сообщить();
		Возврат Ложь;
	КонецЕсли;
	
	КаталогВремФайлов = КаталогВременныхФайлов();
	ФайлВыгрузки = ?(ЗначениеЗаполнено(ИмяФайлаВыгрузки), КаталогВремФайлов + ИмяФайлаВыгрузки, ПолучитьИмяВременногоФайла("tmp"));
	ФайлВыгрузкиДвоичныеДанные = ПолучитьИзВременногоХранилища(АдресФайлаВыгрузкиВоВременномХранилище);
	ФайлВыгрузкиДвоичныеДанные.Записать(ФайлВыгрузки);
	
	СтруктураПараметров = РегламентированнаяОтчетностьКлиентСервер.СформироватьСтруктуруПараметровФайлаВыгрузки(ФайлВыгрузки);
	Попытка
		УдалитьФайлы(ФайлВыгрузки);
	Исключение
	КонецПопытки;
	
	КНД = СтруктураПараметров["КНД"];
	ВерсияФорматаВыгрузки = ?(СтруктураПараметров["ЭтоXML"] = Истина, СтруктураПараметров["ВерсФорм"], СтруктураПараметров["ВерФОтч"]);
	
	ТочныеПараметрыШаблонаЗаданы = (ЗначениеЗаполнено(КНД) И ЗначениеЗаполнено(ВерсияФорматаВыгрузки));
	
	Если ТочныеПараметрыШаблонаЗаданы Тогда
		Пока СтрДлина(ВерсияФорматаВыгрузки) < 7 Цикл
			ВерсияФорматаВыгрузки = ВерсияФорматаВыгрузки + "0";
		КонецЦикла;
		ПрефиксИмениШаблона = СокрЛП(КНД + "_" + ВерсияФорматаВыгрузки);
		ПрефиксИмениМакетаШаблона = "МБ_" + СтрЗаменить(ПрефиксИмениШаблона, ".", "_");
	Иначе
		ПрефиксИмениШаблона = "";
		ПрефиксИмениМакетаШаблона = "МБ_";
	КонецЕсли;
	
	КоличествоВыгруженныхШаблонов = 0;
	ДистрибутивКомпонентыПечатиМашиночитаемыхФорм = Новый СписокЗначений;
	// если точные параметры заданы, то сначала пытаемся извлечь внутренний шаблон
	ШаблоныМашиночитаемыхФорм = РегламентированнаяОтчетностьВызовСервера.ПолучитьШаблоныМашиночитаемыхФорм(ИмяФормыОтчета, ПрефиксИмениМакетаШаблона, КоличествоВыгруженныхШаблонов, ТочныеПараметрыШаблонаЗаданы, ПрефиксИмениШаблона, ДистрибутивКомпонентыПечатиМашиночитаемыхФорм);
	
	Если НЕ ПроверитьИВыборочноОбновитьШаблоныМашиночитаемыхФормНаВебСервисе(ПрефиксИмениШаблона, ШаблоныМашиночитаемыхФорм, Прокси) Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru='Не удалось получить файл выгрузки отчета на сервере!!'");
		Сообщение.Сообщить();
		Возврат Ложь;
	КонецЕсли;
	
	ТекстОписанияОшибки = "";
	ВремяОжиданияДоПолученияОтчета = 0;
	ИДСессии = Строка(Новый УникальныйИдентификатор());
	Если Прокси.CreateReport(ИДСессии, ФайлВыгрузкиДвоичныеДанные, НаименованиеИФНС, ПрефиксИмениШаблона, ТекстОписанияОшибки, ВремяОжиданияДоПолученияОтчета) <> Истина Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru='Не удалось сформировать файл машиночитаемой формы на сервере! " + Символы.ПС + "Подробнее:"+ Символы.ПС + ТекстОписанияОшибки + "'");
		Сообщение.Сообщить();
		Возврат Ложь;
	КонецЕсли;
	
	ПараметрыСформированногоОтчета.ИДСессии = ИДСессии;
	ПараметрыСформированногоОтчета.ВремяОжиданияДоПолученияОтчета = ВремяОжиданияДоПолученияОтчета;
	
	Возврат Истина;
	
КонецФункции

Функция ПолучитьМашиночитаемуюФормуСВебСервиса(ПараметрыСформированногоОтчета) Экспорт
	
	Попытка
		ПараметрыПрокси = ПолучитьИзВременногоХранилища(ПараметрыСформированногоОтчета.АдресПараметровПроксиВоВременномХранилище);
		
		Прокси = ОбщегоНазначения.WSПрокси(ПараметрыПрокси.АдресWSDL,
										   ПараметрыПрокси.URIПространстваИмен,
										   ПараметрыПрокси.ИмяСервиса, 
										   ПараметрыПрокси.ИмяТочкиПодключения, 
										   ПараметрыПрокси.ИмяПользователя, 
										   ПараметрыПрокси.Пароль, 
										   ПараметрыПрокси.Таймаут,
										   Ложь);
	Исключение
		
		ВызватьИсключение НСтр("ru='Не удалось получить подключение к веб-сервису! Проверьте адрес точки подключения, имя, пароль или доступность веб-сервиса.'");
		
	КонецПопытки;
	
	СформированныйОтчетДвоичныеДанные = Неопределено;
	Если Прокси.GetFormByID(ПараметрыСформированногоОтчета.ИДСессии, СформированныйОтчетДвоичныеДанные) = Истина Тогда
		ПараметрыСформированногоОтчета.АдресОтчетаВоВременномХранилище = ПоместитьВоВременноеХранилище(СформированныйОтчетДвоичныеДанные, Новый УникальныйИдентификатор());
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

Функция ПроверитьИВыборочноОбновитьШаблоныМашиночитаемыхФормНаВебСервисе(ПрефиксИмениШаблона, ШаблоныМашиночитаемыхФорм, Прокси)

	Перем СписокНайденныхШаблонов;
	
	Если Прокси.CheckTemplates(ПрефиксИмениШаблона, СписокНайденныхШаблонов) <> Истина Тогда
		Возврат Ложь;
	КонецЕсли;
	
	WSФабрикаXDTO = Прокси.Определение.ФабрикаXDTO;
	WSТипСписокШаблонов = ПолучитьWSТипПоИмени(WSФабрикаXDTO, "TemplatesList");
	Если WSТипСписокШаблонов = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	WSСписокНайденныхШаблонов = СписокНайденныхШаблонов.StringItem;
	Если WSСписокНайденныхШаблонов.Количество() > 0 Тогда
		
		Для каждого ИмяВнешнегоШаблона Из WSСписокНайденныхШаблонов Цикл
			ИмяВнешнегоШаблона = СокрЛП(ИмяВнешнегоШаблона);
			
			КоличествоШаблоновВСписке = ШаблоныМашиночитаемыхФорм.Количество();
			Для Ном = 1 По КоличествоШаблоновВСписке Цикл
				ИмяВнутренногоШаблона = СокрЛП(ШаблоныМашиночитаемыхФорм.Получить(КоличествоШаблоновВСписке - Ном).Представление);
				Если Лев(ИмяВнешнегоШаблона, СтрДлина(ИмяВнешнегоШаблона) - 7) =  Лев(ИмяВнутренногоШаблона, СтрДлина(ИмяВнутренногоШаблона) - 7)
				   И Лев(ИмяВнешнегоШаблона, СтрДлина(ИмяВнешнегоШаблона) - 4) >= Лев(ИмяВнутренногоШаблона, СтрДлина(ИмяВнутренногоШаблона) - 4) Тогда
					ШаблоныМашиночитаемыхФорм.Удалить(КоличествоШаблоновВСписке - Ном);
				КонецЕсли;
			КонецЦикла;
			
		КонецЦикла;
		
	Иначе
		
		Если ШаблоныМашиночитаемыхФорм.Количество() = 0 Тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = НСтр("ru='Отсутствует шаблон для формирования машиночитаемой формы!" + Символы.ПС + "Необходимо зарегистрировать в информационной базе внешний шаблон'");
			Сообщение.Сообщить();
			Возврат Ложь;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ШаблоныМашиночитаемыхФорм.Количество() > 0 Тогда
		
		WSСписокШаблонов = WSФабрикаXDTO.Создать(WSТипСписокШаблонов);
		WSТипЭлементШаблон = ПолучитьWSТипПоИмени(WSФабрикаXDTO, "TemplateItem");
		
		Для каждого ШаблонМашиночитаемойФормы Из ШаблоныМашиночитаемыхФорм Цикл
			WSЭлементШаблон = WSФабрикаXDTO.Создать(WSТипЭлементШаблон);
			WSЭлементШаблон.TemplateName = ШаблонМашиночитаемойФормы.Представление;
			WSЭлементШаблон.TemplateBinData = ШаблонМашиночитаемойФормы.Значение;
			WSЭлементШаблон.DatePlace = ТекущаяДатаСеанса();
			WSСписокШаблонов.TemplateItem.Добавить(WSЭлементШаблон);
		КонецЦикла;
		
		Если Прокси.UploadTemplates(WSСписокШаблонов) <> Истина Тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = НСтр("ru='Не удалось обновить шаблон машиночитаемой формы на веб-сервисе'");
			Сообщение.Сообщить();
			Возврат Ложь;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Истина;

КонецФункции

Функция ПолучитьWSТипПоИмени(WSФабрикаXDTO, ИмяТипаВПакете)

	URIПространстваИмен = "";
	Для каждого WSПакетXDTO Из WSФабрикаXDTO.Пакеты Цикл
		Если Найти(WSПакетXDTO.URIПространстваИмен, "/LibraryRegulatoryReporting/") > 0 Тогда
			URIПространстваИмен = WSПакетXDTO.URIПространстваИмен;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Попытка
		Возврат WSФабрикаXDTO.Тип(URIПространстваИмен, ИмяТипаВПакете);
	Исключение
		Возврат Неопределено;
	КонецПопытки;

КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ МЕХАНИЗМА ПОДДЕРЖКИ ВНЕШНИХ РЕГЛАМЕНТИРОВАННЫХ ОТЧЕТОВ
//

Функция СвойстваВнешнегоОтчета(ОбъектЭлемент, Отчет) Экспорт
	
	СвойстваОтчета = Новый Структура;
	
	// создаем объект из выбранного внешнего отчета
	Если ЭтоАдресВременногоХранилища(Отчет) Тогда 
		ИмяФайлаОтчета = ПолучитьИмяВременногоФайла("erf");
		ПолучитьИзВременногоХранилища(Отчет).Записать(ИмяФайлаОтчета);
		Попытка
			ОбъектОтчет = ВнешниеОтчеты.Создать(ИмяФайлаОтчета);
		Исключение
			
			ТекстПредупреждения = 	"Ошибка при загрузке внешнего отчета:
									|
									|" + ИнформацияОбОшибке().Описание;
			СвойстваОтчета.Вставить("Результат", Ложь);
			СвойстваОтчета.Вставить("ТекстПредупреждения", ТекстПредупреждения);
			Возврат СвойстваОтчета;
		КонецПопытки;
	Иначе
		ОбъектОтчет = Отчет;
	КонецЕсли;
	
	// заполняем свойства внешнего отчета
	ПолнаяВерсияВнешнегоОтчета = ПолучитьВерсиюРегламентированногоОтчета(ОбъектОтчет);
	СвойстваПолнойВерсии = РазложитьПолнуюВерсиюРегламентированногоОтчета(ПолнаяВерсияВнешнегоОтчета);
	
	СвойстваОтчета.Вставить("Результат", Истина);
	
	СвойстваОтчета.Вставить("ИДКонфигурацииОтчета", СвойстваПолнойВерсии.ИДКонфигурации);
	СвойстваОтчета.Вставить("ИДКонфигурацииМетаданные", Метаданные.Имя);
	СвойстваОтчета.Вставить("ИДКонфигурацииИмя", РегламентированнаяОтчетностьПереопределяемый.ИДКонфигурации());
	
	СвойстваОтчета.Вставить("ВерсияКонфигурацииВнешнегоОтчета", СвойстваПолнойВерсии.ВерсияКонфигурации);
	СвойстваОтчета.Вставить("ВерсияКонфигурацииМетаданные", Метаданные.Версия);
	
	СвойстваОтчета.Вставить("КраткаяВерсияВнешнегоОтчета", СвойстваПолнойВерсии.КраткаяВерсия);
	СвойстваОтчета.Вставить("ОбъектОтчетМетаданныеИмя", ОбъектОтчет.Метаданные().Имя);
	
	Возврат СвойстваОтчета;
	
КонецФункции

Функция ЗарегистрироватьВнешнийОтчет(ОбъектСсылка, Отчет) Экспорт
	
	Результат = Новый Структура;
	
	Если ОбщегоНазначенияПовтИсп.РазделениеВключено() Тогда
		ТекстПредупреждения = "Метод не предназначен для использования в модели сервиса";
		Результат.Вставить("Зарегистрирован", Ложь);
		Результат.Вставить("ТекстПредупреждения", ТекстПредупреждения);
		Возврат Результат;
	КонецЕсли;
	
	// создаем объект из выбранного внешнего отчета
	Если ЭтоАдресВременногоХранилища(Отчет) Тогда 
		ИмяФайлаОтчета = ПолучитьИмяВременногоФайла("erf");
		ПолучитьИзВременногоХранилища(Отчет).Записать(ИмяФайлаОтчета);
		Попытка
			ОбъектОтчет = ВнешниеОтчеты.Создать(ИмяФайлаОтчета);
		Исключение
			ТекстПредупреждения = 	"Ошибка при загрузке внешнего отчета:
									|
									|" + ИнформацияОбОшибке().Описание;
			Результат.Вставить("Зарегистрирован", Ложь);
			Результат.Вставить("ТекстПредупреждения", ТекстПредупреждения);
			Возврат Результат;
		КонецПопытки;
	КонецЕсли;
	
	Если ОбъектСсылка = Справочники.РегламентированныеОтчеты.ПустаяСсылка() Тогда 
		ОбъектЭлемент = Неопределено;
	Иначе
		ОбъектЭлемент = ОбъектСсылка.ПолучитьОбъект();
	КонецЕсли;
	
	МетаданныеОтчета = ОбъектОтчет.Метаданные();
	ИмяМетаданныхОтчета = МетаданныеОтчета.Имя;
	СинонимМетаданныхОтчета = МетаданныеОтчета.Синоним;
	КомментарийКМетаданнымОтчета = МетаданныеОтчета.Комментарий;
	
	Если ОбъектЭлемент = Неопределено ИЛИ ПустаяСтрока(ОбъектЭлемент.ИсточникОтчета) Тогда
		Результат.Вставить("ИсточникОтчета", ИмяМетаданныхОтчета);
	КонецЕсли;
	
	Если ОбъектЭлемент = Неопределено ИЛИ ПустаяСтрока(ОбъектЭлемент.Наименование) Тогда
		Результат.Вставить("Наименование", ?(ЗначениеЗаполнено(СинонимМетаданныхОтчета), СинонимМетаданныхОтчета, ОбъектЭлемент.ИсточникОтчета));
	КонецЕсли;
	
	Если ОбъектЭлемент = Неопределено ИЛИ ПустаяСтрока(ОбъектЭлемент.Описание) И ЗначениеЗаполнено(КомментарийКМетаданнымОтчета) И КомментарийКМетаданнымОтчета <> МетаданныеОтчета.Синоним Тогда
		Результат.Вставить("Описание", КомментарийКМетаданнымОтчета);
	КонецЕсли;
	
	Результат.Вставить("ВнешнийОтчетИспользовать", Истина);
	
	ПолнаяВерсияВнешнегоОтчета = ПолучитьВерсиюРегламентированногоОтчета(ОбъектОтчет);
	СвойстваПолнойВерсии = РазложитьПолнуюВерсиюРегламентированногоОтчета(ПолнаяВерсияВнешнегоОтчета);
	
	Результат.Вставить("ВнешнийОтчетВерсия", СвойстваПолнойВерсии.КраткаяВерсия);
	
	Результат.Вставить("Зарегистрирован", Истина);
	Возврат Результат;
	
КонецФункции

Функция ПолучитьВерсиюРегламентированногоОтчета(ОбъектОтчет)
	
	Если РегламентированнаяОтчетностьКлиентСервер.СвойствоОпределено(ОбъектОтчет, "мВерсияОтчета") Тогда
		Возврат ОбъектОтчет.мВерсияОтчета;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

Функция РазложитьПолнуюВерсиюРегламентированногоОтчета(ПолнаяВерсия)
	
	Результат = Новый Структура("ИДКонфигурации, ВерсияКонфигурации, КраткаяВерсия, Подверсия");
	
	Если ЗначениеЗаполнено(ПолнаяВерсия) Тогда
		ВхождениеПробела = Найти(ПолнаяВерсия, " ");
		Если ВхождениеПробела <> 0 Тогда
			Результат.ИДКонфигурации = СокрЛП(Лев(ПолнаяВерсия, ВхождениеПробела - 1));
			Результат.КраткаяВерсия = СокрЛП(Сред(ПолнаяВерсия, ВхождениеПробела + 1));
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Результат.КраткаяВерсия) Тогда
		
		КраткаяВерсия = Результат.КраткаяВерсия;
		СтрДлинаКраткаяВерсия = СтрДлина(КраткаяВерсия);
		Для ОбратныйИндекс = 1 По СтрДлинаКраткаяВерсия Цикл
			Инд = СтрДлинаКраткаяВерсия - ОбратныйИндекс + 1;
			ТекСимв = Сред(КраткаяВерсия, Инд, 1);
			Если ТекСимв = "." Тогда
				Результат.ВерсияКонфигурации = СокрЛП(Лев(КраткаяВерсия, Инд - 1));
				Результат.Подверсия = СокрЛП(Сред(КраткаяВерсия, Инд + 1));
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Процедура УстановитьПараметрыВнешнихРегламентированныхОтчетов(ИмяПараметра = Неопределено, УстановленныеПараметры = Неопределено)Экспорт
	
	ПараметрыСеанса.ПараметрыВнешнихРегламентированныхОтчетов = Новый ФиксированноеСоответствие(Новый Соответствие);
	
КонецПроцедуры

Функция ОбъектОтчета(ЭтаФормаИмя) Экспорт 
	
	Если Найти(ЭтаФормаИмя, "Внешний") > 0 Тогда 
		ФормаИмя = СтрЗаменить(ЭтаФормаИмя, "Внешний", "");
		Возврат ВнешниеОтчеты.Создать(Сред(Лев(ФормаИмя, Найти(ФормаИмя, ".Форма.") - 1), 7));
	Иначе
		Возврат Отчеты[Сред(Лев(ЭтаФормаИмя, Найти(ЭтаФормаИмя, ".Форма.") - 1), 7)].Создать();
	КонецЕсли;
	
КонецФункции

Функция ПолныйПутьКФорме(ИсточникОтчета, ВыбраннаяФорма) Экспорт
	
	ЭтоВнешнийОтчет = РегламентированнаяОтчетность.ЭтоВнешнийОтчет(ИсточникОтчета);
	Возврат ?(ЭтоВнешнийОтчет, "ВнешнийОтчет.", "Отчет.") + ИсточникОтчета + ".Форма." + ВыбраннаяФорма;
	
КонецФункции

Функция ЕстьФункциональнаяОпцияИспользоватьНесколькоОрганизаций()
	
	ФункциональнаяОпцияПрисутствует = Метаданные.ФункциональныеОпции.Найти("ИспользоватьНесколькоОрганизацийРегламентированнаяОтчетность") <> Неопределено;
	
	Возврат ФункциональнаяОпцияПрисутствует;
		
КонецФункции

Функция ИспользуетсяОднаОрганизация() Экспорт
	
	// Если нет функциональной опции, то работает как будто в базе несколько организаций.
	Если НЕ ЕстьФункциональнаяОпцияИспользоватьНесколькоОрганизаций() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	// Если функциональная опция есть, то возвращаем ее значение.
	ИспользуетсяОднаОрганизация = НЕ ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоОрганизацийРегламентированнаяОтчетность");
	
	Возврат ИспользуетсяОднаОрганизация; 
		
КонецФункции

Процедура СпециальногоДобавленияСтроки(ТекТабличноеПоле, ТекТабличноеПолеИмя, ИмяГруппы, НомерВерхЯчейкиОбласти, НомерНижнЯчейкиОбласти) Экспорт

	НазначитьИмяОбластиМЧ = Ложь;
	ВставляемаяОбласть = ТекТабличноеПоле.Область(НомерВерхЯчейкиОбласти, , НомерНижнЯчейкиОбласти, );
	Если Найти(ВставляемаяОбласть.Имя, "МногострочнаяЧасть") Тогда
		НазначитьИмяОбластиМЧ = Истина;
		ИмяМногострочнойЧасти = ВставляемаяОбласть.Имя;
	КонецЕсли; 
	ТекТабличноеПоле.ВставитьОбласть( ВставляемаяОбласть,,ТипСмещенияТабличногоДокумента.ПоВертикали );
	Если НазначитьИмяОбластиМЧ Тогда
		ТекТабличноеПоле.Область(НомерНижнЯчейкиОбласти + 1, , НомерНижнЯчейкиОбласти + (НомерНижнЯчейкиОбласти - НомерВерхЯчейкиОбласти) + 1, ).Имя = "";
		ТекТабличноеПоле.Область(НомерВерхЯчейкиОбласти, , НомерНижнЯчейкиОбласти, ).Имя = ИмяМногострочнойЧасти;
	КонецЕсли;
	
КонецПроцедуры

Процедура СпециальногоУдаленияСтроки(ТекТабличноеПоле, ТекТабличноеПолеИмя, ИмяГруппы, НомерВерхЯчейкиОбласти, НомерНижнЯчейкиОбласти, Постфикс = "ХХ_1") Экспорт
	
	// Определим область удаляемой строки.
	НазначитьИмяОбластиМЧ = Ложь;
	УдаляемаяОбласть = ТекТабличноеПоле.Область(НомерВерхЯчейкиОбласти,, НомерНижнЯчейкиОбласти,);
	Если Найти(УдаляемаяОбласть.Имя, "МногострочнаяЧасть") Тогда
		НазначитьИмяОбластиМЧ = Истина;
		ИмяМногострочнойЧасти = УдаляемаяОбласть.Имя;
	КонецЕсли;
	// Определяем позицию области ячейки номера строки, если она содержится в удаляемой строке.
	ПозицияОбластиЯчейкиНомераСтроки = 0;
	ОбластьЯчейкиНомераСтроки = ТекТабличноеПоле.ПолучитьОбласть(НомерВерхЯчейкиОбласти,, НомерНижнЯчейкиОбласти,).Области.Найти(ИмяГруппы + Постфикс);
	Если ОбластьЯчейкиНомераСтроки <> Неопределено Тогда
		Если НЕ ОбластьЯчейкиНомераСтроки.СодержитЗначение Тогда
			ПозицияОбластиЯчейкиНомераСтроки = ОбластьЯчейкиНомераСтроки.Лево;
		КонецЕсли; 
	КонецЕсли; 
	// Непосредственно удаляем область строки табличного документа.
	ТекТабличноеПоле.УдалитьОбласть(УдаляемаяОбласть,ТипСмещенияТабличногоДокумента.ПоВертикали);
	Если НазначитьИмяОбластиМЧ Тогда
		ТекТабличноеПоле.Область(НомерВерхЯчейкиОбласти,, НомерНижнЯчейкиОбласти,).Имя = ИмяМногострочнойЧасти;
	КонецЕсли;
	// Восстанавливаем ячейку номера строки, если она была в составе удаленной области.
	Если ПозицияОбластиЯчейкиНомераСтроки > 0 Тогда
		ЗаменяемаяОбласть = ТекТабличноеПоле.Область(НомерВерхЯчейкиОбласти, ПозицияОбластиЯчейкиНомераСтроки, НомерНижнЯчейкиОбласти, ПозицияОбластиЯчейкиНомераСтроки);
		ТекстОбластиЯчейкиНомераСтроки = ОбластьЯчейкиНомераСтроки.Текст;
		ЗаполнитьЗначенияСвойств(ЗаменяемаяОбласть, ОбластьЯчейкиНомераСтроки,, "Имя");
		ЗаменяемаяОбласть.Текст = ТекстОбластиЯчейкиНомераСтроки;
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ЖУРНАЛ ОТЧЕТНОСТИ
//

Процедура ОтразитьВЖурналеОтчетов(ПараметрыОтчета) Экспорт
	
	ПредставлениеВида        = ПараметрыОтчета.ПредставлениеВида;
	ДокСсылка                = ПараметрыОтчета.ДокСсылка;
	НаименованиеОтчета       = ПараметрыОтчета.НаименованиеОтчета;
	Организация              = ПараметрыОтчета.Организация;
	ДатаНачалаОП             = ПараметрыОтчета.ДатаНачалаОП;
	ДатаОкончанияОП          = ПараметрыОтчета.ДатаОкончанияОП;
	СтатусОтправки           = ПараметрыОтчета.СтатусОтправки;
	ДатаСоздания             = ПараметрыОтчета.ДатаСоздания;
	ВидКонтролирующегоОргана = ПараметрыОтчета.ВидКонтролирующегоОргана;
	КодКонтролирующегоОргана = ПараметрыОтчета.КодКонтролирующегоОргана;
	Комментарий              = ПараметрыОтчета.Комментарий;
	
	ПредставлениеФинПериода  = ПредставлениеФинансовогоПериода(ДатаНачалаОП, ДатаОкончанияОП);
	
	НачатьТранзакцию();
	
	ОшибкаУстановкиБлокировки = Ложь;
	
	Попытка
		
		// Определение статуса отчета 
		СостояниеСдачиОтчетности = Неопределено;
		ОбъектОтправляетсяВКонтролирующиеОрганы = ЭлектронныйДокументооборотСКонтролирующимиОрганами.ОбъектОтправляетсяВКонтролирующиеОрганы(ДокСсылка);
		Если ОбъектОтправляетсяВКонтролирующиеОрганы Тогда
			ЭлектронныйДокументооборотСКонтролирующимиОрганами.ОпределитьСтатусИСостояниеСдачиОтчетности(
				ДокСсылка, Организация, ВидКонтролирующегоОргана, СостояниеСдачиОтчетности, СтатусОтправки);
		КонецЕсли;
		
		СтруктураКлюча = Новый Структура("Ссылка", ДокСсылка);
		Ключ = РегистрыСведений.ЖурналОтчетовСтатусы.СоздатьКлючЗаписи(СтруктураКлюча);
		Попытка
			ЗаблокироватьДанныеДляРедактирования(Ключ);
		Исключение
			ОшибкаУстановкиБлокировки = Истина;
			ВызватьИсключение;
		КонецПопытки;
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ЖурналОтчетовСтатусы");
		ЭлементБлокировки.УстановитьЗначение("Ссылка", ДокСсылка);
		Блокировка.Заблокировать();
		
		НаборЗаписей = РегистрыСведений.ЖурналОтчетовСтатусы.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Ссылка.Установить(ДокСсылка);
		Запись = НаборЗаписей.Добавить();
		
		Запись.Ссылка								= ДокСсылка;
		Запись.НеОтправляетсяВКонтролирующийОрган   = ОбъектОтправляетсяВКонтролирующиеОрганы <> Истина;
		Запись.Статус								= СтатусОтправки;
		Запись.СостояниеСдачиОтчетности				= СостояниеСдачиОтчетности;
		Запись.НаименованиеОтчета					= НаименованиеОтчета;
		Запись.ВидКонтролирующегоОргана				= ВидКонтролирующегоОргана;
		Запись.КодКонтролирующегоОргана				= КодКонтролирующегоОргана;
		Запись.ПредставлениеКонтролирующегоОргана 	= ПредставлениеКонтролирующегоОргана(Запись);
		Запись.ДатаНачала							= ДатаНачалаОП;
		Запись.ДатаОкончания						= ДатаОкончанияОП;
		Запись.ФинансовыйПериод						= ПредставлениеФинПериода;
		Запись.Организация							= Организация;
		Запись.ВариантОтчета						= ПредставлениеВида;
		Запись.Комментарий							= Комментарий;
		Запись.ПометкаУдаления						= ДокСсылка.ПометкаУдаления;
		Запись.ЕстьКритическиеОшибкиОтправки		= ЭлектронныйДокументооборотСКонтролирующимиОрганами.ЕстьКритическиеСообщения(ДокСсылка);
		
		НаборЗаписей.Записать(Истина);
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		Если ОшибкаУстановкиБлокировки Тогда
			Возврат;
		Иначе
			ЗаписьЖурналаРегистрации(НСтр("ru = 'Журнал отчетов (статусы): ошибка записи'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
				УровеньЖурналаРегистрации.Ошибка, , , ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			Возврат;
		КонецЕсли;
		
	КонецПопытки;
	
КонецПроцедуры

Функция ПредставлениеФинансовогоПериода(ДатаНачалаОтчетногоПериода, ДатаОкончанияОтчетногоПериода) Экспорт

	ДатаНачалаОП    = ?(ЗначениеЗаполнено(ДатаНачалаОтчетногоПериода), ДатаНачалаОтчетногоПериода, Дата(1, 1, 1));
	ДатаОкончанияОП = ?(ЗначениеЗаполнено(ДатаОкончанияОтчетногоПериода), ДатаОкончанияОтчетногоПериода, Дата(1, 1, 1));
	ДатаОкончанияОП = ?(ДатаНачалаОП > ДатаОкончанияОП, ДатаНачалаОП, ДатаОкончанияОП);
	
	ПредставлениеФинПериода = ПредставлениеПериода(НачалоДня(ДатаНачалаОП), КонецДня(ДатаОкончанияОП), "Л=ru_RU;ФП=Истина");
	
	Если ЗначениеЗаполнено(ДатаОкончанияОтчетногоПериода) Тогда
		ПредставлениеФинПериодаГод = ПредставлениеПериода(НачалоГода(ДатаОкончанияОП), КонецГода(ДатаОкончанияОП), "Л=ru_RU;ФП=Истина");
		Если Найти(ПредставлениеФинПериода, ПредставлениеФинПериодаГод) > 0 Тогда
			ПредставлениеФинПериода = СтрЗаменить(СтрЗаменить(ПредставлениеФинПериода, ПредставлениеФинПериодаГод, ""), "  ", " ")
				+ ПредставлениеФинПериодаГод;
		КонецЕсли; 
	КонецЕсли;
		
	Возврат ПредставлениеФинПериода;
	
КонецФункции

Функция ПредставлениеКонтролирующегоОргана(ЗначенияЗаполнения) Экспорт
	
	ПредставлениеКонтролирующегоОргана = "";
	Если ЗначениеЗаполнено(ЗначенияЗаполнения.ВидКонтролирующегоОргана) Тогда
		КодКонтролирующегоОргана = ?(ЗначениеЗаполнено(ЗначенияЗаполнения.КодКонтролирующегоОргана), " " + Строка(ЗначенияЗаполнения.КодКонтролирующегоОргана), "");
		
		Если ЗначенияЗаполнения.ВидКонтролирующегоОргана = Перечисления.ТипыКонтролирующихОрганов.ФСРАР Тогда
			ПредставлениеКонтролирующегоОргана = "ФСРАР" + КодКонтролирующегоОргана;
		Иначе
			ПредставлениеКонтролирующегоОргана = Строка(ЗначенияЗаполнения.ВидКонтролирующегоОргана) + КодКонтролирующегоОргана;
		КОнецЕсли;
	КонецЕсли;
		
	Возврат ПредставлениеКонтролирующегоОргана;
	
КонецФункции

Функция СведенияПоОбъектуНеВходящемуВБРО(Ссылка) Экспорт
	
	ТаблицаОбъектовНеВходящихВБРО = РегламентированнаяОтчетность.ТаблицаОписанияОбъектовРегламентированнойОтчетности();
	СведенияПоОбъекту = ТаблицаОбъектовНеВходящихВБРО.Найти(ТипЗнч(Ссылка), "ТипОбъекта");
	
	// Строку таблицы значений преобразуем в структуру
	Если СведенияПоОбъекту <> Неопределено Тогда
		СтруктураСведений = Новый Структура;
		Для каждого Колонка Из ТаблицаОбъектовНеВходящихВБРО.Колонки Цикл
			СтруктураСведений.Вставить(Колонка.Имя, СведенияПоОбъекту[Колонка.Имя]);
		КонецЦикла;
	Иначе
		СтруктураСведений = Неопределено;
	КонецЕсли;
	
	Возврат СтруктураСведений;
	
КонецФункции

// Обновление ИБ

Процедура ЗаполнитьРегистрЖурналОчетовСтатусы(Параметры = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ВыборкаДетальныеЗаписи = ВыборкаОбъектовНеЗаписанныхВРегистрЖурналОтчетовСтатусы();
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		// Запись в регистр
		Предмет = ВыборкаДетальныеЗаписи.Объект;
		Отказ   = Ложь;
		РегламентированнаяОтчетность.ЗаписьОбъектовРегламентированнойОтчетности(Предмет, Отказ);
		
	КонецЦикла;
	
	НастройкиДанныхФормыСозданияОтчетаВыборка = ХранилищеНастроекДанныхФорм.Выбрать(Новый Структура("КлючОбъекта,КлючНастроек",
		"Обработка.ОбщиеОбъектыРегламентированнойОтчетности.Форма.ФормаСозданияОтчета", "ФормаОтчетность_ФормаСозданияОтчета_РанееСозданныеОтчеты"));
	Пока НастройкиДанныхФормыСозданияОтчетаВыборка.Следующий() Цикл
		Если ТипЗнч(НастройкиДанныхФормыСозданияОтчетаВыборка.Настройки) = Тип("ТаблицаЗначений") Тогда
			ХранилищеНастроекДанныхФорм.Сохранить(НастройкиДанныхФормыСозданияОтчетаВыборка.КлючОбъекта,
				НастройкиДанныхФормыСозданияОтчетаВыборка.КлючНастроек, Неопределено,, НастройкиДанныхФормыСозданияОтчетаВыборка.Пользователь);
		КонецЕсли;
	КонецЦикла;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

Функция ВыборкаОбъектовНеЗаписанныхВРегистрЖурналОтчетовСтатусы()
	
	МассивТипов = Новый Массив;
	МассивТипов.Добавить(Тип("ДокументСсылка.РегламентированныйОтчет"));
	МассивТипов.Добавить(Тип("СправочникСсылка.ЭлектронныеПредставленияРегламентированныхОтчетов"));
	
	// Дополняем таблицу типов объектами, не входящими в БРО
	ТаблицаОписанияОбъектовНеВходящихВБРО 	= РегламентированнаяОтчетность.ТаблицаОписанияОбъектовРегламентированнойОтчетности();
	ОписанияОбъектовНеВходящихВБРО = ТаблицаОписанияОбъектовНеВходящихВБРО.НайтиСтроки(
		Новый Структура("ВидДокумента", Перечисления.СтраницыЖурналаОтчетность.Отчеты));

	Для каждого Описание Из ОписанияОбъектовНеВходящихВБРО Цикл
		МассивТипов.Добавить(Описание.ТипОбъекта);
	КонецЦикла; 
	
	Запрос = Новый Запрос;
	Запрос.Текст = "";
	
	// Составляем запрос
	КоличествоТипов = МассивТипов.Количество();
	Для каждого ТипОбъекта Из МассивТипов Цикл
		
		ИндексЭлемента = МассивТипов.Найти(ТипОбъекта);
		ПолноеИмяТипа  = Метаданные.НайтиПоТипу(ТипОбъекта).ПолноеИмя();
		ИмяТипа        = Метаданные.НайтиПоТипу(ТипОбъекта).Имя;
		
		// Формируем основную часть запроса
		Запрос.Текст = Запрос.Текст + "ВЫБРАТЬ
			|	%1.Ссылка КАК Объект
			|ИЗ
			|	%2 КАК %1
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЖурналОтчетовСтатусы КАК ЖурналОтчетовСтатусы
			|		ПО (ЖурналОтчетовСтатусы.Ссылка = %1.Ссылка)
			|ГДЕ
			|	ЖурналОтчетовСтатусы.Ссылка Есть NULL";
			
		// Берем не все электронные представление, а только не относящиеся к уведомлениям
		Если ТипОбъекта = Тип("СправочникСсылка.ЭлектронныеПредставленияРегламентированныхОтчетов") Тогда
			Запрос.Текст = Запрос.Текст + "
				|И %1.ВидОтчета НЕ В (&ВидыОтчетовРазделаУведомления)";
					
			ВидыОтчетовРазделаУведомления = ЭлектронныйДокументооборотСКонтролирующимиОрганами.ВидыЭлектронныхПредставленийВРазделеУведомления();
			Запрос.УстановитьПараметр("ВидыОтчетовРазделаУведомления", ВидыОтчетовРазделаУведомления);
		КонецЕсли;
			
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "%1", ИмяТипа);
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "%2", ПолноеИмяТипа);
			
		// Добавляем объединение между запросами
		Если ИндексЭлемента <> КоличествоТипов - 1 Тогда
			Запрос.Текст = Запрос.Текст + "
			|
			|ОБЪЕДИНИТЬ
			|
			|";
			
		КонецЕсли;
				
	КонецЦикла;
	
	// Выполнение полученного запроса
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
	Возврат ВыборкаДетальныеЗаписи;
	
КонецФункции

Функция КоличествоОбъектовНеЗаписанныхВРегистрЖурналОтчетовСтатусы() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	КоличествоОбъектов = 0;
	
	ВыборкаОбъектовНеЗаписанныхВРегистр = ВыборкаОбъектовНеЗаписанныхВРегистрЖурналОтчетовСтатусы();
	Пока ВыборкаОбъектовНеЗаписанныхВРегистр.Следующий() Цикл
		Отказ = Ложь;
		СтандартнаяОбработка = Истина;
		РегламентированнаяОтчетностьПереопределяемый.ЗаписьОбъектовРегламентированнойОтчетности(ВыборкаОбъектовНеЗаписанныхВРегистр.Объект, Отказ, СтандартнаяОбработка);
		Если НЕ СтандартнаяОбработка Тогда
			Продолжить;
		КонецЕсли;
		КоличествоОбъектов = КоличествоОбъектов + 1;
	КонецЦикла;
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат КоличествоОбъектов;
	
КонецФункции
