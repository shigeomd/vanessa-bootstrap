#Область СлужебныйПрограммныйИнтерфейс

Процедура ПриРегистрацииОбработчиковВыгрузкиДанных(ТаблицаОбработчиков) Экспорт
	
	НовыйОбработчик = ТаблицаОбработчиков.Добавить();
	НовыйОбработчик.Обработчик = ВыгрузкаЗагрузкаСоставаСтандартногоИнтерфейсаOData;
	НовыйОбработчик.ПередВыгрузкойДанных = Истина;
	НовыйОбработчик.Версия = ВыгрузкаЗагрузкаДанныхСлужебныйСобытия.ВерсияОбработчиков1_0_0_1();
	
КонецПроцедуры

Процедура ПередВыгрузкойДанных(Контейнер) Экспорт
	
	Если ОбщегоНазначенияБТСПовтИсп.ДоступныМеханизмыРежимаСовместимости8_3_5() Тогда
		
		Состав = РаботаВБезопасномРежиме.ВычислитьВБезопасномРежиме("ПолучитьСоставСтандартногоИнтерфейсаOData()");
		СериализуемыйСостав = Новый Массив();
		
		Для Каждого ЭлементСостава Из Состав Цикл
			
			СериализуемыйСостав.Добавить(ЭлементСостава.ПолноеИмя());
			
		КонецЦикла;
		
		ИмяФайла = Контейнер.СоздатьПроизвольныйФайл("xml", ТипДанныхДляСоставаСтандартногоИнтерфейсаOData());
		ВыгрузкаЗагрузкаДанныхСлужебный.ЗаписатьОбъектВФайл(СериализуемыйСостав, ИмяФайла);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриРегистрацииОбработчиковЗагрузкиДанных(ТаблицаОбработчиков) Экспорт
	
	НовыйОбработчик = ТаблицаОбработчиков.Добавить();
	НовыйОбработчик.Обработчик = ВыгрузкаЗагрузкаСоставаСтандартногоИнтерфейсаOData;
	НовыйОбработчик.ПередЗагрузкойДанных = Истина;
	НовыйОбработчик.Версия = ВыгрузкаЗагрузкаДанныхСлужебныйСобытия.ВерсияОбработчиков1_0_0_1();
	
КонецПроцедуры

Процедура ПередЗагрузкойДанных(Контейнер) Экспорт
	
	Если ОбщегоНазначенияБТСПовтИсп.ДоступныМеханизмыРежимаСовместимости8_3_5() Тогда
		
		ИмяФайла = Контейнер.ПолучитьПроизвольныйФайл(ТипДанныхДляСоставаСтандартногоИнтерфейсаOData());
		Состав = ВыгрузкаЗагрузкаДанныхСлужебный.ПрочитатьОбъектИзФайла(ИмяФайла);
		
		Если Состав.Количество() > 0 Тогда
			
			РаботаВБезопасномРежиме.ВыполнитьВБезопасномРежиме("УстановитьСоставСтандартногоИнтерфейсаOData(Параметры)", Состав);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ТипДанныхДляСоставаСтандартногоИнтерфейсаOData()
	
	Возврат "StandardODataInterfaceContent"; // Не локализуется
	
КонецФункции

#КонецОбласти
