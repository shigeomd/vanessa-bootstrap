////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

// Создает временные таблицы со схемой распределения.
// Схема распределения описывает:
// - источники расходов (что подлежит распределению)
// - приемники расходов (что получится в результате распределения)
// - базу - коэффициенты распределения.
// Важно: схема распределения не содержит никаких суммовых показателей.
//
// Создаваемые временные таблицы:
// - Расходы     - пронумерованные наборы аналитики расходов, подлежащих распределению. 
//                 Здесь аналитика расходов - Счет, подразделение, номенклатурная группа, статья затрат
// - РаспределениеНаРасходы,
//   РаспределениеНаСубконто,
//   ОбособленныйНалоговыйУчет - результат распределения, подробнее см. НовыйРезультатРаспределения()
Процедура СоздатьСхемуРаспределения(МенеджерВременныхТаблиц, ПравилаРаспределения, Период, Организация) Экспорт
	
	Если ПравилаРаспределения.Количество() = 0 Тогда
		СоздатьПустуюСхемуРаспределения(МенеджерВременныхТаблиц);
		Возврат;
	КонецЕсли;
	
	ПравилаРаспределенияРасходов.СоздатьБазыРаспределения(
		МенеджерВременныхТаблиц, 
		ПравилаРаспределения, 
		Период, 
		Организация);
	
	РезультатРаспределения = НовыйРезультатРаспределения();
	
	// Создает временную таблицу
	// Расходы
	МаксимальныйИдентификатор = СоздатьРасходы(МенеджерВременныхТаблиц, ПравилаРаспределения);
	
	ЕстьЧтоРаспределять = МаксимальныйИдентификатор <> -1;
	
	НомерИтерации = 0;
	
	Пока ЕстьЧтоРаспределять Цикл
		
		НомерИтерации = НомерИтерации + 1;
		
		ЕстьЧтоРаспределять = Ложь;
		
		Для Каждого ПравилоРаспределения Из ПравилаРаспределения Цикл
			
			// Порядок правил важен: каждое следующее правило пытается распределить то,
			// что не смогло распределить предыдущее
			
			// Создает РасходыПоПравилу
			Если ОтобратьРасходы(МенеджерВременныхТаблиц, ПравилоРаспределения) Тогда
			
				Если Не ПравилоРаспределения.ЕстьБазаРаспределения Тогда
					
					ЗафиксироватьРасходыБезБазыРаспределения(МенеджерВременныхТаблиц, ПравилоРаспределения, РезультатРаспределения);
					
				Иначе
					// Создает РезультатРаспределения
					Если СоздатьРезультатРаспределения(МенеджерВременныхТаблиц, ПравилоРаспределения) Тогда
					
						// Создает НовыеРасходы
						ДобавленыНовыеРасходы = СоздатьНовыеРасходы(МенеджерВременныхТаблиц, ПравилоРаспределения, МаксимальныйИдентификатор);
						ЕстьЧтоРаспределять = ЕстьЧтоРаспределять ИЛИ ДобавленыНовыеРасходы;
						
						ДополнитьРезультатРаспределения(МенеджерВременныхТаблиц, ПравилоРаспределения, РезультатРаспределения);
						
						// Обновляет Расходы, удаляет РезультатРаспределения, НовыеРасходы
						ОбновитьРасходы(МенеджерВременныхТаблиц, ПравилоРаспределения.Действие);
						
					КонецЕсли;
				
					ЗафиксироватьНеРаспределенныеРасходы(МенеджерВременныхТаблиц, ПравилоРаспределения, РезультатРаспределения);
			
					УничтожитьТаблицу(МенеджерВременныхТаблиц, "РезультатРаспределения");
					
				КонецЕсли;
				
			КонецЕсли;
			
			УничтожитьТаблицу(МенеджерВременныхТаблиц, "РасходыПоПравилу");
			
		КонецЦикла; // По правилам распределения
		
	КонецЦикла; // Пока есть новые расходы
	
	// Обновляет КлючиВершин, использует Расходы
	ОбновитьКлючиВершин(МенеджерВременныхТаблиц);
	
	// РаспределениеНаРасходы
	// РаспределениеНаСубконто
	// ОбособленныйНалоговыйУчет
	// НеУдалосьРаспределить
	ПоместитьРезультатРаспределенияВоВременныеТаблицы(МенеджерВременныхТаблиц, РезультатРаспределения);
	
КонецПроцедуры

// Создает пустые временные таблицы со схемой распределения
Процедура СоздатьПустуюСхемуРаспределения(МенеджерВременныхТаблиц)
	
	// Пустая таблица Расходы
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ПустоеПодразделение", БухгалтерскийУчетПереопределяемый.ПустоеПодразделение());
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	0 КАК Идентификатор,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК Счет,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ПустаяСсылка) КАК ВидДеятельности,
	|	&ПустоеПодразделение КАК Подразделение,
	|	ЗНАЧЕНИЕ(Справочник.НоменклатурныеГруппы.ПустаяСсылка) КАК НоменклатурнаяГруппа,
	|	ЗНАЧЕНИЕ(Справочник.СтатьиЗатрат.ПустаяСсылка) КАК СтатьяЗатрат,
	|	ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка) КАК Номенклатура,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыРасходовНУ.ПустаяСсылка) КАК ВидРасходовНУ,
	|	ЗНАЧЕНИЕ(Перечисление.ОсобенностиНалоговогоУчетаРасходов.ПустаяСсылка) КАК НалоговыйУчет,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК КорСчет,
	|	НЕОПРЕДЕЛЕНО КАК Субконто1,
	|	НЕОПРЕДЕЛЕНО КАК Субконто2,
	|	НЕОПРЕДЕЛЕНО КАК Субконто3,
	|	ИСТИНА КАК МожноРаспределить,
	|	ИСТИНА КАК МожноВыделитьНалоговыйУчет,
	|	ЛОЖЬ КАК СубконтоПользователяОпределены
	|ПОМЕСТИТЬ Расходы
	|ГДЕ
	|	ЛОЖЬ";
	Запрос.Выполнить();
	
	ПоместитьРезультатРаспределенияВоВременныеТаблицы(МенеджерВременныхТаблиц, НовыйРезультатРаспределения());
	
КонецПроцедуры

Процедура УничтожитьСхемуРаспределения(МенеджерВременныхТаблиц) Экспорт
	
	УничтожитьТаблицу(МенеджерВременныхТаблиц, "Расходы");
	УничтожитьТаблицу(МенеджерВременныхТаблиц, "РаспределениеНаСубконто");
	УничтожитьТаблицу(МенеджерВременныхТаблиц, "РаспределениеНаРасходы");
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СОГЛАШЕНИЯ

// Возвращает разрезы, в которых распределяются расходы
//
// Возвращаемое значение:
//  Структура. Ключ - имя разреза, значение - его тип.
//
Функция РазрезыРаспределения()
	
	РазрезыРаспределения = Новый Структура;
	// Это разрезы аналитики, в которых распределяются расходы.
	// Как следствие, такая аналитика может определяться в ходе распределения расходов.
	
	РазрезыРаспределения.Вставить("Счет",                 Новый ОписаниеТипов("ПланСчетовСсылка.Хозрасчетный"));
	РазрезыРаспределения.Вставить("Подразделение",        БухгалтерскийУчетКлиентСерверПереопределяемый.ОписаниеТиповПодразделения());
	РазрезыРаспределения.Вставить("ВидДеятельности",      Новый ОписаниеТипов("ПеречислениеСсылка.ВидыДеятельностиДляНалоговогоУчетаЗатрат"));
	РазрезыРаспределения.Вставить("НоменклатурнаяГруппа", Новый ОписаниеТипов("СправочникСсылка.НоменклатурныеГруппы"));
	РазрезыРаспределения.Вставить("СтатьяЗатрат",         Новый ОписаниеТипов("СправочникСсылка.СтатьиЗатрат"));
	РазрезыРаспределения.Вставить("ВидРасходовНУ",        Новый ОписаниеТипов("ПеречислениеСсылка.ВидыРасходовНУ"));
	РазрезыРаспределения.Вставить("НалоговыйУчет",        Новый ОписаниеТипов("ПеречислениеСсылка.ОсобенностиНалоговогоУчетаРасходов"));
	РазрезыРаспределения.Вставить("КорСчет",              Новый ОписаниеТипов("ПланСчетовСсылка.Хозрасчетный"));
	
	// Также есть особое поле Номенклатура: 
	// оно не используется в ходе распределения, но может быть определено в результате распределения 
	// (и поэтому присутствует в запросах распределения)
	
	Возврат РазрезыРаспределения;
	
КонецФункции

// Возвращает коллекцию для хранения результата распределения расходов
Функция НовыйРезультатРаспределения()
	
	ТипИдентификатораВершины = УчетЗатрат.ТипИдентификатораВершины();
	ТипИмениПравила          = ПравилаРаспределенияРасходов.ТипИмени();
	
	// РаспределениеНаРасходы (# источника, значения аналитики распределения, база)
	// (результат распределения)
	РаспределениеНаРасходы = Новый ТаблицаЗначений;
	РаспределениеНаРасходы.Колонки.Добавить("Правило",       ТипИмениПравила);
	РаспределениеНаРасходы.Колонки.Добавить("Идентификатор", ТипИдентификатораВершины);
	ДобавитьКолонкиАналитикаРаспределения(РаспределениеНаРасходы);
	РаспределениеНаРасходы.Колонки.Добавить("База", Новый ОписаниеТипов("Число"));
	
	// РаспределениеНаСубконто (# источника - значения субконто, база)
	РаспределениеНаСубконто = Новый ТаблицаЗначений;
	РаспределениеНаСубконто.Колонки.Добавить("Правило",       ТипИмениПравила);
	РаспределениеНаСубконто.Колонки.Добавить("Идентификатор", ТипИдентификатораВершины);
	Для Каждого КлючИЗначение Из УчетЗатрат.РазрезыУчета() Цикл
		РаспределениеНаСубконто.Колонки.Добавить(КлючИЗначение.Ключ, КлючИЗначение.Значение);
	КонецЦикла;
	РаспределениеНаСубконто.Колонки.Добавить("Номенклатура", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	РаспределениеНаСубконто.Колонки.Добавить("База", Новый ОписаниеТипов("Число"));
	
	// ОбособленныйНалоговыйУчет (# источника, значения аналитики распределения, база)
	// (результат обособления)
	ОбособленныйНалоговыйУчет = Новый ТаблицаЗначений;
	ОбособленныйНалоговыйУчет.Колонки.Добавить("Правило",       ТипИмениПравила);
	ОбособленныйНалоговыйУчет.Колонки.Добавить("Идентификатор", ТипИдентификатораВершины);
	ДобавитьКолонкиАналитикаРаспределения(ОбособленныйНалоговыйУчет);
	ОбособленныйНалоговыйУчет.Колонки.Добавить("ТипРазниц",     Новый ОписаниеТипов("ПеречислениеСсылка.ВидыДвиженийРасчетСебестоимости"));
	
	// ЗафиксированныеРасходы (# источника)
	// (результат действия Зафиксировать)
	ЗафиксированныеРасходы = Новый ТаблицаЗначений;
	ЗафиксированныеРасходы.Колонки.Добавить("Правило",       ТипИмениПравила);
	ЗафиксированныеРасходы.Колонки.Добавить("Идентификатор", ТипИдентификатораВершины);
	
	// НеУдалосьРаспределить (# источника, имя правила)
	// (расходы, попавшие под отбор, но не распределенные)
	НеУдалосьРаспределить = Новый ТаблицаЗначений;
	НеУдалосьРаспределить.Колонки.Добавить("Правило",       ТипИмениПравила);
	НеУдалосьРаспределить.Колонки.Добавить("Идентификатор", ТипИдентификатораВершины);
	НеУдалосьРаспределить.Индексы.Добавить("Идентификатор");
	
	// Скомпонуем коллекцию
	РезультатРаспределения = Новый Структура;
	РезультатРаспределения.Вставить("РаспределениеНаРасходы",    РаспределениеНаРасходы);
	РезультатРаспределения.Вставить("РаспределениеНаСубконто",   РаспределениеНаСубконто);
	РезультатРаспределения.Вставить("ОбособленныйНалоговыйУчет", ОбособленныйНалоговыйУчет);
	РезультатРаспределения.Вставить("ЗафиксированныеРасходы",    ЗафиксированныеРасходы);
	РезультатРаспределения.Вставить("НеУдалосьРаспределить",     НеУдалосьРаспределить);
	
	Возврат РезультатРаспределения;
	
КонецФункции

Процедура ДобавитьКолонкиАналитикаРаспределения(ТаблицаЗначений)
	
	Для Каждого КлючИЗначение Из РазрезыРаспределения() Цикл
		ТаблицаЗначений.Колонки.Добавить(КлючИЗначение.Ключ, КлючИЗначение.Значение);
	КонецЦикла;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ИСХОДНЫЕ ДАННЫЕ

Функция СоздатьРасходы(МенеджерВременныхТаблиц, ПравилаРаспределения)
	
	// Большая часть аналитики расходов уже получена из первичных документов и помещена в КлючиВершин.
	// Дополним эти данные аналитикой расходов, которые уже известны, но не содержатся в таблице КлючиВершин
	// - из баз распределения к правилам РаспределитьНаСубконто
	// Всю новую аналитику нумеруем.
	// Затем представим аналитику не в виде наборов субконто ("КлючиВершин"),
	// а в виде наборов аналитики распределения расходов ("Расходы").
	// 
	// По ходу распределения расходов может быть выявлена новая аналитика расходов
	// (как результат распределения расходов на расходы).
	// Ее тоже нумеруем. Для этого функция СоздатьРасходы() возвращает максимальный идентификатор.
	//
	// По окончании распределения расходов обновляем КлючиВершин.
	
	// Создадим таблицу ВТ_КлючиБазРаспределения - она содержит различные ключи всех баз распределения с направленим "Субконто"
	ТекстЗапроса =  // Текст содержит служебный комментарий "//ОБЪЕДИНИТЬ"
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК Счет,
	|	&ПустоеПодразделение КАК Подразделение,
	|	НЕОПРЕДЕЛЕНО КАК Субконто1,
	|	НЕОПРЕДЕЛЕНО КАК Субконто2,
	|	НЕОПРЕДЕЛЕНО КАК Субконто3,
	|	ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка) КАК Номенклатура,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК КорСчет
	|ПОМЕСТИТЬ ВТ_КлючиБазРаспределения
	|ГДЕ
	|	ЛОЖЬ
	|
	|//ОБЪЕДИНИТЬ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Счет,
	|	Подразделение,
	|	Субконто1,
	|	Субконто2,
	|	Субконто3,
	|	Номенклатура,
	|	КорСчет";
	
	ШаблонТекстаЗапросаБазыРаспределения = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	БазаРаспределения.Счет,
	|	ЕСТЬNULL(БазаРаспределения.Подразделение, &ПустоеПодразделение) КАК Подразделение,
	|	ЕСТЬNULL(БазаРаспределения.Субконто1, НЕОПРЕДЕЛЕНО) КАК Субконто1,
	|	ЕСТЬNULL(БазаРаспределения.Субконто2, НЕОПРЕДЕЛЕНО) КАК Субконто2,
	|	ЕСТЬNULL(БазаРаспределения.Субконто3, НЕОПРЕДЕЛЕНО) КАК Субконто3,
	|	ЕСТЬNULL(БазаРаспределения.Номенклатура, ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)) КАК Номенклатура,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК КорСчет
	|ИЗ
	|	ИмяБазыРаспределения КАК БазаРаспределения
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СчетаРасходов КАК СчетаРасходов
	|		ПО БазаРаспределения.Счет = СчетаРасходов.Счет";
	
	
	Для Каждого Правило Из ПравилаРаспределения Цикл
		Если Правило.ЕстьБазаРаспределения И Правило.Действие = "РаспределитьНаСубконто" Тогда
			
			ТекстЗапросаБазыРаспределения = СтрЗаменить(
				ШаблонТекстаЗапросаБазыРаспределения, 
				"ИмяБазыРаспределения", 
				Правило.БазаРаспределения.Имя);
				
			ТекстЗапроса = СтрЗаменить(
				ТекстЗапроса, 
				"//ОБЪЕДИНИТЬ", 
				"ОБЪЕДИНИТЬ " + Символы.ПС + ТекстЗапросаБазыРаспределения + Символы.ПС + "//ОБЪЕДИНИТЬ");
				
		КонецЕсли;
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ПустоеПодразделение", БухгалтерскийУчетПереопределяемый.ПустоеПодразделение());
	Запрос.Текст = ТекстЗапроса;
	Запрос.Выполнить();
	
	// Получим максимальный идентификатор
	Запрос = Новый Запрос();
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЕСТЬNULL(МАКСИМУМ(КлючиВершин.Идентификатор), -1) КАК МаксимальныйИдентификатор
	|ИЗ
	|	КлючиВершин КАК КлючиВершин";
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий(); // Всегда будет одна запись
	МаксимальныйИдентификатор = Выборка.МаксимальныйИдентификатор;
	
	// Получим новые наборы аналитики (которых не было выявлено из первичных документов)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Расходы.Счет КАК Счет,
	|	Расходы.Подразделение КАК Подразделение,
	|	Расходы.Субконто1 КАК Субконто1,
	|	Расходы.Субконто2 КАК Субконто2,
	|	Расходы.Субконто3 КАК Субконто3,
	|	Расходы.Номенклатура КАК Номенклатура,
	|	Расходы.КорСчет КАК КорСчет
	|ИЗ
	|	ВТ_КлючиБазРаспределения КАК Расходы
	|		ЛЕВОЕ СОЕДИНЕНИЕ КлючиВершин КАК КлючиВершин
	|		ПО Расходы.Счет = КлючиВершин.Счет
	|			И Расходы.Подразделение = КлючиВершин.Подразделение
	|			И Расходы.Субконто1 = КлючиВершин.Субконто1
	|			И Расходы.Субконто2 = КлючиВершин.Субконто2
	|			И Расходы.Субконто3 = КлючиВершин.Субконто3
	|			И Расходы.Номенклатура = КлючиВершин.Номенклатура
	|			И Расходы.КорСчет = КлючиВершин.КорСчет
	|ГДЕ
	|	КлючиВершин.Счет ЕСТЬ NULL 
	|
	|УПОРЯДОЧИТЬ ПО
	|	Счет,
	|	Подразделение,
	|	Субконто1,
	|	Субконто2,
	|	Субконто3,
	|	Номенклатура,
	|	КорСчет";
	
	НовыеКлючиВершин = Запрос.Выполнить().Выгрузить();
	
	
	Если НовыеКлючиВершин.Количество() = 0 Тогда
		
		Запрос = Новый Запрос();
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		Запрос.УстановитьПараметр("ПустоеПодразделение", БухгалтерскийУчетПереопределяемый.ПустоеПодразделение());
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	0 КАК Идентификатор,
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК Счет,
		|	&ПустоеПодразделение КАК Подразделение,
		|	НЕОПРЕДЕЛЕНО КАК Субконто1,
		|	НЕОПРЕДЕЛЕНО КАК Субконто2,
		|	НЕОПРЕДЕЛЕНО КАК Субконто3,
		|	ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка) КАК Номенклатура,
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК КорСчет
		|ПОМЕСТИТЬ ВТ_НовыеКлючиВершин
		|ГДЕ
		|	ЛОЖЬ";
		Запрос.Выполнить();
		
	Иначе
	
		// Пронумеруем новые ключи расходов
		НовыеКлючиВершин.Колонки.Добавить("Идентификатор", УчетЗатрат.ТипИдентификатораВершины());
		Для Каждого СтрокаТаблицы Из НовыеКлючиВершин Цикл
			МаксимальныйИдентификатор = МаксимальныйИдентификатор + 1;
			СтрокаТаблицы.Идентификатор = МаксимальныйИдентификатор;
		КонецЦикла;
		
		Запрос = Новый Запрос();
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		Запрос.УстановитьПараметр("НовыеКлючиВершин", НовыеКлючиВершин);
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	КлючиВершин.Идентификатор КАК Идентификатор,
		|	КлючиВершин.Счет КАК Счет,
		|	КлючиВершин.Подразделение КАК Подразделение,
		|	КлючиВершин.Субконто1 КАК Субконто1,
		|	КлючиВершин.Субконто2 КАК Субконто2,
		|	КлючиВершин.Субконто3 КАК Субконто3,
		|	КлючиВершин.Номенклатура КАК Номенклатура,
		|	КлючиВершин.КорСчет КАК КорСчет
		|ПОМЕСТИТЬ ВТ_НовыеКлючиВершин
		|ИЗ
		|	&НовыеКлючиВершин КАК КлючиВершин";
		Запрос.Выполнить();
		
	КонецЕсли;
	
	// Теперь представим полную коллекцию ключей расходов в виде аналитики распределения расходов.
	// Результат поместим во временную таблицу Расходы
	//
	// Ключ расходов может содержать заполненную пользовательскую аналитику - 
	// оборотные субконто, кроме перечисленных явно в РазрезыРаспределения().
	// В результате одному набору аналитики распределения, предусмотренной в этом модуле, может соответствовать несколько ключей.
	// Чтобы это не приводило к искажению распределения, важно, чтобы при распределении "на расходы" 
	// (т.е. с помощью действий РаспределитьНаРасходы и ВыделитьНалоговыйУчет)
	// одному набору аналитики соответствовал ровно один ключ.
	// Считаем, что для распределения расходов подходят только те ключи, в которых все значения пользовательской аналитики - Неопределено.
	// То есть, не подходят такие ключи, у которых СубконтоПользователяОпределены
	// Если подходящего ключа нет, то создадим его при распределении "на расходы" в обычном порядке.
	//
	// По этой же причине в таблице Расходы хранятся и значения Субконто1, Субконто2, Субконто3.
	// Это позволяет избежать потери значений пользовательской аналитики при преобразовании данных 
	// о новых расходах из баз распределения в аналитику расходов.
	// Сохраненные значения субконто используются в ОбновитьКлючиВершин(), когда по данным таблицы Расходы формируются новые ключи аналитики.
	//
	// В ходе получения данных о расходах, разбиваем расходы по видам деятельности.
	// В системе нет четкого признака, по которым расходы можно отнести к тому или иному виду деятельности.
	// Для расходов текущего периода вид деятельности можно определить по статье расходов.
	// Для расходов в НЗП вид деятельности можно попробовать угадать:
	// - если по НГ есть расходы в текущем периоде и все эти расходы относятся к ЕНВД, то НЗП по этой НГ тоже относится к ЕНВД
	// - в остальных случаях НЗП относится к деятельности, не облагаемой ЕНВД
	// См. в тексте запроса ВТ_ОсобыйПорядокНалогообложения
	Запрос = Новый Запрос();
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("НовыеКлючиВершин", НовыеКлючиВершин);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КлючиВершин.Идентификатор КАК Идентификатор,
	|	КлючиВершин.Счет КАК Счет,
	|	КлючиВершин.Подразделение КАК Подразделение,
	|	КлючиВершин.Субконто1 КАК Субконто1,
	|	КлючиВершин.Субконто2 КАК Субконто2,
	|	КлючиВершин.Субконто3 КАК Субконто3,
	|	КлючиВершин.Номенклатура КАК Номенклатура,
	|	КлючиВершин.КорСчет КАК КорСчет
	|ПОМЕСТИТЬ ВТ_КлючиВершин
	|ИЗ
	|	ВТ_НовыеКлючиВершин КАК КлючиВершин
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КлючиВершин.Идентификатор,
	|	КлючиВершин.Счет,
	|	КлючиВершин.Подразделение,
	|	КлючиВершин.Субконто1,
	|	КлючиВершин.Субконто2,
	|	КлючиВершин.Субконто3,
	|	КлючиВершин.Номенклатура,
	|	КлючиВершин.КорСчет
	|ИЗ
	|	КлючиВершин КАК КлючиВершин
	|ГДЕ
	|	КлючиВершин.РазделУчета = ""Расходы""
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Счет
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Расходы.Идентификатор КАК Идентификатор,
	|	Расходы.Счет КАК Счет,
	|	Расходы.Подразделение КАК Подразделение,
	|	ВЫБОР
	|		КОГДА СубконтоНоменклатурныеГруппы.НомерСубконто = 1
	|			ТОГДА Расходы.Субконто1
	|		КОГДА СубконтоНоменклатурныеГруппы.НомерСубконто = 2
	|			ТОГДА Расходы.Субконто2
	|		КОГДА СубконтоНоменклатурныеГруппы.НомерСубконто = 3
	|			ТОГДА Расходы.Субконто3
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.НоменклатурныеГруппы.ПустаяСсылка)
	|	КОНЕЦ КАК НоменклатурнаяГруппа,
	|	ВЫБОР
	|		КОГДА СубконтоСтатьиЗатрат.НомерСубконто = 1
	|			ТОГДА Расходы.Субконто1
	|		КОГДА СубконтоСтатьиЗатрат.НомерСубконто = 2
	|			ТОГДА Расходы.Субконто2
	|		КОГДА СубконтоСтатьиЗатрат.НомерСубконто = 3
	|			ТОГДА Расходы.Субконто3
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СтатьиЗатрат.ПустаяСсылка)
	|	КОНЕЦ КАК СтатьяЗатрат,
	|	Расходы.Номенклатура КАК Номенклатура,
	|	Расходы.КорСчет КАК КорСчет,
	|	Расходы.Субконто1 КАК Субконто1,
	|	Расходы.Субконто2 КАК Субконто2,
	|	Расходы.Субконто3 КАК Субконто3,
	|	ВЫБОР
	|		КОГДА СубконтоПользователя.Субконто1
	|					И Расходы.Субконто1 <> НЕОПРЕДЕЛЕНО
	|				ИЛИ СубконтоПользователя.Субконто2
	|					И Расходы.Субконто2 <> НЕОПРЕДЕЛЕНО
	|				ИЛИ СубконтоПользователя.Субконто3
	|					И Расходы.Субконто3 <> НЕОПРЕДЕЛЕНО
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СубконтоПользователяОпределены
	|ПОМЕСТИТЬ ВТ_АналитикаРасходов
	|ИЗ
	|	ВТ_КлючиВершин КАК Расходы
	|		ЛЕВОЕ СОЕДИНЕНИЕ СубконтоНоменклатурныеГруппы КАК СубконтоНоменклатурныеГруппы
	|		ПО Расходы.Счет = СубконтоНоменклатурныеГруппы.Счет
	|		ЛЕВОЕ СОЕДИНЕНИЕ СубконтоСтатьиЗатрат КАК СубконтоСтатьиЗатрат
	|		ПО Расходы.Счет = СубконтоСтатьиЗатрат.Счет
	|		ЛЕВОЕ СОЕДИНЕНИЕ СубконтоПользователя КАК СубконтоПользователя
	|		ПО Расходы.Счет = СубконтоПользователя.Счет
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Счет,
	|	Подразделение,
	|	НоменклатурнаяГруппа,
	|	СтатьяЗатрат,
	|	Номенклатура,
	|	КорСчет
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Расходы.Счет КАК Счет,
	|	Расходы.Подразделение КАК Подразделение,
	|	Расходы.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа
	|ПОМЕСТИТЬ ВТ_ОсобыйПорядокНалогообложения
	|ИЗ
	|	ВТ_АналитикаРасходов КАК Расходы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СтатьиЗатрат КАК СтатьиЗатрат
	|		ПО Расходы.СтатьяЗатрат = СтатьиЗатрат.Ссылка
	|			И (СтатьиЗатрат.ВидДеятельностиДляНалоговогоУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ОсобыйПорядокНалогообложения))
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Счет,
	|	Подразделение,
	|	НоменклатурнаяГруппа
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Расходы.Идентификатор КАК Идентификатор,
	|	Расходы.Счет КАК Счет,
	|	ВЫБОР
	|		КОГДА НЕ СтатьиЗатрат.ВидДеятельностиДляНалоговогоУчетаЗатрат ЕСТЬ NULL 
	|			ТОГДА СтатьиЗатрат.ВидДеятельностиДляНалоговогоУчетаЗатрат
	|		КОГДА НЕ ОсобыйПорядокНалогообложения.Счет ЕСТЬ NULL 
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ОсобыйПорядокНалогообложения)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ОсновнаяСистемаНалогообложения)
	|	КОНЕЦ КАК ВидДеятельности,
	|	Расходы.Подразделение КАК Подразделение,
	|	Расходы.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
	|	Расходы.СтатьяЗатрат КАК СтатьяЗатрат,
	|	Расходы.Номенклатура КАК Номенклатура,
	|	ЕСТЬNULL(СтатьиЗатрат.ВидРасходовНУ, ЗНАЧЕНИЕ(Перечисление.ВидыРасходовНУ.ПустаяСсылка)) КАК ВидРасходовНУ,
	|	ЗНАЧЕНИЕ(Перечисление.ОсобенностиНалоговогоУчетаРасходов.ПустаяСсылка) КАК НалоговыйУчет,
	|	Расходы.КорСчет КАК КорСчет,
	|	Расходы.Субконто1 КАК Субконто1,
	|	Расходы.Субконто2 КАК Субконто2,
	|	Расходы.Субконто3 КАК Субконто3,
	|	ИСТИНА КАК МожноРаспределить,
	|	ИСТИНА КАК МожноВыделитьНалоговыйУчет,
	|	Расходы.СубконтоПользователяОпределены КАК СубконтоПользователяОпределены
	|ПОМЕСТИТЬ Расходы
	|ИЗ
	|	ВТ_АналитикаРасходов КАК Расходы
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СтатьиЗатрат КАК СтатьиЗатрат
	|		ПО Расходы.СтатьяЗатрат = СтатьиЗатрат.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ОсобыйПорядокНалогообложения КАК ОсобыйПорядокНалогообложения
	|		ПО Расходы.Счет = ОсобыйПорядокНалогообложения.Счет
	|			И Расходы.Подразделение = ОсобыйПорядокНалогообложения.Подразделение
	|			И Расходы.НоменклатурнаяГруппа = ОсобыйПорядокНалогообложения.НоменклатурнаяГруппа
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Счет,
	|	ВидДеятельности,
	|	Подразделение,
	|	НоменклатурнаяГруппа,
	|	СтатьяЗатрат,
	|	Номенклатура,
	|	ВидРасходовНУ,
	|	НалоговыйУчет,
	|	КорСчет";
	Запрос.Выполнить();
	
	// Завершающие мероприятия:
	// - уберем за собой
	
	Запрос.Текст = 
	"УНИЧТОЖИТЬ ВТ_КлючиБазРаспределения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_НовыеКлючиВершин
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_КлючиВершин
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_АналитикаРасходов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_ОсобыйПорядокНалогообложения";
	Запрос.Выполнить();
	
	Возврат МаксимальныйИдентификатор;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// РАСПРЕДЕЛЕНИЕ РАСХОДОВ

Функция ОтобратьРасходы(МенеджерВременныхТаблиц, ПравилоРаспределения)
	
	ПараметрыТекста = Новый Структура;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Расходы.Идентификатор КАК Идентификатор,
	|	Расходы.Счет КАК Счет,
	|	Расходы.ВидДеятельности КАК ВидДеятельности,
	|	Расходы.Подразделение КАК Подразделение,
	|	Расходы.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
	|	Расходы.СтатьяЗатрат КАК СтатьяЗатрат,
	|	Расходы.Номенклатура КАК Номенклатура,
	|	Расходы.ВидРасходовНУ КАК ВидРасходовНУ,
	|	Расходы.НалоговыйУчет КАК НалоговыйУчет,
	|	Расходы.КорСчет КАК КорСчет,
	|	Расходы.МожноРаспределить КАК МожноРаспределить,
	|	Расходы.МожноВыделитьНалоговыйУчет КАК МожноВыделитьНалоговыйУчет
	|ПОМЕСТИТЬ РасходыПоПравилу
	|ИЗ
	|	Расходы КАК Расходы
	|ГДЕ
	|	[МожноВыполнитьДействие]
	|	[ОтборРасходов]
	|
	|[Индексы]";
	
	Если ПравилоРаспределения.Действие = "РаспределитьНаСубконто" Тогда
		ПараметрыТекста.Вставить("МожноВыполнитьДействие", "Расходы.МожноРаспределить");
		ЗаполнитьИндексы(ПараметрыТекста, ПравилоРаспределения);
	ИначеЕсли ПравилоРаспределения.Действие = "РаспределитьНаРасходы" Тогда
		ПараметрыТекста.Вставить("МожноВыполнитьДействие", "Расходы.МожноРаспределить");
		ЗаполнитьИндексы(ПараметрыТекста, ПравилоРаспределения);
	ИначеЕсли ПравилоРаспределения.Действие = "ВыделитьНалоговыйУчет" Тогда
		ПараметрыТекста.Вставить("МожноВыполнитьДействие", "Расходы.МожноВыделитьНалоговыйУчет");
		ЗаполнитьИндексы(ПараметрыТекста, ПравилоРаспределения);
	Иначе  // Зафиксируем расходы
		ПараметрыТекста.Вставить("МожноВыполнитьДействие", "Расходы.МожноВыделитьНалоговыйУчет И Расходы.МожноРаспределить");
		ПараметрыТекста.Вставить("Индексы",                "");
	КонецЕсли;
	
	ЗаполнитьОтборРасходов(ПараметрыТекста, ПравилоРаспределения);
	ТекстЗапроса = СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(ТекстЗапроса, ПараметрыТекста);
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Для Каждого ПолеОтбора Из ПравилоРаспределения.ОтборРасходов Цикл
		Запрос.УстановитьПараметр(ПолеОтбора.Ключ, ПолеОтбора.Значение);
	КонецЦикла;
	
	Запрос.Выполнить();
	
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	РасходыПоПравилу.Идентификатор
	|ИЗ
	|	РасходыПоПравилу КАК РасходыПоПравилу";
	Возврат НЕ Запрос.Выполнить().Пустой();
	
КонецФункции

Функция СоздатьРезультатРаспределения(МенеджерВременныхТаблиц, ПравилоРаспределения)
	
	ПараметрыТекста = Новый Структура;
	
	Если ПравилоРаспределения.Действие = "РаспределитьНаСубконто" Тогда
		
		// Приемник выражен в виде набора субконто. 
		// Все поля берем из базы. 
		// Поле Номенклатура нужно для обслуживания настройки встречного выпуска 
		// и формирования сведений о себестоимости наименования продукции (услуг).
		// Не проверяем распределение "Само на себя".
		
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	Расходы.Идентификатор КАК Идентификатор,
		|	БазаРаспределения.База КАК База,
		|	БазаРаспределения.Счет КАК Счет,
		|	БазаРаспределения.Подразделение КАК Подразделение,
		|	БазаРаспределения.Субконто1 КАК Субконто1,
		|	БазаРаспределения.Субконто2 КАК Субконто2,
		|	БазаРаспределения.Субконто3 КАК Субконто3,
		|	БазаРаспределения.Номенклатура КАК Номенклатура
		|ПОМЕСТИТЬ РезультатРаспределения
		|ИЗ
		|	РасходыПоПравилу КАК Расходы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ [БазаРаспределения] КАК БазаРаспределения
		|		ПО [УсловияСоединения]
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Идентификатор";
			
		ЗаполнитьУсловияСоединения(ПараметрыТекста, ПравилоРаспределения);
		ПараметрыТекста.Вставить("БазаРаспределения", ПравилоРаспределения.БазаРаспределения.Имя);
		
	ИначеЕсли ПравилоРаспределения.Действие = "РаспределитьНаРасходы" Тогда
		
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	Расходы.Идентификатор КАК Идентификатор,
		|	БазаРаспределения.База,
		|	ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка) КАК Номенклатура
		|	[ПоляПриемника]
		|ПОМЕСТИТЬ РезультатРаспределения
		|ИЗ
		|	РасходыПоПравилу КАК Расходы
		|		ЛЕВОЕ СОЕДИНЕНИЕ [БазаРаспределения] КАК БазаРаспределения
		|			[ОтборПоСчетамБазыРаспределения]
		|		ПО [УсловияСоединения]
		|
		|ГДЕ
		|	БазаРаспределения.База IS NULL 
		|	ИЛИ НЕ ([РасходыСовпадают])
		|   ИЛИ Расходы.Номенклатура <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Идентификатор";
		
		
		ЗаполнитьПоляПриемника(ПараметрыТекста, ПравилоРаспределения); // И РасходыСовпадают
		ЗаполнитьУсловияСоединения(ПараметрыТекста, ПравилоРаспределения);
		
		// Отбор по счетам базы распределения нужен для того, чтобы исключить появление в результате распределения на расходы
		// счетов, не являющихся счетами расходов.
		// При использовании платформы 8.3.5 такой отбор может оказаться целесообразным выполнять раньше - 
		// при подготовке баз распределения.
		Если Не ПравилоРаспределения.ПоляПриемника.Свойство("Счет") Тогда
			ПараметрыТекста.Вставить("ОтборПоСчетамБазыРаспределения", "");
		Иначе
			ПараметрыТекста.Вставить(
				"ОтборПоСчетамБазыРаспределения", 
				"ВНУТРЕННЕЕ СОЕДИНЕНИЕ СчетаРасходов КАК СчетаРасходов
				|			ПО БазаРаспределения.Счет = СчетаРасходов.Счет");
		КонецЕсли;
		
		ПараметрыТекста.Вставить("БазаРаспределения", ПравилоРаспределения.БазаРаспределения.Имя);
		
	ИначеЕсли ПравилоРаспределения.Действие = "ВыделитьНалоговыйУчет" Тогда
		
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	Расходы.Идентификатор КАК Идентификатор,
		|   Неопределено КАК База,
		|	БазаРаспределения.ТипРазниц,
		|	ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка) КАК Номенклатура
		|	[ПоляПриемника]
		|ПОМЕСТИТЬ РезультатРаспределения
		|ИЗ
		|	РасходыПоПравилу КАК Расходы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ [БазаРаспределения] КАК БазаРаспределения
		|		ПО [УсловияСоединения]
		|
		|ГДЕ
		|	НЕ ([РасходыСовпадают])
		|   ИЛИ Расходы.Номенклатура <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Идентификатор";
			
		ЗаполнитьПоляПриемника(ПараметрыТекста, ПравилоРаспределения); // И РасходыСовпадают
		ЗаполнитьУсловияСоединения(ПараметрыТекста, ПравилоРаспределения);
		ЗаполнитьОтборРасходов(ПараметрыТекста, ПравилоРаспределения);
		
		ПараметрыТекста.Вставить("БазаРаспределения", ПравилоРаспределения.БазаРаспределения.Имя);
		
	Иначе  // Зафиксируем расходы
		
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	Расходы.Идентификатор,
		|   Неопределено КАК База
		|ПОМЕСТИТЬ РезультатРаспределения
		|ИЗ
		|	РасходыПоПравилу КАК Расходы
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Расходы.Идентификатор";
			
		ЗаполнитьОтборРасходов(ПараметрыТекста, ПравилоРаспределения);
		
	КонецЕсли;
	
	ТекстЗапроса = СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(ТекстЗапроса, ПараметрыТекста);
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Для Каждого ПолеОтбора Из ПравилоРаспределения.ОтборРасходов Цикл
		Запрос.УстановитьПараметр(ПолеОтбора.Ключ, ПолеОтбора.Значение);
	КонецЦикла;
	
	Запрос.Выполнить();
	
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	РезультатРаспределения.Идентификатор
	|ИЗ
	|	РезультатРаспределения КАК РезультатРаспределения
	|ГДЕ
	|	НЕ РезультатРаспределения.База ЕСТЬ NULL";
	Возврат НЕ Запрос.Выполнить().Пустой();
	
КонецФункции

Процедура ЗаполнитьОтборРасходов(ПараметрыТекста, ПравилоРаспределения)
	
	ОтборРасходов = Новый Массив;
	
	Для Каждого ПолеОтбора Из ПравилоРаспределения.ОтборРасходов Цикл
		Если ПолеОтбора.Ключ = "Счет" ИЛИ ТипЗнч(ПолеОтбора.Значение) = Тип("Массив") Тогда
			ОтборРасходов.Добавить("И Расходы." + ПолеОтбора.Ключ + " В (&" + ПолеОтбора.Ключ + ")");
		Иначе
			ОтборРасходов.Добавить("И Расходы." + ПолеОтбора.Ключ + " = &" + ПолеОтбора.Ключ);
		КонецЕсли;
	КонецЦикла;
	
	ПодготовитьСекциюЗапроса(
		ПараметрыТекста, 
		"ОтборРасходов", 
		ОтборРасходов,
		"",  // Префикс секции
		""); // Разделитель
	
КонецПроцедуры

Процедура ЗаполнитьУсловияСоединения(ПараметрыТекста, ПравилоРаспределения)

	УсловияСоединения = Новый Массив;
	Для Каждого ПолеИсточника Из ПравилоРаспределения.ПоляИсточника Цикл
		УсловияСоединения.Добавить("Расходы." + ПолеИсточника.Ключ + " = БазаРаспределения." + ПолеИсточника.Значение);
	КонецЦикла;
	
	ПодготовитьСекциюЗапроса(
		ПараметрыТекста, 
		"УсловияСоединения", 
		УсловияСоединения, 
		"", // префикс секции 
		" И", // разделитель
		"ИСТИНА"); // текст для пустого списка
		
КонецПроцедуры

Процедура ЗаполнитьПоляПриемника(ПараметрыТекста, ПравилоРаспределения)
	
	// Также заполняет РасходыСовпадают
	РасходыСовпадают = Новый Массив;
	ПоляПриемника    = Новый Массив;
	
	// Определим поля, которые отсутствуют в базе и должны быть "унаследованы" из источника
	ПоляИзИсточника = РазрезыРаспределения(); // Все возможные поля
	
	Для Каждого ПолеПриемника Из ПравилоРаспределения.ПоляПриемника Цикл
		ПоляПриемника.Добавить("БазаРаспределения." + ПолеПриемника.Ключ + " КАК " + ПолеПриемника.Ключ);
		ПоляИзИсточника.Удалить(ПолеПриемника.Ключ);
		РасходыСовпадают.Добавить("БазаРаспределения." + ПолеПриемника.Ключ + " = Расходы." + ПолеПриемника.Ключ);
	КонецЦикла;
	
	Для Каждого ПолеПриемника Из ПоляИзИсточника Цикл
		ПоляПриемника.Добавить("Расходы." + ПолеПриемника.Ключ + " КАК " + ПолеПриемника.Ключ);
	КонецЦикла;

	ПодготовитьСекциюЗапроса(
		ПараметрыТекста, 
		"ПоляПриемника", 
		ПоляПриемника,
		",");
	
	ПодготовитьСекциюЗапроса(
		ПараметрыТекста, 
		"РасходыСовпадают", 
		РасходыСовпадают, 
		"",      // префикс секции 
		" И",    // разделитель
		"Ложь"); // текст для пустого списка
	
КонецПроцедуры

Процедура ЗаполнитьИндексы(ПараметрыТекста, ПравилоРаспределения)

	Индексы = Новый Массив;
	Для Каждого ПолеИсточника Из ПравилоРаспределения.ПоляИсточника Цикл
		Индексы.Добавить(ПолеИсточника.Ключ);
	КонецЦикла;
	
	ПодготовитьСекциюЗапроса(
		ПараметрыТекста, 
		"Индексы", 
		Индексы, 
		"ИНДЕКСИРОВАТЬ ПО" + Символы.ПС, // префикс секции 
		", " + Символы.ПС,  // разделитель
		"");                // текст для пустого списка
		
КонецПроцедуры

Процедура ПодготовитьСекциюЗапроса(ПараметрыТекста, ИмяСекции, ДанныеСекции, ПрефиксСекции = "", Разделитель = ",", ТекстНетДанных = "")
	
	Если ДанныеСекции.Количество() = 0 Тогда
		ПараметрыТекста.Вставить(ИмяСекции, ТекстНетДанных);
	Иначе
		ТекстСекции = СтроковыеФункцииКлиентСервер.СтрокаИзМассиваПодстрок(ДанныеСекции, Разделитель + Символы.ПС);
		ПараметрыТекста.Вставить(ИмяСекции, ПрефиксСекции + ТекстСекции);
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТКА РЕЗУЛЬТАТА ИТЕРАЦИИ РАСПРЕДЕЛЕНИЯ

Процедура ДополнитьРезультатРаспределения(МенеджерВременныхТаблиц, ПравилоРаспределения, РезультатРаспределения)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Если ПравилоРаспределения.Действие = "РаспределитьНаСубконто" Тогда
	
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	РезультатРаспределения.Идентификатор,
		|	РезультатРаспределения.Счет,
		|	РезультатРаспределения.Подразделение,
		|	РезультатРаспределения.Субконто1,
		|	РезультатРаспределения.Субконто2,
		|	РезультатРаспределения.Субконто3,
		|	РезультатРаспределения.Номенклатура,
		|	РезультатРаспределения.База
		|ИЗ
		|	РезультатРаспределения КАК РезультатРаспределения
		|ГДЕ
		|	НЕ РезультатРаспределения.База ЕСТЬ NULL 
		|
		|УПОРЯДОЧИТЬ ПО
		|	РезультатРаспределения.Идентификатор,
		|	РезультатРаспределения.База";
		
		ХранилищеРезультата = РезультатРаспределения.РаспределениеНаСубконто;
		
	ИначеЕсли ПравилоРаспределения.Действие = "РаспределитьНаРасходы" Тогда
	
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	РезультатРаспределения.Идентификатор,
		|	РезультатРаспределения.База,
		|	РезультатРаспределения.Счет,
		|	РезультатРаспределения.ВидДеятельности,
		|	РезультатРаспределения.Подразделение,
		|	РезультатРаспределения.НоменклатурнаяГруппа,
		|	РезультатРаспределения.СтатьяЗатрат,
		|	РезультатРаспределения.ВидРасходовНУ,
		|	РезультатРаспределения.НалоговыйУчет,
		|	РезультатРаспределения.КорСчет
		|ИЗ
		|	РезультатРаспределения КАК РезультатРаспределения
		|ГДЕ
		|	НЕ РезультатРаспределения.База ЕСТЬ NULL 
		|
		|УПОРЯДОЧИТЬ ПО
		|	РезультатРаспределения.Идентификатор,
		|	РезультатРаспределения.База";
		
		ХранилищеРезультата = РезультатРаспределения.РаспределениеНаРасходы;
		
	ИначеЕсли ПравилоРаспределения.Действие = "ВыделитьНалоговыйУчет" Тогда
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	РезультатРаспределения.Идентификатор,
		|	РезультатРаспределения.ТипРазниц,
		|	РезультатРаспределения.Счет,
		|	РезультатРаспределения.ВидДеятельности,
		|	РезультатРаспределения.Подразделение,
		|	РезультатРаспределения.НоменклатурнаяГруппа,
		|	РезультатРаспределения.СтатьяЗатрат,
		|	РезультатРаспределения.ВидРасходовНУ,
		|	РезультатРаспределения.НалоговыйУчет,
		|	РезультатРаспределения.КорСчет
		|ИЗ
		|	РезультатРаспределения КАК РезультатРаспределения
		|ГДЕ
		|	НЕ РезультатРаспределения.База ЕСТЬ NULL 
		|
		|УПОРЯДОЧИТЬ ПО
		|	РезультатРаспределения.Идентификатор";
		
		ХранилищеРезультата = РезультатРаспределения.ОбособленныйНалоговыйУчет;
		
	ИначеЕсли ПравилоРаспределения.Действие = "Зафиксировать" Тогда
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	РезультатРаспределения.Идентификатор
		|ИЗ
		|	РезультатРаспределения КАК РезультатРаспределения
		|ГДЕ
		|	НЕ РезультатРаспределения.База ЕСТЬ NULL 
		|
		|УПОРЯДОЧИТЬ ПО
		|	РезультатРаспределения.Идентификатор";
		
		ХранилищеРезультата = РезультатРаспределения.ЗафиксированныеРасходы;
		
	Иначе
		
		Возврат;
		
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = ХранилищеРезультата.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		НоваяСтрока.Правило = ПравилоРаспределения.Имя;
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗафиксироватьРасходыБезБазыРаспределения(МенеджерВременныхТаблиц, ПравилоРаспределения, РезультатРаспределения)

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РасходыПоПравилу.Идентификатор
	|ИЗ
	|	РасходыПоПравилу КАК РасходыПоПравилу";
	
	ХранилищеРезультата = РезультатРаспределения.НеУдалосьРаспределить;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		НайденнаяСтрока = ХранилищеРезультата.Найти(Выборка.Идентификатор, "Идентификатор");
		
		Если НайденнаяСтрока = Неопределено Тогда
			НоваяСтрока = ХранилищеРезультата.Добавить();
			НоваяСтрока.Идентификатор = Выборка.Идентификатор;
			НоваяСтрока.Правило       = ПравилоРаспределения.Имя;
		Иначе
			НайденнаяСтрока.Правило   = ПравилоРаспределения.Имя;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗафиксироватьНеРаспределенныеРасходы(МенеджерВременныхТаблиц, ПравилоРаспределения, РезультатРаспределения)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РезультатРаспределения.Идентификатор
	|ИЗ
	|	РезультатРаспределения КАК РезультатРаспределения
	|ГДЕ
	|	РезультатРаспределения.База ЕСТЬ NULL ";
	
	ХранилищеРезультата = РезультатРаспределения.НеУдалосьРаспределить;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		НайденнаяСтрока = ХранилищеРезультата.Найти(Выборка.Идентификатор, "Идентификатор");
		
		Если НайденнаяСтрока = Неопределено Тогда
			НоваяСтрока = ХранилищеРезультата.Добавить();
			НоваяСтрока.Идентификатор = Выборка.Идентификатор;
			НоваяСтрока.Правило       = ПравилоРаспределения.Имя;
		Иначе
			НайденнаяСтрока.Правило   = ПравилоРаспределения.Имя;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры
	
	
Функция СоздатьНовыеРасходы(МенеджерВременныхТаблиц, ПравилоРаспределения, МаксимальныйИдентификатор)

	НовыеРасходы = Новый ТаблицаЗначений;
	НовыеРасходы.Колонки.Добавить("Идентификатор", УчетЗатрат.ТипИдентификатораВершины());
	ДобавитьКолонкиАналитикаРаспределения(НовыеРасходы);
	НовыеРасходы.Колонки.Добавить("Номенклатура", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	
	// Новые расходы не могут появиться при распределении на "Субконто", 
	// потому что при таком распределении аналитика расходов полностью получается из базы распределения.
	// Как следствие, до распределения уже известна аналитика, которая получится при таком распределении.
	Если ПравилоРаспределения.Действие = "РаспределитьНаРасходы" ИЛИ ПравилоРаспределения.Действие = "ВыделитьНалоговыйУчет" Тогда
	
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	РезультатРаспределения.Счет КАК Счет,
		|	РезультатРаспределения.ВидДеятельности КАК ВидДеятельности,
		|	РезультатРаспределения.Подразделение КАК Подразделение,
		|	РезультатРаспределения.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
		|	РезультатРаспределения.СтатьяЗатрат КАК СтатьяЗатрат,
		|	ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка) КАК Номенклатура,
		|	РезультатРаспределения.ВидРасходовНУ КАК ВидРасходовНУ,
		|	РезультатРаспределения.НалоговыйУчет КАК НалоговыйУчет,
		|	РезультатРаспределения.КорСчет КАК КорСчет
		|ПОМЕСТИТЬ Приемники
		|ИЗ
		|	РезультатРаспределения КАК РезультатРаспределения
		|ГДЕ
		|	НЕ РезультатРаспределения.База ЕСТЬ NULL 
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Счет,
		|	ВидДеятельности,
		|	Подразделение,
		|	НоменклатурнаяГруппа,
		|	СтатьяЗатрат,
		|	Номенклатура,
		|	ВидРасходовНУ,
		|	НалоговыйУчет,
		|	КорСчет
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Приемники.Счет КАК Счет,
		|	Приемники.ВидДеятельности КАК ВидДеятельности,
		|	Приемники.Подразделение КАК Подразделение,
		|	Приемники.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
		|	Приемники.СтатьяЗатрат КАК СтатьяЗатрат,
		|	Приемники.Номенклатура КАК Номенклатура,
		|	Приемники.ВидРасходовНУ КАК ВидРасходовНУ,
		|	Приемники.НалоговыйУчет КАК НалоговыйУчет,
		|	Приемники.КорСчет КАК КорСчет
		|ИЗ
		|	Приемники КАК Приемники
		|		ЛЕВОЕ СОЕДИНЕНИЕ Расходы КАК Расходы
		|		ПО Приемники.Счет = Расходы.Счет
		|			И Приемники.ВидДеятельности = Расходы.ВидДеятельности
		|			И Приемники.Подразделение = Расходы.Подразделение
		|			И Приемники.НоменклатурнаяГруппа = Расходы.НоменклатурнаяГруппа
		|			И Приемники.СтатьяЗатрат = Расходы.СтатьяЗатрат
		|			И Приемники.Номенклатура = Расходы.Номенклатура
		|			И Приемники.ВидРасходовНУ = Расходы.ВидРасходовНУ
		|			И Приемники.НалоговыйУчет = Расходы.НалоговыйУчет
		|			И Приемники.КорСчет = Расходы.КорСчет
		|			И НЕ Расходы.СубконтоПользователяОпределены
		|ГДЕ
		|	Расходы.Счет ЕСТЬ NULL 
		|
		|УПОРЯДОЧИТЬ ПО
		|	Счет,
		|	ВидДеятельности,
		|	Подразделение,
		|	НоменклатурнаяГруппа,
		|	СтатьяЗатрат,
		|	Номенклатура,
		|	ВидРасходовНУ,
		|	НалоговыйУчет,
		|	КорСчет";
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			НоваяСтрока = НовыеРасходы.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
			МаксимальныйИдентификатор = МаксимальныйИдентификатор + 1;
			НоваяСтрока.Идентификатор = МаксимальныйИдентификатор;
		КонецЦикла;
		
		Запрос.Текст = 
		"УНИЧТОЖИТЬ Приемники";
		Запрос.Выполнить();
		
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("НовыеРасходы", НовыеРасходы);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НовыеРасходы.Идентификатор,
	|	НовыеРасходы.Счет,
	|	НовыеРасходы.ВидДеятельности,
	|	НовыеРасходы.Подразделение,
	|	НовыеРасходы.НоменклатурнаяГруппа,
	|	НовыеРасходы.СтатьяЗатрат,
	|	НовыеРасходы.Номенклатура,
	|	НовыеРасходы.ВидРасходовНУ,
	|	НовыеРасходы.НалоговыйУчет,
	|	НовыеРасходы.КорСчет
	|ПОМЕСТИТЬ НовыеРасходы
	|ИЗ
	|	&НовыеРасходы КАК НовыеРасходы";
	
	Запрос.Выполнить();
	
	Возврат НовыеРасходы.Количество() > 0;
	
КонецФункции

Процедура ОбновитьРасходы(МенеджерВременныхТаблиц, Действие)

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("МожноРаспределить",          Действие = "ВыделитьНалоговыйУчет"); // После остальных действий распределять нельзя
	Запрос.УстановитьПараметр("МожноВыделитьНалоговыйУчет", Ложь); // Любое действие с расходами приводит к тому, что больше нельзя выделить налоговый учет
	// См. также комментарий о порядке правил в НовыеПравилаРаспределения()
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Расходы.Идентификатор,
	|	Расходы.Счет,
	|	Расходы.ВидДеятельности,
	|	Расходы.Подразделение,
	|	Расходы.НоменклатурнаяГруппа,
	|	Расходы.СтатьяЗатрат,
	|	Расходы.Номенклатура,
	|	Расходы.ВидРасходовНУ,
	|	Расходы.НалоговыйУчет,
	|	Расходы.КорСчет,
	|	Расходы.Субконто1,
	|	Расходы.Субконто2,
	|	Расходы.Субконто3,
	|	Расходы.МожноРаспределить,
	|	Расходы.МожноВыделитьНалоговыйУчет,
	|	Расходы.СубконтоПользователяОпределены
	|ПОМЕСТИТЬ СтарыеРасходы
	|ИЗ
	|	Расходы КАК Расходы
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Расходы.Идентификатор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ Расходы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РезультатРаспределения.Идентификатор
	|ПОМЕСТИТЬ РаспределенныеРасходы
	|ИЗ
	|	РезультатРаспределения КАК РезультатРаспределения
	|ГДЕ
	|	НЕ РезультатРаспределения.База ЕСТЬ NULL 
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	РезультатРаспределения.Идентификатор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Расходы.Идентификатор КАК Идентификатор,
	|	Расходы.Счет КАК Счет,
	|	Расходы.ВидДеятельности КАК ВидДеятельности,
	|	Расходы.Подразделение КАК Подразделение,
	|	Расходы.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
	|	Расходы.СтатьяЗатрат КАК СтатьяЗатрат,
	|	Расходы.Номенклатура КАК Номенклатура,
	|	Расходы.ВидРасходовНУ КАК ВидРасходовНУ,
	|	Расходы.НалоговыйУчет КАК НалоговыйУчет,
	|	Расходы.КорСчет КАК КорСчет,
	|	Расходы.Субконто1 КАК Субконто1,
	|	Расходы.Субконто2 КАК Субконто2,
	|	Расходы.Субконто3 КАК Субконто3,
	|	ВЫБОР
	|		КОГДА РаспределенныеРасходы.Идентификатор ЕСТЬ NULL 
	|			ТОГДА Расходы.МожноРаспределить
	|		ИНАЧЕ Расходы.МожноРаспределить
	|				И &МожноРаспределить
	|	КОНЕЦ КАК МожноРаспределить,
	|	ВЫБОР
	|		КОГДА РаспределенныеРасходы.Идентификатор ЕСТЬ NULL 
	|			ТОГДА Расходы.МожноВыделитьНалоговыйУчет
	|		ИНАЧЕ Расходы.МожноВыделитьНалоговыйУчет
	|				И &МожноВыделитьНалоговыйУчет
	|	КОНЕЦ КАК МожноВыделитьНалоговыйУчет,
	|	Расходы.СубконтоПользователяОпределены КАК СубконтоПользователяОпределены
	|ПОМЕСТИТЬ Расходы
	|ИЗ
	|	СтарыеРасходы КАК Расходы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РаспределенныеРасходы КАК РаспределенныеРасходы
	|		ПО Расходы.Идентификатор = РаспределенныеРасходы.Идентификатор
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НовыеРасходы.Идентификатор,
	|	НовыеРасходы.Счет,
	|	НовыеРасходы.ВидДеятельности,
	|	НовыеРасходы.Подразделение,
	|	НовыеРасходы.НоменклатурнаяГруппа,
	|	НовыеРасходы.СтатьяЗатрат,
	|	НовыеРасходы.Номенклатура,
	|	НовыеРасходы.ВидРасходовНУ,
	|	НовыеРасходы.НалоговыйУчет,
	|	НовыеРасходы.КорСчет,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	ИСТИНА,
	|	ИСТИНА,
	|	ЛОЖЬ
	|ИЗ
	|	НовыеРасходы КАК НовыеРасходы
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Счет,
	|	ВидДеятельности,
	|	Подразделение,
	|	НоменклатурнаяГруппа,
	|	СтатьяЗатрат,
	|	Номенклатура,
	|	ВидРасходовНУ,
	|	НалоговыйУчет,
	|	КорСчет,
	|	МожноРаспределить,
	|	МожноВыделитьНалоговыйУчет
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ СтарыеРасходы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ РаспределенныеРасходы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ НовыеРасходы";
	
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура УничтожитьТаблицу(МенеджерВременныхТаблиц, ИмяТаблицы)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("УНИЧТОЖИТЬ %1", ИмяТаблицы);
	Запрос.Выполнить();
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ПОДГОТОВКА ВОЗВРАЩАЕМЫХ ДАННЫХ

Процедура ОбновитьКлючиВершин(МенеджерВременныхТаблиц)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Расходы.Идентификатор КАК Идентификатор,
	|	Расходы.Счет КАК Счет,
	|	Расходы.Подразделение КАК Подразделение,
	|	ВЫБОР
	|		КОГДА СубконтоНоменклатурныеГруппы.НомерСубконто = 1
	|			ТОГДА Расходы.НоменклатурнаяГруппа
	|		КОГДА СубконтоСтатьиЗатрат.НомерСубконто = 1
	|			ТОГДА Расходы.СтатьяЗатрат
	|		ИНАЧЕ Расходы.Субконто1
	|	КОНЕЦ КАК Субконто1,
	|	ВЫБОР
	|		КОГДА СубконтоНоменклатурныеГруппы.НомерСубконто = 2
	|			ТОГДА Расходы.НоменклатурнаяГруппа
	|		КОГДА СубконтоСтатьиЗатрат.НомерСубконто = 2
	|			ТОГДА Расходы.СтатьяЗатрат
	|		ИНАЧЕ Расходы.Субконто2
	|	КОНЕЦ КАК Субконто2,
	|	ВЫБОР
	|		КОГДА СубконтоНоменклатурныеГруппы.НомерСубконто = 3
	|			ТОГДА Расходы.НоменклатурнаяГруппа
	|		КОГДА СубконтоСтатьиЗатрат.НомерСубконто = 3
	|			ТОГДА Расходы.СтатьяЗатрат
	|		ИНАЧЕ Расходы.Субконто3
	|	КОНЕЦ КАК Субконто3,
	|	Расходы.Номенклатура КАК Номенклатура,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК КорСчет
	|ПОМЕСТИТЬ ВТ_НовыеИдентификаторыВершин
	|ИЗ
	|	Расходы КАК Расходы
	|		ЛЕВОЕ СОЕДИНЕНИЕ ИдентификаторыВершин КАК ИдентификаторыВершин
	|		ПО Расходы.Идентификатор = ИдентификаторыВершин.Идентификатор
	|		ЛЕВОЕ СОЕДИНЕНИЕ СубконтоНоменклатурныеГруппы КАК СубконтоНоменклатурныеГруппы
	|		ПО Расходы.Счет = СубконтоНоменклатурныеГруппы.Счет
	|		ЛЕВОЕ СОЕДИНЕНИЕ СубконтоСтатьиЗатрат КАК СубконтоСтатьиЗатрат
	|		ПО Расходы.Счет = СубконтоСтатьиЗатрат.Счет
	|ГДЕ
	|	ИдентификаторыВершин.Идентификатор ЕСТЬ NULL 
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Счет,
	|	Подразделение,
	|	Субконто1,
	|	Субконто2,
	|	Субконто3,
	|	Номенклатура,
	|	КорСчет
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Новые.Счет КАК Счет,
	|	Новые.Подразделение КАК Подразделение,
	|	Новые.Субконто1 КАК Субконто1,
	|	Новые.Субконто2 КАК Субконто2,
	|	Новые.Субконто3 КАК Субконто3,
	|	Новые.Номенклатура КАК Номенклатура,
	|	Новые.КорСчет КАК КорСчет,
	|	МИНИМУМ(Новые.Идентификатор) КАК Идентификатор
	|ПОМЕСТИТЬ ВТ_НовыеКлючиВершин
	|ИЗ
	|	ВТ_НовыеИдентификаторыВершин КАК Новые
	|		ЛЕВОЕ СОЕДИНЕНИЕ КлючиВершин КАК Старые
	|		ПО Новые.Счет = Старые.Счет
	|			И Новые.Подразделение = Старые.Подразделение
	|			И Новые.Субконто1 = Старые.Субконто1
	|			И Новые.Субконто2 = Старые.Субконто2
	|			И Новые.Субконто3 = Старые.Субконто3
	|			И Новые.Номенклатура = Старые.Номенклатура
	|			И Новые.КорСчет = Старые.КорСчет
	|ГДЕ
	|	Старые.Счет ЕСТЬ NULL 
	|
	|СГРУППИРОВАТЬ ПО
	|	Новые.Счет,
	|	Новые.Подразделение,
	|	Новые.Субконто1,
	|	Новые.Субконто2,
	|	Новые.Субконто3,
	|	Новые.Номенклатура,
	|	Новые.КорСчет
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Счет
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КлючиВершин.Идентификатор,
	|	КлючиВершин.РазделУчета КАК РазделУчета,
	|	КлючиВершин.Счет КАК Счет,
	|	КлючиВершин.Подразделение КАК Подразделение,
	|	КлючиВершин.Субконто1 КАК Субконто1,
	|	КлючиВершин.Субконто2 КАК Субконто2,
	|	КлючиВершин.Субконто3 КАК Субконто3,
	|	КлючиВершин.Номенклатура КАК Номенклатура,
	|	КлючиВершин.КорСчет КАК КорСчет
	|ПОМЕСТИТЬ ВТ_СтарыеКлючиВершин
	|ИЗ
	|	КлючиВершин КАК КлючиВершин
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КлючиВершин.Идентификатор,
	|	КлючиВершин.РазделУчета КАК РазделУчета,
	|	КлючиВершин.Счет КАК Счет,
	|	КлючиВершин.Подразделение КАК Подразделение,
	|	КлючиВершин.Субконто1 КАК Субконто1,
	|	КлючиВершин.Субконто2 КАК Субконто2,
	|	КлючиВершин.Субконто3 КАК Субконто3,
	|	КлючиВершин.Номенклатура КАК Номенклатура,
	|	КлючиВершин.КорСчет КАК КорСчет,
	|	КлючиВершин.НеОборотноеСубконто1 КАК НеОборотноеСубконто1,
	|	КлючиВершин.НеОборотноеСубконто2 КАК НеОборотноеСубконто2,
	|	КлючиВершин.НеОборотноеСубконто3 КАК НеОборотноеСубконто3
	|ПОМЕСТИТЬ ВТ_СтарыеИдентификаторыВершин
	|ИЗ
	|	ИдентификаторыВершин КАК КлючиВершин
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ КлючиВершин
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ИдентификаторыВершин
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Новые.Идентификатор,
	|	""Расходы"" КАК РазделУчета,
	|	Новые.Счет КАК Счет,
	|	Новые.Подразделение КАК Подразделение,
	|	Новые.Субконто1 КАК Субконто1,
	|	Новые.Субконто2 КАК Субконто2,
	|	Новые.Субконто3 КАК Субконто3,
	|	Новые.Номенклатура КАК Номенклатура,
	|	Новые.КорСчет КАК КорСчет
	|ПОМЕСТИТЬ КлючиВершин
	|ИЗ
	|	ВТ_НовыеКлючиВершин КАК Новые
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Старые.Идентификатор,
	|	Старые.РазделУчета,
	|	Старые.Счет,
	|	Старые.Подразделение,
	|	Старые.Субконто1,
	|	Старые.Субконто2,
	|	Старые.Субконто3,
	|	Старые.Номенклатура,
	|	Старые.КорСчет
	|ИЗ
	|	ВТ_СтарыеКлючиВершин КАК Старые
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Счет,
	|	Подразделение,
	|	Субконто1,
	|	Субконто2,
	|	Субконто3,
	|	Номенклатура,
	|	КорСчет
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Новые.Идентификатор,
	|	""Расходы"" КАК РазделУчета,
	|	Новые.Счет КАК Счет,
	|	Новые.Подразделение КАК Подразделение,
	|	Новые.Субконто1 КАК Субконто1,
	|	Новые.Субконто2 КАК Субконто2,
	|	Новые.Субконто3 КАК Субконто3,
	|	Новые.Номенклатура КАК Номенклатура,
	|	Новые.КорСчет КАК КорСчет,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ОборотныеСубконто.Субконто1, ЛОЖЬ)
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ Новые.Субконто1
	|	КОНЕЦ КАК НеОборотноеСубконто1,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ОборотныеСубконто.Субконто2, ЛОЖЬ)
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ Новые.Субконто2
	|	КОНЕЦ КАК НеОборотноеСубконто2,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ОборотныеСубконто.Субконто3, ЛОЖЬ)
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ Новые.Субконто3
	|	КОНЕЦ КАК НеОборотноеСубконто3
	|ПОМЕСТИТЬ ИдентификаторыВершин
	|ИЗ
	|	ВТ_НовыеИдентификаторыВершин КАК Новые
	|		ЛЕВОЕ СОЕДИНЕНИЕ ОборотныеСубконто КАК ОборотныеСубконто
	|		ПО Новые.Счет = ОборотныеСубконто.Счет
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Старые.Идентификатор,
	|	Старые.РазделУчета,
	|	Старые.Счет,
	|	Старые.Подразделение,
	|	Старые.Субконто1,
	|	Старые.Субконто2,
	|	Старые.Субконто3,
	|	Старые.Номенклатура,
	|	Старые.КорСчет,
	|	Старые.НеОборотноеСубконто1,
	|	Старые.НеОборотноеСубконто2,
	|	Старые.НеОборотноеСубконто3
	|ИЗ
	|	ВТ_СтарыеИдентификаторыВершин КАК Старые
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Новые.Идентификатор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_НовыеКлючиВершин
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_СтарыеКлючиВершин
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_НовыеИдентификаторыВершин
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_СтарыеИдентификаторыВершин";
	
	Запрос.Выполнить();
	
	
КонецПроцедуры

Процедура ПоместитьРезультатРаспределенияВоВременныеТаблицы(МенеджерВременныхТаблиц, РезультатРаспределения)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("РаспределениеНаСубконто",   РезультатРаспределения.РаспределениеНаСубконто);
	Запрос.УстановитьПараметр("РаспределениеНаРасходы",    РезультатРаспределения.РаспределениеНаРасходы);
	Запрос.УстановитьПараметр("ОбособленныйНалоговыйУчет", РезультатРаспределения.ОбособленныйНалоговыйУчет);
	Запрос.УстановитьПараметр("ЗафиксированныеРасходы",    РезультатРаспределения.ЗафиксированныеРасходы);
	Запрос.УстановитьПараметр("НеУдалосьРаспределить",     РезультатРаспределения.НеУдалосьРаспределить);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РезультатРаспределения.Правило,
	|	РезультатРаспределения.Идентификатор,
	|	РезультатРаспределения.Счет КАК Счет,
	|	РезультатРаспределения.Подразделение КАК Подразделение,
	|	РезультатРаспределения.Субконто1 КАК Субконто1,
	|	РезультатРаспределения.Субконто2 КАК Субконто2,
	|	РезультатРаспределения.Субконто3 КАК Субконто3,
	|	РезультатРаспределения.Номенклатура КАК Номенклатура,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК КорСчет,
	|	РезультатРаспределения.База
	|ПОМЕСТИТЬ РаспределениеНаСубконто
	|ИЗ
	|	&РаспределениеНаСубконто КАК РезультатРаспределения
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Счет,
	|	Подразделение,
	|	Субконто1,
	|	Субконто2,
	|	Субконто3,
	|	Номенклатура,
	|	КорСчет
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РезультатРаспределения.Правило,
	|	РезультатРаспределения.Идентификатор КАК Идентификатор,
	|	РезультатРаспределения.Счет КАК Счет,
	|	РезультатРаспределения.ВидДеятельности КАК ВидДеятельности,
	|	РезультатРаспределения.Подразделение КАК Подразделение,
	|	РезультатРаспределения.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
	|	РезультатРаспределения.СтатьяЗатрат КАК СтатьяЗатрат,
	|	ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка) КАК Номенклатура,
	|	РезультатРаспределения.ВидРасходовНУ КАК ВидРасходовНУ,
	|	РезультатРаспределения.НалоговыйУчет КАК НалоговыйУчет,
	|	РезультатРаспределения.КорСчет КАК КорСчет,
	|	РезультатРаспределения.База КАК База
	|ПОМЕСТИТЬ ВТ_РаспределениеНаРасходы
	|ИЗ
	|	&РаспределениеНаРасходы КАК РезультатРаспределения
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Счет,
	|	ВидДеятельности,
	|	Подразделение,
	|	НоменклатурнаяГруппа,
	|	СтатьяЗатрат,
	|	Номенклатура,
	|	ВидРасходовНУ,
	|	НалоговыйУчет,
	|	КорСчет
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РезультатРаспределения.Правило,
	|	РезультатРаспределения.Идентификатор КАК Источник,
	|	Расходы.Идентификатор КАК Приемник,
	|	РезультатРаспределения.База
	|ПОМЕСТИТЬ РаспределениеНаРасходы
	|ИЗ
	|	ВТ_РаспределениеНаРасходы КАК РезультатРаспределения
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Расходы КАК Расходы
	|		ПО РезультатРаспределения.Счет = Расходы.Счет
	|			И РезультатРаспределения.ВидДеятельности = Расходы.ВидДеятельности
	|			И РезультатРаспределения.Подразделение = Расходы.Подразделение
	|			И РезультатРаспределения.НоменклатурнаяГруппа = Расходы.НоменклатурнаяГруппа
	|			И РезультатРаспределения.СтатьяЗатрат = Расходы.СтатьяЗатрат
	|			И РезультатРаспределения.Номенклатура = Расходы.Номенклатура
	|			И РезультатРаспределения.ВидРасходовНУ = Расходы.ВидРасходовНУ
	|			И РезультатРаспределения.НалоговыйУчет = Расходы.НалоговыйУчет
	|			И РезультатРаспределения.КорСчет = Расходы.КорСчет
	|			И НЕ Расходы.СубконтоПользователяОпределены
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Источник,
	|	Приемник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РезультатРаспределения.Правило,
	|	РезультатРаспределения.Идентификатор КАК Идентификатор,
	|	РезультатРаспределения.ТипРазниц КАК ТипРазниц,
	|	РезультатРаспределения.Счет КАК Счет,
	|	РезультатРаспределения.ВидДеятельности КАК ВидДеятельности,
	|	РезультатРаспределения.Подразделение КАК Подразделение,
	|	РезультатРаспределения.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
	|	РезультатРаспределения.СтатьяЗатрат КАК СтатьяЗатрат,
	|	ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка) КАК Номенклатура,
	|	РезультатРаспределения.ВидРасходовНУ КАК ВидРасходовНУ,
	|	РезультатРаспределения.НалоговыйУчет КАК НалоговыйУчет,
	|	РезультатРаспределения.КорСчет КАК КорСчет
	|ПОМЕСТИТЬ ВТ_ОбособленныйНалоговыйУчет
	|ИЗ
	|	&ОбособленныйНалоговыйУчет КАК РезультатРаспределения
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Счет,
	|	ВидДеятельности,
	|	Подразделение,
	|	НоменклатурнаяГруппа,
	|	СтатьяЗатрат,
	|	Номенклатура,
	|	ВидРасходовНУ,
	|	НалоговыйУчет,
	|	КорСчет
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РезультатРаспределения.Правило,
	|	РезультатРаспределения.Идентификатор КАК Источник,
	|	Расходы.Идентификатор КАК Приемник,
	|	РезультатРаспределения.ТипРазниц
	|ПОМЕСТИТЬ ОбособленныйНалоговыйУчет
	|ИЗ
	|	ВТ_ОбособленныйНалоговыйУчет КАК РезультатРаспределения
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Расходы КАК Расходы
	|		ПО РезультатРаспределения.Счет = Расходы.Счет
	|			И РезультатРаспределения.ВидДеятельности = Расходы.ВидДеятельности
	|			И РезультатРаспределения.Подразделение = Расходы.Подразделение
	|			И РезультатРаспределения.НоменклатурнаяГруппа = Расходы.НоменклатурнаяГруппа
	|			И РезультатРаспределения.СтатьяЗатрат = Расходы.СтатьяЗатрат
	|			И РезультатРаспределения.Номенклатура = Расходы.Номенклатура
	|			И РезультатРаспределения.ВидРасходовНУ = Расходы.ВидРасходовНУ
	|			И РезультатРаспределения.НалоговыйУчет = Расходы.НалоговыйУчет
	|			И РезультатРаспределения.КорСчет = Расходы.КорСчет
	|			И НЕ Расходы.СубконтоПользователяОпределены
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Источник,
	|	Приемник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РезультатРаспределения.Правило,
	|	РезультатРаспределения.Идентификатор
	|ПОМЕСТИТЬ ЗафиксированныеРасходы
	|ИЗ
	|	&ЗафиксированныеРасходы КАК РезультатРаспределения
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	РезультатРаспределения.Идентификатор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_РаспределениеНаРасходы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_ОбособленныйНалоговыйУчет
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НеУдалосьРаспределить.Идентификатор КАК Идентификатор,
	|	НеУдалосьРаспределить.Правило КАК Правило
	|ПОМЕСТИТЬ ВТ_НеУдалосьРаспределить
	|ИЗ
	|	&НеУдалосьРаспределить КАК НеУдалосьРаспределить
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Идентификатор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НеУдалосьРаспределить.Идентификатор КАК Идентификатор,
	|	НеУдалосьРаспределить.Правило КАК Правило
	|ПОМЕСТИТЬ НеУдалосьРаспределить
	|ИЗ
	|	ВТ_НеУдалосьРаспределить КАК НеУдалосьРаспределить
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Расходы КАК Расходы
	|		ПО НеУдалосьРаспределить.Идентификатор = Расходы.Идентификатор
	|ГДЕ
	|	Расходы.МожноРаспределить
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Идентификатор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_НеУдалосьРаспределить";
	
	Запрос.Выполнить();
	
КонецПроцедуры
