
// Возвращает массив счетов учета налогов и взносов,
// расчет и отражение которых производится автоматически
Функция СчетаУчетаАвтоначисляемыхНалоговВключаемыхВРасходы() Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Хозрасчетный.Ссылка КАК Счет
	|ИЗ
	|	ПланСчетов.Хозрасчетный КАК Хозрасчетный
	|ГДЕ
	|	Хозрасчетный.Ссылка В ИЕРАРХИИ 
	|		(ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НДФЛ),
	|			ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ЗемельныйНалог),
	|			ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ТранспортныйНалог),
	|			ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НалогНаИмущество),
	|			ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НДСНалоговогоАгента),
	|			ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыПоСоциальномуСтрахованию))
	|	И НЕ Хозрасчетный.ЗапретитьИспользоватьВПроводках"
	;

	ТаблицаСчетов = Запрос.Выполнить().Выгрузить();
	
	Возврат ТаблицаСчетов.ВыгрузитьКолонку("Счет");
	
КонецФункции

// Возвращает массив счетов учета налогов, уменьшающих прибыль
Функция СчетаУчетаНалоговУменьшающихПрибыль() Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Хозрасчетный.Ссылка КАК Счет
	|ИЗ
	|	ПланСчетов.Хозрасчетный КАК Хозрасчетный
	|ГДЕ
	|	Хозрасчетный.Ссылка В ИЕРАРХИИ 
	|		(ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ЕНВД),
	|			ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ЕНприУСН))
	|	И НЕ Хозрасчетный.ЗапретитьИспользоватьВПроводках"
	;

	ТаблицаСчетов = Запрос.Выполнить().Выгрузить();
	
	Возврат ТаблицаСчетов.ВыгрузитьКолонку("Счет");
	
КонецФункции

Функция ВидыПлатежейВГосБюджетНалоговые() Экспорт

	ВидыПлатежей = Новый Массив;
	ВидыПлатежей.Добавить(Перечисления.ВидыПлатежейВГосБюджет.Налог);
	ВидыПлатежей.Добавить(Перечисления.ВидыПлатежейВГосБюджет.НалогАкт);
	ВидыПлатежей.Добавить(Перечисления.ВидыПлатежейВГосБюджет.НалогСам);
	
	Возврат ВидыПлатежей;

КонецФункции // ВидыПлатежейВГосБюджетНалоговые()

Функция СчетаУчетаВзносовСФОТ() Экспорт

	СчетаВзносов = Новый Массив;
	
	СчетаВзносов.Добавить(ПланыСчетов.Хозрасчетный.ПФР_страх); // 69.02.1 (до 2014 года)
	СчетаВзносов.Добавить(ПланыСчетов.Хозрасчетный.ПФР_нак);   // 69.02.2 (до 2014 года)
	СчетаВзносов.Добавить(ПланыСчетов.Хозрасчетный.ПФР_ОПС);   // 69.02.7 (с 2014 года)
	СчетаВзносов.Добавить(ПланыСчетов.Хозрасчетный.ФСС);       // 69.01
	СчетаВзносов.Добавить(ПланыСчетов.Хозрасчетный.ФСС_НСиПЗ); // 69.11
	СчетаВзносов.Добавить(ПланыСчетов.Хозрасчетный.ФОМС);      // 69.03.1
	
	Возврат БухгалтерскийУчет.ПолучитьМассивСчетовССубсчетами(СчетаВзносов, Ложь,, Ложь);
	
КонецФункции
