#Область ПрограммныйИнтерфейс

#Область ПорядокУчетаРасчетов

// Возвращает перечень реквизитов документа, предназначенных для учета взаиморасчетов.
// Этим реквизитам посвящен эта область модуля.
// Все упомянутые в функции реквизиты должны быть в документе, для которых вызваются процедуры и функции 
// из этой области.
//
// Функция возвращает реквизиты, имеющие смысл в данном сеансе.
//
// В прикладных алгоритмах может потребоваться учитывать, что при определенных значениях реквизитов документа
// некоторые или все эти реквизиты могут оказаться не нужны.
// Для того, чтобы учесть эти особенности конкретного экземпляра документа, следует передать параметр ОсобенностиДокумента
//
// Параметры:
//  ОсобенностиДокумента - см. НовыйОсобенностиУчетаРасчетовДокумента()
//
// Возвращаемое значение:
//  Структура, ключи которой - имена реквизитов 
//
Функция РеквизитыДокументаПорядокУчетаРасчетов(ОсобенностиДокумента = Неопределено) Экспорт
	
	Реквизиты = Новый Структура;
	
	Если ОсобенностиДокумента = Неопределено Тогда
		ТребуетсяУчетРасчетов = Истина;
		ТребуетсяУчетАвансов  = Истина;
	Иначе
		ТребуетсяУчетРасчетов = ОсобенностиДокумента.ТребуетсяУчетРасчетов;
		ТребуетсяУчетАвансов  = ОсобенностиДокумента.ТребуетсяУчетАвансов;
	КонецЕсли;
	
	Если ТребуетсяУчетРасчетов Тогда
		
		Если СчетаУчетаВДокументахВызовСервераПовтИсп.ПользовательУправляетСчетамиУчета() Тогда
			Реквизиты.Вставить("СчетУчетаРасчетовСКонтрагентом");
			Если ТребуетсяУчетАвансов Тогда
				Реквизиты.Вставить("СчетУчетаРасчетовПоАвансам");
			КонецЕсли;
		КонецЕсли;
		
		Если ТребуетсяУчетАвансов Тогда
			Если ПолучитьФункциональнуюОпцию("УправлениеЗачетомАвансовПогашениемЗадолженности") Тогда
				Реквизиты.Вставить("СпособЗачетаАвансов");
				Реквизиты.Вставить("ЗачетАвансов");
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Реквизиты;

КонецФункции

// Процедура обновляет текст надписи ПорядокУчетаРасчетов на форме
// и устанавливает необходимые свойства элемента управления 
// в зависимости от заполненности счетов учета.
//
Процедура УстановитьПорядокУчетаРасчетов(Форма, ОсобенностиДокумента = Неопределено) Экспорт

	Объект   = Форма.Объект;
	Элементы = Форма.Элементы;
	
	ПорядокУчетаРасчетов = СвойстваФормыПорядокУчетаРасчетов(Объект, ОсобенностиДокумента);
	Форма.ПорядокУчетаРасчетов                       = ПорядокУчетаРасчетов.Представление;
	Форма.СчетаРасчетовЗаполнены                     = ПорядокУчетаРасчетов.Заполнен;
	Элементы.ПорядокУчетаРасчетов.ПропускатьПриВводе = ПорядокУчетаРасчетов.Заполнен;
	Элементы.ПорядокУчетаРасчетов.Видимость          = (ПорядокУчетаРасчетов.Реквизиты.Количество() > 0);
	
КонецПроцедуры

// Процедура сохраняет в документе счета учета и способ зачета авансов,
// выбранные в форме ВыборПорядкаУчетаРасчетов.
//
Процедура ОбработкаВыбораПорядокУчетаРасчетов(Форма, ВыбранноеЗначение) Экспорт

	Объект = Форма.Объект;

	ЗаполнитьЗначенияСвойств(Объект, ВыбранноеЗначение);
	
	Реквизиты = РеквизитыДокументаПорядокУчетаРасчетов();
	Если ПолучитьФункциональнуюОпцию("УправлениеЗачетомАвансовПогашениемЗадолженности") Тогда
		ТаблицаЗачетАвансов = ПолучитьИзВременногоХранилища(ВыбранноеЗначение.АдресХранилищаЗачетАвансов);
		Объект.ЗачетАвансов.Загрузить(ТаблицаЗачетАвансов);
	КонецЕсли;
	
	Форма.Модифицированность = Истина;

КонецПроцедуры

// Конструктор коллекции, которая описывает особенности учета расчетов в конкретном экземпляре документа.
// 
Функция НовыйОсобенностиУчетаРасчетовДокумента() Экспорт
	
	Особенности = Новый Структура;
	Особенности.Вставить("ТребуетсяУчетРасчетов", Истина);
	Особенности.Вставить("ТребуетсяУчетАвансов",  Истина);
	
	Возврат Особенности;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область ПорядокУчетаРасчетов

Функция СвойстваФормыПорядокУчетаРасчетов(Объект, ОсобенностиДокумента)
	
	Результат = Новый Структура;
	Результат.Вставить("Реквизиты",         РеквизитыДокументаПорядокУчетаРасчетов(ОсобенностиДокумента));
	Результат.Вставить("Представление",     "");
	Результат.Вставить("ТребуютЗаполнения", Новый Структура);
	Результат.Вставить("Заполнен",          Истина);
	
	Реквизиты = Результат.Реквизиты; // Для упрощения кода. При изменении свойств у Реквизиты меняется Результат.
	ЗаполнитьЗначенияСвойств(Реквизиты, Объект);
	
	// Подготовим текст и перечень реквизитов, требующих заполнения
	ЭлементыТекста = Новый Массив;
	
	Счета = Новый Массив; // Для счетов выводим код счета
	Счета.Добавить("СчетУчетаРасчетовСКонтрагентом");
	Счета.Добавить("СчетУчетаРасчетовПоАвансам");
	
	Для Каждого ИмяРеквизита Из Счета Цикл
		
		Если Не Реквизиты.Свойство(ИмяРеквизита) Тогда
			// Не выводится
			Продолжить;
		КонецЕсли;
		
		Счет = Реквизиты[ИмяРеквизита];
		
		Если Не ЗначениеЗаполнено(Счет) Тогда
			ЭлементыТекста.Добавить(ОбщегоНазначенияБПКлиентСервер.ПредставлениеНезаполненногоЗначения());
			Результат.ТребуютЗаполнения.Вставить(ИмяРеквизита);
		Иначе
			СвойствоСчета = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Счет);
			ЭлементыТекста.Добавить(СвойствоСчета.Код);
		КонецЕсли;
		
	КонецЦикла;
	
	Если Реквизиты.Свойство("СпособЗачетаАвансов") Тогда
		
		СпособЗачетаАвансов = Реквизиты.СпособЗачетаАвансов;
		
		Если СпособЗачетаАвансов = Перечисления.СпособыЗачетаАвансов.Автоматически Тогда
			ЭлементыТекста.Добавить(НСтр("ru = 'зачет аванса автоматически'"));
		ИначеЕсли СпособЗачетаАвансов = Перечисления.СпособыЗачетаАвансов.ПоДокументу Тогда
			ЭлементыТекста.Добавить(НСтр("ru = 'зачет аванса по документам'"));
			Если Реквизиты.Свойство("ЗачетАвансов") И ЗначениеЗаполнено(Реквизиты.ЗачетАвансов) Тогда
				Результат.ТребуютЗаполнения.Вставить(ИмяРеквизита);
			КонецЕсли;
		ИначеЕсли СпособЗачетаАвансов = Перечисления.СпособыЗачетаАвансов.НеЗачитывать Тогда
			ЭлементыТекста.Добавить(НСтр("ru = 'аванс не зачитывается'"));
		КонецЕсли;

	КонецЕсли;
	
	Если ЭлементыТекста.Количество() = 0 Тогда
		Результат.Представление = НСтр("ru = 'Не требуется'");
	Иначе
		Результат.Представление = СтроковыеФункцииКлиентСервер.СтрокаИзМассиваПодстрок(ЭлементыТекста, НСтр("ru = ', '"));
		Если Не ПустаяСтрока(Результат.Представление) Тогда
			// Предложение следует начинать с заглавной буквы
			Результат.Представление = ВРег(Лев(Результат.Представление, 1)) + Сред(Результат.Представление, 2);
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти
