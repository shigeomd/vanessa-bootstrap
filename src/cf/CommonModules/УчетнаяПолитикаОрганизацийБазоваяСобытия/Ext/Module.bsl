
#Область СлужебныеПроцедурыИФункции

Процедура УстановитьРегистрациюВНалоговомОргане(Источник, Отказ) Экспорт
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(Источник) Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	МИНИМУМ(ИсторияРегистрацийВНалоговомОргане.Период) КАК Период
	|ИЗ
	|	РегистрСведений.ИсторияРегистрацийВНалоговомОргане КАК ИсторияРегистрацийВНалоговомОргане
	|ГДЕ
	|	ИсторияРегистрацийВНалоговомОргане.РегистрацияВНалоговомОргане = &РегистрацияВНалоговомОргане";
	
	Запрос.УстановитьПараметр("РегистрацияВНалоговомОргане", Источник.РегистрацияВНалоговомОргане);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ПериодЗаписи = Выборка.Период;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ПериодЗаписи) Тогда
		ПериодЗаписи = ЗарплатаКадрыКлиентСервер.ДатаОтсчетаПериодическихСведенийСПериодомМесяц();
	КонецЕсли;
	
	Набор = РегистрыСведений.ИсторияРегистрацийВНалоговомОргане.СоздатьНаборЗаписей();
	Набор.Отбор.СтруктурнаяЕдиница.Установить(Источник.Ссылка);
	
	Запись = Набор.Добавить();
	Запись.Период = ПериодЗаписи;
	Запись.СтруктурнаяЕдиница = Источник.Ссылка;
	Запись.РегистрацияВНалоговомОргане = Источник.РегистрацияВНалоговомОргане;
	
	Набор.ДополнительныеСвойства.Вставить("ОтключитьПроверкуДатыЗапретаИзменения", Истина);
	
	Набор.Записать();
	
	ОбновитьПовторноИспользуемыеЗначения();
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

#КонецОбласти
