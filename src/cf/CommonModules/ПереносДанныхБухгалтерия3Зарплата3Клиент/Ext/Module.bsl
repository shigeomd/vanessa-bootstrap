#Область ПрограммныйИнтерфейс


// Обработчик, вызываемый при начале работы системы.
//
// Параметры:
//		Параметры - описание см ОбщегоНазначенияКлиентПереопределяемый.ПриНачалеРаботыСистемы
//
Процедура ПриНачалеРаботыСистемы(Параметры) Экспорт
	
	Если НЕ ПустаяСтрока(ПараметрЗапуска) Тогда
		
		ПараметрыЗапуска = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ПараметрЗапуска, ";");
		ПервыйПараметр = ВРег(ПараметрыЗапуска[0]);
		
		Если ПервыйПараметр = Врег("ЗарплатаКадрыПереносДанных") Тогда
			ДополнительныеПараметры = Новый Структура("ИмяФайлаИнструкций", СтрЗаменить(ПараметрыЗапуска[1], "*", " "));
			Параметры.ИнтерактивнаяОбработка = Новый ОписаниеОповещения("ПереносДанныхБухгалтерия3Зарплата3", ЭтотОбъект, ДополнительныеПараметры);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

Процедура ПереносДанныхБухгалтерия3Зарплата3(Результат, ДополнительныеПараметры) Экспорт

	Оповещение = Новый ОписаниеОповещения("ПереносДанныхБухгалтерия3Зарплата3Завершение", ЭтотОбъект);
	ОткрытьФорму("Обработка.ПереносДанныхБухгалтерия3Зарплата3.Форма.Форма",ДополнительныеПараметры,,,,,Оповещение);	

КонецПроцедуры

Процедура ПереносДанныхБухгалтерия3Зарплата3Завершение(Результат, ДополнительныеПараметры) Экспорт

	ЗавершитьРаботуСистемы(Ложь);

КонецПроцедуры

#КонецОбласти
