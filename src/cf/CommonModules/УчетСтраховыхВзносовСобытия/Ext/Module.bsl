
#Область СлужебныеПроцедурыИФункции

Процедура УстановитьФункциональныеОпцииУчетСтраховыхВзносов(Источник, Отказ) Экспорт
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(Источник) Тогда
		Возврат;
	КонецЕсли;
	
	ЗаписиФО = РегистрыСведений.НастройкиУчетаСтраховыхВзносов.СоздатьНаборЗаписей();
	ЗаписиФО.Отбор.Организация.Установить(Источник.Ссылка);
	ЗаписиФО.Прочитать();
	Если ЗаписиФО.Количество() = 0 Тогда
		СтрокаФО = ЗаписиФО.Добавить();
		СтрокаФО.Организация = Источник.Ссылка;
		ЗаписиФО.Записать();
	КонецЕсли;

КонецПроцедуры

Процедура ЗаполнитьСтавкуФССНСНовойОрганизации(Источник, Отказ) Экспорт
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(Источник) Тогда
		Возврат;
	КонецЕсли;
	
	ЗаписиСтавокФССНС = РегистрыСведений.СтавкаВзносаНаСтрахованиеОтНесчастныхСлучаев.СоздатьНаборЗаписей();
	ЗаписиСтавокФССНС.ДополнительныеСвойства.Вставить("ПропуститьПроверкуЗапретаИзменения");
	ЗаписиСтавокФССНС.Отбор.Организация.Установить(Источник.Ссылка);
	ЗаписиСтавокФССНС.Прочитать();
	Если ЗаписиСтавокФССНС.Количество() = 0 Тогда
		
		ЗаписьПоУмолчанию = РегистрыСведений.СтавкаВзносаНаСтрахованиеОтНесчастныхСлучаев.ЗаписьПоУмолчанию();
		НоваяЗапись = ЗаписиСтавокФССНС.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяЗапись, ЗаписьПоУмолчанию);
		НоваяЗапись.Организация = Источник.Ссылка;
		ЗаписиСтавокФССНС.Записать();
		
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьФункциональныеОпцииИспользованияСтраховыхВзносовПоКлассамУсловийТрудаПриЗаписи(Источник, Отказ, Замещение) Экспорт
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(Источник) Тогда
		Возврат;
	КонецЕсли;
	
	УчетСтраховыхВзносов.УстановитьФункциональныеОпцииИспользованияСтраховыхВзносовПоКлассамУсловийТруда();

КонецПроцедуры

#КонецОбласти
