////////////////////////////////////////////////////////////////////////////////
// Процедуры и функции для печати договоров.
//  
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Выполняет команду формирования текста договора на основании шаблона.
//
Функция ВыполнитьКомандуПечатиТекстаДоговора(ОписаниеКоманды) Экспорт

	Если ТипЗнч(ОписаниеКоманды.ОбъектыПечати) <> Тип("Массив")
		ИЛИ ОписаниеКоманды.ОбъектыПечати.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	// Работаем только с одним объектом (первым в массиве)
	ОбъектПечати = ОписаниеКоманды.ОбъектыПечати[0];
	
	ФайлДоговора = ПечатьДоговоровВызовСервера.ФайлДоговораДляОбъекта(ОбъектПечати);

	Если ЗначениеЗаполнено(ФайлДоговора) Тогда

		ПараметрыФормы = Новый Структура();
		ПараметрыФормы.Вставить("ОбъектПечати", ОбъектПечати);
		ПараметрыФормы.Вставить("ФайлДоговора", ФайлДоговора);

		ОткрытьФорму("Справочник.ДоговорыКонтрагентов.Форма.ФормаРедактированияТекстаДоговора", ПараметрыФормы, ОписаниеКоманды.Форма);
		
	Иначе
	
		// Это первый вызов печати текст договора, файл еще не был создан,
		// поэтому предложим пользователю сначала выбрать шаблон договора, 
		// на основании которого создавать текст.
		ДополнительныеПараметры = Новый Структура();
		ДополнительныеПараметры.Вставить("ОбъектПечати", 	ОбъектПечати);
		ДополнительныеПараметры.Вставить("ВладелецФормы", 	ОписаниеКоманды.Форма);
		
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"ВыполнитьКомандуПечатиТекстаДоговораЗавершение", ЭтотОбъект, ДополнительныеПараметры);
			
		ОткрытьФорму("Справочник.ШаблоныДоговоров.ФормаВыбора",
			,
			,
			,
			,
			,
			ОписаниеОповещения,
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
	КонецЕсли;

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ВыполнитьКомандуПечатиТекстаДоговораЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт

	Если НЕ ЗначениеЗаполнено(РезультатЗакрытия) Тогда
		Возврат;
	КонецЕсли;
	
	Если ОценкаПроизводительностиВызовСервераПовтИсп.ВыполнятьЗамерыПроизводительности() Тогда
		ИмяМакетаШаблона = ОбщегоНазначенияБПВызовСервера.ЗначениеРеквизитаОбъекта(РезультатЗакрытия, "ИмяМакета");
		Если ТипЗнч(ДополнительныеПараметры.ОбъектПечати) = Тип("ДокументСсылка.СчетНаОплатуПокупателю") Тогда
			КлючеваяОперация = "ЗаполнениеДоговораИзСчетаПоШаблону";
		Иначе
			КлючеваяОперация = "ЗаполнениеДоговораПоШаблону";
		КонецЕсли;
		
		Если ИмяМакетаШаблона = "ДоговорПоставки"
			ИЛИ ИмяМакетаШаблона = "ДоговорОказанияУслуг"
			ИЛИ ИмяМакетаШаблона = "ДоговорПодряда" Тогда
				КлючеваяОперация = КлючеваяОперация + СтрЗаменить(ИмяМакетаШаблона, "Договор", "");
		КонецЕсли;
		ОценкаПроизводительностиКлиентСервер.НачатьЗамерВремени(КлючеваяОперация);
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("ОбъектПечати", 	ДополнительныеПараметры.ОбъектПечати);
	ПараметрыФормы.Вставить("ШаблонДоговора", 	РезультатЗакрытия);
	ОткрытьФорму("Справочник.ДоговорыКонтрагентов.Форма.ФормаРедактированияТекстаДоговора", 
		ПараметрыФормы, 
		ДополнительныеПараметры.ВладелецФормы);

КонецПроцедуры

#КонецОбласти
