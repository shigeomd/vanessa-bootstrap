
Функция ИспользоватьВнешнийМодуль() Экспорт
	
	Возврат Константы.ДокументооборотСКонтролирующимиОрганами_ИспользоватьВнешнийМодуль.Получить();
	
КонецФункции

Функция ИнициализироватьКонтекстДокументооборотаСНалоговымиОрганами() Экспорт
	
	ИмяОбработки_ДокументооборотСКО = Неопределено;
	КонтекстЭДО						= Неопределено;
	ЭтоПерваяИтерация				= Истина;
	ИнициализироватьКонтекст 		= Истина;
	
	Пока ИнициализироватьКонтекст Цикл
		
		ИнициализироватьКонтекст = Ложь;
		
		ВнешниеОбъектыХранилище = Константы.ДокументооборотСКонтролирующимиОрганами_ВнешнийМодуль;
		ДвоичныеДанныеОбработки = ВнешниеОбъектыХранилище.Получить().Получить();
		Если ДвоичныеДанныеОбработки <> Неопределено Тогда
			
			Адрес = ПоместитьВоВременноеХранилище(ДвоичныеДанныеОбработки);
			Попытка
				ИмяОбработки_ДокументооборотСКО = ВнешниеОбработки.Подключить(Адрес, "Обработка_ДокументооборотСКО", Ложь);
			Исключение
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Не удалось загрузить внешний модуль для документооборота с налоговыми органами:
				|" + ИнформацияОбОшибке().Описание + "
				|Будет использован модуль, встроенный в конфигурацию.");
			КонецПопытки;
		КонецЕсли;
		
		КонтекстЭДО = НЕ (ИмяОбработки_ДокументооборотСКО = Неопределено);
		
		ЭтоПерваяИтерация = Ложь;
		
	КонецЦикла;
	
	Возврат (КонтекстЭДО <> Неопределено);
	
КонецФункции

Функция ЕстьПравоНаДОсКО(ПравоНаВнешнюю) Экспорт 
	
	Если ПравоНаВнешнюю Тогда 
		Возврат Истина;
	Иначе
		Возврат ПравоДоступа("Использование", Метаданные.Обработки.ДокументооборотСКонтролирующимиОрганами);
	КонецЕсли;
	
КонецФункции

Функция ПолучитьОбработкуЭДО(ТекстСообщения = "") Экспорт
	
	Если НЕ ОбщегоНазначенияПовтИсп.РазделениеВключено() И ПодключатьВнешнююОбработкуЭДО() И НЕ ОбновлениеИнформационнойБазы.НеобходимоОбновлениеИнформационнойБазы() Тогда
		Если ЕстьПравоНаДОсКО(Истина) Тогда 
			ОбъектОбработка = ВнешниеОбработки.Создать("Обработка_ДокументооборотСКО");
			ОбъектОбработка.ПутьКОбъекту = "ВнешняяОбработка.Обработка_ДокументооборотСКО";
			Возврат ОбъектОбработка;
		Иначе
			ТекстСообщения = НСтр("ru = 'Недостаточно прав для использования методов электронного документооборота с контролирующими органами.'");
			Возврат Неопределено;
		КонецЕсли;
	Иначе
		Если ЕстьПравоНаДОсКО(Ложь) Тогда 
			ОбъектОбработка = Обработки.ДокументооборотСКонтролирующимиОрганами.Создать();
			ОбъектОбработка.ПутьКОбъекту = "Обработка.ДокументооборотСКонтролирующимиОрганами";
			Возврат ОбъектОбработка;
		Иначе
			ТекстСообщения = НСтр("ru = 'Недостаточно прав для использования методов электронного документооборота с контролирующими органами.'");
			Возврат Неопределено;
		КонецЕсли;
	КонецЕсли;
	
КонецФункции


Функция ЕстьДоступНаЧтение()
	
	Возврат ПравоДоступа("Чтение", Метаданные.Константы.ДокументооборотСКонтролирующимиОрганами_ИспользоватьВнешнийМодуль)
	И ПравоДоступа("Чтение", Метаданные.Константы.ДокументооборотСКонтролирующимиОрганами_ВнешнийМодуль);
	
КонецФункции


Функция ПодключатьВнешнююОбработкуЭДО() Экспорт
	
	// если есть доступ
	Если ЕстьДоступНаЧтение() Тогда 
		Если Константы.ДокументооборотСКонтролирующимиОрганами_ИспользоватьВнешнийМодуль.Получить() Тогда 
			Попытка 
				ОбработкаЭДО = ВнешниеОбработки.Создать("Обработка_ДокументооборотСКО");
			Исключение
				ОбработкаЭДО = Неопределено;
			КонецПопытки;
			Если ОбработкаЭДО <> Неопределено Тогда 
				Возврат Истина;
			Иначе 
				Возврат ИнициализироватьКонтекстДокументооборотаСНалоговымиОрганами();
			КонецЕсли;
		Иначе 
			Возврат Ложь;
		КонецЕсли;
	Иначе 
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

Процедура ЗаполнитьПараметрыФормы(ПараметрыФормы, Параметры) Экспорт
	
	ВозможныеПредопределенныеПараметры = Новый Структура("ВыборГруппИЭлементов, ЗакрыватьПриВыборе, ЗакрыватьПриЗакрытииВладельца, КлючНазначенияИспользования, МножественныйВыбор, Отбор, ПараметрыФормы, ПараметрыФункциональныхОпций, РазрешитьВыборКорня, РежимВыбора, ТекущаяСтрока, ТолькоПросмотр, ФиксированныеНастройки");
	ЗаполнитьЗначенияСвойств(ВозможныеПредопределенныеПараметры, Параметры);
	Для Каждого ВозможныйПредопределенныйПараметр Из ВозможныеПредопределенныеПараметры Цикл
		Если ЗначениеЗаполнено(ВозможныйПредопределенныйПараметр.Значение) Тогда
			ПараметрыФормы.Добавить(ВозможныйПредопределенныйПараметр.Значение, ВозможныйПредопределенныйПараметр.Ключ);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Функция ТекущемуПользователюАОДоступен() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ПользователиУчетныхЗаписейДокументооборота.УчетнаяЗапись
	               |ИЗ
	               |	РегистрСведений.ПользователиУчетныхЗаписейДокументооборота КАК ПользователиУчетныхЗаписейДокументооборота
	               |		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |			Организации.Ссылка КАК Ссылка,
	               |			Организации.УчетнаяЗаписьОбмена КАК УчетнаяЗаписьОбмена
	               |		ИЗ
	               |			Справочник.Организации КАК Организации
	               |		ГДЕ
	               |			Организации.ВидОбменаСКонтролирующимиОрганами = &ВидОбменаСКонтролирующимиОрганами
	               |			И Организации.УчетнаяЗаписьОбмена <> &ПустаяУчетнаяЗапись) КАК Организации
	               |		ПО ПользователиУчетныхЗаписейДокументооборота.УчетнаяЗапись = Организации.УчетнаяЗаписьОбмена
	               |ГДЕ
	               |	НЕ ПользователиУчетныхЗаписейДокументооборота.УчетнаяЗапись.ОтключитьАвтообмен
	               |	И НЕ ПользователиУчетныхЗаписейДокументооборота.УчетнаяЗапись.ПометкаУдаления
	               |	И ПользователиУчетныхЗаписейДокументооборота.Пользователь.Ссылка = &ТекущийПользователь
	               |	И НЕ ПользователиУчетныхЗаписейДокументооборота.Пользователь.ПометкаУдаления
	               |	И НЕ Организации.Ссылка ЕСТЬ NULL ";
	
	Запрос.УстановитьПараметр("ВидОбменаСКонтролирующимиОрганами", Перечисления.ВидыОбменаСКонтролирующимиОрганами.ОбменВУниверсальномФормате);
	Запрос.УстановитьПараметр("ПустаяУчетнаяЗапись", Справочники.УчетныеЗаписиДокументооборота.ПустаяСсылка());
	Запрос.УстановитьПараметр("ТекущийПользователь", Пользователи.ТекущийПользователь());
	
	Попытка
		Возврат Запрос.Выполнить().Выбрать().Следующий();
	Исключение
		Возврат Ложь;
	КонецПопытки;
	
КонецФункции

Функция ТекущемуПользователюЭДОДоступен() Экспорт
	
	Если Не ОбщегоНазначенияПовтИсп.ДоступноИспользованиеРазделенныхДанных() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ПользователиУчетныхЗаписейДокументооборота.УчетнаяЗапись
	               |ИЗ
	               |	РегистрСведений.ПользователиУчетныхЗаписейДокументооборота КАК ПользователиУчетныхЗаписейДокументооборота
	               |		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |			Организации.Ссылка КАК Ссылка,
	               |			Организации.УчетнаяЗаписьОбмена КАК УчетнаяЗаписьОбмена
	               |		ИЗ
	               |			Справочник.Организации КАК Организации
	               |		ГДЕ
	               |			Организации.ВидОбменаСКонтролирующимиОрганами = &ВидОбменаСКонтролирующимиОрганами
	               |			И Организации.УчетнаяЗаписьОбмена <> &ПустаяУчетнаяЗапись) КАК Организации
	               |		ПО ПользователиУчетныхЗаписейДокументооборота.УчетнаяЗапись = Организации.УчетнаяЗаписьОбмена
	               |ГДЕ
	               |	НЕ ПользователиУчетныхЗаписейДокументооборота.УчетнаяЗапись.ОтключитьАвтообмен
	               |	И НЕ ПользователиУчетныхЗаписейДокументооборота.УчетнаяЗапись.ПометкаУдаления
	               |	И ПользователиУчетныхЗаписейДокументооборота.Пользователь.Ссылка = &ТекущийПользователь
	               |	И НЕ ПользователиУчетныхЗаписейДокументооборота.Пользователь.ПометкаУдаления
	               |	И НЕ Организации.Ссылка ЕСТЬ NULL ";
	
	Запрос.УстановитьПараметр("ВидОбменаСКонтролирующимиОрганами", Перечисления.ВидыОбменаСКонтролирующимиОрганами.ОбменВУниверсальномФормате);
	Запрос.УстановитьПараметр("ПустаяУчетнаяЗапись", Справочники.УчетныеЗаписиДокументооборота.ПустаяСсылка());
	Запрос.УстановитьПараметр("ТекущийПользователь", Пользователи.ТекущийПользователь());
	
	Попытка
		Возврат Запрос.Выполнить().Выбрать().Следующий();
	Исключение
		Возврат Ложь;
	КонецПопытки;
	
КонецФункции

Функция ПолучитьВыбранныйCSPИзВременныхНастроек() Экспорт
	
	Возврат ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		"МастерФормированияЗаявкиНаПодключение",
		"ВыбранныйКриптопровайдер",
		,,
		
	);
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////

Процедура ПриИнициализацииФормыРегламентированногоОтчета(Отчет, ОрганизацияОтчета, КонтролирующийОрган, ПараметрыПрорисовкиКнопокОтправки, ПараметрыПрорисовкиПанели) Экспорт
	
	// прорисовываем кнопки отправки
	ПараметрыПрорисовкиКнопокОтправки 	= ПараметрыПрорисовкиКнопокОтправки(ОрганизацияОтчета, КонтролирующийОрган);
	// прорисовываем панель отправки
	ПараметрыПрорисовкиПанели 			= ПараметрыПрорисовкиПанелиОтправки(Отчет, ОрганизацияОтчета, КонтролирующийОрган);
	
КонецПроцедуры

Функция ПараметрыПрорисовкиКнопокОтправки(ОрганизацияОтчета, КонтролирующийОрган) Экспорт
	
	ПараметрыПрорисовкиКнопокОтправки = Новый Структура;
	
	Попытка
		
		КонтекстЭДОСервер = ПолучитьОбработкуЭДО();
		
		ПараметрыПрорисовкиКнопокОтправки.Вставить("ГруппаОтправкаВКонтролирующийОрган", Истина);
		
		СвойстваОрганизации = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ОрганизацияОтчета,
			"ВидОбменаСКонтролирующимиОрганами, УчетнаяЗаписьОбмена");
		
		// прорисовываем кнопки отправки
		ПредназначенаДляДокументооборотаСКонтролирующимОрганом = Истина;
		УчетнаяЗаписьДокументооборота = СвойстваОрганизации.УчетнаяЗаписьОбмена;
		
		ДокументооборотНастроенВУнивесальномФормате = 
			СвойстваОрганизации.ВидОбменаСКонтролирующимиОрганами = ПредопределенноеЗначение("Перечисление.ВидыОбменаСКонтролирующимиОрганами.ОбменВУниверсальномФормате")
			И ЗначениеЗаполнено(УчетнаяЗаписьДокументооборота);
		
		Если ДокументооборотНастроенВУнивесальномФормате Тогда
			
			Если (КонтролирующийОрган = "ФНС" И УчетнаяЗаписьПредназначенаДляДокументооборотаСФНС(УчетнаяЗаписьДокументооборота) <> Истина)
				ИЛИ (КонтролирующийОрган = "ПФР" И УчетнаяЗаписьПредназначенаДляДокументооборотаСПФР(УчетнаяЗаписьДокументооборота) <> Истина)
				ИЛИ (КонтролирующийОрган = "ФСГС" И УчетнаяЗаписьПредназначенаДляДокументооборотаСФСГС(УчетнаяЗаписьДокументооборота) <> Истина) Тогда
				ПредназначенаДляДокументооборотаСКонтролирующимОрганом = Ложь;
			КонецЕсли;
			
			ПараметрыПрорисовкиКнопокОтправки.Вставить("ПроверитьВИнтернете", Истина);		
			Если НЕ ПредназначенаДляДокументооборотаСКонтролирующимОрганом Тогда
				ПараметрыПрорисовкиКнопокОтправки.Вставить("ПроверитьВИнтернете", Ложь);
				
			Иначе
				Попытка
					ОбменИлиОнлайнПроверкаОтключена = УчетнаяЗаписьДокументооборота.ОбменНапрямую ИЛИ НЕ УчетнаяЗаписьДокументооборота.ИспользоватьСервисОнлайнПроверкиОтчетов;
				Исключение
					ОбменИлиОнлайнПроверкаОтключена = Истина;
				КонецПопытки;
				
				Если ОбменИлиОнлайнПроверкаОтключена Тогда
					ПараметрыПрорисовкиКнопокОтправки.Вставить("ПроверитьВИнтернете", Ложь);
					
				Иначе
					Если УчетнаяЗаписьДокументооборота.СпецоператорСвязи <> Перечисления.СпецоператорыСвязи.Такском И (КонтекстЭДОСервер = Неопределено
						ИЛИ Найти(";" + ВРег(СокрЛП(ЭлектронныйДокументооборотСКонтролирующимиОрганамиВызовСервера.ПолучитьПараметрСпецоператора(УчетнаяЗаписьДокументооборота.СпецоператорСвязи, "ОнлайнПроверкаКонтролирующиеОрганы"))) + ";", ";" + ВРег(КонтролирующийОрган) + ";") = 0) Тогда
						ПараметрыПрорисовкиКнопокОтправки.Вставить("ПроверитьВИнтернете", Ложь);
					Иначе
						ПараметрыПрорисовкиКнопокОтправки.Вставить("ПроверитьВИнтернете", Истина);
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		Иначе
			// Если нет учетной записи, то оставляем меню ПроверитьВИнтернете, чтобы по нему показывать предложение подключиться
			ПараметрыПрорисовкиКнопокОтправки.Вставить("ПроверитьВИнтернете", Истина);
		КонецЕсли;
		
	Исключение
		
		ПараметрыПрорисовкиКнопокОтправки.Вставить("ГруппаОтправкаВКонтролирующийОрган", Истина);
		ПараметрыПрорисовкиКнопокОтправки.Вставить("ПроверитьВИнтернете", Ложь);
		
	КонецПопытки;
	
	Возврат ПараметрыПрорисовкиКнопокОтправки;
	
КонецФункции

Функция УчетнаяЗаписьПредназначенаДляДокументооборотаСФНС(УчетнаяЗапись) Экспорт
	
	Запрос = Новый Запрос("ВЫБРАТЬ РАЗРЕШЕННЫЕ
	                      |	УчетныеЗаписиДокументооборота.ПредназначенаДляДокументооборотаСФНС
	                      |ИЗ
	                      |	Справочник.УчетныеЗаписиДокументооборота КАК УчетныеЗаписиДокументооборота
	                      |ГДЕ
	                      |	УчетныеЗаписиДокументооборота.Ссылка = &Ссылка");
	Запрос.УстановитьПараметр("Ссылка", УчетнаяЗапись);
	Попытка
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Возврат Выборка.ПредназначенаДляДокументооборотаСФНС;
		Иначе
			Возврат Неопределено;
		КонецЕсли;
	Исключение
		
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		
		ЗаписьЖурналаРегистрации(
			НСтр("ru = 'Проверка наличия возможности отправки в ФНС для учетной записи'"), 
			УровеньЖурналаРегистрации.Ошибка,,,
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке));
		
		Возврат Неопределено;
	КонецПопытки;
	
КонецФункции

Функция УчетнаяЗаписьПредназначенаДляДокументооборотаСПФР(УчетнаяЗапись) Экспорт
	
	Запрос = Новый Запрос("ВЫБРАТЬ РАЗРЕШЕННЫЕ
	                      |	УчетныеЗаписиДокументооборота.ПредназначенаДляДокументооборотаСПФР
	                      |ИЗ
	                      |	Справочник.УчетныеЗаписиДокументооборота КАК УчетныеЗаписиДокументооборота
	                      |ГДЕ
	                      |	УчетныеЗаписиДокументооборота.Ссылка = &Ссылка");
	Запрос.УстановитьПараметр("Ссылка", УчетнаяЗапись);
	Попытка
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Возврат Выборка.ПредназначенаДляДокументооборотаСПФР;
		Иначе
			Возврат Неопределено;
		КонецЕсли;
	Исключение
		
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		
		ЗаписьЖурналаРегистрации(
			НСтр("ru = 'Проверка наличия возможности отправки в ПФР для учетной записи'"), 
			УровеньЖурналаРегистрации.Ошибка,,,
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке));

		Возврат Неопределено;
	КонецПопытки;
	
КонецФункции

Функция УчетнаяЗаписьПредназначенаДляДокументооборотаСФСГС(УчетнаяЗапись) Экспорт
	
	Запрос = Новый Запрос("ВЫБРАТЬ РАЗРЕШЕННЫЕ
	                      |	УчетныеЗаписиДокументооборота.ПредназначенаДляДокументооборотаСФСГС
	                      |ИЗ
	                      |	Справочник.УчетныеЗаписиДокументооборота КАК УчетныеЗаписиДокументооборота
	                      |ГДЕ
	                      |	УчетныеЗаписиДокументооборота.Ссылка = &Ссылка");
	Запрос.УстановитьПараметр("Ссылка", УчетнаяЗапись);
	Попытка
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Возврат Выборка.ПредназначенаДляДокументооборотаСФСГС;
		Иначе
			Возврат Неопределено;
		КонецЕсли;
	Исключение
		
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		
		ЗаписьЖурналаРегистрации(
			НСтр("ru = 'Проверка наличия возможности отправки в Росстат для учетной записи'"), 
			УровеньЖурналаРегистрации.Ошибка,,,
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке));
		
		Возврат Неопределено;
	КонецПопытки;
	
КонецФункции

Функция ПолучитьСообщенияЦиклаОбмена(ЦиклОбмена, ТипыСообщений = Неопределено, ПомеченныеНаУдаление = Ложь) Экспорт
	
	Запрос = Новый Запрос("ВЫБРАТЬ РАЗРЕШЕННЫЕ
	                      |	ТранспортноеСообщение.Ссылка,
	                      |	ТранспортноеСообщение.Статус,
	                      |	ТранспортноеСообщение.Тип,
	                      |	ТранспортноеСообщение.ПометкаУдаления,
	                      |	ТранспортноеСообщение.Дата,
	                      |	ТранспортноеСообщение.ИдентификаторСообщения,
	                      |	ТранспортноеСообщение.ДатаТранспорта,
	                      |	ТранспортноеСообщение.Основание,
	                      |	ТранспортноеСообщение.ПротоколСОшибкой,
	                      |	ТранспортноеСообщение.Тема
	                      |ИЗ
	                      |	Документ.ТранспортноеСообщение КАК ТранспортноеСообщение
	                      |ГДЕ
	                      |	ТранспортноеСообщение.ЦиклОбмена = &ЦиклОбмена");
	Запрос.УстановитьПараметр("ЦиклОбмена", ЦиклОбмена);
	
	Если НЕ ПомеченныеНаУдаление Тогда
		Запрос.Текст = Запрос.Текст + "
		                      |	И ТранспортноеСообщение.ПометкаУдаления = &ПометкаУдаления";
		Запрос.УстановитьПараметр("ПометкаУдаления", Ложь);
	КонецЕсли;
	
	Если ТипыСообщений <> Неопределено Тогда
		Запрос.Текст = Запрос.Текст + "
		                      |	И ТранспортноеСообщение.Тип В (&ТипыСообщений)";
		Запрос.УстановитьПараметр("ТипыСообщений", ТипыСообщений);
	КонецЕсли;
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Функция СообщениеЗашифровано(Сообщение) Экспорт
	
	Запрос = Новый Запрос("ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	                      |	ИСТИНА КАК Поле1
	                      |ИЗ
	                      |	РегистрСведений.ТранспортныеКонтейнеры КАК ТранспортныеКонтейнеры
	                      |ГДЕ
	                      |	ТранспортныеКонтейнеры.ТранспортноеСообщение = &ТранспортноеСообщение");
	Запрос.УстановитьПараметр("ТранспортноеСообщение", Сообщение);
	Возврат НЕ Запрос.Выполнить().Пустой();
	
КонецФункции

Функция СообщениеРасшифровано(Сообщение) Экспорт
	
	Запрос = Новый Запрос("ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	                      |	ИСТИНА КАК Поле1
	                      |ИЗ
	                      |	РегистрСведений.СодержимоеТранспортныхКонтейнеров КАК СодержимоеТранспортныхКонтейнеров
	                      |ГДЕ
	                      |	СодержимоеТранспортныхКонтейнеров.ТранспортноеСообщение = &ТранспортноеСообщение");
	Запрос.УстановитьПараметр("ТранспортноеСообщение", Сообщение);
	Возврат НЕ Запрос.Выполнить().Пустой();
	
КонецФункции

Функция ПолучитьПоследнийЦиклОбмена(Объект) Экспорт
	
	Если НЕ ПравоДоступа("Чтение", Метаданные.Документы.ТранспортноеСообщение) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Запрос = Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 1
						  |	ТранспортноеСообщение.ЦиклОбмена,
						  |	ТранспортноеСообщение.ЦиклОбмена.ДатаСоздания КАК ЦиклОбменаДатаСоздания
						  |ИЗ
						  |	Документ.ТранспортноеСообщение КАК ТранспортноеСообщение
						  |ГДЕ
						  |	ТранспортноеСообщение.ЦиклОбмена.Предмет = &Объект
						  |	И ТранспортноеСообщение.ЦиклОбмена.Тип В(&Тип)
						  |	И ТранспортноеСообщение.ЦиклОбмена.ПометкаУдаления = &ПометкаУдаления
						  |	И (ТранспортноеСообщение.Статус = &СтатусПолученное
						  |			ИЛИ ТранспортноеСообщение.Статус = &СтатусОтправленное
						  |			ИЛИ ТранспортноеСообщение.Тип В (&ТипПервичноеСодержащееОтчетность)
						  |				И ТранспортноеСообщение.Статус = &СтатусИсходящее)
						  |
						  |ОБЪЕДИНИТЬ ВСЕ
						  |
						  |ВЫБРАТЬ ПЕРВЫЕ 1
						  |	ТранспортноеСообщение.ЦиклОбмена,
						  |	ТранспортноеСообщение.ЦиклОбмена.ДатаСоздания
						  |ИЗ
						  |	Документ.ТранспортноеСообщение КАК ТранспортноеСообщение
						  |ГДЕ
						  |	ТранспортноеСообщение.ЦиклОбмена.Тип В(&Тип)
						  |	И ТранспортноеСообщение.ЦиклОбмена.ПометкаУдаления = &ПометкаУдаления
						  |	И (ТранспортноеСообщение.Статус = &СтатусПолученное
						  |			ИЛИ ТранспортноеСообщение.Статус = &СтатусОтправленное
						  |			ИЛИ ТранспортноеСообщение.Тип В (&ТипПервичноеСодержащееОтчетность)
						  |				И ТранспортноеСообщение.Статус = &СтатусИсходящее)
						  |	И &Объект В (ТранспортноеСообщение.ЦиклОбмена.ДополнительныеПредметы.Предмет)
						  |
						  |УПОРЯДОЧИТЬ ПО
						  |	ЦиклОбменаДатаСоздания УБЫВ");
	Запрос.УстановитьПараметр("Объект", Объект);
	
	ТипМассив = Новый Массив;
	ТипМассив.Добавить(Перечисления.ТипыЦикловОбмена.НалоговаяИлиБухгалтерскаяОтчетность);
	ТипМассив.Добавить(Перечисления.ТипыЦикловОбмена.ОтчетностьПФР);
	ТипМассив.Добавить(Перечисления.ТипыЦикловОбмена.НеформализованнаяПерепискаПФРИсходящие);
	ТипМассив.Добавить(Перечисления.ТипыЦикловОбмена.НеформализованнаяПерепискаПФРВходящие);
	ТипМассив.Добавить(Перечисления.ТипыЦикловОбмена.Форма2НДФЛ);
	ТипМассив.Добавить(Перечисления.ТипыЦикловОбмена.ОбращениеНП);
	ТипМассив.Добавить(Перечисления.ТипыЦикловОбмена.Представление);
	ТипМассив.Добавить(Перечисления.ТипыЦикловОбмена.ИОН);
	ТипМассив.Добавить(Перечисления.ТипыЦикловОбмена.Документ);
	ТипМассив.Добавить(Перечисления.ТипыЦикловОбмена.Заявление);
	ТипМассив.Добавить(Перечисления.ТипыЦикловОбмена.ОтчетностьФСГС);
	ТипМассив.Добавить(Перечисления.ТипыЦикловОбмена.ПисьменноеОбращениеВФСГС);
	ТипМассив.Добавить(Перечисления.ТипыЦикловОбмена.ИОС);
	ТипМассив.Добавить(Перечисления.ТипыЦикловОбмена.ЗапросНаВыпискуИзЕГРЮЛ_ЕГРИП);
	ТипМассив.Добавить(Перечисления.ТипыЦикловОбмена.ПисьмоНО);
	ТипМассив.Добавить(Перечисления.ТипыЦикловОбмена.Рассылка);
	ТипМассив.Добавить(Перечисления.ТипыЦикловОбмена.РассылкаГрупповая);
	ТипМассив.Добавить(Перечисления.ТипыЦикловОбмена.ИндивидуальноеИнформированиеФСГС);
	Запрос.УстановитьПараметр("Тип", ТипМассив);
	
	ТипМассивПервичное = Новый Массив;
	ТипМассивПервичное.Добавить(Перечисления.ТипыТранспортныхСообщений.ДекларацияНП);
	ТипМассивПервичное.Добавить(Перечисления.ТипыТранспортныхСообщений.ПервичноеСообщениеСодержащееОтчетностьПФР);
	ТипМассивПервичное.Добавить(Перечисления.ТипыТранспортныхСообщений.ПисьмоИсходящееПФР);
	ТипМассивПервичное.Добавить(Перечисления.ТипыТранспортныхСообщений.ПисьмоВходящееПФР);
	ТипМассивПервичное.Добавить(Перечисления.ТипыТранспортныхСообщений.Форма2НДФЛНП);
	ТипМассивПервичное.Добавить(Перечисления.ТипыТранспортныхСообщений.ОбращениеНП);
	ТипМассивПервичное.Добавить(Перечисления.ТипыТранспортныхСообщений.ПредставлениеНП);
	ТипМассивПервичное.Добавить(Перечисления.ТипыТранспортныхСообщений.ЗапросНП);
	ТипМассивПервичное.Добавить(Перечисления.ТипыТранспортныхСообщений.ДокументНО);
	ТипМассивПервичное.Добавить(Перечисления.ТипыТранспортныхСообщений.ЗаявлениеНП);
	ТипМассивПервичное.Добавить(Перечисления.ТипыТранспортныхСообщений.ПервичноеСообщениеСодержащееОтчетностьФСГС);
	ТипМассивПервичное.Добавить(Перечисления.ТипыТранспортныхСообщений.ПисьмоИсходящееФСГС);
	ТипМассивПервичное.Добавить(Перечисления.ТипыТранспортныхСообщений.ПервичноеСообщениеСодержащееЗапросПФР);
	ТипМассивПервичное.Добавить(Перечисления.ТипыТранспортныхСообщений.ЗапросНаВыпискуЕРГЮЛ_ЕГРИП);
	ТипМассивПервичное.Добавить(Перечисления.ТипыТранспортныхСообщений.ПисьмоНО);
	ТипМассивПервичное.Добавить(Перечисления.ТипыТранспортныхСообщений.РассылкаНО);
	ТипМассивПервичное.Добавить(Перечисления.ТипыТранспортныхСообщений.ПисьмоВходящееФСГС);
	
	Запрос.УстановитьПараметр("ТипПервичноеСодержащееОтчетность", ТипМассивПервичное);
	
	Запрос.УстановитьПараметр("СтатусПолученное", Перечисления.СтатусыПисем.Полученное);
	Запрос.УстановитьПараметр("СтатусОтправленное", Перечисления.СтатусыПисем.Отправленное);
	Запрос.УстановитьПараметр("СтатусИсходящее", Перечисления.СтатусыПисем.Исходящее);
	Запрос.УстановитьПараметр("ПометкаУдаления", Ложь);
	
	Попытка
		РезультатЗапроса = Запрос.Выполнить();
	Исключение
		Возврат Неопределено;
	КонецПопытки;
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		Если ТипЗнч(Выборка.ЦиклОбмена) = Тип("СправочникСсылка.ЦиклыОбмена") Тогда
			Возврат Выборка.ЦиклОбмена;
		КонецЕсли;
	КонецЦикла;
	Возврат Справочники.ЦиклыОбмена.ПустаяСсылка();
	
КонецФункции

Функция ПолучитьСсылкуНаПротоколПоЦиклуОбмена(ЦиклОбмена)
	
	ТипыПротоколов = Новый Массив;
	ТипыПротоколов.Добавить(Перечисления.ТипыТранспортныхСообщений.ПротоколПФР);
	ТипыПротоколов.Добавить(Перечисления.ТипыТранспортныхСообщений.ПротоколВходногоКонтроляОтчетностиФСГС);
	ТипыПротоколов.Добавить(Перечисления.ТипыТранспортныхСообщений.РезультатПриемаДекларацияНО);
	ТипыПротоколов.Добавить(Перечисления.ТипыТранспортныхСообщений.РезультатОбработкиДекларацияНО);
	ТипыПротоколов.Добавить(Перечисления.ТипыТранспортныхСообщений.РезультатПриемаЗаявлениеНО);
	ТипыПротоколов.Добавить(Перечисления.ТипыТранспортныхСообщений.РезультатОбработкиЗаявлениеНО);
	ТипыПротоколов.Добавить(Перечисления.ТипыТранспортныхСообщений.РезультатПриемаФорма2НДФЛНО);
	ТипыПротоколов.Добавить(Перечисления.ТипыТранспортныхСообщений.РезультатПриемаПредставлениеНО);
	
	Запрос = Новый Запрос("ВЫБРАТЬ РАЗРЕШЕННЫЕ
	                      |	ТранспортноеСообщение.Ссылка
	                      |ИЗ
	                      |	Документ.ТранспортноеСообщение КАК ТранспортноеСообщение
	                      |ГДЕ
	                      |	ТранспортноеСообщение.ЦиклОбмена = &ЦиклОбмена
	                      |	И ТранспортноеСообщение.Тип В(&ТипыПротоколов)
	                      |
	                      |УПОРЯДОЧИТЬ ПО
	                      |	ТранспортноеСообщение.Тип.Порядок УБЫВ");
	Запрос.УстановитьПараметр("ЦиклОбмена", ЦиклОбмена);
	Запрос.УстановитьПараметр("ТипыПротоколов", ТипыПротоколов);
	
	Попытка
		Выборка = Запрос.Выполнить().Выбрать();
	Исключение
		Возврат Неопределено;
	КонецПопытки;
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Ссылка;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

Функция ПолучитьСвойстваАктуальногоПротокола(СсылкаНаОтчет) Экспорт
	
	ЦиклОбмена = ПолучитьПоследнийЦиклОбмена(СсылкаНаОтчет);
	Если НЕ ЗначениеЗаполнено(ЦиклОбмена) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Протокол = ПолучитьСсылкуНаПротоколПоЦиклуОбмена(ЦиклОбмена);
	
	Возврат Новый Структура("Дата, Протокол", ЦиклОбмена.ДатаСоздания, Протокол);
	
КонецФункции

Процедура УстановитьПараметрСеансаТекущиеУчетныеЗаписиНалогоплательщика(ИмяПараметра = Неопределено, УстановленныеПараметры = Неопределено)Экспорт
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	ПользователиУчетныхЗаписейДокументооборота.УчетнаяЗапись.Ссылка КАК УчетнаяЗапись
	                      |ИЗ
	                      |	РегистрСведений.ПользователиУчетныхЗаписейДокументооборота КАК ПользователиУчетныхЗаписейДокументооборота
	                      |ГДЕ
	                      |	ПользователиУчетныхЗаписейДокументооборота.Пользователь = &Пользователь");
	Запрос.УстановитьПараметр("Пользователь", Пользователи.ТекущийПользователь());
	МассивУчетныхЗаписей = Новый Массив;
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		МассивУчетныхЗаписей.Добавить(Выборка.УчетнаяЗапись);
	КонецЦикла;
	МассивУчетныхЗаписейФиксированныйМассив = Новый ФиксированныйМассив(МассивУчетныхЗаписей);
	ПараметрыСеанса.ТекущиеУчетныеЗаписиНалогоплательщика = МассивУчетныхЗаписейФиксированныйМассив;
	Если УстановленныеПараметры <> Неопределено Тогда 
		УстановленныеПараметры.Добавить("ТекущиеУчетныеЗаписиНалогоплательщика");
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриНажатииНаКнопкуОтправкиВКонтролирующийОрган(ОрганизацияОтчета, КонтролирующийОрган, НастроенОбменВУниверсальномФормате, УчетнаяЗаписьПредназначенаДляДокументооборотаСКО) Экспорт
	
	НастроенОбменВУниверсальномФормате = Ложь;
	УчетнаяЗаписьПредназначенаДляДокументооборотаСКО = Ложь;
	
	СвойстваОрганизации = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		ОрганизацияОтчета, "ВидОбменаСКонтролирующимиОрганами, УчетнаяЗаписьОбмена");
	
	УчетнаяЗаписьДокументооборота = СвойстваОрганизации.УчетнаяЗаписьОбмена;
	Если СвойстваОрганизации.ВидОбменаСКонтролирующимиОрганами = ПредопределенноеЗначение("Перечисление.ВидыОбменаСКонтролирующимиОрганами.ОбменВУниверсальномФормате")
	И ЗначениеЗаполнено(УчетнаяЗаписьДокументооборота) Тогда
		НастроенОбменВУниверсальномФормате = Истина;
		Если КонтролирующийОрган = "ФНС" Тогда
			УчетнаяЗаписьПредназначенаДляДокументооборотаСКО = УчетнаяЗаписьПредназначенаДляДокументооборотаСФНС(УчетнаяЗаписьДокументооборота);
		ИначеЕсли КонтролирующийОрган = "ПФР" Тогда
			УчетнаяЗаписьПредназначенаДляДокументооборотаСКО = УчетнаяЗаписьПредназначенаДляДокументооборотаСПФР(УчетнаяЗаписьДокументооборота);
		ИначеЕсли КонтролирующийОрган = "ФСГС" Тогда
			УчетнаяЗаписьПредназначенаДляДокументооборотаСКО = УчетнаяЗаписьПредназначенаДляДокументооборотаСФСГС(УчетнаяЗаписьДокументооборота);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////
//
// АВТОМАТИЧЕСКИЙ ОБМЕН С КОНТРОЛИРУЮЩИМИ ОРГАНАМИ
//

#Область АвтоматическийОбменСКонтролирующимиОрганами

// Процедура обработки регламентного задания по автообмену с Контролирующими органами
//
// Параметры:
//	нет
//
Процедура ВыполнитьОбменСКОПоРегламентномуЗаданию() Экспорт 
	
	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания();
	
	// инициализируем контекст ЭДО - модуль обработки
	ТекстСообщения = "";
	КонтекстЭДОСервер = ПолучитьОбработкуЭДО(ТекстСообщения);
	Если КонтекстЭДОСервер = Неопределено Тогда 
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Автоматический обмен с контролирующими органами'"), 
		УровеньЖурналаРегистрации.Предупреждение, , , ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	КонтекстЭДОСервер.ВыполнитьОбменСКОПоРегламентномуЗаданию();
	
КонецПроцедуры

Процедура АвтообменСКО(КодОбластиДанных,
					   УникальныйИдентификаторУчетнойЗаписи,
					   НаименованиеУчетнойЗаписи,
					   СерверSMTP,
					   ПортSMTP,
					   ТребуетсяSMTPАутентификация,
					   ИмяПользователяSMTP,
					   ПарольSMTP,
					   СерверPOP3,
					   ПортPOP3,
					   ИмяПользователяPOP3,
					   ПарольPOP3) Экспорт
	
	// инициализируем контекст ЭДО - модуль обработки
	ТекстСообщения = "";
	КонтекстЭДОСервер = ПолучитьОбработкуЭДО(ТекстСообщения);
	Если КонтекстЭДОСервер = Неопределено Тогда 
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Автоматический обмен с контролирующими органами'"), 
		УровеньЖурналаРегистрации.Предупреждение, , , ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	КонтекстЭДОСервер.АвтообменСКО(КодОбластиДанных,
					   УникальныйИдентификаторУчетнойЗаписи,
					   НаименованиеУчетнойЗаписи,
					   СерверSMTP,
					   ПортSMTP,
					   ТребуетсяSMTPАутентификация,
					   ИмяПользователяSMTP,
					   ПарольSMTP,
					   СерверPOP3,
					   ПортPOP3,
					   ИмяПользователяPOP3,
					   ПарольPOP3);
	
КонецПроцедуры

Функция ВключитьАвтообменПоУчетнымЗаписяхДО(УчетныеЗаписи) Экспорт 
	
	УстановитьПривилегированныйРежим(Истина);
	
	НачатьТранзакцию();
	Попытка
		Для Каждого УчетнаяЗапись Из УчетныеЗаписи Цикл
			УчетнаяЗаписьОбъект = УчетнаяЗапись.ПолучитьОбъект();
			УчетнаяЗаписьОбъект.ОтключитьАвтообмен = Ложь;
			УчетнаяЗаписьОбъект.Записать();
		КонецЦикла;
		ПолучитьКлючиАвтообменаПоУчетнымЗаписямДО(Истина);
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецФункции

Функция ИспользованиеАвтообменаСКОНастроеноКорректно(ОбластьДанных, УчетныеЗаписи) Экспорт
	
	ТаблицаКлючей = ПолучитьКлючиАвтообменаПоУчетнымЗаписямДО();

	ОбластьДанных = ОбщегоНазначения.ЗначениеРазделителяСеанса();
	Если ТаблицаКлючей.Количество() > 0 Тогда
		ОбластьДанных = ТаблицаКлючей[0].ОбластьДанных;                      
	КонецЕсли;
	
	Результат = Истина;
	УчетныеЗаписи = Новый Массив;
	Если ОбластьДанных <> ОбщегоНазначения.ЗначениеРазделителяСеанса() Тогда
		Для Каждого СтрокаТаблицы Из ТаблицаКлючей Цикл
			УчетныеЗаписи.Добавить(СтрокаТаблицы.УчетнаяЗапись);
		КонецЦикла;
		Результат = Ложь;
	КонецЕсли;		
		
	Возврат Результат;
	
КонецФункции

Функция ОтключитьАвтообменПоУчетнымЗаписяхДО(УчетныеЗаписи) Экспорт 
	
	УстановитьПривилегированныйРежим(Истина);
	
	НачатьТранзакцию();
	Попытка
		Для Каждого УчетнаяЗапись Из УчетныеЗаписи Цикл
			УчетнаяЗаписьОбъект = УчетнаяЗапись.ПолучитьОбъект();
			УчетнаяЗаписьОбъект.ОтключитьАвтообмен = Истина;
			УчетнаяЗаписьОбъект.Записать();
		КонецЦикла;
		ПолучитьКлючиАвтообменаПоУчетнымЗаписямДО(Истина);
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецФункции

Функция ПолучитьКлючиАвтообменаПоУчетнымЗаписямДО(Перезаполнить = Ложь) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТаблицаКлючей = Константы.КлючиАвтообменаПоУчетнымЗаписямДО.Получить().Получить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	УчетныеЗаписиДокументооборота.Ссылка
	|ИЗ
	|	Справочник.УчетныеЗаписиДокументооборота КАК УчетныеЗаписиДокументооборота
	|ГДЕ
	|	НЕ УчетныеЗаписиДокументооборота.ОтключитьАвтообмен";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если ТаблицаКлючей = Неопределено ИЛИ Перезаполнить Тогда
		ТаблицаКлючей = Новый ТаблицаЗначений;
		ТаблицаКлючей.Колонки.Добавить("ОбластьДанных", Новый ОписаниеТипов("Число"));
		ТаблицаКлючей.Колонки.Добавить("УчетнаяЗапись", Новый ОписаниеТипов("СправочникСсылка.УчетныеЗаписиДокументооборота"));

		Пока Выборка.Следующий() Цикл
			НоваяСтрока = ТаблицаКлючей.Добавить();
			НоваяСтрока.ОбластьДанных = ОбщегоНазначения.ЗначениеРазделителяСеанса();
			НоваяСтрока.УчетнаяЗапись = Выборка.Ссылка;
		КонецЦикла;
		Константы.КлючиАвтообменаПоУчетнымЗаписямДО.Установить(Новый ХранилищеЗначения(ТаблицаКлючей));
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат ТаблицаКлючей;
	
КонецФункции

#КонецОбласти


Функция ПолучитьНастройкиФСС(ОрганизацияСсылка) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Результат = Новый Структура;
	
	КонтекстЭДО = ПолучитьОбработкуЭДО();
	ОнлайнПроверкаДоступна = Истина;
	ИспользоватьОбмен = КонтекстЭДО.ОрганизацияИспользуетОбменСФСС(ОрганизацияСсылка, , ОнлайнПроверкаДоступна);
	
	Результат.Вставить("ИспользоватьОбмен", ИспользоватьОбмен);
	Результат.Вставить("ОнлайнПроверкаДоступна", ОнлайнПроверкаДоступна);
	Возврат Результат;
	
КонецФункции

Функция ПолучитьНастройкиФСРАР(ОрганизацияСсылка) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	КонтекстЭДО = ПолучитьОбработкуЭДО();
	Результат = КонтекстЭДО.НастройкиФСРАР(ОрганизацияСсылка, Истина);
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьНастройкиРПН(ОрганизацияСсылка) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	КонтекстЭДО = ПолучитьОбработкуЭДО();
	Результат = КонтекстЭДО.НастройкиРПН(ОрганизацияСсылка, Истина);
	
	Возврат Результат;
	
КонецФункции

Функция УчетнаяЗаписьОрганизации(Организация) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	КонтекстЭДО = ПолучитьОбработкуЭДО();
	Возврат КонтекстЭДО.УчетнаяЗаписьОрганизации(Организация);
	
КонецФункции

#Область НоваяФормаРегламентированнойОтчетности

Функция ПараметрыПрорисовкиПанелиОтправки(Знач Ссылка, Знач Организация, Знач КонтролирующийОрган = "ФНС") Экспорт
	
	КонтекстЭДОСервер = ДокументооборотСКОВызовСервера.ПолучитьОбработкуЭДО();
	Если КонтекстЭДОСервер = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	// Получение параметров прорисовки
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ПолучатьДаты", Ложь);
	ДополнительныеПараметры.Вставить("ПолучатьОшибкиОтправки", Истина);
	ТекущееСостояние = КонтекстЭДОСервер.ТекущееСостояниеОтправки(Ссылка, КонтролирующийОрган, ДополнительныеПараметры);
	
	Возврат ТекущееСостояние;
	
КонецФункции

Функция ПараметрыПрорисовкиСтатуса(Знач Ссылка, КонтролирующийОрган = "ФНС") Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	КонтекстЭДОСервер = ДокументооборотСКОВызовСервера.ПолучитьОбработкуЭДО();
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ПолучатьДаты", Ложь);
	ДополнительныеПараметры.Вставить("ПолучатьОшибкиОтправки", Ложь);
	ТекущееСостояние = КонтекстЭДОСервер.ТекущееСостояниеОтправки(Ссылка, КонтролирующийОрган, ДополнительныеПараметры);
	
	УстановитьПривилегированныйРежим(Ложь);
		
	Возврат ТекущееСостояние;
	
КонецФункции

// Обновление ИБ
Процедура ЗаполнитьРегистрЖурналОтправокВКонтролирующиеОрганы(Параметры = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Документы.ЗапросНаИнформационноеОбслуживаниеСтрахователя.ПерезаполнитьЗначениеРеквизитаНаДату();
	
	КонтекстЭДОСервер = ДокументооборотСКОВызовСервера.ПолучитьОбработкуЭДО();
	КонтекстЭДОСервер.ЗаполнитьРегистрЖурналОтправокВКонтролирующиеОрганы();
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

Функция КоличествоОбъектовНеЗаписанныхВРегистрЖурналОтправокВКонтролирующиеОрганы() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	КонтекстЭДОСервер 	= ДокументооборотСКОВызовСервера.ПолучитьОбработкуЭДО();
	КоличествоОбъектов 	= КонтекстЭДОСервер.КоличествоОбъектовНеЗаписанныхВРегистрЖурналОтправокВКонтролирующиеОрганы();
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат  КоличествоОбъектов;
	
КонецФункции

Функция ЦветФонаПанелиОтправкиПоСтатусу(СостояниеСдачиОтчетности) Экспорт
	
	ЦветФона = ЦветаСтиля.ЦветФонаНеначавшейсяОтправки; 
	Если СостояниеСдачиОтчетности = Перечисления.СостояниеСдачиОтчетности.ДокументооборотНачат 
		ИЛИ СостояниеСдачиОтчетности = Перечисления.СостояниеСдачиОтчетности.ТребуетсяПодтверждениеПриема Тогда
		ЦветФона = ЦветаСтиля.ЦветФонаТекущейОтправки;// желтый
	ИначеЕсли СостояниеСдачиОтчетности = Перечисления.СостояниеСдачиОтчетности.ОтправленоИзКонтролирующегоОргана
		ИЛИ СостояниеСдачиОтчетности = Перечисления.СостояниеСдачиОтчетности.ДокументооборотНеНачат
		ИЛИ СостояниеСдачиОтчетности = Перечисления.СостояниеСдачиОтчетности.ПриемПодтвержден Тогда
		ЦветФона = ЦветаСтиля.ЦветФонаНеначавшейсяОтправки;// серый
	ИначеЕсли СостояниеСдачиОтчетности = Перечисления.СостояниеСдачиОтчетности.ОтрицательныйРезультатДокументооборота Тогда
		ЦветФона = ЦветаСтиля.ЦветФонаОшибкиОтправки;// красный
	ИначеЕсли СостояниеСдачиОтчетности = Перечисления.СостояниеСдачиОтчетности.ПоложительныйРезультатДокументооборота Тогда
		ЦветФона = ЦветаСтиля.ЦветФонаУдачнойОтправки;// зеленый
	КонецЕсли;
	
	Возврат ЦветФона;
	
КонецФункции

Функция ЕстьДоступККонтекстуЭДО() Экспорт
	
	КонтекстЭДОСервер = ДокументооборотСКОВызовСервера.ПолучитьОбработкуЭДО();
	Возврат КонтекстЭДОСервер <> Неопределено;
	
КонецФункции

#КонецОбласти
