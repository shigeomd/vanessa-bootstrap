
#Область СлужебныйПрограммныйИнтерфейс

// Устанавливает отображение команды "Пересчитать" в документах, рассчитывающих начисления
// по нескольким сотрудникам.
//
// Параметры:
//		Форма						- УправляемаяФорма
//      УстановитьАктивность		- Булево, если Истина кнопка активна.
//		ОтображатьКнопкуПересчета	- Булево, содержит свойство "Видимость".
//
Процедура УстановитьОтображениеКнопкиПересчитать(Форма, УстановитьАктивность = Ложь, ОтображатьКнопкуПересчета = Истина) Экспорт
	
	КнопкаПересчитатьДокумент = Форма.Элементы.Найти("ПересчитатьДокумент");
	Если КнопкаПересчитатьДокумент = Неопределено
		ИЛИ КнопкаПересчитатьДокумент.Доступность = УстановитьАктивность Тогда
		
		Возврат;
		
	КонецЕсли; 
	
	Если УстановитьАктивность Тогда
		ЦветТекстаКнопки = ОбщегоНазначенияКлиент.ЦветСтиля("ЦветОсобогоТекста");
		ЦветФонаКнопки = ОбщегоНазначенияКлиент.ЦветСтиля("ВыделеннаяКнопкаФон");
	Иначе
		ЦветТекстаКнопки = ОбщегоНазначенияКлиент.ЦветСтиля("ЦветТекстаКнопки");
		ЦветФонаКнопки = ОбщегоНазначенияКлиент.ЦветСтиля("ЦветФонаКнопки");
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		"ПересчитатьДокумент",
		"ЦветТекста",
		ЦветТекстаКнопки);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		"ПересчитатьДокумент",
		"ЦветФона",
		ЦветФонаКнопки);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		"ПересчитатьДокумент",
		"Доступность",
		УстановитьАктивность);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		"ПересчитатьДокумент",
		"Видимость",
		ОтображатьКнопкуПересчета И НЕ Форма.РассчитыватьДокументыПриРедактировании);
	
КонецПроцедуры

#КонецОбласти
