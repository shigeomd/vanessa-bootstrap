////////////////////////////////////////////////////////////////////////////////
// КадровыйУчетБазовый: методы кадрового учета, работающие на стороне сервера.
//  
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Формирует кадровые приказы, при включении ведения кадрового учета документами,
// выполняется в фоновом задании.
//
// Параметры:
//		ПараметрыЗадания - Необходим для поддержки работы длительных операций, для запуска
//							в качестве фонового задания, не используется.
//		АдресВоВременномХранилище - Необходим для поддержки работы длительных операций, для запуска
//							в качестве фонового задания, не используется.
//
Процедура СформироватьКадровыеПриказы(ПараметрыЗадания = Неопределено, АдресВоВременномХранилище = Неопределено) Экспорт
	
	ФОИспользоватьКадровыйУчет = ПолучитьФункциональнуюОпцию("ИспользоватьКадровыйУчет");
	
	Если Не ФОИспользоватьКадровыйУчет Тогда
		Возврат;
	КонецЕсли;
		
	УстановитьПривилегированныйРежим(Истина);
	
	// Получение необходимых текущих данных.
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДатаСеанса());
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	&ТекущаяДата КАК Период,
		|	Сотрудники.Ссылка КАК Сотрудник
		|ПОМЕСТИТЬ ВТСотрудники
		|ИЗ
		|	Справочник.Сотрудники КАК Сотрудники";
		
	Запрос.Выполнить();
	
	ОписательВременныхТаблиц = КадровыйУчет.ОписательВременныхТаблицДляСоздатьВТКадровыеДанныеСотрудников(
		Запрос.МенеджерВременныхТаблиц,
		"ВТСотрудники");
			
	КадровыйУчет.СоздатьВТКадровыеДанныеСотрудников(
		ОписательВременныхТаблиц,
		Ложь,
		"ГоловнаяОрганизация,ТекущаяОрганизация,ТекущееПодразделение,ТекущаяДолжность,ТекущийВидЗанятости,ДатаПриема,ДатаУвольнения,ТекущаяТарифнаяСтавка,ТекущийСпособРасчетаАванса,ТекущийАванс");
		
	// Отсев сотрудников, у которых даты приема и увольнения установлены некорректно
	// и сотрудников для которых ранее создавались кадровые приказы.
	Запрос.Текст =
		"ВЫБРАТЬ
		|	СотрудникиОрганизации.Сотрудник КАК Сотрудник,
		|	СотрудникиОрганизации.ТекущаяОрганизация КАК ТекущаяОрганизация,
		|	СотрудникиОрганизации.ТекущееПодразделение КАК ТекущееПодразделение,
		|	СотрудникиОрганизации.ТекущаяДолжность КАК ТекущаяДолжность,
		|	СотрудникиОрганизации.ТекущийВидЗанятости КАК ТекущийВидЗанятости,
		|	СотрудникиОрганизации.ДатаПриема КАК ДатаПриема,
		|	СотрудникиОрганизации.ДатаУвольнения КАК ДатаУвольнения,
		|	СотрудникиОрганизации.ТекущаяТарифнаяСтавка КАК ТекущаяТарифнаяСтавка,
		|	СотрудникиОрганизации.ТекущийСпособРасчетаАванса КАК ТекущийСпособРасчетаАванса,
		|	СотрудникиОрганизации.ТекущийАванс КАК ТекущийАванс,
		|	ЕСТЬNULL(ПриемНаРаботу.Ссылка, ЗНАЧЕНИЕ(Документ.ПриемНаРаботу.ПустаяСсылка)) КАК ПриказОПриеме,
		|	ЕСТЬNULL(Увольнение.Ссылка, ЗНАЧЕНИЕ(Документ.Увольнение.ПустаяСсылка)) КАК ПриказОбУвольнении
		|ИЗ
		|	ВТКадровыеДанныеСотрудников КАК СотрудникиОрганизации
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПриемНаРаботу КАК ПриемНаРаботу
		|		ПО СотрудникиОрганизации.Сотрудник = ПриемНаРаботу.Сотрудник
		|			И (ПриемНаРаботу.Проведен)
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.Увольнение КАК Увольнение
		|		ПО СотрудникиОрганизации.Сотрудник = Увольнение.Сотрудник
		|			И (Увольнение.Проведен)
		|ГДЕ
		|	(ЕСТЬNULL(ПриемНаРаботу.Ссылка, ЗНАЧЕНИЕ(Документ.ПриемНаРаботу.ПустаяСсылка)) = ЗНАЧЕНИЕ(Документ.ПриемНаРаботу.ПустаяСсылка)
		|			ИЛИ СотрудникиОрганизации.ДатаУвольнения <> ДАТАВРЕМЯ(1, 1, 1)
		|				И СотрудникиОрганизации.ДатаУвольнения <> СотрудникиОрганизации.ДатаПриема
		|				И ЕСТЬNULL(Увольнение.Ссылка, ЗНАЧЕНИЕ(Документ.Увольнение.ПустаяСсылка)) = ЗНАЧЕНИЕ(Документ.Увольнение.ПустаяСсылка))
		|	И СотрудникиОрганизации.ГоловнаяОрганизация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
		|	И СотрудникиОрганизации.ТекущаяОрганизация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
		|
		|УПОРЯДОЧИТЬ ПО
		|	Сотрудник
		|АВТОУПОРЯДОЧИВАНИЕ";
		
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
		
	ПовременныеНачисления = РасчетЗарплаты.НачисленияПоКатегории(Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ПовременнаяОплатаТруда);
	
	НачислениеОклад = Неопределено;
	Если ПовременныеНачисления.Количество() > 0 Тогда 
		НачислениеОклад = ПовременныеНачисления[0];
	КонецЕсли;	
	
	ДанныеСотрудников = РезультатЗапроса.Выбрать();
	Пока ДанныеСотрудников.Следующий() Цикл
		
		// Создание приказов о приеме
		Если ЗначениеЗаполнено(ДанныеСотрудников.ДатаПриема)
			И НЕ ЗначениеЗаполнено(ДанныеСотрудников.ПриказОПриеме) Тогда
			
			ДокументПриемНаРаботу = Документы.ПриемНаРаботу.СоздатьДокумент();
			
			ДокументПриемНаРаботу.Дата = ДанныеСотрудников.ДатаПриема;
			ДокументПриемНаРаботу.ДатаПриема = ДанныеСотрудников.ДатаПриема;
			ДокументПриемНаРаботу.Организация = ДанныеСотрудников.ТекущаяОрганизация;
			ДокументПриемНаРаботу.Сотрудник = ДанныеСотрудников.Сотрудник;
			ДокументПриемНаРаботу.Подразделение = ДанныеСотрудников.ТекущееПодразделение;
			ДокументПриемНаРаботу.Должность = ДанныеСотрудников.ТекущаяДолжность;
			ДокументПриемНаРаботу.ВидЗанятости = ДанныеСотрудников.ТекущийВидЗанятости;
			ДокументПриемНаРаботу.СпособРасчетаАванса = ДанныеСотрудников.ТекущийСпособРасчетаАванса;
			ДокументПриемНаРаботу.Аванс = ДанныеСотрудников.ТекущийАванс;
			
			// Добавление начисления "Оклад" и установка размера тарифной ставки.
			Если НачислениеОклад <> Неопределено Тогда 
				Начисление = ДокументПриемНаРаботу.Начисления.Добавить();
				Начисление.Начисление = НачислениеОклад;
				Начисление.Размер = ДанныеСотрудников.ТекущаяТарифнаяСтавка;
			КонецЕсли;
			
			ДокументПриемНаРаботу.ДополнительныеСвойства.Вставить("ОтключитьПроверкуДатыЗапретаИзменения", Истина);
			
			Попытка
				ДокументПриемНаРаботу.Записать(РежимЗаписиДокумента.Проведение);
			Исключение
				
				ДокументПриемНаРаботу.Записать(РежимЗаписиДокумента.Запись);
				
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru='При создании кадровых приказов не удалось провести приказ об увольнении сотрудника %1, необходимо выполнить проведение в ручном режиме.'"),
					ДанныеСотрудников.Сотрудник);
					
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					ТекстСообщения,
					ДокументПриемНаРаботу.Ссылка);
					
			КонецПопытки;
			
		КонецЕсли; 
		
		// Создание приказов об увольнении.
		Если ЗначениеЗаполнено(ДанныеСотрудников.ДатаУвольнения)
			И НЕ ЗначениеЗаполнено(ДанныеСотрудников.ПриказОбУвольнении) Тогда
			
			ДокументУвольнение = Документы.Увольнение.СоздатьДокумент();
			
			ДокументУвольнение.Дата = ДанныеСотрудников.ДатаУвольнения;
			ДокументУвольнение.ДатаУвольнения = ДанныеСотрудников.ДатаУвольнения;
			ДокументУвольнение.Организация = ДанныеСотрудников.ТекущаяОрганизация;
			ДокументУвольнение.Сотрудник = ДанныеСотрудников.Сотрудник;
			
			ДокументУвольнение.ДополнительныеСвойства.Вставить("ОтключитьПроверкуДатыЗапретаИзменения", Истина);
			
			Попытка
				ДокументУвольнение.Записать(РежимЗаписиДокумента.Проведение);
			Исключение
				
				ДокументУвольнение.Записать(РежимЗаписиДокумента.Запись);
				
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru='При создании кадровых приказов не удалось провести приказ о приеме сотрудника %1, необходимо выполнить проведение в ручном режиме.'"),
					ДанныеСотрудников.Сотрудник);
					
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					ТекстСообщения,
					ДокументУвольнение.Ссылка);
					
			КонецПопытки;
			
		КонецЕсли; 
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ЗапросВТВсеЗначенияДоступаСотрудников(Сотрудники) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Сотрудники", Сотрудники);
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Сотрудники.Ссылка КАК Сотрудник,
		|	Сотрудники.ГоловнаяОрганизация КАК ЗначениеДоступа
		|ПОМЕСТИТЬ ВТВсеЗначенияДоступаСотрудников
		|ИЗ
		|	Справочник.Сотрудники КАК Сотрудники
		|ГДЕ
		|	Сотрудники.Ссылка В(&Сотрудники)
		|	И Сотрудники.ГоловнаяОрганизация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	КадроваяИсторияСотрудников.Сотрудник,
		|	КадроваяИсторияСотрудников.Организация
		|ИЗ
		|	РегистрСведений.КадроваяИсторияСотрудников КАК КадроваяИсторияСотрудников
		|ГДЕ
		|	КадроваяИсторияСотрудников.Сотрудник В(&Сотрудники)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ТекущиеКадровыеДанныеСотрудников.Сотрудник,
		|	ТекущиеКадровыеДанныеСотрудников.ТекущаяОрганизация
		|ИЗ
		|	РегистрСведений.ТекущиеКадровыеДанныеСотрудников КАК ТекущиеКадровыеДанныеСотрудников
		|ГДЕ
		|	ТекущиеКадровыеДанныеСотрудников.Сотрудник В(&Сотрудники)
		|	И ТекущиеКадровыеДанныеСотрудников.ТекущаяОрганизация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)";
	
	Возврат Запрос;
	
КонецФункции

Процедура СформироватьКадровыеДвижения(РегистраторОбъект, Движения, КадровыеДвижения, ЗаполнятьНаборЗаписей = Истина) Экспорт
	
	Если КадровыеДвижения.Количество() > 0 Тогда
		Движения.КадроваяИсторияСотрудников.Записывать = Истина;
	КонецЕсли; 
	
	Если ЗаполнятьНаборЗаписей Тогда
		
		ТаблицаДляПроведения = КадровыеДвижения.Скопировать();
		
		ТаблицаДляПроведения.Колонки.ДатаСобытия.Имя = "Период";
		Движения.КадроваяИсторияСотрудников.Загрузить(ТаблицаДляПроведения);
		
	КонецЕсли; 
	
	Если РегистраторОбъект.ДополнительныеСвойства.Свойство("ОтключитьПроверкуДатыЗапретаИзменения")
		И РегистраторОбъект.ДополнительныеСвойства.ОтключитьПроверкуДатыЗапретаИзменения Тогда
		
		Движения.КадроваяИсторияСотрудников.ДополнительныеСвойства.Вставить("ОтключитьПроверкуДатыЗапретаИзменения", Истина);
		
	КонецЕсли;
	
КонецПроцедуры

Функция ДоговорыФизическихЛиц(ТаблицаФизическихЛиц) Экспорт
	
	ТаблицаДоговоров = Новый ТаблицаЗначений;
	ТаблицаДоговоров.Колонки.Добавить("ФизическоеЛицо", 	Новый ОписаниеТипов("СправочникСсылка.ФизическиеЛица"));
	ТаблицаДоговоров.Колонки.Добавить("ВидДоговора", 		Новый ОписаниеТипов("Булево"));
	ТаблицаДоговоров.Колонки.Добавить("ВидДоговораГПХ", 	Новый ОписаниеТипов("Булево"));
	ТаблицаДоговоров.Колонки.Добавить("Номер", 				Новый ОписаниеТипов("Строка"));
	ТаблицаДоговоров.Колонки.Добавить("Дата", 				Новый ОписаниеТипов("Дата"));
	ТаблицаДоговоров.Колонки.Добавить("Организация", 		Новый ОписаниеТипов("СправочникСсылка.Организации"));
	ТаблицаДоговоров.Колонки.Добавить("Начало",				Новый ОписаниеТипов("Дата"));
	ТаблицаДоговоров.Колонки.Добавить("Окончание",			Новый ОписаниеТипов("Дата"));
	
	ТаблицаГоловныхОрганизаций = ТаблицаФизическихЛиц.Скопировать(, "ГоловнаяОрганизация,НачалоПериода,ОкончаниеПериода");
	ТаблицаГоловныхОрганизаций.Свернуть("ГоловнаяОрганизация,НачалоПериода,ОкончаниеПериода");
	
	Для каждого СтрокаГоловнойОрганизации Из ТаблицаГоловныхОрганизаций Цикл
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		Запрос.УстановитьПараметр("НачалоПериода", СтрокаГоловнойОрганизации.НачалоПериода);
		Запрос.УстановитьПараметр("ОкончаниеПериода", СтрокаГоловнойОрганизации.ОкончаниеПериода);
		
		ПараметрыПолученияСотрудников = КадровыйУчет.ПараметрыПолученияСотрудниковОрганизацийПоСпискуФизическихЛиц();
		ПараметрыПолученияСотрудников.Организация = СтрокаГоловнойОрганизации.ГоловнаяОрганизация;
		ПараметрыПолученияСотрудников.ОтбиратьПоГоловнойОрганизации = Истина;
		ПараметрыПолученияСотрудников.НачалоПериода		= СтрокаГоловнойОрганизации.НачалоПериода;
		ПараметрыПолученияСотрудников.ОкончаниеПериода	= СтрокаГоловнойОрганизации.ОкончаниеПериода;
		ПараметрыПолученияСотрудников.КадровыеДанные	= "ПриказОПриемеНомер,ПриказОПриемеДата,ДатаПриема,ДатаУвольнения";
		
		СписокФизическихЛиц = ТаблицаФизическихЛиц.Скопировать(Новый Структура("ГоловнаяОрганизация,НачалоПериода,ОкончаниеПериода",
			СтрокаГоловнойОрганизации.ГоловнаяОрганизация, СтрокаГоловнойОрганизации.НачалоПериода, СтрокаГоловнойОрганизации.ОкончаниеПериода),
			"ФизическоеЛицо").ВыгрузитьКолонку("ФизическоеЛицо");
		
		ПараметрыПолученияСотрудников.СписокФизическихЛиц = СписокФизическихЛиц;
		
		КадровыйУчет.СоздатьВТСотрудникиОрганизации(Запрос.МенеджерВременныхТаблиц, Истина, ПараметрыПолученияСотрудников);
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	СотрудникиОрганизации.Сотрудник,
			|	&НачалоПериода КАК НачалоПериода,
			|	&ОкончаниеПериода КАК ОкончаниеПериода
			|ПОМЕСТИТЬ ВТСотрудникиПериоды
			|ИЗ
			|	ВТСотрудникиОрганизации КАК СотрудникиОрганизации";
			
		Запрос.Выполнить();
		
		ПараметрыПолученияРабочихМест = КадровыйУчет.ПараметрыДляЗапросВТРабочиеМестаСотрудниковПоВременнойТаблице();
		КадровыйУчет.СоздатьВТРабочиеМестаСотрудниковПоВременнойТаблице(Запрос.МенеджерВременныхТаблиц, Истина, ПараметрыПолученияРабочихМест);
		
		Запрос.Текст =
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	РабочиеМестаСотрудников.Период,
			|	РабочиеМестаСотрудников.Сотрудник,
			|	РабочиеМестаСотрудников.ФизическоеЛицо,
			|	ЛОЖЬ КАК ВидДоговора,
			|	ЛОЖЬ КАК ВидДоговораГПХ,
			|	СотрудникиОрганизации.ПриказОПриемеНомер КАК Номер,
			|	НАЧАЛОПЕРИОДА(СотрудникиОрганизации.ПриказОПриемеДата, ДЕНЬ) КАК Дата,
			|	РабочиеМестаСотрудников.Организация,
			|	НАЧАЛОПЕРИОДА(СотрудникиОрганизации.ДатаПриема, ДЕНЬ) КАК Начало,
			|	ВЫБОР
			|		КОГДА СотрудникиОрганизации.ДатаУвольнения МЕЖДУ &НачалоПериода И &ОкончаниеПериода
			|			ТОГДА НАЧАЛОПЕРИОДА(СотрудникиОрганизации.ДатаУвольнения, ДЕНЬ)
			|		ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1)
			|	КОНЕЦ КАК Окончание
			|ПОМЕСТИТЬ ВТДоговорыПредварительно
			|ИЗ
			|	ВТРабочиеМестаСотрудников КАК РабочиеМестаСотрудников
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСотрудникиОрганизации КАК СотрудникиОрганизации
			|		ПО РабочиеМестаСотрудников.Сотрудник = СотрудникиОрганизации.Сотрудник";
			
		Запрос.Выполнить();
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	Договоры.Период,
			|	Договоры.Сотрудник,
			|	Договоры.ФизическоеЛицо,
			|	Договоры.ВидДоговора,
			|	Договоры.ВидДоговораГПХ,
			|	Договоры.Номер,
			|	Договоры.Дата,
			|	Договоры.Организация,
			|	Договоры.Начало,
			|	Договоры.Окончание,
			|	МИНИМУМ(НАЧАЛОПЕРИОДА(ДоговорыДругаяОрганизация.Период, ДЕНЬ)) КАК СледующийПериод
			|ПОМЕСТИТЬ ВТДоговорыСОкончанием
			|ИЗ
			|	ВТДоговорыПредварительно КАК Договоры
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДоговорыПредварительно КАК ДоговорыДругаяОрганизация
			|		ПО Договоры.Сотрудник = ДоговорыДругаяОрганизация.Сотрудник
			|			И Договоры.Организация <> ДоговорыДругаяОрганизация.Организация
			|			И Договоры.Период < ДоговорыДругаяОрганизация.Период
			|
			|СГРУППИРОВАТЬ ПО
			|	Договоры.Период,
			|	Договоры.Сотрудник,
			|	Договоры.ФизическоеЛицо,
			|	Договоры.ВидДоговора,
			|	Договоры.ВидДоговораГПХ,
			|	Договоры.Номер,
			|	Договоры.Дата,
			|	Договоры.Организация,
			|	Договоры.Начало,
			|	Договоры.Окончание
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ДоговорыСОкончанием.ФизическоеЛицо КАК ФизическоеЛицо,
			|	ДоговорыСОкончанием.ВидДоговора КАК ВидДоговора,
			|	ДоговорыСОкончанием.ВидДоговораГПХ КАК ВидДоговораГПХ,
			|	ДоговорыСОкончанием.Номер КАК Номер,
			|	ДоговорыСОкончанием.Дата КАК Дата,
			|	ДоговорыСОкончанием.Организация КАК Организация,
			|	ЕСТЬNULL(ДоговорыСНовымНачалом.СледующийПериод, ДоговорыСОкончанием.Начало) КАК Начало,
			|	ВЫБОР
			|		КОГДА ДоговорыСОкончанием.СледующийПериод <> ДАТАВРЕМЯ(1, 1, 1)
			|			ТОГДА ДОБАВИТЬКДАТЕ(ДоговорыСОкончанием.СледующийПериод, ДЕНЬ, -1)
			|		ИНАЧЕ ДоговорыСОкончанием.Окончание
			|	КОНЕЦ КАК Окончание
			|ИЗ
			|	ВТДоговорыСОкончанием КАК ДоговорыСОкончанием
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДоговорыСОкончанием КАК ДоговорыСНовымНачалом
			|		ПО ДоговорыСОкончанием.Сотрудник = ДоговорыСНовымНачалом.Сотрудник
			|			И (НАЧАЛОПЕРИОДА(ДоговорыСОкончанием.Период, ДЕНЬ) = ДоговорыСНовымНачалом.СледующийПериод)
			|
			|УПОРЯДОЧИТЬ ПО
			|	Организация,
			|	ФизическоеЛицо,
			|	ВидДоговора УБЫВ,
			|	Начало";
			
		ТаблицаДоговоровГоловнойОрганизации = Запрос.Выполнить().Выгрузить();
		
		Для каждого СтрокаТаблицаДоговоровГоловнойОрганизации Из ТаблицаДоговоровГоловнойОрганизации Цикл
			ЗаполнитьЗначенияСвойств(ТаблицаДоговоров.Добавить(), СтрокаТаблицаДоговоровГоловнойОрганизации);
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат ТаблицаДоговоров;
	
КонецФункции

#Область ФункцииДляРегламентированнойОтчетности

// Функция рассчитывает среднесписочную численность работников, 
// среднесписочную численность женщин, среднесписочную численность инвалидов за период.
//
// Параметры:
//		Организация
//		НачалоПериода
//		КонецПериода
//
Функция СреднесписочнаяЧисленностьРаботающих(Организация, НачалоПериода, КонецПериода) Экспорт 
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	ЗарплатаКадрыОбщиеНаборыДанных.СоздатьВТПериоды(Запрос.МенеджерВременныхТаблиц, НачалоПериода, КонецПериода, "ДЕНЬ", "Дата", "Календарь");

	Запрос.Текст = "ВЫБРАТЬ
	               |	Календарь.Дата КАК Дата,
	               |	НАЧАЛОПЕРИОДА(Календарь.Дата, МЕСЯЦ) КАК Месяц
	               |ПОМЕСТИТЬ ВТСписокДат
	               |ИЗ
	               |	Календарь КАК Календарь
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ТекущиеКадровыеДанныеСотрудников.Сотрудник КАК Сотрудник,
	               |	СписокДат.Дата КАК Дата,
	               |	СписокДат.Месяц
	               |ПОМЕСТИТЬ ВТДатыИСотрудники
	               |ИЗ
	               |	ВТСписокДат КАК СписокДат
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТекущиеКадровыеДанныеСотрудников КАК ТекущиеКадровыеДанныеСотрудников
	               |			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Сотрудники КАК Сотрудники
	               |			ПО ТекущиеКадровыеДанныеСотрудников.Сотрудник = Сотрудники.Ссылка
	               |				И ТекущиеКадровыеДанныеСотрудников.ФизическоеЛицо = Сотрудники.ФизическоеЛицо
	               |				И (ТекущиеКадровыеДанныеСотрудников.ТекущийВидЗанятости = ЗНАЧЕНИЕ(Перечисление.ВидыЗанятости.ОсновноеМестоРаботы))
	               |		ПО СписокДат.Дата >= ТекущиеКадровыеДанныеСотрудников.ДатаПриема
	               |			И (СписокДат.Дата <= ТекущиеКадровыеДанныеСотрудников.ДатаУвольнения
	               |					И ТекущиеКадровыеДанныеСотрудников.ДатаУвольнения <> ДАТАВРЕМЯ(1, 1, 1)
	               |				ИЛИ ТекущиеКадровыеДанныеСотрудников.ДатаУвольнения = ДАТАВРЕМЯ(1, 1, 1))
	               |ГДЕ
	               |	(ТекущиеКадровыеДанныеСотрудников.ТекущаяОрганизация = &Организация
	               |			ИЛИ ТекущиеКадровыеДанныеСотрудников.ТекущаяОрганизация ЕСТЬ NULL )
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	СУММА(ВЫБОР
	               |			КОГДА СписокДат.Сотрудник ЕСТЬ НЕ NULL 
	               |				ТОГДА 1
	               |			ИНАЧЕ 0
	               |		КОНЕЦ) КАК КоличествоСотрудников,
	               |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ СписокДат.Дата) КАК ДнейВМесяце,
	               |	СписокДат.Месяц
	               |ПОМЕСТИТЬ ВТСредняяЧисленностьПоМесяцам
	               |ИЗ
	               |	ВТДатыИСотрудники КАК СписокДат
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	СписокДат.Месяц
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ЕСТЬNULL(СУММА(ВЫРАЗИТЬ(СредняяЧисленностьПоМесяцам.КоличествоСотрудников / СредняяЧисленностьПоМесяцам.ДнейВМесяце КАК ЧИСЛО(15, 0))) / КОЛИЧЕСТВО(СредняяЧисленностьПоМесяцам.Месяц), 0) КАК СреднесписочнаяЧисленность
	               |ИЗ
	               |	ВТСредняяЧисленностьПоМесяцам КАК СредняяЧисленностьПоМесяцам";
	
	Запрос.УстановитьПараметр("Организация", Организация);
	УстановитьПривилегированныйРежим(Истина) ;
	Выборка = Запрос.Выполнить().Выбрать();
	УстановитьПривилегированныйРежим(Ложь);
	
	Если Выборка.Следующий() Тогда
		Возврат Окр(Выборка.СреднесписочнаяЧисленность);
	КонецЕсли;
	
	Возврат 0;
	
КонецФункции

#КонецОбласти

#Область ФормированиеТекстовЗапросовВременныхТаблиц

Функция ПараметрыПолученияОсновныхСотрудниковФизическихЛиц(СписокФизическихЛиц, Организация, НачалоПериода, ОкончаниеПериода) Экспорт
	
	ПараметрыПолученияСотрудниковОрганизаций = КадровыйУчет.ПараметрыПолученияСотрудниковОрганизацийПоСпискуФизическихЛиц();
	Если ЗначениеЗаполнено(Организация) Тогда
		ПараметрыПолученияСотрудниковОрганизаций.Организация = ЗарплатаКадрыПовтИсп.ГоловнаяОрганизация(Организация);
	КонецЕсли;
	ПараметрыПолученияСотрудниковОрганизаций.ОтбиратьПоГоловнойОрганизации 	= Истина;
	ПараметрыПолученияСотрудниковОрганизаций.СписокФизическихЛиц			= СписокФизическихЛиц;
	ПараметрыПолученияСотрудниковОрганизаций.НачалоПериода					= НачалоПериода;
	ПараметрыПолученияСотрудниковОрганизаций.ОкончаниеПериода				= ?(НачалоПериода > ОкончаниеПериода, НачалоПериода, ОкончаниеПериода);
	ПараметрыПолученияСотрудниковОрганизаций.КадровыеДанные					= "ФизическоеЛицо,ВидЗанятости,ДатаПриема";
	
	Возврат ПараметрыПолученияСотрудниковОрганизаций;
	
КонецФункции

Функция ЗапросВТОбъектПоИдентификатору(ПолноеИмяОбъекта, ИмяВТОбъектПоИдентификатору, ИдентификаторОбъекта) Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Объекты.Ссылка КАК " + ИдентификаторОбъекта + "
		|ПОМЕСТИТЬ " + ИмяВТОбъектПоИдентификатору + "
		|ИЗ
		|	" + ПолноеИмяОбъекта + " КАК Объекты
		|ГДЕ
		|	Объекты.ИдентификаторОбъекта = """ + ИдентификаторОбъекта + """";
		
	Возврат Запрос;
	
КонецФункции

Функция ЗапросВТОбъектПредопределенноеЗначение(ПолноеИмяОбъекта, ИмяВТОбъектПредопределенноеЗначение, ИмяЭлемента) Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("ПредопределенноеЗначениеСсылка", ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент(ПолноеИмяОбъекта + "." + ИмяЭлемента));
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Объекты.Ссылка КАК " + ИмяЭлемента + "
		|ПОМЕСТИТЬ " + ИмяВТОбъектПредопределенноеЗначение + "
		|ИЗ
		|	" + ПолноеИмяОбъекта + " КАК Объекты
		|ГДЕ
		|	Объекты.Ссылка = &ПредопределенноеЗначениеСсылка";
		
	Возврат Запрос;
	
КонецФункции

Функция ЗапросВТФункциональныеОпцииОрганизаций(ТолькоРазрешенные, ИмяВТФункциональныеОпцииОрганизаций, МассивФункциональныхОпций) Экспорт
	
	Запрос = Новый Запрос;
	
	СоответствиеХранилищ = Новый Соответствие;
	МассивФункциональныхОпцийСправочникаОрганизации = Новый Массив;
	СоответствиеПериодичностиХранилищ = Новый Соответствие;
	
	Для каждого ИмяФункциональнойОпции Из МассивФункциональныхОпций Цикл
		
		Если ВРег(ИмяФункциональнойОпции) = "ОРГАНИЗАЦИЯ" Тогда
			Продолжить;
		КонецЕсли; 
		
		МетаданныеФункциональнойОпции = Метаданные.ФункциональныеОпции[ИмяФункциональнойОпции];
		ИмяОбъектаХранилища = МетаданныеФункциональнойОпции.Хранение.Родитель().ПолноеИмя();
		
		Если ИмяОбъектаХранилища = "Справочник.Организации" Тогда
			
			МассивФункциональныхОпцийСправочникаОрганизации.Добавить( Новый Структура("ИмяФункциональнойОпции,ИмяХранилища", ИмяФункциональнойОпции, МетаданныеФункциональнойОпции.Хранение.Имя));
			
		Иначе
			
			СоответствиеХранилища = СоответствиеХранилищ.Получить(ИмяОбъектаХранилища);
			Если СоответствиеХранилища = Неопределено Тогда
				СоответствиеХранилища = Новый Массив;
			КонецЕсли;
			
			СоответствиеХранилища.Добавить(Новый Структура("ИмяФункциональнойОпции,ИмяХранилища", ИмяФункциональнойОпции, МетаданныеФункциональнойОпции.Хранение.Имя));
			СоответствиеХранилищ.Вставить(ИмяОбъектаХранилища, СоответствиеХранилища);
			
		КонецЕсли;
		
	КонецЦикла;
	
	ТекстЗапроса = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Организации.Ссылка КАК Организация";
		
	Для каждого СтруктураОписания Из МассивФункциональныхОпцийСправочникаОрганизации Цикл

		ТекстЗапроса = ТекстЗапроса +
			",
			|	Организации." + СтруктураОписания.ИмяХранилища + " КАК " +  СтруктураОписания.ИмяФункциональнойОпции;
						
	КонецЦикла;
	
	Для каждого СоответствиеХранилища Из СоответствиеХранилищ Цикл
		
		МетаданныеОбъектаХранилища = Метаданные.НайтиПоПолномуИмени(СоответствиеХранилища.Ключ);
		
		СоответствиеПериодичностиХранилищ.Вставить(СоответствиеХранилища.Ключ, МетаданныеОбъектаХранилища.ПериодичностьРегистраСведений);
		
		Если ОбщегоНазначения.ИмяБазовогоТипаПоОбъектуМетаданных(МетаданныеОбъектаХранилища) = ОбщегоНазначения.ИмяТипаРегистрыСведений() Тогда
			
			Для каждого СтруктураОписания Из СоответствиеХранилища.Значение Цикл

				ТекстЗапроса = ТекстЗапроса +
					",
					|	" + СтрЗаменить(СоответствиеХранилища.Ключ, ".", "") + "." + СтруктураОписания.ИмяХранилища + " КАК " +  СтруктураОписания.ИмяФункциональнойОпции;
						
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЦикла;
	
	ТекстЗапроса = ТекстЗапроса +
		"
		|ПОМЕСТИТЬ " + ИмяВТФункциональныеОпцииОрганизаций + "
		|ИЗ Справочник.Организации КАК Организации";
	
	Для каждого СоответствиеХранилища Из СоответствиеХранилищ Цикл
			
		ПериодичностьРегистраСведений = СоответствиеПериодичностиХранилищ.Получить(СоответствиеХранилища.Ключ);
		
		Если ПериодичностьРегистраСведений = Метаданные.СвойстваОбъектов.ПериодичностьРегистраСведений.Непериодический Тогда
			ПутьКОбъектуХранилища = СоответствиеХранилища.Ключ;
		Иначе
			ПутьКОбъектуХранилища = СоответствиеХранилища.Ключ + ".СрезПоследних";
		КонецЕсли;
				
		ТекстЗапроса = ТекстЗапроса +
			"
			|	ЛЕВОЕ СОЕДИНЕНИЕ " + ПутьКОбъектуХранилища + " КАК " + СтрЗаменить(СоответствиеХранилища.Ключ, ".", "") + "
			|		ПО Организации.Ссылка = " + СтрЗаменить(СоответствиеХранилища.Ключ, ".", "") + ".Организация";
			
	КонецЦикла;
	
	Запрос.Текст = ТекстЗапроса;
	
	Возврат Запрос;
	
КонецФункции

Функция ЗапросВТСотрудникиОрганизации(ТолькоРазрешенные, ИмяВТСотрудникиОрганизации, Параметры) Экспорт
	
	Перем СписокФизическихЛиц;
	Перем ИмяВТФизическиеЛица;
	Перем ГоловнаяОрганизация;
	
	Если ЗначениеЗаполнено(Параметры.Организация) Тогда
		ГоловнаяОрганизация = ЗарплатаКадрыПовтИсп.ГоловнаяОрганизация(Параметры.Организация);
	КонецЕсли; 
	
	Параметры.Свойство("СписокФизическихЛиц", СписокФизическихЛиц);
	Параметры.Свойство("ИмяВТФизическиеЛица", ИмяВТФизическиеЛица);
	
	Запрос = Новый Запрос;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьКадровыйУчет") Тогда
		
		ИмяВТСотрудникиПериоды = ЗарплатаКадрыОбщиеНаборыДанных.УникальноеИмяТекстаЗапроса("ВТСотрудникиПериоды");
		
		Если ТипЗнч(Параметры.НачалоПериода) = Тип("Строка") Тогда
			ТекстДатаНачала = Параметры.НачалоПериода;
		Иначе
			ТекстДатаНачала = "ДАТАВРЕМЯ(" + Формат(Параметры.НачалоПериода, "ДФ='гггг, М, д, Ч, м, с'; ДП=") + ")";
		КонецЕсли;
		
		Если ТипЗнч(Параметры.ОкончаниеПериода) = Тип("Строка") Тогда
			ТекстДатаОкончания = Параметры.ОкончаниеПериода;
		Иначе
			ТекстДатаОкончания = "ДАТАВРЕМЯ(" + Формат(Параметры.ОкончаниеПериода, "ДФ='гггг, М, д, Ч, м, с'; ДП=") + ")";
		КонецЕсли;
		
		Запрос.Текст = "ВЫБРАТЬ " + ?(ТолькоРазрешенные, "РАЗРЕШЕННЫЕ", "") + "
			|	Сотрудники.Ссылка КАК Сотрудник,
			|	" + ТекстДатаНачала + " КАК ДатаНачала,
			|	" + ТекстДатаОкончания + " КАК ДатаОкончания
			|ПОМЕСТИТЬ " + ИмяВТСотрудникиПериоды + "
			|ИЗ
			|	Справочник.Сотрудники КАК Сотрудники";
			
		ТекстУсловий = "";
		ТекстСоединения = "";
		
		Если СписокФизическихЛиц <> Неопределено Тогда
				
			ТекстУсловий = "Сотрудники.Ссылка.ФизическоеЛицо В (&МассивФизическихЛиц)";
					
			Запрос.УстановитьПараметр("МассивФизическихЛиц", СписокФизическихЛиц); 
				
		ИначеЕсли ИмяВТФизическиеЛица <> Неопределено Тогда
			
			ТекстСоединения = "
				|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ " + ИмяВТФизическиеЛица + " КАК ФизическиеЛица
				|		ПО Сотрудники.Ссылка.ФизическоеЛицо = ФизическиеЛица." + Параметры.ИмяПоляФизическоеЛицо;
			
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ГоловнаяОрганизация) Тогда
			
			ТекстУсловий = ?(ПустаяСтрока(ТекстУсловий), "", ТекстУсловий + Символы.ПС + "	И ")
				+ "Сотрудники.ГоловнаяОрганизация = &ГоловнаяОрганизация";
			Запрос.УстановитьПараметр("ГоловнаяОрганизация", ГоловнаяОрганизация);
			
		КонецЕсли; 
		
		Если НЕ ПустаяСтрока(ТекстСоединения) Тогда
			Запрос.Текст = Запрос.Текст + ТекстСоединения;
		КонецЕсли; 
		
		Если НЕ ПустаяСтрока(ТекстУсловий) Тогда
			Запрос.Текст = Запрос.Текст + "
				|ГДЕ
				|	" + ТекстУсловий;
		КонецЕсли; 
		
		Запрос.Текст = Запрос.Текст + "
			|{ГДЕ
			|	Сотрудники.Ссылка.* КАК Сотрудник,
			|	Сотрудники.ГоловнаяОрганизация.*,
			|	Сотрудники.ГоловнойСотрудник.*,
			|	Сотрудники.ФизическоеЛицо.*}";
				
		ИмяВТРабочиеМестаСотрудников = ЗарплатаКадрыОбщиеНаборыДанных.УникальноеИмяТекстаЗапроса("ВТРабочиеМестаСотрудников");
		
		ПараметрыПолученияРабочихМест = КадровыйУчет.ПараметрыДляЗапросВТРабочиеМестаСотрудниковПоВременнойТаблице(
			ИмяВТСотрудникиПериоды, , "ДатаНачала", "ДатаОкончания");
			
		ЗаполнитьЗначенияСвойств(ПараметрыПолученияРабочихМест, Параметры);
		ЗапросВТРабочиеМеста = КадровыйУчет.ЗапросВТРабочиеМестаСотрудниковПоВременнойТаблице(ТолькоРазрешенные, ИмяВТРабочиеМестаСотрудников, ПараметрыПолученияРабочихМест);
		
		ЗарплатаКадрыОбщиеНаборыДанных.ОбъединитьЗапросы(Запрос, ЗапросВТРабочиеМеста);
		
		Если ЗначениеЗаполнено(Параметры.КадровыеДанные) Тогда
			ИмяВТСотрудникиОрганизацииДляКадровойИстории = ЗарплатаКадрыОбщиеНаборыДанных.УникальноеИмяТекстаЗапроса(ИмяВТСотрудникиОрганизации);
		Иначе
			ИмяВТСотрудникиОрганизацииДляКадровойИстории = ИмяВТСотрудникиОрганизации;
		КонецЕсли;
		
		Запрос.Текст = Запрос.Текст
			+ ЗарплатаКадрыОбщиеНаборыДанных.РазделительЗапросов()
			+ "
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	" + ТекстДатаОкончания + " КАК Период,
			|	РабочиеМестаСотрудников.Сотрудник КАК Сотрудник,
			|	РабочиеМестаСотрудников.ФизическоеЛицо КАК ФизическоеЛицо,
			|	МАКСИМУМ(РабочиеМестаСотрудников.ПериодЗаписи) КАК ПериодЗаписи
			|ПОМЕСТИТЬ " + ИмяВТСотрудникиОрганизацииДляКадровойИстории + "
			|ИЗ
			|	" + ИмяВТРабочиеМестаСотрудников + " КАК РабочиеМестаСотрудников
			|СГРУППИРОВАТЬ ПО
			|	" + ТекстДатаОкончания + ",
			|	РабочиеМестаСотрудников.Сотрудник,
			|	РабочиеМестаСотрудников.ФизическоеЛицо";
			
		ДополнитьЗапросВТСотрудникиОрганизацииКадровымиДанными(
			Запрос,
			ТолькоРазрешенные,
			ИмяВТСотрудникиОрганизации,
			ИмяВТСотрудникиОрганизацииДляКадровойИстории,
			Параметры.КадровыеДанные);
			
	Иначе
		
		ИмяВТСотрудникиПериоды = ЗарплатаКадрыОбщиеНаборыДанных.УникальноеИмяТекстаЗапроса("ВТСотрудникиПериоды");
		
		Если ТипЗнч(Параметры.ОкончаниеПериода) = Тип("Строка") Тогда
			ТекстПериод = Параметры.ОкончаниеПериода;
		Иначе
			ТекстПериод = "ДАТАВРЕМЯ(" + Формат(Параметры.ОкончаниеПериода, "ДФ='гггг, М, д, Ч, м, с'; ДП=") + ")";
		КонецЕсли;
		
		Запрос.Текст = "ВЫБРАТЬ " + ?(ТолькоРазрешенные, "РАЗРЕШЕННЫЕ", "") + "
			|	Сотрудники.Ссылка КАК Сотрудник,
			|	" + ТекстПериод + " КАК Период
			|ПОМЕСТИТЬ " + ИмяВТСотрудникиПериоды + "
			|ИЗ
			|	Справочник.Сотрудники КАК Сотрудники";
			
		Если СписокФизическихЛиц <> Неопределено Тогда
				
				Запрос.Текст = Запрос.Текст + "
					|ГДЕ
					|	 Сотрудники.Ссылка.ФизическоеЛицо В (&МассивФизическихЛиц)";
					
				Запрос.УстановитьПараметр("МассивФизическихЛиц", СписокФизическихЛиц); 
				
		ИначеЕсли ИмяВТФизическиеЛица <> Неопределено Тогда
			
			Запрос.Текст = Запрос.Текст + "
				|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ " + ИмяВТФизическиеЛица + " КАК ФизическиеЛица
				|		ПО Сотрудники.Ссылка.ФизическоеЛицо = ФизическиеЛица." + Параметры.ИмяПоляФизическоеЛицо;
			
		КонецЕсли; 
		
		ИмяВТКадровыеДанныеСотрудников = ЗарплатаКадрыОбщиеНаборыДанных.УникальноеИмяТекстаЗапроса("ВТКадровыеДанныеСотрудников");
		
		МассивНеобходимыхКадровыхДанных = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок("ФизическоеЛицо,ОформленПоТрудовомуДоговору,ДатаПриема,ДатаУвольнения,ГоловнаяОрганизация,Организация,Подразделение");
		
		Если ТипЗнч(Параметры.КадровыеДанные) = Тип("Строка") Тогда
			МассивЗаказанныхКадровыхДанных = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Параметры.КадровыеДанные);
		Иначе
			МассивЗаказанныхКадровыхДанных = Параметры.КадровыеДанные;
		КонецЕсли;
		
		Для каждого ИмяПоля Из МассивЗаказанныхКадровыхДанных Цикл
			Если МассивНеобходимыхКадровыхДанных.Найти(ИмяПоля) = Неопределено Тогда
				МассивНеобходимыхКадровыхДанных.Добавить(ИмяПоля)
			КонецЕсли; 
		КонецЦикла;
		
		ПоляОтбораСотрудников = КадровыйУчет.ПоляОтбораСотрудников();
		
		ОписательВременнойТаблицыОтборов = КадровыйУчет.ОписаниеВременнойТаблицыОтборовСотрудников(
			ИмяВТСотрудникиПериоды,
			ПоляОтбораСотрудников.Сотрудник,
			ПоляОтбораСотрудников.Период);
			
		ЗапросВТКадровыеДанныеСотрудников = КадровыйУчет.ЗапросВТКадровыеДанныеСотрудников(
			ТолькоРазрешенные,
			ОписательВременнойТаблицыОтборов,
			МассивНеобходимыхКадровыхДанных,
			,
			,
			ИмяВТКадровыеДанныеСотрудников);
			
		ЗарплатаКадрыОбщиеНаборыДанных.ОбъединитьЗапросы(Запрос, ЗапросВТКадровыеДанныеСотрудников);
		
		Запрос.Текст = Запрос.Текст +
			ЗарплатаКадрыОбщиеНаборыДанных.РазделительЗапросов() +
			"ВЫБРАТЬ
			|	КадровыеДанныеСотрудников.Период КАК ПериодЗаписи,
			|	КадровыеДанныеСотрудников.Период КАК Период,
			|	КадровыеДанныеСотрудников.Сотрудник КАК Сотрудник,
			|	КадровыеДанныеСотрудников.ФизическоеЛицо КАК ФизическоеЛицо";
			
		Для каждого ИмяПоля Из МассивЗаказанныхКадровыхДанных Цикл
			
			Если ВРег(ИмяПоля) = "ПЕРИОД"
				ИЛИ ВРег(ИмяПоля) = "СОТРУДНИК"
				ИЛИ ВРег(ИмяПоля) = "ФИЗИЧЕСКОЕЛИЦО" Тогда
				
				Продолжить;
				
			КонецЕсли;
			
			Запрос.Текст = Запрос.Текст + ",
				|	КадровыеДанныеСотрудников." + ИмяПоля + " КАК " + ИмяПоля;
				
		КонецЦикла;
			
		Запрос.Текст = Запрос.Текст + "
			|ПОМЕСТИТЬ " + ИмяВТСотрудникиОрганизации + "
			|ИЗ
			|	" + ИмяВТКадровыеДанныеСотрудников + " КАК КадровыеДанныеСотрудников";
			
		ТекстЗапросаУсловий = "";
		
		Если ПолучитьФункциональнуюОпцию("ИспользоватьНачислениеЗарплаты")
			И (ЗначениеЗаполнено(Параметры.НачалоПериода)
				ИЛИ ЗначениеЗаполнено(Параметры.ОкончаниеПериода)) Тогда
				
			Если ТипЗнч(Параметры.НачалоПериода) = Тип("Строка") Тогда
				ТекстНачалоПериода = Параметры.НачалоПериода;
			Иначе
				ТекстНачалоПериода = "ДАТАВРЕМЯ(" + Формат(Параметры.НачалоПериода, "ДФ='гггг, М, д, Ч, м, с'; ДП=") + ")";
			КонецЕсли;
			
			Если ТипЗнч(Параметры.ОкончаниеПериода) = Тип("Строка") Тогда
				ТекстОкончаниеПериода = Параметры.ОкончаниеПериода;
			Иначе
				ТекстОкончаниеПериода = "ДАТАВРЕМЯ(" + Формат(Параметры.ОкончаниеПериода, "ДФ='гггг, М, д, Ч, м, с'; ДП=") + ")";
			КонецЕсли;
		
			ТекстЗапросаУсловий = "
				|			КадровыеДанныеСотрудников.ОформленПоТрудовомуДоговору
				|			И КадровыеДанныеСотрудников.ДатаПриема > ДАТАВРЕМЯ(1, 1, 1)
				|			И (КадровыеДанныеСотрудников.ДатаПриема <= " + ТекстОкончаниеПериода + "
				|				ИЛИ " + ТекстОкончаниеПериода + " = ДАТАВРЕМЯ(1, 1, 1))
				|			И (КадровыеДанныеСотрудников.ДатаУвольнения >= " + ТекстНачалоПериода + "
				|				ИЛИ КадровыеДанныеСотрудников.ДатаУвольнения = ДАТАВРЕМЯ(1, 1, 1))
				|";
			
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Параметры.Подразделение) Тогда
			
			Если НЕ ПустаяСтрока(ТекстЗапросаУсловий) Тогда
				ТекстЗапросаУсловий = ТекстЗапросаУсловий + "
					|	И ";
			КонецЕсли;
			
			ТекстЗапросаУсловий = ТекстЗапросаУсловий + "КадровыеДанныеСотрудников.Подразделение В ИЕРАРХИИ (&Подразделение)";
			
			Запрос.УстановитьПараметр("Подразделение", Параметры.Подразделение);
			
		КонецЕсли; 
		
		Если ЗначениеЗаполнено(Параметры.Организация) Тогда
			
			Если НЕ ПустаяСтрока(ТекстЗапросаУсловий) Тогда
				ТекстЗапросаУсловий = ТекстЗапросаУсловий + "
					|	И ";
			КонецЕсли;
			
			ТекстЗапросаУсловий = ТекстЗапросаУсловий + "КадровыеДанныеСотрудников.ГоловнаяОрганизация = &ГоловнаяОрганизация";
			Запрос.УстановитьПараметр("ГоловнаяОрганизация", ГоловнаяОрганизация);
			
			Если НЕ Параметры.ОтбиратьПоГоловнойОрганизации Тогда
				
				Если НЕ ПустаяСтрока(ТекстЗапросаУсловий) Тогда
					ТекстЗапросаУсловий = ТекстЗапросаУсловий + "
						|	И ";
				КонецЕсли;
				
				ТекстЗапросаУсловий = ТекстЗапросаУсловий + "КадровыеДанныеСотрудников.Организация = &Организация";
				Запрос.УстановитьПараметр("Организация", Параметры.Организация);
				
			КонецЕсли;
			
		КонецЕсли;
		
		Если НЕ ПустаяСтрока(ТекстЗапросаУсловий) Тогда
			
			Запрос.Текст = Запрос.Текст + "
				|ГДЕ
				|	" + ТекстЗапросаУсловий;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Запрос;
	
КонецФункции

Процедура ДополнитьЗапросВТСотрудникиОрганизацииКадровымиДанными(Запрос, ТолькоРазрешенные, ИмяВТСотрудникиОрганизации, ИмяВТСотрудникиОрганизацииПромежуточная, КадровыеДанные) Экспорт
	
	Если Не ЗначениеЗаполнено(КадровыеДанные) Тогда
		Возврат;
	КонецЕсли; 
	
	Если ТиПЗнч(КадровыеДанные) = Тип("Строка") Тогда
		МассивНеобходимыхКадровыхДанных = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(КадровыеДанные);
	Иначе
		МассивНеобходимыхКадровыхДанных = КадровыеДанные;
	КонецЕсли;
	
	
	Если МассивНеобходимыхКадровыхДанных.Найти("ФизическоеЛицо") = Неопределено Тогда
		МассивНеобходимыхКадровыхДанных.Добавить("ФизическоеЛицо");
	КонецЕсли; 
	
	ПоляОтбораСотрудников = КадровыйУчет.ПоляОтбораСотрудников();
	
	ОписательВременнойТаблицыОтборов = КадровыйУчет.ОписаниеВременнойТаблицыОтборовСотрудников(
		ИмяВТСотрудникиОрганизацииПромежуточная,
		ПоляОтбораСотрудников.Сотрудник,
		ПоляОтбораСотрудников.Период);
		
	ЗапросВТКадровыеДанныеСотрудников = КадровыйУчет.ЗапросВТКадровыеДанныеСотрудников(
		ТолькоРазрешенные,
		ОписательВременнойТаблицыОтборов,
		МассивНеобходимыхКадровыхДанных,
		,
		,
		ИмяВТСотрудникиОрганизации);
		
	ЗарплатаКадрыОбщиеНаборыДанных.ОбъединитьЗапросы(Запрос, ЗапросВТКадровыеДанныеСотрудников);

КонецПроцедуры

// Возвращает структуру, содержащую описание временной таблицы, используемой в качестве фильтра
// для получения контактной информации.
//
//	Параметры:
//		ИмяВТОтборОбъектов - Строка, имя временной таблицы, служащей отбором для получаемой контактной информации.
//		ИмяРеквизитаОбъект - Строка, имя реквизита во временной таблице, содержащего объект отбора контактной информации.
//		ИмяРеквизитаВид - Строка, имя реквизита во временной таблице, содержащего вид контактной информации, если не указано
//							в результирующую таблицу будет помещена контактная информация любого вида.
//		ИмяРеквизитаТип - Строка, имя реквизита во временной таблице, содержащего тип контактной информации, если не указано
//							в результирующую таблицу будет помещена контактная информация любого типа.
//
//	Возвращаемое значение:
//		Структура - содержащая ключи:
//			ИмяВТОтборОбъектов
//			ИмяРеквизитаОбъект
//			ИмяРеквизитаВид
//			ИмяРеквизитаТип
//
Функция ОписаниеВременнойТаблицыОтборовКонтактнойИнформации(ИмяВТОтборОбъектов, ИмяРеквизитаОбъект, ИмяРеквизитаВид = "", ИмяРеквизитаТип = "") Экспорт
	
	ОписаниеВременнойТаблицы = Новый Структура;
	
	ОписаниеВременнойТаблицы.Вставить("ИмяВТОтборОбъектов", ИмяВТОтборОбъектов);
	ОписаниеВременнойТаблицы.Вставить("ИмяРеквизитаОбъект", ИмяРеквизитаОбъект);
	ОписаниеВременнойТаблицы.Вставить("ИмяРеквизитаВид", ИмяРеквизитаВид);
	ОписаниеВременнойТаблицы.Вставить("ИмяРеквизитаТип", ИмяРеквизитаТип);
	
	Возврат ОписаниеВременнойТаблицы;
	
КонецФункции

// Возвращает запрос, с подготовленным текстом, для создания временной таблицы, содержащей контактную информацию.
//
//	Параметры:
//		ТолькоРазрешенные - Булево, если истина - запрос строится с учетом прав доступа к объектам, содержащим
//							контактную информацию, если ложь - без учета прав доступа (возможно, для выполнения запроса
//							потребуется установить привилегированный режим).
//		ИмяВТКонтактнаяИнформация - Строка, имя временной таблицы, которая создастся в ходе выполнения запроса.
//		ПолноеИмяОбъектаМетаданных - Строка, полное имя объекта метаданных, содержащего контактную информацию
//							(например, "Справочник.ФизическиеЛица")
//		ОписаниеВременнойТаблицыОтборов - Структура, подготовленная с помощью метода
//		                                  "ОписаниеВременнойТаблицыОтборовКонтактнойИнформации".
//
//	Возвращаемое значение:
//		Запрос
//
Функция ЗапросВТКонтактнаяИнформация(ТолькоРазрешенные, ИмяВТКонтактнаяИнформация, ПолноеИмяОбъектаМетаданных, ОписаниеВременнойТаблицыОтборов) Экспорт
	
	Запрос = Новый Запрос;
	
	// Формирование текста запроса.
	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	КонтактнаяИнформация.Ссылка КАК Объект,
		|	КонтактнаяИнформация.НомерСтроки,
		|	КонтактнаяИнформация.Тип,
		|	КонтактнаяИнформация.Вид,
		|	КонтактнаяИнформация.Представление,
		|	КонтактнаяИнформация.ЗначенияПолей
		|ПОМЕСТИТЬ ВТВсяКонтактнаяИнформация
		|ИЗ
		|	&КонтактнаяИнформация КАК КонтактнаяИнформация
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТОтборКонтактнойИнформации КАК ОтборКонтактнойИнформации
		|		ПО КонтактнаяИнформация.Ссылка = ОтборКонтактнойИнформации.РеквизитОбъекта
		|		И &ТекстОтбораКонтактнойИнформации
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВсяКонтактнаяИнформация.Объект,
		|	ВсяКонтактнаяИнформация.Вид,
		|	МИНИМУМ(ВсяКонтактнаяИнформация.НомерСтроки) КАК НомерСтроки
		|ПОМЕСТИТЬ ВТПервыеЗначенияПоВидуКонтактнойИнформации
		|ИЗ
		|	ВТВсяКонтактнаяИнформация КАК ВсяКонтактнаяИнформация
		|
		|СГРУППИРОВАТЬ ПО
		|	ВсяКонтактнаяИнформация.Объект,
		|	ВсяКонтактнаяИнформация.Вид
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВсяКонтактнаяИнформация.Объект,
		|	ВсяКонтактнаяИнформация.Тип,
		|	ВсяКонтактнаяИнформация.Вид,
		|	ВсяКонтактнаяИнформация.Представление,
		|	ВсяКонтактнаяИнформация.ЗначенияПолей
		|ПОМЕСТИТЬ ВТКонтактнаяИнформация
		|ИЗ
		|	ВТВсяКонтактнаяИнформация КАК ВсяКонтактнаяИнформация
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПервыеЗначенияПоВидуКонтактнойИнформации КАК ПервыеЗначенияПоВидуКонтактнойИнформации
		|		ПО ВсяКонтактнаяИнформация.Объект = ПервыеЗначенияПоВидуКонтактнойИнформации.Объект
		|			И ВсяКонтактнаяИнформация.Вид = ПервыеЗначенияПоВидуКонтактнойИнформации.Вид
		|			И ВсяКонтактнаяИнформация.НомерСтроки = ПервыеЗначенияПоВидуКонтактнойИнформации.НомерСтроки";
		
	ТекстОтбораКонтактнойИнформации = "";
	Если НЕ ПустаяСтрока(ОписаниеВременнойТаблицыОтборов.ИмяРеквизитаВид) Тогда
		ТекстОтбораКонтактнойИнформации =  "
			|			И КонтактнаяИнформация.Вид = ОтборКонтактнойИнформации.РеквизитВид";
	КонецЕсли;
		
	Если НЕ ПустаяСтрока(ОписаниеВременнойТаблицыОтборов.ИмяРеквизитаТип) Тогда
		ТекстОтбораКонтактнойИнформации = ТекстОтбораКонтактнойИнформации + "
			|			И КонтактнаяИнформация.Тип = ОтборКонтактнойИнформации.РеквизитТип";
	КонецЕсли;
		
	// Замена ключевых выражений
	Если НЕ ТолькоРазрешенные Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "РАЗРЕШЕННЫЕ", "");
	КонецЕсли; 
	
	ИмяВТВсяКонтактнаяИнформация = ЗарплатаКадрыОбщиеНаборыДанных.УникальноеИмяТекстаЗапроса("ВТВсяКонтактнаяИнформация");
	ИмяВТПервыеЗначенияПоВидуКонтактнойИнформации = ЗарплатаКадрыОбщиеНаборыДанных.УникальноеИмяТекстаЗапроса("ВТПервыеЗначенияПоВидуКонтактнойИнформации");
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТКонтактнаяИнформация", ИмяВТКонтактнаяИнформация);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТОтборКонтактнойИнформации", ОписаниеВременнойТаблицыОтборов.ИмяВТОтборОбъектов);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТВсяКонтактнаяИнформация", ИмяВТВсяКонтактнаяИнформация);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТПервыеЗначенияПоВидуКонтактнойИнформации", ИмяВТПервыеЗначенияПоВидуКонтактнойИнформации);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&КонтактнаяИнформация", ПолноеИмяОбъектаМетаданных + ".КонтактнаяИнформация");
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "РеквизитОбъекта", ОписаниеВременнойТаблицыОтборов.ИмяРеквизитаОбъект);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "И &ТекстОтбораКонтактнойИнформации", ТекстОтбораКонтактнойИнформации);
	
	Если НЕ ПустаяСтрока(ОписаниеВременнойТаблицыОтборов.ИмяРеквизитаВид) Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "РеквизитВид", ОписаниеВременнойТаблицыОтборов.ИмяРеквизитаВид);
	КонецЕсли; 
	
	Если НЕ ПустаяСтрока(ОписаниеВременнойТаблицыОтборов.ИмяРеквизитаТип) Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "РеквизитТип", ОписаниеВременнойТаблицыОтборов.ИмяРеквизитаТип);
	КонецЕсли;
	
	Возврат Запрос;
	
КонецФункции

#КонецОбласти

#Область ПроцедурыИФункцииОбновленияКонтактнойИнформацииФизическихЛиц

// Процедура обновляет значения реквизитов для видов контактной информации 
// используемой в подсистеме Зарплата и Кадры.
//
Процедура ОбновитьВидыКонтактнойИнформацииФизическогоЛица() Экспорт
	
	ОбновитьВидыКонтактнойИнформацииПоПорядку(1);
	ОбновитьПорядкиКонтактнойИнформации();
	
КонецПроцедуры

Процедура ОбновитьПорядкиКонтактнойИнформации() Экспорт
		
	Запрос = Новый Запрос;
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВидыКонтактнойИнформации.Ссылка) КАК Количество,
		|	ВидыКонтактнойИнформации.РеквизитДопУпорядочивания
		|ПОМЕСТИТЬ ВТКоличествоВидовОдногоПорядка
		|ИЗ
		|	Справочник.ВидыКонтактнойИнформации КАК ВидыКонтактнойИнформации
		|
		|СГРУППИРОВАТЬ ПО
		|	ВидыКонтактнойИнформации.РеквизитДопУпорядочивания
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МИНИМУМ(ВТКоличествоВидовОдногоПорядка.РеквизитДопУпорядочивания) КАК РеквизитДопУпорядочивания
		|ПОМЕСТИТЬ ВТМинимальныНарушенныйПорядок
		|ИЗ
		|	ВТКоличествоВидовОдногоПорядка КАК ВТКоличествоВидовОдногоПорядка
		|ГДЕ
		|	ВТКоличествоВидовОдногоПорядка.Количество > 1
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВидыКонтактнойИнформации.Ссылка,
		|	ВТМинимальныНарушенныйПорядок.РеквизитДопУпорядочивания
		|ИЗ
		|	Справочник.ВидыКонтактнойИнформации КАК ВидыКонтактнойИнформации
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТМинимальныНарушенныйПорядок КАК ВТМинимальныНарушенныйПорядок
		|		ПО ВидыКонтактнойИнформации.РеквизитДопУпорядочивания >= ВТМинимальныНарушенныйПорядок.РеквизитДопУпорядочивания
		|
		|УПОРЯДОЧИТЬ ПО
		|	ВидыКонтактнойИнформации.РеквизитДопУпорядочивания,
		|	ВидыКонтактнойИнформации.Наименование";
		
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		Счетчик = 0;
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			СправочникОбъект = Выборка.Ссылка.ПолучитьОбъект();
			СправочникОбъект.РеквизитДопУпорядочивания = Выборка.РеквизитДопУпорядочивания + Счетчик;
			СправочникОбъект.ОбменДанными.Загрузка = Истина;
			СправочникОбъект.Записать();
			
			Счетчик = Счетчик + 1;
			
		КонецЦикла;
		
	КонецЕсли; 

КонецПроцедуры

Процедура ОбновитьВидыКонтактнойИнформацииПоПорядку(Порядок = Неопределено) Экспорт
	
	УправлениеКонтактнойИнформацией.ОбновитьВидКонтактнойИнформации(
		Справочники.ВидыКонтактнойИнформации.ТелефонДомашнийФизическиеЛица,
		Перечисления.ТипыКонтактнойИнформации.Телефон,
		"",
		Истина,
		Ложь,
		Ложь,
		?(Порядок = Неопределено, Неопределено, Порядок + 1));
		
	УправлениеКонтактнойИнформацией.ОбновитьВидКонтактнойИнформации(
		Справочники.ВидыКонтактнойИнформации.ТелефонРабочийФизическиеЛица,
		Перечисления.ТипыКонтактнойИнформации.Телефон,
		"",
		Истина,
		Ложь,
		Ложь,
		?(Порядок = Неопределено, Неопределено, Порядок + 2),
		Ложь);
		
	НастройкиПроверки = Новый Структура;
	НастройкиПроверки.Вставить("АдресТолькоРоссийский", Истина);
	НастройкиПроверки.Вставить("ПроверятьКорректность", Ложь);
	НастройкиПроверки.Вставить("ЗапрещатьВводНекорректного", Ложь);
	НастройкиПроверки.Вставить("СкрыватьНеактуальныеАдреса", Ложь);
	НастройкиПроверки.Вставить("ВключатьСтрануВПредставление", Истина);
	
	УправлениеКонтактнойИнформацией.ОбновитьВидКонтактнойИнформации(
		Справочники.ВидыКонтактнойИнформации.АдресПоПропискеФизическиеЛица,
		Перечисления.ТипыКонтактнойИнформации.Адрес,
		"",
		Ложь,
		Истина,
		Ложь,
		?(Порядок = Неопределено, Неопределено, Порядок + 3),
		,
		НастройкиПроверки);
		
	УправлениеКонтактнойИнформацией.ОбновитьВидКонтактнойИнформации(
		Справочники.ВидыКонтактнойИнформации.АдресМестаПроживанияФизическиеЛица,
		Перечисления.ТипыКонтактнойИнформации.Адрес,
		"",
		Ложь,
		Истина,
		Ложь,
		?(Порядок = Неопределено, Неопределено, Порядок + 4),
		,
		НастройкиПроверки);
		
	НастройкиПроверки.Вставить("АдресТолькоРоссийский", Ложь);
		
	УправлениеКонтактнойИнформацией.ОбновитьВидКонтактнойИнформации(
		Справочники.ВидыКонтактнойИнформации.АдресДляИнформированияФизическиеЛица,
		Перечисления.ТипыКонтактнойИнформации.Адрес,
		"",
		Ложь,
		Истина,
		Ложь,
		?(Порядок = Неопределено, Неопределено, Порядок + 6),
		,
		НастройкиПроверки);
		
	НастройкиПроверки.Вставить("ВключатьСтрануВПредставление", Истина);
	
	УправлениеКонтактнойИнформацией.ОбновитьВидКонтактнойИнформации(
		Справочники.ВидыКонтактнойИнформации.АдресЗаПределамиРФФизическиеЛица,
		Перечисления.ТипыКонтактнойИнформации.Адрес,
		"",
		Истина,
		Истина,
		Ложь,
		?(Порядок = Неопределено, Неопределено, Порядок + 5),
		,
		НастройкиПроверки);
		
КонецПроцедуры

#КонецОбласти

#Область ПолучениеРабочихМестСотрудниковОрганизации

Функция ПараметрыДляЗапросВТРабочиеМестаСотрудников() Экспорт
		
	ПараметрыПолученияРабочихМестСотрудников = Новый Структура;
	ПараметрыПолученияРабочихМестСотрудников.Вставить("Организация");
	ПараметрыПолученияРабочихМестСотрудников.Вставить("Подразделение");
	ПараметрыПолученияРабочихМестСотрудников.Вставить("ОтбиратьПоГоловнойОрганизации", Ложь);
	
	Возврат ПараметрыПолученияРабочихМестСотрудников;
	
КонецФункции

Функция ЗапросВТРабочиеМестаСотрудниковПоВременнойТаблице(ТолькоРазрешенные, ИмяВТРабочиеМестаСотрудников, Параметры) Экспорт
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьКадровыйУчет") Тогда
		
		// Инициализация параметров получения таблицы регистра.
		ОписаниеФильтра = ЗарплатаКадрыОбщиеНаборыДанных.ОписаниеФильтраДляСоздатьВТИмяРегистра(
			Параметры.ИмяВТСотрудникиПериоды,
			"Сотрудник"
		);
			
		// Описание переименований полей таблицы отборов.
		ОписаниеФильтра.СоответствиеИзмеренийРегистраИзмерениямФильтра.Вставить("Сотрудник", Параметры.ИмяПоляСотрудник);
		ОписаниеФильтра.СоответствиеИзмеренийРегистраИзмерениямФильтра.Вставить("ДатаНачала", Параметры.ИмяПоляНачалоПериода);
		ОписаниеФильтра.СоответствиеИзмеренийРегистраИзмерениямФильтра.Вставить("ДатаОкончания", Параметры.ИмяПоляОкончаниеПериода);
		
		ПараметрыПостроения = ЗарплатаКадрыОбщиеНаборыДанных.ПараметрыПостроенияДляСоздатьВТИмяРегистра();
		
		// Установка отбора по организации.
		Если ЗначениеЗаполнено(Параметры.Организация) Тогда
			ЗарплатаКадрыОбщиеНаборыДанных.ДобавитьВКоллекциюОтбор(ПараметрыПостроения.Отборы, "ГоловнаяОрганизация", "=", ЗарплатаКадрыПовтИсп.ГоловнаяОрганизация(Параметры.Организация));
			Если НЕ Параметры.ОтбиратьПоГоловнойОрганизации Тогда
				ЗарплатаКадрыОбщиеНаборыДанных.ДобавитьВКоллекциюОтбор(ПараметрыПостроения.Отборы, "Организация", "=", Параметры.Организация);
			КонецЕсли;
		КонецЕсли; 
		
		// Установка отбора по подразделению.
		Если ЗначениеЗаполнено(Параметры.Подразделение) Тогда
			ЗарплатаКадрыОбщиеНаборыДанных.ДобавитьВКоллекциюОтбор(ПараметрыПостроения.Отборы, "Подразделение", " В ИЕРАРХИИ ", Параметры.Подразделение);
		КонецЕсли; 
		
		// Дополнение таблицы регистра значениями на начало периода.
		ПараметрыПостроения.ВключатьЗаписиНаНачалоПериода = Истина;
		
		// Исключение из записей на начало периода ранее уволенных.
		ЗарплатаКадрыОбщиеНаборыДанных.ДобавитьВКоллекциюОтбор(ПараметрыПостроения.ОтборыЗаписейНаНачалоПериода, "ВидСобытия", " <> ", Перечисления.ВидыКадровыхСобытий.Увольнение);
		
		ЗарплатаКадрыОбщиеНаборыДанных.ДобавитьВКоллекциюОтбор(
			ПараметрыПостроения.Отборы,
			"ВЫРАЗИТЬ(РегистрСведений.Сотрудник КАК Справочник.Сотрудники).ГоловнойСотрудник",
			"=",
			"РегистрСведений.Сотрудник", Ложь
		);
		
		ИмяВТКадроваяИсторияСотрудников = ЗарплатаКадрыОбщиеНаборыДанных.УникальноеИмяТекстаЗапроса("ВТКадроваяИсторияСотрудников");
		
		// Получение готового запроса
		Запрос = ЗарплатаКадрыОбщиеНаборыДанных.ЗапросВТИмяРегистра(
			"КадроваяИсторияСотрудников",
			ТолькоРазрешенные,
			ОписаниеФильтра,
			ПараметрыПостроения,
			ИмяВТКадроваяИсторияСотрудников
		);
			
		ПоляВТ = КадровыйУчет.ПоляВТРабочиеМестаСотрудников();
		
		ТекстЗапроса = "";
		ДобавитьЗапятую = Ложь;
		Для каждого ОписаниеПоля Из ПоляВТ Цикл
			
			Если ДобавитьЗапятую Тогда
				ТекстЗапроса = ТекстЗапроса + ",";
			Иначе
				ДобавитьЗапятую = Истина;
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(ОписаниеПоля.Значение) Тогда
				ПутьКДанным = "КадроваяИсторияСотрудников." + ОписаниеПоля.Ключ
			Иначе
				ПутьКДанным = ОписаниеПоля.Значение;
			КонецЕсли;
			
			ТекстЗапроса = ТекстЗапроса + Символы.ПС + ПутьКДанным + " КАК " + ОписаниеПоля.Ключ;
				
		КонецЦикла;
			
		ТекстЗапроса =
			"ВЫБРАТЬ
			|	КадроваяИсторияСотрудников.ПериодЗаписи,
			|" + ТекстЗапроса + "
			|ПОМЕСТИТЬ ВТРабочиеМестаСотрудников
			|ИЗ
			|	ВТКадроваяИсторияСотрудников КАК КадроваяИсторияСотрудников
			|{ГДЕ
			|	КадроваяИсторияСотрудников.Сотрудник.*,
			|	КадроваяИсторияСотрудников.ГоловнаяОрганизация.*,
			|	КадроваяИсторияСотрудников.Организация.*,
			|	КадроваяИсторияСотрудников.Подразделение.*,
			|	КадроваяИсторияСотрудников.Сотрудник.ГоловнойСотрудник.* КАК ГоловнойСотрудник,
			|	КадроваяИсторияСотрудников.ФизическоеЛицо.*}";

		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ВТКадроваяИсторияСотрудников", ИмяВТКадроваяИсторияСотрудников);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ВТРабочиеМестаСотрудников", ИмяВТРабочиеМестаСотрудников);
		
		ЗарплатаКадрыОбщиеНаборыДанных.ОбъединитьТекстыЗапросов(Запрос.Текст, ТекстЗапроса);
		
	Иначе
		
		Запрос = Новый Запрос;
	
		УсловияОтбора = "";
		
		Если ЗначениеЗаполнено(Параметры.Организация) Тогда
			
			Если Параметры.ОтбиратьПоГоловнойОрганизации Тогда
				УсловияОтбора = "ТекущиеКадровыеДанныеСотрудников.ГоловнаяОрганизация = &ТекущиеКадровыеДанныеСотрудниковОрганизация";
				ТекущиеКадровыеДанныеСотрудниковОрганизация = ЗарплатаКадрыПовтИсп.ГоловнаяОрганизация(Параметры.Организация);
			Иначе
				УсловияОтбора = "ТекущиеКадровыеДанныеСотрудников.ТекущаяОрганизация = &ТекущиеКадровыеДанныеСотрудниковОрганизация";
				ТекущиеКадровыеДанныеСотрудниковОрганизация = Параметры.Организация;
			КонецЕсли;
			
			Запрос.УстановитьПараметр("ТекущиеКадровыеДанныеСотрудниковОрганизация", ТекущиеКадровыеДанныеСотрудниковОрганизация);
			
		КонецЕсли; 
		
		Если ЗначениеЗаполнено(Параметры.Подразделение) Тогда
			УсловияОтбора = ?(ПустаяСтрока(УсловияОтбора), "", УсловияОтбора + Символы.ПС + "	И ") + "ТекущиеКадровыеДанныеСотрудников.ТекущееПодразделение В ИЕРАРХИИ (&ТекущиеКадровыеДанныеСотрудниковПодразделение)";
			Запрос.УстановитьПараметр("ТекущиеКадровыеДанныеСотрудниковПодразделение", Параметры.Подразделение);
		КонецЕсли; 
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ВЫБОР
			|		КОГДА ТекущиеКадровыеДанныеСотрудников.ДатаПриема > СотрудникиПериоды.НачалоПериода
			|			ТОГДА ТекущиеКадровыеДанныеСотрудников.ДатаПриема
			|		ИНАЧЕ СотрудникиПериоды.НачалоПериода
			|	КОНЕЦ КАК Период,
			|	ТекущиеКадровыеДанныеСотрудников.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
			|	ТекущиеКадровыеДанныеСотрудников.ТекущаяОрганизация КАК Организация,
			|	ТекущиеКадровыеДанныеСотрудников.Сотрудник КАК Сотрудник,
			|	ТекущиеКадровыеДанныеСотрудников.ФизическоеЛицо КАК ФизическоеЛицо,
			|	ТекущиеКадровыеДанныеСотрудников.ТекущееПодразделение КАК Подразделение,
			|	ТекущиеКадровыеДанныеСотрудников.ТекущаяДолжность КАК Должность,
			|	1 КАК КоличествоСтавок,
			|	ТекущиеКадровыеДанныеСотрудников.ТекущийВидЗанятости КАК ВидЗанятости,
			|	ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Прием) КАК ВидСобытия
			|ПОМЕСТИТЬ ВТРабочиеМестаСотрудников
			|ИЗ
			|	ВТСотрудникиПериоды КАК СотрудникиПериоды
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ТекущиеКадровыеДанныеСотрудников КАК ТекущиеКадровыеДанныеСотрудников
			|		ПО СотрудникиПериоды.Сотрудник = ТекущиеКадровыеДанныеСотрудников.Сотрудник
			|			И СотрудникиПериоды.ОкончаниеПериода >= ТекущиеКадровыеДанныеСотрудников.ДатаПриема
			|			И (ТекущиеКадровыеДанныеСотрудников.ДатаПриема <> ДАТАВРЕМЯ(1, 1, 1))
			|			И (ТекущиеКадровыеДанныеСотрудников.ДатаУвольнения = ДАТАВРЕМЯ(1, 1, 1)
			|				ИЛИ СотрудникиПериоды.НачалоПериода <= ТекущиеКадровыеДанныеСотрудников.ДатаУвольнения
			|					И ТекущиеКадровыеДанныеСотрудников.ДатаУвольнения > ТекущиеКадровыеДанныеСотрудников.ДатаПриема)
			|			И (ТекущиеКадровыеДанныеСотрудников.ТекущаяОрганизация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка))
			|			И (ТекущиеКадровыеДанныеСотрудников.ТекущийВидЗанятости <> ЗНАЧЕНИЕ(Перечисление.ВидыЗанятости.ПустаяСсылка))
			|ГДЕ
			|	&УсловияОтбора
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ДОБАВИТЬКДАТЕ(ТекущиеКадровыеДанныеСотрудников.ДатаУвольнения, ДЕНЬ, 1),
			|	ТекущиеКадровыеДанныеСотрудников.ГоловнаяОрганизация,
			|	ТекущиеКадровыеДанныеСотрудников.ТекущаяОрганизация,
			|	ТекущиеКадровыеДанныеСотрудников.Сотрудник,
			|	ТекущиеКадровыеДанныеСотрудников.ФизическоеЛицо,
			|	ТекущиеКадровыеДанныеСотрудников.ТекущееПодразделение,
			|	ТекущиеКадровыеДанныеСотрудников.ТекущаяДолжность,
			|	1,
			|	ТекущиеКадровыеДанныеСотрудников.ТекущийВидЗанятости,
			|	ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Увольнение)
			|ИЗ
			|	ВТСотрудникиПериоды КАК СотрудникиПериоды
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ТекущиеКадровыеДанныеСотрудников КАК ТекущиеКадровыеДанныеСотрудников
			|		ПО СотрудникиПериоды.Сотрудник = ТекущиеКадровыеДанныеСотрудников.Сотрудник
			|			И (ТекущиеКадровыеДанныеСотрудников.ДатаУвольнения > ТекущиеКадровыеДанныеСотрудников.ДатаПриема)
			|			И СотрудникиПериоды.НачалоПериода <= ТекущиеКадровыеДанныеСотрудников.ДатаУвольнения
			|			И СотрудникиПериоды.ОкончаниеПериода >= ТекущиеКадровыеДанныеСотрудников.ДатаУвольнения
			|			И (ТекущиеКадровыеДанныеСотрудников.ТекущаяОрганизация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка))
			|			И (ТекущиеКадровыеДанныеСотрудников.ТекущийВидЗанятости <> ЗНАЧЕНИЕ(Перечисление.ВидыЗанятости.ПустаяСсылка))
			|ГДЕ
			|	&УсловияОтбора";
			
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТРабочиеМестаСотрудников", ИмяВТРабочиеМестаСотрудников);
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТСотрудникиПериоды",  Параметры.ИмяВТСотрудникиПериоды);
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "СотрудникиПериоды.Сотрудник", "СотрудникиПериоды." + Параметры.ИмяПоляСотрудник);
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "СотрудникиПериоды.НачалоПериода", "СотрудникиПериоды." + Параметры.ИмяПоляНачалоПериода);
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "СотрудникиПериоды.ОкончаниеПериода", "СотрудникиПериоды." + Параметры.ИмяПоляОкончаниеПериода);
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловияОтбора", ?(ПустаяСтрока(УсловияОтбора), "(ИСТИНА)", УсловияОтбора));
		
	КонецЕсли;
		
	Возврат Запрос;
	
КонецФункции

Функция ПоляВТРабочиеМестаСотрудников() Экспорт
	
	ПоляВТРабочиеМеста = Новый Структура;
	ПоляВТРабочиеМеста.Вставить("Период");
	ПоляВТРабочиеМеста.Вставить("ГоловнаяОрганизация");
	ПоляВТРабочиеМеста.Вставить("Организация");
	ПоляВТРабочиеМеста.Вставить("Сотрудник");
	ПоляВТРабочиеМеста.Вставить("ФизическоеЛицо");
	ПоляВТРабочиеМеста.Вставить("Подразделение");
	ПоляВТРабочиеМеста.Вставить("Должность");
	ПоляВТРабочиеМеста.Вставить("КоличествоСтавок", "1");
	ПоляВТРабочиеМеста.Вставить("ВидЗанятости");
	ПоляВТРабочиеМеста.Вставить("ВидСобытия");
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьКадровыйУчет") Тогда
		ПоляВТРабочиеМеста.Вставить("ДокументОснование", "КадроваяИсторияСотрудников.Регистратор");
	КонецЕсли; 
	
	Возврат ПоляВТРабочиеМеста;
	
КонецФункции

#КонецОбласти

#Область ПроцедурыИФункцииФормированияОтчетов

Функция ДополнительныеСведенияУнифицированнойФормыТ2(СтрокиДанных, ДатаОтчета) Экспорт
	
	ДополнительныеСведения = Новый Соответствие;
	ДанныеЗаполненияКадровойИстории = Новый Соответствие;
	
	НастройкиПечатныхФорм = ЗарплатаКадрыПовтИсп.НастройкиПечатныхФорм();
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьКадровыйУчет") Тогда
			
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		
		ТаблицаСотрудников = Новый ТаблицаЗначений;
		ТаблицаСотрудников.Колонки.Добавить("Сотрудник", Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
		ТаблицаСотрудников.Колонки.Добавить("ДатаНачала", Новый ОписаниеТипов("Дата"));
		ТаблицаСотрудников.Колонки.Добавить("ДатаОкончания", Новый ОписаниеТипов("Дата"));
		
		Для каждого СтрокаСотрудника Из СтрокиДанных Цикл
			НоваяСтрокаТаблицыСотрудников = ТаблицаСотрудников.Добавить();
			НоваяСтрокаТаблицыСотрудников.Сотрудник = СтрокаСотрудника.РабочееМестоСотрудник;
			НоваяСтрокаТаблицыСотрудников.ДатаОкончания = СтрокаСотрудника.ПараметрыДанныхПериод;
		КонецЦикла;
		
		ОписаниеФильтра = ЗарплатаКадрыОбщиеНаборыДанных.ОписаниеФильтраДляСоздатьВТИмяРегистра(ТаблицаСотрудников);
		
		ПараметрыПостроения = ЗарплатаКадрыОбщиеНаборыДанных.ПараметрыПостроенияДляСоздатьВТИмяРегистра();
		ЗарплатаКадрыОбщиеНаборыДанных.ДобавитьВКоллекциюОтбор(ПараметрыПостроения.Отборы, "ВидСобытия", "<>", "ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Увольнение)");
	
		ЗарплатаКадрыОбщиеНаборыДанных.СоздатьВТИмяРегистра(
			"КадроваяИсторияСотрудников",
			Запрос.МенеджерВременныхТаблиц,
			Истина,
			ОписаниеФильтра,
			ПараметрыПостроения);
		
		ОписанияНачислений = КадровыйУчет.ОписанияСоставаНачисленийПоВременнойТаблице(Запрос.МенеджерВременныхТаблиц, "ВТКадроваяИсторияСотрудников");
	
		Запрос.Текст =
			"ВЫБРАТЬ
			|	КадроваяИсторияСотрудников.Период КАК Период,
			|	КадроваяИсторияСотрудников.Сотрудник КАК Сотрудник,
			|	КадроваяИсторияСотрудников.Подразделение КАК Подразделение,
			|	КадроваяИсторияСотрудников.Должность КАК Должность,
			|	КадроваяИсторияСотрудников.Регистратор.Номер КАК РегистраторНомер,
			|	КадроваяИсторияСотрудников.Регистратор.Дата КАК РегистраторДата
			|ИЗ
			|	ВТКадроваяИсторияСотрудников КАК КадроваяИсторияСотрудников
			|
			|УПОРЯДОЧИТЬ ПО
			|	Сотрудник,
			|	Период";
		
		ВыборкаКадроваяИстория = Запрос.Выполнить().Выбрать();
		Пока ВыборкаКадроваяИстория.СледующийПоЗначениюПоля("Сотрудник") Цикл
			
			ДанныеЗаполненияКадровойИсторииПоСотруднику = Новый Массив;
			ОписанияСотрудника = ОписанияНачислений.Получить(ВыборкаКадроваяИстория.Сотрудник);
			
			Пока ВыборкаКадроваяИстория.Следующий() Цикл
				
				СтруктураСтроки = Новый Структура();
				СтруктураСтроки.Вставить("ДатаПеревода", ВыборкаКадроваяИстория.Период);
				Если НастройкиПечатныхФорм.ВыводитьПолнуюИерархиюПодразделений И ЗначениеЗаполнено(ВыборкаКадроваяИстория.Подразделение) Тогда
					СтруктураСтроки.Вставить("ПодразделениеПеревода", ВыборкаКадроваяИстория.Подразделение.ПолноеНаименование());
				Иначе
					СтруктураСтроки.Вставить("ПодразделениеПеревода", ВыборкаКадроваяИстория.Подразделение);
				КонецЕсли; 
				СтруктураСтроки.Вставить("ДолжностьПеревода", ВыборкаКадроваяИстория.Должность);
				
				Если ОписанияСотрудника <> Неопределено Тогда
					ОписанияПериода = ОписанияСотрудника.Получить(ВыборкаКадроваяИстория.Период);
				КонецЕсли; 
				
				Если ОписанияПериода = Неопределено Тогда
					ОписанияПериода = ОписаниеСоставаНачислений();
				КонецЕсли; 
				
				СтруктураСтроки.Вставить("ТарифнаяСтавка", ПредставлениеОкладаИНадбавок(ОписанияПериода));
				
				Если НастройкиПечатныхФорм.УдалятьПрефиксыОрганизацииИИБИзНомеровКадровыхПриказов Тогда
					НомерПриказа = ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(ВыборкаКадроваяИстория.РегистраторНомер, Истина, Истина);
				Иначе
					НомерПриказа = ВыборкаКадроваяИстория.РегистраторНомер;
				КонецЕсли;
			
				СтруктураСтроки.Вставить("ОснованиеПеревода", НСтр("ru='Пр'") + ". № "
						+ НомерПриказа + " " + НСтр("ru='от'") + " " + Формат(ВыборкаКадроваяИстория.РегистраторДата, "ДЛФ=D"));
				
				ДанныеЗаполненияКадровойИсторииПоСотруднику.Добавить(СтруктураСтроки);
				
			КонецЦикла;
			
			ДанныеЗаполненияКадровойИстории.Вставить(ВыборкаКадроваяИстория.Сотрудник, ДанныеЗаполненияКадровойИсторииПоСотруднику);
			
		КонецЦикла;
		
	КонецЕсли;
	
	ДополнительныеСведения.Вставить("ДанныеЗаполненияКадровойИстории", ДанныеЗаполненияКадровойИстории);
	
	// Отпуска
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Сотрудники", СтрокиДанных.ВыгрузитьКолонку("РабочееМестоСотрудник"));
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Отпуск.Сотрудник КАК Сотрудник,
		|	Отпуск.Номер КАК НомерПриказа,
		|	Отпуск.Дата КАК ДатаПриказа,
		|	Отпуск.ДатаНачалаОсновногоОтпуска КАК ДатаС,
		|	Отпуск.ДатаОкончанияОсновногоОтпуска КАК ДатаПо,
		|	Отпуск.КоличествоДнейОсновногоОтпуска КАК ДнейОтпуска,
		|	Отпуск.НачалоПериодаЗаКоторыйПредоставляетсяОтпуск КАК РабочийГодС,
		|	Отпуск.КонецПериодаЗаКоторыйПредоставляетсяОтпуск КАК РабочийГодПо,
		|	Отпуск.Основание
		|ИЗ
		|	Документ.Отпуск КАК Отпуск
		|ГДЕ
		|	Отпуск.Проведен
		|	И Отпуск.Сотрудник В(&Сотрудники)
		|
		|УПОРЯДОЧИТЬ ПО
		|	Сотрудник,
		|	ДатаС";
		
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		ДанныеЗаполнения = Новый Соответствие;
		ВыборкаОтпуска = РезультатЗапроса.Выбрать();
		Пока ВыборкаОтпуска.СледующийПоЗначениюПоля("Сотрудник") Цикл
			
			ДанныеЗаполненияПоСотруднику = Новый Массив;
			Пока ВыборкаОтпуска.Следующий() Цикл
				
				СтруктураСтроки = Новый Структура();
				СтруктураСтроки.Вставить("ВидОтпуска", НСтр("ru='Основной'"));
				СтруктураСтроки.Вставить("ДатаС", ВыборкаОтпуска.ДатаС);
				СтруктураСтроки.Вставить("ДатаПо", ВыборкаОтпуска.ДатаПо);
				СтруктураСтроки.Вставить("ДнейОтпуска", ВыборкаОтпуска.ДнейОтпуска);
				СтруктураСтроки.Вставить("РабочийГодС", ВыборкаОтпуска.РабочийГодС);
				СтруктураСтроки.Вставить("РабочийГодПо", ВыборкаОтпуска.РабочийГодПо);
				СтруктураСтроки.Вставить("ОснованиеОтпуска", "Пр.№ "+ СокрЛП(ВыборкаОтпуска.НомерПриказа) +" от "
						+ Формат(ВыборкаОтпуска.ДатаПриказа, "ДЛФ=D") + " " + ВыборкаОтпуска.Основание);
						
				ДанныеЗаполненияПоСотруднику.Добавить(СтруктураСтроки);
				
			КонецЦикла;
			
			ДанныеЗаполнения.Вставить(ВыборкаОтпуска.Сотрудник, ДанныеЗаполненияПоСотруднику);
			
		КонецЦикла;
			
		ДополнительныеСведения.Вставить("ДанныеЗаполненияОтпуска", ДанныеЗаполнения);

	КонецЕсли; 
	
	Возврат ДополнительныеСведения;
	
КонецФункции

Функция СформироватьЗапросДляТ1(МассивОбъектов) Экспорт
	
	ПечатьПоЭлементуСправочника = Ложь;
	Если МассивОбъектов.Количество() > 0 Тогда
		ПечатьПоЭлементуСправочника = ТипЗнч(МассивОбъектов[0]) = Тип("СправочникСсылка.Сотрудники");
	КонецЕсли; 
	
	Если ПечатьПоЭлементуСправочника И
		НЕ ПолучитьФункциональнуюОпцию("ИспользоватьКадровыйУчет") Тогда
		
		Возврат СформироватьПоЭлементамСправочникаЗапросДляТ1Т8(МассивОбъектов, "ДатаПриема");
		
	КонецЕсли; 
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	КадровыйУчет.СоздатьВТДанныеДокументовПриемНаРаботу(МенеджерВременныхТаблиц, МассивОбъектов);
	
	Возврат СформироватьПоДокументамЗапросДляТ1Т8(МенеджерВременныхТаблиц, МассивОбъектов, Истина);
	
КонецФункции

Функция СформироватьЗапросДляТ5(МассивОбъектов) Экспорт
	
	ПечатьПоЭлементуСправочника = Ложь;
	Если МассивОбъектов.Количество() > 0 Тогда
		ПечатьПоЭлементуСправочника = ТипЗнч(МассивОбъектов[0]) = Тип("СправочникСсылка.Сотрудники");
	КонецЕсли; 
	
	Если ПечатьПоЭлементуСправочника И
		НЕ ПолучитьФункциональнуюОпцию("ИспользоватьКадровыйУчет") Тогда
		
		Возврат СформироватьПоЭлементамСправочникаЗапросДляТ5(МассивОбъектов);
		
	КонецЕсли; 
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	КадровыйУчет.СоздатьВТДанныеДокументовКадровыхПеремещений(МенеджерВременныхТаблиц, МассивОбъектов);
	
	Возврат СформироватьПоДокументамЗапросДляТ5(МенеджерВременныхТаблиц, МассивОбъектов);
	
КонецФункции

Функция СформироватьЗапросДляТ6(МассивОбъектов) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Параметры.Вставить("МассивОбъектов", МассивОбъектов);
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Отпуск.Сотрудник,
		|	Отпуск.ДатаНачалаСобытия КАК Период
		|ПОМЕСТИТЬ ВТСотрудникиПериоды
		|ИЗ
		|	Документ.Отпуск КАК Отпуск
		|ГДЕ
		|	Отпуск.Ссылка В(&МассивОбъектов)
		|
		|СГРУППИРОВАТЬ ПО
		|	Отпуск.Сотрудник,
		|	Отпуск.ДатаНачалаСобытия";
	
	Запрос.Выполнить();
	
	ОписательВременныхТаблиц = КадровыйУчет.ОписательВременныхТаблицДляСоздатьВТКадровыеДанныеСотрудников(
		Запрос.МенеджерВременныхТаблиц,
		"ВТСотрудникиПериоды");
	КадровыйУчет.СоздатьВТКадровыеДанныеСотрудников(ОписательВременныхТаблиц, Истина, "ФизическоеЛицо,ФИОПолные,ТабельныйНомер,Организация,Подразделение,Должность");
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КадровыеДанныеСотрудников.Период,
	|	КадровыеДанныеСотрудников.Сотрудник,
	|	КадровыеДанныеСотрудников.ФизическоеЛицо,
	|	ВЫРАЗИТЬ(КадровыеДанныеСотрудников.Организация КАК Справочник.Организации) КАК Организация,
	|	КадровыеДанныеСотрудников.ФИОПолные КАК Работник,
	|	КадровыеДанныеСотрудников.ТабельныйНомер КАК ТабельныйНомер,
	|	ВЫРАЗИТЬ(КадровыеДанныеСотрудников.Подразделение КАК Справочник.ПодразделенияОрганизаций) КАК Подразделение,
	|	ВЫРАЗИТЬ(КадровыеДанныеСотрудников.Должность КАК Справочник.Должности) КАК Должность
	|ПОМЕСТИТЬ ВТДанныеДляПечати
	|ИЗ
	|	ВТКадровыеДанныеСотрудников КАК КадровыеДанныеСотрудников
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТДанныеДляПечати.Сотрудник,
	|	Отпуск.Организация КАК Организация,
	|	ВТДанныеДляПечати.Организация.КодПоОКПО КАК КодПоОКПО,
	|	ВТДанныеДляПечати.Работник,
	|	Отпуск.Номер КАК НомерДок,
	|	Отпуск.Дата КАК ДатаДок,
	|	ВТДанныеДляПечати.ТабельныйНомер,
	|	ЕСТЬNULL(ВТДанныеДляПечати.Подразделение, ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)) КАК Подразделение,
	|	ЕСТЬNULL(ВТДанныеДляПечати.Должность.Наименование, """") КАК Должность,
	|	Отпуск.НачалоПериодаЗаКоторыйПредоставляетсяОтпуск,
	|	Отпуск.КонецПериодаЗаКоторыйПредоставляетсяОтпуск,
	|	Отпуск.ДатаНачалаОсновногоОтпуска КАК ДатаНачала,
	|	Отпуск.ДатаОкончанияОсновногоОтпуска КАК ДатаОкончания,
	|	Отпуск.КоличествоДнейОсновногоОтпуска КАК Продолжительность,
	|	ИСТИНА КАК ЭтоОсновнойОтпуск,
	|	Отпуск.Ссылка КАК Ссылка,
	|	Отпуск.ДатаНачалаСобытия,
	|	Отпуск.Дата КАК Дата,
	|	Отпуск.Руководитель КАК Руководитель,
	|	Отпуск.ДолжностьРуководителя КАК ДолжностьРуководителя
	|ПОМЕСТИТЬ ВТДанныеДокументов
	|ИЗ
	|	ВТДанныеДляПечати КАК ВТДанныеДляПечати
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.Отпуск КАК Отпуск
	|		ПО ВТДанныеДляПечати.Сотрудник = Отпуск.Сотрудник
	|			И ВТДанныеДляПечати.Период = Отпуск.ДатаНачалаСобытия
	|ГДЕ
	|	Отпуск.Ссылка В(&МассивОбъектов)";
	
	Запрос.Выполнить();
	
	ИменаПолей = Новый Массив;
	ИменаПолей.Добавить("Руководитель");
	ЗарплатаКадры.СоздатьВТФИООтветственныхЛиц(Запрос.МенеджерВременныхТаблиц, Истина, ИменаПолей, "ВТДанныеДокументов");
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ДанныеДокументов.Сотрудник,
		|	ДанныеДокументов.Организация,
		|	ВЫРАЗИТЬ(ДанныеДокументов.Организация КАК Справочник.Организации).НаименованиеПолное КАК НазваниеОрганизации,
		|	ДанныеДокументов.КодПоОКПО,
		|	ДанныеДокументов.Работник,
		|	ДанныеДокументов.НомерДок,
		|	ДанныеДокументов.ДатаДок,
		|	ДанныеДокументов.ТабельныйНомер,
		|	ДанныеДокументов.Подразделение,
		|	ДанныеДокументов.Должность,
		|	ДанныеДокументов.НачалоПериодаЗаКоторыйПредоставляетсяОтпуск,
		|	ДанныеДокументов.КонецПериодаЗаКоторыйПредоставляетсяОтпуск,
		|	ДанныеДокументов.ДатаНачала,
		|	ДанныеДокументов.ДатаОкончания,
		|	ДанныеДокументов.Продолжительность,
		|	ДанныеДокументов.ЭтоОсновнойОтпуск,
		|	ДанныеДокументов.Ссылка КАК Ссылка,
		|	ДанныеДокументов.ДатаНачалаСобытия,
		|	ДанныеДокументов.Дата,
		|	ДанныеДокументов.Руководитель,
		|	ДанныеДокументов.ДолжностьРуководителя,
		|	ФИОРуководителя.РасшифровкаПодписи КАК РуководительРасшифровкаПодписи
		|ИЗ
		|	ВТДанныеДокументов КАК ДанныеДокументов
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТФИООтветственныхЛиц КАК ФИОРуководителя
		|		ПО ДанныеДокументов.Дата = ФИОРуководителя.Дата
		|			И ДанныеДокументов.Ссылка = ФИОРуководителя.Ссылка
		|			И ДанныеДокументов.Руководитель = ФИОРуководителя.ФизическоеЛицо
		|ИТОГИ ПО
		|	Ссылка";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат РезультатЗапроса;	
	
КонецФункции

Функция СформироватьЗапросДляТ8(МассивОбъектов) Экспорт
	
	ПечатьПоЭлементуСправочника = Ложь;
	Если МассивОбъектов.Количество() > 0 Тогда
		ПечатьПоЭлементуСправочника = ТипЗнч(МассивОбъектов[0]) = Тип("СправочникСсылка.Сотрудники");
	КонецЕсли; 
	
	Если ПечатьПоЭлементуСправочника И
		НЕ ПолучитьФункциональнуюОпцию("ИспользоватьКадровыйУчет") Тогда
		
		Возврат СформироватьПоЭлементамСправочникаЗапросДляТ1Т8(МассивОбъектов, "ДатаУвольнения", Истина);
		
	КонецЕсли;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	КадровыйУчет.СоздатьВТДанныеДокументовУвольнение(МенеджерВременныхТаблиц, МассивОбъектов);
	
	Возврат СформироватьПоДокументамЗапросДляТ1Т8(МенеджерВременныхТаблиц, МассивОбъектов, Ложь);
	
КонецФункции	

Процедура СоздатьВТДанныеДокументовПриемНаРаботу(МенеджерВременныхТаблиц, МассивОбъектов) Экспорт
	
	ПечатьПоЭлементуСправочника = Ложь;
	Если МассивОбъектов.Количество() > 0 Тогда
		ПечатьПоЭлементуСправочника = ТипЗнч(МассивОбъектов[0]) = Тип("СправочникСсылка.Сотрудники");
	КонецЕсли; 
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Параметры.Вставить("МассивОбъектов", МассивОбъектов);
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ПриемНаРаботу.Ссылка,
		|	ПриемНаРаботу.ДатаПриема КАК Дата,
		|	ПриемНаРаботу.Сотрудник,
		|	ПриемНаРаботу.ДатаПриема КАК Период,
		|	ПриемНаРаботу.Номер КАК НомерДок,
		|	ПриемНаРаботу.Дата КАК ДатаДок,
		|	ПриемНаРаботу.Руководитель,
		|	ПриемНаРаботу.ДолжностьРуководителя,
		|	ПриемНаРаботу.УсловияПриема,
		|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаЗавершенияТрудовогоДоговора,
		|	0 КАК ДлительностьИспытательногоСрока
		|ПОМЕСТИТЬ ВТДанныеДокументов
		|ИЗ
		|	Документ.ПриемНаРаботу КАК ПриемНаРаботу
		|ГДЕ
		|	ПриемНаРаботу.Проведен
		|	И ПриемНаРаботу.Ссылка В(&МассивОбъектов)";
		
	Если ПечатьПоЭлементуСправочника Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ПриемНаРаботу.Ссылка В(&МассивОбъектов)", "ПриемНаРаботу.Сотрудник В(&МассивОбъектов)");
	КонецЕсли;
	
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура СоздатьВТДанныеДокументовКадровыхПеремещений(МенеджерВременныхТаблиц, МассивОбъектов) Экспорт
	
	ПечатьПоЭлементуСправочника = Ложь;
	Если МассивОбъектов.Количество() > 0 Тогда
		ПечатьПоЭлементуСправочника = ТипЗнч(МассивОбъектов[0]) = Тип("СправочникСсылка.Сотрудники");
	КонецЕсли; 
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Если ПечатьПоЭлементуСправочника Тогда
		
		ИзмеренияДаты = Новый ТаблицаЗначений;
		ИзмеренияДаты.Колонки.Добавить("Период", Новый ОписаниеТипов("Дата"));
		ИзмеренияДаты.Колонки.Добавить("Сотрудник", Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
		
		Для каждого Сотрудник Из МассивОбъектов Цикл
			ИзмеренияДаты.Добавить().Сотрудник = Сотрудник;
		КонецЦикла;
		
		ПараметрыПостроения = ЗарплатаКадрыОбщиеНаборыДанных.ПараметрыПостроенияДляСоздатьВТИмяРегистраСрез();
		ЗарплатаКадрыОбщиеНаборыДанных.ДобавитьВКоллекциюОтбор(ПараметрыПостроения.Отборы, "ВидСобытия", "=", Перечисления.ВидыКадровыхСобытий.Перемещение);
		
		ЗапросКадровыхПереводов = ЗарплатаКадрыОбщиеНаборыДанных.ЗапросВТИмяРегистраСрез(
			"КадроваяИсторияСотрудников",
			Истина,
			ЗарплатаКадрыОбщиеНаборыДанных.ОписаниеФильтраДляСоздатьВТИмяРегистра(ИзмеренияДаты),
			ПараметрыПостроения,
			Истина,
			"");
		
		Запрос.Параметры.Вставить("МассивОбъектов", ЗапросКадровыхПереводов.Выполнить().Выгрузить().ВыгрузитьКолонку("Регистратор"));
		
	Иначе
		Запрос.Параметры.Вставить("МассивОбъектов", МассивОбъектов);
	КонецЕсли;
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	КадровыйПеревод.Ссылка,
		|	КадровыйПеревод.ДатаНачала КАК Дата,
		|	КадровыйПеревод.Сотрудник КАК Сотрудник,
		|	КадровыйПеревод.ДатаНачала КАК ДатаНачала,
		|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаОкончания,
		|	КадровыйПеревод.Номер КАК НомерДок,
		|	КадровыйПеревод.Дата КАК ДатаДок,
		|	КадровыйПеревод.Руководитель,
		|	КадровыйПеревод.ДолжностьРуководителя,
		|	КадровыйПеревод.ФизическоеЛицо,
		|	КадровыйПеревод.ОснованиеПеревода,
		|	КадровыйПеревод.ПричинаПеревода
		|ПОМЕСТИТЬ ВТДанныеДокументовКадровыхПеремещений
		|ИЗ
		|	Документ.КадровыйПеревод КАК КадровыйПеревод
		|ГДЕ
		|	КадровыйПеревод.Ссылка В(&МассивОбъектов)
		|	И КадровыйПеревод.Проведен";
	
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура СоздатьВТДанныеДокументовУвольнение(МенеджерВременныхТаблиц, МассивОбъектов) Экспорт
	
	ПечатьПоЭлементуСправочника = Ложь;
	Если МассивОбъектов.Количество() > 0 Тогда
		ПечатьПоЭлементуСправочника = ТипЗнч(МассивОбъектов[0]) = Тип("СправочникСсылка.Сотрудники");
	КонецЕсли; 
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Параметры.Вставить("МассивОбъектов", МассивОбъектов);
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Увольнение.Ссылка,
		|	Увольнение.ДатаУвольнения КАК Дата,
		|	Увольнение.Сотрудник,
		|	Увольнение.ДатаУвольнения КАК Период,
		|	Увольнение.Номер КАК НомерДок,
		|	Увольнение.Дата КАК ДатаДок,
		|	Увольнение.СтатьяТКРФ,
		|	Увольнение.СтатьяТКРФ.ТекстОснования КАК ТекстОснования,
		|	Увольнение.Руководитель,
		|	Увольнение.ДолжностьРуководителя,
		|	Увольнение.ОснованиеУвольнения
		|ПОМЕСТИТЬ ВТДанныеДокументов
		|ИЗ
		|	Документ.Увольнение КАК Увольнение
		|ГДЕ
		|	Увольнение.Проведен
		|	И Увольнение.Ссылка В(&МассивОбъектов)";
	
	Если ПечатьПоЭлементуСправочника Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Увольнение.Ссылка В(&МассивОбъектов)", "Увольнение.Сотрудник В(&МассивОбъектов)");
	КонецЕсли;
	
	Запрос.Выполнить();
	
КонецПроцедуры

Функция СформироватьПоЭлементамСправочникаЗапросДляТ1Т8(МассивОбъектов, ИмяРеквизитаДатаСобытия, ДанныеДляТ8 = Ложь)

	КадровыеДанные = КадровыйУчет.КадровыеДанныеСотрудников(Истина, МассивОбъектов, ИмяРеквизитаДатаСобытия, ТекущаяДатаСеанса());
	
	Запрос = Новый Запрос;
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Параметры.Вставить("КадровыеДанные", КадровыеДанные);
	Запрос.Параметры.Вставить("ДатаСобытия", ТекущаяДатаСеанса());
	Запрос.Параметры.Вставить("ДанныеДляТ8", ДанныеДляТ8);

	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	Сотрудники.Сотрудник КАК Сотрудник,
		|	ВЫБОР КОГДА Сотрудники." + ИмяРеквизитаДатаСобытия + " = ДАТАВРЕМЯ(1,1,1) ТОГДА &ДатаСобытия ИНАЧЕ Сотрудники." + ИмяРеквизитаДатаСобытия + " КОНЕЦ КАК Период
		|ПОМЕСТИТЬ ВТФизическиеЛица
		|ИЗ
		|	&КадровыеДанные КАК Сотрудники";
	
	Запрос.Выполнить();
	
	ОписательВременныхТаблиц = КадровыйУчет.ОписательВременныхТаблицДляСоздатьВТКадровыеДанныеСотрудников(
		Запрос.МенеджерВременныхТаблиц,
		"ВТФизическиеЛица");
	КадровыйУчет.СоздатьВТКадровыеДанныеСотрудников(ОписательВременныхТаблиц, Истина, "Организация,Подразделение,Должность,ДатаПриема,ДатаУвольнения,ТабельныйНомер,ФИОПолные,Пол");
	
	Запрос.УстановитьПараметр("НачисленияСотрудников",
		КадровыйУчет.ТаблицаНачисленийСотрудниковПоВременнойТаблице(Запрос.МенеджерВременныхТаблиц, "ВТФизическиеЛица"));
		
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	КадровыеДанныеСотрудников.Организация,
		|	КадровыеДанныеСотрудников.Период
		|ИЗ
		|	ВТКадровыеДанныеСотрудников КАК КадровыеДанныеСотрудников";
	
	ТаблицаОтветственныхЛиц = Новый ТаблицаЗначений;
	ТаблицаОтветственныхЛиц.Колонки.Добавить("Организация", Новый ОписаниеТипов("СправочникСсылка.Организации"));
	ТаблицаОтветственныхЛиц.Колонки.Добавить("РуководительРасшифровкаПодписи", Новый ОписаниеТипов("Строка"));
	
	Сведения = Новый СписокЗначений;
	// Получаем данные из глобальной общей функции.
	Сведения.Добавить("", "ФИОРук");
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если ЗначениеЗаполнено(Выборка.Организация) Тогда
			
			НоваяСтрокаОтветственныхЛиц = ТаблицаОтветственныхЛиц.Добавить();
			НоваяСтрокаОтветственныхЛиц.Организация = Выборка.Организация;
			
			ОргСведения = РегламентированнаяОтчетностьВызовСервера.ПолучитьСведенияОбОрганизации(Выборка.Организация, Выборка.Период, Сведения);
			ОргСведения.Свойство("ФИОРук", 		НоваяСтрокаОтветственныхЛиц.РуководительРасшифровкаПодписи);
			
		КонецЕсли; 
		
	КонецЦикла;
	
	Запрос.УстановитьПараметр("ТаблицаОтветственныхЛиц", ТаблицаОтветственныхЛиц);
	
	ИмяВТНачисления = ЗарплатаКадрыОбщиеНаборыДанных.УникальноеИмяТекстаЗапроса("ВТНачисления");
	ИмяВТОтветственныеЛица = ЗарплатаКадрыОбщиеНаборыДанных.УникальноеИмяТекстаЗапроса("ВТОтветственныеЛица");
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	НачисленияСотрудников.Период,
		|	НачисленияСотрудников.Сотрудник,
		|	НачисленияСотрудников.ТарифнаяСтавка,
		|	НачисленияСотрудников.Надбавка
		|ПОМЕСТИТЬ ВТНачисления
		|ИЗ
		|	&НачисленияСотрудников КАК НачисленияСотрудников
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаОтветственныхЛиц.Организация,
		|	ТаблицаОтветственныхЛиц.РуководительРасшифровкаПодписи
		|ПОМЕСТИТЬ ВТОтветственныеЛица
		|ИЗ
		|	&ТаблицаОтветственныхЛиц КАК ТаблицаОтветственныхЛиц
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	КадровыеДанныеСотрудников.Сотрудник
		|ИЗ
		|	ВТКадровыеДанныеСотрудников КАК КадровыеДанныеСотрудников
		|ГДЕ
		|	КадровыеДанныеСотрудников.ДатаПриема = ДАТАВРЕМЯ(1, 1, 1)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	КадровыеДанныеСотрудников.Сотрудник
		|ИЗ
		|	ВТКадровыеДанныеСотрудников КАК КадровыеДанныеСотрудников
		|ГДЕ
		|	КадровыеДанныеСотрудников.ДатаУвольнения = ДАТАВРЕМЯ(1, 1, 1)
		|	И &ДанныеДляТ8
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	КадровыеДанныеСотрудников.Сотрудник КАК Ссылка,
		|	КадровыеДанныеСотрудников.Организация.НаименованиеПолное КАК НазваниеОрганизации,
		|	КадровыеДанныеСотрудников.Организация.КодПоОКПО,
		|	КадровыеДанныеСотрудников.ДатаПриема КАК ДатаПриема,
		|	КадровыеДанныеСотрудников.ДатаУвольнения КАК ДатаУвольнения,
		|	КадровыеДанныеСотрудников.ФИОПолные КАК Работник,
		|	КадровыеДанныеСотрудников.ТабельныйНомер КАК ТабельныйНомер,
		|	КадровыеДанныеСотрудников.Подразделение КАК Подразделение,
		|	КадровыеДанныеСотрудников.Должность.Наименование КАК Должность,
		|	ЕСТЬNULL(Начисления.ТарифнаяСтавка, 0) КАК ТарифнаяСтавка,
		|	КадровыеДанныеСотрудников.Пол КАК Пол,
		|	ЕСТЬNULL(Начисления.Надбавка, """") КАК Надбавка,
		|	"""" КАК ТекстОснования,
		|	ОтветственныеЛица.РуководительРасшифровкаПодписи
		|ИЗ
		|	ВТКадровыеДанныеСотрудников КАК КадровыеДанныеСотрудников
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОтветственныеЛица КАК ОтветственныеЛица
		|		ПО КадровыеДанныеСотрудников.Организация = ОтветственныеЛица.Организация
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНачисления КАК Начисления
		|		ПО КадровыеДанныеСотрудников.Сотрудник = Начисления.Сотрудник
		|			И КадровыеДанныеСотрудников.Период = Начисления.Период
		|
		|УПОРЯДОЧИТЬ ПО
		|	Работник";
		
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТНачисления", ИмяВТНачисления);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТОтветственныеЛица", ИмяВТОтветственныеЛица);
	
	РезультатЗапросов = Запрос.ВыполнитьПакет();
	
	Если НЕ РезультатЗапросов[РезультатЗапросов.Количество() - 3].Пустой() Тогда
		
		Выборка = РезультатЗапросов[РезультатЗапросов.Количество() - 3].Выбрать();
		Выборка.Следующий();
		
		ТекстИсключения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='С сотрудником %1 не оформлен трудовой договор'"),
			Выборка.Сотрудник);
		
		ВызватьИсключение ТекстИсключения;
		
	КонецЕсли; 
	
	Если НЕ РезультатЗапросов[РезультатЗапросов.Количество() - 2].Пустой() Тогда
		
		Выборка = РезультатЗапросов[РезультатЗапросов.Количество() - 2].Выбрать();
		Выборка.Следующий();
		
		ТекстИсключения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='У сотрудника %1 не заполнена дата увольнения'"),
			Выборка.Сотрудник);
		
		ВызватьИсключение ТекстИсключения;
		
	КонецЕсли; 
	
	Возврат РезультатЗапросов[РезультатЗапросов.Количество() - 1];	
	
КонецФункции	

Функция СформироватьПоЭлементамСправочникаЗапросДляТ5(МассивОбъектов)

	Запрос = Новый Запрос;
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц; 
	
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Параметры.Вставить("МассивСотрудников", МассивОбъектов);
	Запрос.Параметры.Вставить("ДатаСреза", ТекущаяДатаСеанса());

	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	Сотрудники.Ссылка КАК Сотрудник,
		|	&ДатаСреза КАК Период,
		|	Сотрудники.ФизическоеЛицо
		|ПОМЕСТИТЬ ВТФизическиеЛица
		|ИЗ
		|	Справочник.Сотрудники КАК Сотрудники
		|ГДЕ
		|	Сотрудники.Ссылка В(&МассивСотрудников)";
	
	Запрос.Выполнить();
	
	ОписательВременныхТаблиц = КадровыйУчет.ОписательВременныхТаблицДляСоздатьВТКадровыеДанныеСотрудников(
		МенеджерВременныхТаблиц,
		"ВТФизическиеЛица");
	КадровыйУчет.СоздатьВТКадровыеДанныеСотрудников(ОписательВременныхТаблиц, Истина, "Организация,Подразделение,Должность,ФиоПолные,ДатаПриема");
	
	Запрос.УстановитьПараметр("НачисленияСотрудников",
		КадровыйУчет.ТаблицаНачисленийСотрудниковПоВременнойТаблице(Запрос.МенеджерВременныхТаблиц, "ВТФизическиеЛица"));
		
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	КадровыеДанныеСотрудников.Организация,
		|	КадровыеДанныеСотрудников.Период
		|ИЗ
		|	ВТКадровыеДанныеСотрудников КАК КадровыеДанныеСотрудников";
	
	ТаблицаОтветственныхЛиц = Новый ТаблицаЗначений;
	ТаблицаОтветственныхЛиц.Колонки.Добавить("Организация", Новый ОписаниеТипов("СправочникСсылка.Организации"));
	ТаблицаОтветственныхЛиц.Колонки.Добавить("РуководительРасшифровкаПодписи", Новый ОписаниеТипов("Строка"));
	
	Сведения = Новый СписокЗначений;
	// Получаем данные из глобальной общей функции.
	Сведения.Добавить("", "ФИОРук");
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если ЗначениеЗаполнено(Выборка.Организация) Тогда
			
			НоваяСтрокаОтветственныхЛиц = ТаблицаОтветственныхЛиц.Добавить();
			НоваяСтрокаОтветственныхЛиц.Организация = Выборка.Организация;
			
			ОргСведения = РегламентированнаяОтчетностьВызовСервера.ПолучитьСведенияОбОрганизации(Выборка.Организация, Выборка.Период, Сведения);
			ОргСведения.Свойство("ФИОРук", 		НоваяСтрокаОтветственныхЛиц.РуководительРасшифровкаПодписи);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Запрос.УстановитьПараметр("ТаблицаОтветственныхЛиц", ТаблицаОтветственныхЛиц);
	
	ИмяВТНачисления = ЗарплатаКадрыОбщиеНаборыДанных.УникальноеИмяТекстаЗапроса("ВТНачисления");
	ИмяВТОтветственныеЛица = ЗарплатаКадрыОбщиеНаборыДанных.УникальноеИмяТекстаЗапроса("ВТОтветственныеЛица");
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	НачисленияСотрудников.Период,
		|	НачисленияСотрудников.Сотрудник,
		|	НачисленияСотрудников.ТарифнаяСтавка,
		|	НачисленияСотрудников.Надбавка
		|ПОМЕСТИТЬ ВТНачисления
		|ИЗ
		|	&НачисленияСотрудников КАК НачисленияСотрудников
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаОтветственныхЛиц.Организация,
		|	ТаблицаОтветственныхЛиц.РуководительРасшифровкаПодписи
		|ПОМЕСТИТЬ ВТОтветственныеЛица
		|ИЗ
		|	&ТаблицаОтветственныхЛиц КАК ТаблицаОтветственныхЛиц
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	КадровыеДанныеСотрудников.Сотрудник
		|ИЗ
		|	ВТКадровыеДанныеСотрудников КАК КадровыеДанныеСотрудников
		|ГДЕ
		|	КадровыеДанныеСотрудников.ДатаПриема = ДАТАВРЕМЯ(1, 1, 1)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Сотрудники.Ссылка,
		|	Организации.НаименованиеПолное КАК НазваниеОрганизации,
		|	Организации.КодПоОКПО,
		|	&ДатаСреза КАК ДатаНачала,
		|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаОкончания,
		|	""постоянно"" КАК ВидПеревода,
		|	КадровыеДанныеСотрудников.ФИОПолные КАК Работник,
		|	Сотрудники.Код КАК ТабельныйНомер,
		|	ЕСТЬNULL(КадровыеДанныеСотрудников.Подразделение, ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)) КАК НовоеПодразделение,
		|	ЕСТЬNULL(КадровыеДанныеСотрудников.Должность.Наименование, """") КАК НоваяДолжность,
		|	ЕСТЬNULL(Начисления.ТарифнаяСтавка, 0) КАК ТарифнаяСтавка,
		|	ОтветственныеЛица.РуководительРасшифровкаПодписи,
		|	ЕСТЬNULL(Начисления.Надбавка, """") КАК Надбавка
		|ИЗ
		|	Справочник.Сотрудники КАК Сотрудники
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТКадровыеДанныеСотрудников КАК КадровыеДанныеСотрудников
		|			ЛЕВОЕ СОЕДИНЕНИЕ ВТОтветственныеЛица КАК ОтветственныеЛица
		|			ПО КадровыеДанныеСотрудников.Организация = ОтветственныеЛица.Организация
		|			ЛЕВОЕ СОЕДИНЕНИЕ ВТНачисления КАК Начисления
		|			ПО КадровыеДанныеСотрудников.Сотрудник = Начисления.Сотрудник
		|				И КадровыеДанныеСотрудников.Период = Начисления.Период
		|		ПО Сотрудники.Ссылка = КадровыеДанныеСотрудников.Сотрудник
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК Организации
		|		ПО (КадровыеДанныеСотрудников.Организация = Организации.Ссылка)
		|ГДЕ
		|	Сотрудники.Ссылка В(&МассивСотрудников)
		|
		|УПОРЯДОЧИТЬ ПО
		|	Работник";
		
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТНачисления", ИмяВТНачисления);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТОтветственныеЛица", ИмяВТОтветственныеЛица);
	
	Запрос.Параметры.Вставить("МассивСотрудников", МассивОбъектов);
	Запрос.Параметры.Вставить("ДатаСреза", ТекущаяДатаСеанса());
	
	РезультатЗапросов = Запрос.ВыполнитьПакет();
	
	Если НЕ РезультатЗапросов[РезультатЗапросов.Количество() - 2].Пустой() Тогда
		
		Выборка = РезультатЗапросов[РезультатЗапросов.Количество() - 2].Выбрать();
		Выборка.Следующий();
		
		ТекстИсключения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='С сотрудником %1 не оформлен трудовой договор'"),
			Выборка.Сотрудник);
		
		ВызватьИсключение ТекстИсключения;
		
	КонецЕсли; 
	
	Возврат РезультатЗапросов[РезультатЗапросов.Количество() - 1];	
	
КонецФункции

Функция СформироватьПоДокументамЗапросДляТ1Т8(МенеджерВременныхТаблиц, МассивОбъектов, ПечатьТ1)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	ПечатьПоЭлементуСправочника = Ложь;
	Если МассивОбъектов.Количество() > 0 Тогда
		ПечатьПоЭлементуСправочника = ТипЗнч(МассивОбъектов[0]) = Тип("СправочникСсылка.Сотрудники");
	КонецЕсли; 
	
	Если ПечатьПоЭлементуСправочника Тогда
		
		Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
		Запрос.Текст =
			"ВЫБРАТЬ
			|	Сотрудники.Ссылка КАК Сотрудник
			|ИЗ
			|	Справочник.Сотрудники КАК Сотрудники
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДанныеДокументов КАК ВТДанныеДокументов
			|		ПО Сотрудники.Ссылка = ВТДанныеДокументов.Сотрудник
			|ГДЕ
			|	Сотрудники.Ссылка В(&МассивОбъектов)
			|	И ВТДанныеДокументов.Сотрудник ЕСТЬ NULL ";
			
		РезультатЗапроса = Запрос.Выполнить();
		Если НЕ РезультатЗапроса.Пустой() Тогда
			
			Выборка = РезультатЗапроса.Выбрать();
			Выборка.Следующий();
			
			Если ПечатьТ1 Тогда
				
				ТекстИсключения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru='По сотруднику %1 не оформлен приказ о приеме'"),
					Выборка.Сотрудник);
				
			Иначе
				
				ТекстИсключения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru='По сотруднику %1 не оформлен приказ об увольнении'"),
					Выборка.Сотрудник);
					
			КонецЕсли;
			
			ВызватьИсключение ТекстИсключения;
		
		КонецЕсли; 
		
	КонецЕсли; 
	
	Запрос.УстановитьПараметр("НачисленияСотрудников",
		КадровыйУчет.ТаблицаНачисленийСотрудниковПоВременнойТаблице(Запрос.МенеджерВременныхТаблиц, "ВТДанныеДокументов"));
	
	ОписательВременныхТаблиц = КадровыйУчет.ОписательВременныхТаблицДляСоздатьВТКадровыеДанныеСотрудников(
		Запрос.МенеджерВременныхТаблиц,
		"ВТДанныеДокументов");
		
	КадровыйУчет.СоздатьВТКадровыеДанныеСотрудников(
		ОписательВременныхТаблиц,
		Истина,
		КадровыйУчет.КадровыеДанныеДляПечатиКадровыхПриказов());
	
	ИменаПолейОтветственныхЛиц = Новый Массив;
	ИменаПолейОтветственныхЛиц.Добавить("Руководитель");
	
	ЗарплатаКадры.СоздатьВТФИООтветственныхЛиц(Запрос.МенеджерВременныхТаблиц, Ложь, ИменаПолейОтветственныхЛиц, "ВТДанныеДокументов");
	
	ИмяВТНачисления = ЗарплатаКадрыОбщиеНаборыДанных.УникальноеИмяТекстаЗапроса("ВТНачисления");
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	НачисленияСотрудников.Период,
		|	НачисленияСотрудников.Сотрудник,
		|	НачисленияСотрудников.ТарифнаяСтавка,
		|	НачисленияСотрудников.Надбавка
		|ПОМЕСТИТЬ ВТНачисления
		|ИЗ
		|	&НачисленияСотрудников КАК НачисленияСотрудников
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	КадровыеДанныеСотрудников.Организация.КодПоОКПО КАК КодПоОКПО,
		|	ВЫБОР
		|		КОГДА (ВЫРАЗИТЬ(КадровыеДанныеСотрудников.Организация.НаименованиеПолное КАК СТРОКА(1))) = """"
		|			ТОГДА ВЫБОР
		|					КОГДА (ВЫРАЗИТЬ(КадровыеДанныеСотрудников.Организация.НаименованиеСокращенное КАК СТРОКА(1))) = """"
		|						ТОГДА КадровыеДанныеСотрудников.Организация.Наименование
		|					ИНАЧЕ КадровыеДанныеСотрудников.Организация.НаименованиеСокращенное
		|				КОНЕЦ
		|		ИНАЧЕ КадровыеДанныеСотрудников.Организация.НаименованиеПолное
		|	КОНЕЦ КАК НазваниеОрганизации,
		|	КадровыеДанныеСотрудников.ФИОПолные КАК Работник,
		|	ФИООтветственныхЛиц.РасшифровкаПодписи КАК РуководительРасшифровкаПодписи,
		|	ЕСТЬNULL(Начисления.ТарифнаяСтавка, 0) КАК ТарифнаяСтавка,
		|	ЕСТЬNULL(Начисления.Надбавка, """") КАК Надбавка,
		|	&КадровыеДанныеСотрудников,
		|	&ДанныеДокументов
		|ИЗ
		|	ВТДанныеДокументов КАК ДанныеДокументов
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКадровыеДанныеСотрудников КАК КадровыеДанныеСотрудников
		|		ПО ДанныеДокументов.Сотрудник = КадровыеДанныеСотрудников.Сотрудник
		|			И ДанныеДокументов.Период = КадровыеДанныеСотрудников.Период
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТФИООтветственныхЛиц КАК ФИООтветственныхЛиц
		|		ПО ДанныеДокументов.Руководитель = ФИООтветственныхЛиц.ФизическоеЛицо
		|			И ДанныеДокументов.Ссылка = ФИООтветственныхЛиц.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНачисления КАК Начисления
		|		ПО ДанныеДокументов.Период = Начисления.Период
		|			И ДанныеДокументов.Сотрудник = Начисления.Сотрудник";
		
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&КадровыеДанныеСотрудников", "КадровыеДанныеСотрудников.*");
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ДанныеДокументов", "ДанныеДокументов.*");
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТНачисления", ИмяВТНачисления);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат РезультатЗапроса;	
	
КонецФункции

Функция СформироватьПоДокументамЗапросДляТ5(МенеджерВременныхТаблиц, МассивОбъектов)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	ПечатьПоЭлементуСправочника = Ложь;
	Если МассивОбъектов.Количество() > 0 Тогда
		ПечатьПоЭлементуСправочника = ТипЗнч(МассивОбъектов[0]) = Тип("СправочникСсылка.Сотрудники");
	КонецЕсли; 
	
	Если ПечатьПоЭлементуСправочника Тогда
		
		Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
		Запрос.Текст =
			"ВЫБРАТЬ
			|	Сотрудники.Ссылка КАК Сотрудник
			|ИЗ
			|	Справочник.Сотрудники КАК Сотрудники
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДанныеДокументовКадровыхПеремещений КАК ВТДанныеДокументов
			|		ПО Сотрудники.Ссылка = ВТДанныеДокументов.Сотрудник
			|ГДЕ
			|	Сотрудники.Ссылка В(&МассивОбъектов)
			|	И ВТДанныеДокументов.Сотрудник ЕСТЬ NULL ";
			
		РезультатЗапроса = Запрос.Выполнить();
		Если НЕ РезультатЗапроса.Пустой() Тогда
			
			Выборка = РезультатЗапроса.Выбрать();
			Выборка.Следующий();
			
			ТекстИсключения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='По сотруднику %1 нет оформленных приказов о кадровом переводе'"),
				Выборка.Сотрудник);
				
			ВызватьИсключение ТекстИсключения;
		
		КонецЕсли; 
		
	КонецЕсли; 
	
	Запрос.УстановитьПараметр("НачисленияСотрудников",
		КадровыйУчет.ТаблицаНачисленийСотрудниковПоВременнойТаблице(Запрос.МенеджерВременныхТаблиц, "ВТДанныеДокументовКадровыхПеремещений", "ДатаНачала"));
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ДанныеДокументов.Сотрудник,
		|	ДОБАВИТЬКДАТЕ(ДанныеДокументов.ДатаНачала, ДЕНЬ, -1) КАК Период
		|ПОМЕСТИТЬ ВТСотрудникиПериоды
		|ИЗ
		|	ВТДанныеДокументовКадровыхПеремещений КАК ДанныеДокументов
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВТДанныеДокументов.Сотрудник,
		|	ВТДанныеДокументов.ДатаНачала
		|ИЗ
		|	ВТДанныеДокументовКадровыхПеремещений КАК ВТДанныеДокументов";
	
	Запрос.Выполнить();
	
	ОписательВременныхТаблиц = КадровыйУчет.ОписательВременныхТаблицДляСоздатьВТКадровыеДанныеСотрудников(
		Запрос.МенеджерВременныхТаблиц,
		"ВТСотрудникиПериоды");
		
	КадровыйУчет.СоздатьВТКадровыеДанныеСотрудников(
		ОписательВременныхТаблиц,
		Истина,
		КадровыйУчет.КадровыеДанныеДляПечатиКадровыхПриказов());

	ИменаПолейОтветственныхЛиц = Новый Массив;
	ИменаПолейОтветственныхЛиц.Добавить("Руководитель");
	
	ЗарплатаКадры.СоздатьВТФИООтветственныхЛиц(Запрос.МенеджерВременныхТаблиц, Ложь, ИменаПолейОтветственныхЛиц, "ВТДанныеДокументовКадровыхПеремещений");
	
	ИмяВТНачисления = ЗарплатаКадрыОбщиеНаборыДанных.УникальноеИмяТекстаЗапроса("ВТНачисления");
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	НачисленияСотрудников.Период,
		|	НачисленияСотрудников.Сотрудник,
		|	НачисленияСотрудников.ТарифнаяСтавка,
		|	НачисленияСотрудников.Надбавка
		|ПОМЕСТИТЬ ВТНачисления
		|ИЗ
		|	&НачисленияСотрудников КАК НачисленияСотрудников
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА ДанныеДокументов.ДатаОкончания = ДАТАВРЕМЯ(1, 1, 1)
		|			ТОГДА ""постоянно""
		|		ИНАЧЕ ""временно""
		|	КОНЕЦ КАК ВидПеревода,
		|	КадровыеДанныеСотрудниковПредыдущие.Организация.КодПоОКПО КАК КодПоОКПО,
		|	ВЫБОР
		|		КОГДА (ВЫРАЗИТЬ(КадровыеДанныеСотрудниковПредыдущие.Организация.НаименованиеПолное КАК СТРОКА(1))) = """"
		|			ТОГДА ВЫБОР
		|					КОГДА (ВЫРАЗИТЬ(КадровыеДанныеСотрудниковПредыдущие.Организация.НаименованиеСокращенное КАК СТРОКА(1))) = """"
		|						ТОГДА КадровыеДанныеСотрудниковПредыдущие.Организация.Наименование
		|					ИНАЧЕ КадровыеДанныеСотрудниковПредыдущие.Организация.НаименованиеСокращенное
		|				КОНЕЦ
		|		ИНАЧЕ КадровыеДанныеСотрудниковПредыдущие.Организация.НаименованиеПолное
		|	КОНЕЦ КАК НазваниеОрганизации,
		|	ФИООтветственныхЛиц.РасшифровкаПодписи КАК РуководительРасшифровкаПодписи,
		|	КадровыеДанныеСотрудниковПредыдущие.Подразделение КАК Подразделение,
		|	КадровыеДанныеСотрудниковПредыдущие.Должность КАК Должность,
		|	КадровыеДанныеСотрудниковПредыдущие.ТабельныйНомер КАК ТабельныйНомер,
		|	ЕСТЬNULL(КадровыеДанныеСотрудниковНовые.ФИОПолные, """") КАК Работник,
		|	КадровыеДанныеСотрудниковНовые.Подразделение КАК НовоеПодразделение,
		|	КадровыеДанныеСотрудниковНовые.Должность КАК НоваяДолжность,
		|	ЕСТЬNULL(Начисления.ТарифнаяСтавка, 0) КАК ТарифнаяСтавка,
		|	ЕСТЬNULL(Начисления.Надбавка, """") КАК Надбавка,
		|	&ТекстКадровыеДанныеСотрудников,
		|	&ДанныеДокументов
		|ИЗ
		|	ВТДанныеДокументовКадровыхПеремещений КАК ДанныеДокументов
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКадровыеДанныеСотрудников КАК КадровыеДанныеСотрудниковПредыдущие
		|		ПО ДанныеДокументов.Сотрудник = КадровыеДанныеСотрудниковПредыдущие.Сотрудник
		|			И ДанныеДокументов.ДатаНачала > КадровыеДанныеСотрудниковПредыдущие.Период
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКадровыеДанныеСотрудников КАК КадровыеДанныеСотрудниковНовые
		|		ПО ДанныеДокументов.Сотрудник = КадровыеДанныеСотрудниковНовые.Сотрудник
		|			И ДанныеДокументов.ДатаНачала = КадровыеДанныеСотрудниковНовые.Период
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТФИООтветственныхЛиц КАК ФИООтветственныхЛиц
		|		ПО ДанныеДокументов.Руководитель = ФИООтветственныхЛиц.ФизическоеЛицо
		|			И ДанныеДокументов.Ссылка = ФИООтветственныхЛиц.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНачисления КАК Начисления
		|		ПО ДанныеДокументов.ДатаНачала = Начисления.Период
		|			И ДанныеДокументов.Сотрудник = Начисления.Сотрудник";
		
	ТекстКадровыеДанныеСотрудников = "";
	Для каждого ИмяКадровыхДанных Из СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(КадровыйУчет.КадровыеДанныеДляПечатиКадровыхПриказов()) Цикл
		
		Если ВРег(ИмяКадровыхДанных) = ВРег("Организация")
			ИЛИ ВРег(ИмяКадровыхДанных) = ВРег("Подразделение")
			ИЛИ ВРег(ИмяКадровыхДанных) = ВРег("Должность")
			ИЛИ ВРег(ИмяКадровыхДанных) = ВРег("ФИОПолные") Тогда
			
			Продолжить;
			
		ИначеЕсли ВРег(ИмяКадровыхДанных) = ВРег("ТрудовойДоговорНомер")
				ИЛИ ВРег(ИмяКадровыхДанных) = ВРег("ТрудовойДоговорДата") Тогда
				
			ТекстКадровыеДанныеСотрудников = 
				?(ПустаяСтрока(ТекстКадровыеДанныеСотрудников), "", ТекстКадровыеДанныеСотрудников + Символы.ПС)
				+ "КадровыеДанныеСотрудниковПредыдущие." + ИмяКадровыхДанных + " КАК " + ИмяКадровыхДанных + ",";
			
		Иначе
			
			Если ВРег(ИмяКадровыхДанных) = ВРег("РазрядКатегория") Тогда
				
				ТекстКадровыеДанныеСотрудников = 
					?(ПустаяСтрока(ТекстКадровыеДанныеСотрудников), "", ТекстКадровыеДанныеСотрудников + Символы.ПС)
					+ "КадровыеДанныеСотрудниковПредыдущие." + ИмяКадровыхДанных + " КАК " + ИмяКадровыхДанных + ",";
				
			КонецЕсли;
			
			ТекстКадровыеДанныеСотрудников = 
				?(ПустаяСтрока(ТекстКадровыеДанныеСотрудников), "", ТекстКадровыеДанныеСотрудников + Символы.ПС)
				+ "КадровыеДанныеСотрудниковНовые." + ИмяКадровыхДанных + " КАК Новый" + ИмяКадровыхДанных + ",";
			
		КонецЕсли;
		
	КонецЦикла;
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстКадровыеДанныеСотрудников,", ТекстКадровыеДанныеСотрудников);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ДанныеДокументов", "ДанныеДокументов.*");
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТНачисления", ИмяВТНачисления);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат РезультатЗапроса;	
	
КонецФункции

Функция КадровыеДанныеДляПечатиКадровыхПриказов() Экспорт
	
	Возврат "ФИОПолные,Пол,ТабельныйНомер,Организация,Подразделение,Должность,ДатаПриема,ДатаУвольнения";
	
КонецФункции

Функция ТаблицаНачисленийСотрудниковПоВременнойТаблице(МенеджерВременныхТаблиц, ИмяВТСотрудникиПериоды, ИмяПоляПериод, ИмяПоляСотрудник) Экспорт
	
	НачисленияСотрудников = Новый ТаблицаЗначений;
	НачисленияСотрудников.Колонки.Добавить("Сотрудник", Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
	НачисленияСотрудников.Колонки.Добавить("Период", Новый ОписаниеТипов("Дата"));
	НачисленияСотрудников.Колонки.Добавить("ТарифнаяСтавка", Новый ОписаниеТипов("Число"));
	НачисленияСотрудников.Колонки.Добавить("Надбавка", Новый ОписаниеТипов("Строка"));
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьНачислениеЗарплаты") Тогда
		
		ОписанияНачислений = КадровыйУчет.ОписанияСоставаНачисленийПоВременнойТаблице(МенеджерВременныхТаблиц, ИмяВТСотрудникиПериоды, ИмяПоляПериод, ИмяПоляСотрудник);
		
		Для каждого ОписаниеСотрудника Из ОписанияНачислений Цикл
			
			Для каждого ОписаниеПериода Из ОписаниеСотрудника.Значение Цикл
				
				НоваяСтрока = НачисленияСотрудников.Добавить();
				
				НоваяСтрока.Сотрудник = ОписаниеСотрудника.Ключ;
				НоваяСтрока.Период = ОписаниеПериода.Ключ;
				НоваяСтрока.ТарифнаяСтавка = ОписаниеПериода.Значение.ОкладТариф;
				НоваяСтрока.Надбавка = ОписаниеПериода.Значение.ОписаниеНадбавок;
				
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЕсли; 
	
	Возврат НачисленияСотрудников;
	
КонецФункции

Функция ОписанияСоставаНачисленийПоВременнойТаблице(МенеджерВременныхТаблиц, ИмяВТСотрудникиПериоды, ИмяПоляПериод, ИмяПоляСотрудник) Экспорт
	
	ОписательВременныхТаблиц = КадровыйУчет.ОписательВременныхТаблицДляСоздатьВТКадровыеДанныеСотрудников(
		МенеджерВременныхТаблиц,
		ИмяВТСотрудникиПериоды,
		ИмяПоляСотрудник + "," + ИмяПоляПериод);
		
	ОписательВременныхТаблиц.ИмяВТКадровыеДанныеСотрудников = ЗарплатаКадрыОбщиеНаборыДанных.УникальноеИмяТекстаЗапроса("ВТКадровыеДанныеСотрудников");;
		
	КадровыйУчет.СоздатьВТКадровыеДанныеСотрудников(ОписательВременныхТаблиц, Истина, "ТарифнаяСтавка");
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	КадровыеДанныеСотрудников.Период КАК Период,
		|	КадровыеДанныеСотрудников.Сотрудник КАК Сотрудник,
		|	КадровыеДанныеСотрудников.ТарифнаяСтавка КАК ТарифнаяСтавка
		|ИЗ
		|	ВТКадровыеДанныеСотрудников КАК КадровыеДанныеСотрудников
		|ИТОГИ ПО
		|	Сотрудник";
		
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТКадровыеДанныеСотрудников", ОписательВременныхТаблиц.ИмяВТКадровыеДанныеСотрудников);
		
	ОписанияНачисленийПоПериодам = Новый Соответствие;
		
	ВыборкаПоСотрудникам = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаПоСотрудникам.Следующий() Цикл
		
		ОписаниеНачисленийПоПериодам = Новый Соответствие;
		ВыборкаПоПериодам = ВыборкаПоСотрудникам.Выбрать();
		Пока ВыборкаПоПериодам.Следующий() Цикл
			
			ОписаниеСостава = ОписаниеСоставаНачислений();
			ОписаниеСостава.ПоказательОкладТариф = НСтр("ru='Оклад'");
			ОписаниеСостава.ОкладТариф = ВыборкаПоПериодам.ТарифнаяСтавка;
			ОписаниеСостава.ОписаниеОклада = ОписаниеСостава.ПоказательОкладТариф
				+ ?(ОписаниеСостава.ОкладТариф = 0, "", ": " + Формат(ОписаниеСостава.ОкладТариф, "ЧДЦ=0"));
				
			ОписаниеНачисленийПоПериодам.Вставить(ВыборкаПоПериодам.Период, ОписаниеСостава);
			
		КонецЦикла;
		
		ОписанияНачисленийПоПериодам.Вставить(ВыборкаПоСотрудникам.Сотрудник, ОписаниеНачисленийПоПериодам);
		
	КонецЦикла; 
	
	Возврат ОписанияНачисленийПоПериодам;
		
КонецФункции

Функция ОписаниеСоставаНачислений() Экспорт
	
	ОписаниеСостава = Новый Структура("ПоказательТариф,Тариф,ОписаниеОклада,ОписаниеНадбавок");
	ОписаниеСостава.Вставить("ПоказательОкладТариф", "");
	ОписаниеСостава.Вставить("ОкладТариф", 0);
	ОписаниеСостава.Вставить("ОписаниеОклада", "");
	ОписаниеСостава.Вставить("ОписаниеНадбавок", "");

	Возврат ОписаниеСостава;
	
КонецФункции

Функция ПредставлениеОкладаИНадбавок(ОписанияСотрудника)
	
	Если ПустаяСтрока(ОписанияСотрудника.ОписаниеОклада) Тогда
		Возврат ОписанияСотрудника.ОписаниеНадбавок;
	ИначеЕсли ПустаяСтрока(ОписанияСотрудника.ОписаниеНадбавок) Тогда
		Возврат Формат(ОписанияСотрудника.ОкладТариф, "ЧДЦ=0");
	Иначе
		Возврат ОписанияСотрудника.ОписаниеОклада + ", " + ОписанияСотрудника.ОписаниеНадбавок;
	КонецЕсли;
	
КонецФункции

Функция ПараметрыПолученияСотрудниковОрганизацийПоСпискуФизическихЛиц() Экспорт
		
	ПараметрыПолученияСотрудниковОрганизаций = ПараметрыПолученияСотрудниковОрганизаций();
	
	ПараметрыПолученияСотрудниковОрганизаций.Вставить("СписокФизическихЛиц");
	
	Возврат ПараметрыПолученияСотрудниковОрганизаций;
	
КонецФункции

Функция ПараметрыПолученияСотрудниковОрганизацийПоВременнойТаблице() Экспорт
		
	ПараметрыПолученияСотрудниковОрганизаций = ПараметрыПолученияСотрудниковОрганизаций();
	
	ПараметрыПолученияСотрудниковОрганизаций.Вставить("ИмяВТФизическиеЛица", "ВТФизическиеЛица");
	ПараметрыПолученияСотрудниковОрганизаций.Вставить("ИмяПоляФизическоеЛицо", "ФизическоеЛицо");

	Возврат ПараметрыПолученияСотрудниковОрганизаций;
	
КонецФункции

Функция ПараметрыПолученияСотрудниковОрганизаций()
		
	ПараметрыПолученияСотрудниковОрганизаций = Новый Структура("Организация,Подразделение");
	
	ПараметрыПолученияСотрудниковОрганизаций.Вставить("КадровыеДанные", "");
	ПараметрыПолученияСотрудниковОрганизаций.Вставить("ОтбиратьПоГоловнойОрганизации", Ложь);
	ПараметрыПолученияСотрудниковОрганизаций.Вставить("НачалоПериода", '00010101');
	ПараметрыПолученияСотрудниковОрганизаций.Вставить("ОкончаниеПериода", '00010101');

	Возврат ПараметрыПолученияСотрудниковОрганизаций;
	
КонецФункции

Функция ПараметрыПолученияРабочихМестВОрганизацийПоСпискуФизическихЛиц() Экспорт
	
	Возврат КадровыйУчет.ПараметрыПолученияСотрудниковОрганизацийПоСпискуФизическихЛиц();
	
КонецФункции

Функция ПараметрыПолученияРабочихМестВОрганизацийПоВременнойТаблице() Экспорт
	
	Возврат КадровыйУчет.ПараметрыПолученияСотрудниковОрганизацийПоВременнойТаблице();
	
КонецФункции

Процедура ОбновитьТекущиеКадровыеДанныеСотрудников(Запрос) Экспорт
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Сотрудники.Ссылка КАК Сотрудник,
	|	Сотрудники.ФизическоеЛицо КАК ФизическоеЛицо,
	|	Сотрудники.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	ЕСТЬNULL(КадроваяИсторияСотрудниковСрезПоследних.Организация, ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)) КАК ТекущаяОрганизация,
	|	ЕСТЬNULL(КадроваяИсторияСотрудниковСрезПоследних.Подразделение, ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)) КАК ТекущееПодразделение,
	|	ЕСТЬNULL(КадроваяИсторияСотрудниковСрезПоследних.Должность, ЗНАЧЕНИЕ(Справочник.Должности.ПустаяСсылка)) КАК ТекущаяДолжность,
	|	ЕСТЬNULL(КадроваяИсторияСотрудниковПрием.Период, ВЫБОР
	|			КОГДА ЕСТЬNULL(КадроваяИсторияСотрудниковСрезПоследних.Должность, ЗНАЧЕНИЕ(Справочник.Должности.ПустаяСсылка)) = ЗНАЧЕНИЕ(Справочник.Должности.ПустаяСсылка)
	|				ТОГДА ДАТАВРЕМЯ(1, 1, 1)
	|			ИНАЧЕ ТекущиеКадровыеДанныеСотрудников.ДатаПриема
	|		КОНЕЦ) КАК ДатаПриема,
	|	ЕСТЬNULL(ДОБАВИТЬКДАТЕ(КадроваяИсторияСотрудниковУвольнение.Период, ДЕНЬ, -1), ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаУвольнения,
	|	ЕСТЬNULL(КадроваяИсторияСотрудниковСрезПоследних.ВидЗанятости, ЗНАЧЕНИЕ(Перечисление.ВидыЗанятости.ПустаяСсылка)) КАК ТекущийВидЗанятости,
	|	ЕСТЬNULL(ТекущиеКадровыеДанныеСотрудников.ТекущаяОрганизация, ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)) КАК ТекущаяОрганизацияПредыдущая,
	|	ЕСТЬNULL(ТекущиеКадровыеДанныеСотрудников.ТекущееПодразделение, ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)) КАК ТекущееПодразделениеПредыдущее,
	|	ЕСТЬNULL(ТекущиеКадровыеДанныеСотрудников.ТекущаяДолжность, ЗНАЧЕНИЕ(Справочник.Должности.ПустаяСсылка)) КАК ТекущаяДолжностьПредыдущая,
	|	ЕСТЬNULL(ТекущиеКадровыеДанныеСотрудников.ДатаПриема, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаПриемаПредыдущая,
	|	ЕСТЬNULL(ТекущиеКадровыеДанныеСотрудников.ДатаУвольнения, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаУвольненияПредыдущая,
	|	ЕСТЬNULL(ТекущиеКадровыеДанныеСотрудников.ТекущийВидЗанятости, ДАТАВРЕМЯ(1, 1, 1)) КАК ВидЗанятостиПредыдущий
	|ПОМЕСТИТЬ ВТНовыеКадровыеДанные
	|ИЗ
	|	Справочник.Сотрудники КАК Сотрудники
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТекущиеКадровыеДанныеСотрудников КАК ТекущиеКадровыеДанныеСотрудников
	|		ПО Сотрудники.Ссылка = ТекущиеКадровыеДанныеСотрудников.Сотрудник
	|			И Сотрудники.ФизическоеЛицо = ТекущиеКадровыеДанныеСотрудников.ФизическоеЛицо
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКадроваяИсторияСотрудниковСрезПоследних КАК КадроваяИсторияСотрудниковСрезПоследних
	|		ПО Сотрудники.Ссылка = КадроваяИсторияСотрудниковСрезПоследних.Сотрудник
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКадроваяИсторияСотрудников КАК КадроваяИсторияСотрудниковПрием
	|		ПО Сотрудники.Ссылка = КадроваяИсторияСотрудниковПрием.Сотрудник
	|			И (КадроваяИсторияСотрудниковПрием.ВидСобытия = ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Прием))
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКадроваяИсторияСотрудников КАК КадроваяИсторияСотрудниковУвольнение
	|		ПО Сотрудники.Ссылка = КадроваяИсторияСотрудниковУвольнение.Сотрудник
	|			И (КадроваяИсторияСотрудниковУвольнение.ВидСобытия = ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Увольнение))
	|ГДЕ
	|	Сотрудники.Ссылка В(&МассивСотрудниковДляОбновления)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НовыеКадровыеДанные.Сотрудник,
	|	НовыеКадровыеДанные.ТекущаяОрганизация,
	|	НовыеКадровыеДанные.ТекущееПодразделение,
	|	НовыеКадровыеДанные.ТекущаяДолжность,
	|	НовыеКадровыеДанные.ДатаПриема,
	|	НовыеКадровыеДанные.ДатаУвольнения,
	|	НовыеКадровыеДанные.ТекущийВидЗанятости,
	|	НовыеКадровыеДанные.ФизическоеЛицо,
	|	НовыеКадровыеДанные.ГоловнаяОрганизация
	|ИЗ
	|	ВТНовыеКадровыеДанные КАК НовыеКадровыеДанные
	|ГДЕ
	|	(НовыеКадровыеДанные.ТекущаяОрганизация <> НовыеКадровыеДанные.ТекущаяОрганизацияПредыдущая
	|			ИЛИ НовыеКадровыеДанные.ТекущееПодразделение <> НовыеКадровыеДанные.ТекущееПодразделениеПредыдущее
	|			ИЛИ НовыеКадровыеДанные.ТекущаяДолжность <> НовыеКадровыеДанные.ТекущаяДолжностьПредыдущая
	|			ИЛИ НовыеКадровыеДанные.ДатаПриема <> НовыеКадровыеДанные.ДатаПриемаПредыдущая
	|			ИЛИ НовыеКадровыеДанные.ДатаУвольнения <> НовыеКадровыеДанные.ДатаУвольненияПредыдущая
	|			ИЛИ НовыеКадровыеДанные.ТекущийВидЗанятости <> НовыеКадровыеДанные.ВидЗанятостиПредыдущий)";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			НаборЗаписей = РегистрыСведений.ТекущиеКадровыеДанныеСотрудников.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Сотрудник.Установить(Выборка.Сотрудник, Истина); 
			Запись = НаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(Запись, Выборка);
			
			НаборЗаписей.Записать();
			
		КонецЦикла;
	
	КонецЕсли;
			
КонецПроцедуры

Процедура ОбновитьТекущиеТарифныеСтавки(МассивСотрудниковДляОбновления) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("МассивСотрудниковДляОбновления", МассивСотрудниковДляОбновления);
	
	// Подготовим таблицу ИзмеренияДаты для получения среза последних.
	ИзмеренияДаты = Новый ТаблицаЗначений;
	ИзмеренияДаты.Колонки.Добавить("Сотрудник", Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
	ИзмеренияДаты.Колонки.Добавить("Период", Новый ОписаниеТипов("Дата"));
	
	Для Каждого Сотрудник Из МассивСотрудниковДляОбновления Цикл
		СтрокаИзмеренияДаты = ИзмеренияДаты.Добавить();
		СтрокаИзмеренияДаты.Сотрудник = Сотрудник;
	КонецЦикла;
	
	// Получим последние значения
	ЗарплатаКадрыОбщиеНаборыДанных.СоздатьВТИмяРегистраСрезПоследних(
		"ПлановыеНачисления",
		Запрос.МенеджерВременныхТаблиц,
		Истина,
		ЗарплатаКадрыОбщиеНаборыДанных.ОписаниеФильтраДляСоздатьВТИмяРегистра(ИзмеренияДаты));
	
	ЗарплатаКадрыОбщиеНаборыДанных.СоздатьВТИмяРегистраСрезПоследних(
		"ПлановыеАвансы",
		Запрос.МенеджерВременныхТаблиц,
		Истина,
		ЗарплатаКадрыОбщиеНаборыДанных.ОписаниеФильтраДляСоздатьВТИмяРегистра(ИзмеренияДаты));
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ПлановыеНачисленияСрезПоследних.Сотрудник КАК Сотрудник,
		|	СУММА(ПлановыеНачисленияСрезПоследних.Размер) КАК Размер
		|ПОМЕСТИТЬ ВТРазмерНачисленийСотрудников
		|ИЗ
		|	ВТПлановыеНачисленияСрезПоследних КАК ПлановыеНачисленияСрезПоследних
		|ГДЕ
		|	ПлановыеНачисленияСрезПоследних.Размер <> 0
		|
		|СГРУППИРОВАТЬ ПО
		|	ПлановыеНачисленияСрезПоследних.Сотрудник
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Сотрудники.Ссылка КАК Сотрудник,
		|	Сотрудники.ФизическоеЛицо КАК ФизическоеЛицо,
		|	Сотрудники.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
		|	ЕСТЬNULL(РазмерНачисленийСотрудников.Размер, 0) КАК ТекущаяТарифнаяСтавка,
		|	ЕСТЬNULL(ПлановыеАвансы.СпособРасчетаАванса, ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаАванса.ПустаяСсылка)) КАК ТекущийСпособРасчетаАванса,
		|	ЕСТЬNULL(ПлановыеАвансы.Аванс, 0) КАК ТекущийАванс,
		|	ЕСТЬNULL(ТекущаяТарифнаяСтавкаСотрудников.ТекущаяТарифнаяСтавка, 0) КАК ТекущаяТарифнаяСтавкаПрежняя,
		|	ЕСТЬNULL(ТекущаяТарифнаяСтавкаСотрудников.ТекущийСпособРасчетаАванса, ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаАванса.ПустаяСсылка)) КАК ТекущийСпособРасчетаАвансаПрежняя,
		|	ЕСТЬNULL(ТекущаяТарифнаяСтавкаСотрудников.ТекущийАванс, 0) КАК ТекущийАвансПрежний,
		|	ЕСТЬNULL(ТекущиеКадровыеДанныеСотрудников.ТекущаяОрганизация, ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)) КАК ТекущаяОрганизация
		|ПОМЕСТИТЬ ВТНовыеКадровыеДанные
		|ИЗ
		|	Справочник.Сотрудники КАК Сотрудники
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТекущаяТарифнаяСтавкаСотрудников КАК ТекущаяТарифнаяСтавкаСотрудников
		|		ПО Сотрудники.Ссылка = ТекущаяТарифнаяСтавкаСотрудников.Сотрудник
		|			И Сотрудники.ФизическоеЛицо = ТекущаяТарифнаяСтавкаСотрудников.ФизическоеЛицо
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТекущиеКадровыеДанныеСотрудников КАК ТекущиеКадровыеДанныеСотрудников
		|		ПО Сотрудники.Ссылка = ТекущиеКадровыеДанныеСотрудников.Сотрудник
		|			И Сотрудники.ФизическоеЛицо = ТекущиеКадровыеДанныеСотрудников.ФизическоеЛицо
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТРазмерНачисленийСотрудников КАК РазмерНачисленийСотрудников
		|		ПО Сотрудники.Ссылка = РазмерНачисленийСотрудников.Сотрудник
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПлановыеАвансыСрезПоследних КАК ПлановыеАвансы
		|		ПО Сотрудники.Ссылка = ПлановыеАвансы.Сотрудник
		|ГДЕ
		|	Сотрудники.Ссылка В(&МассивСотрудниковДляОбновления)
		|	И ТекущиеКадровыеДанныеСотрудников.ДатаУвольнения = ДАТАВРЕМЯ(1, 1, 1)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НовыеКадровыеДанные.Сотрудник,
		|	НовыеКадровыеДанные.ФизическоеЛицо,
		|	НовыеКадровыеДанные.ГоловнаяОрганизация,
		|	НовыеКадровыеДанные.ТекущаяОрганизация,
		|	НовыеКадровыеДанные.ТекущаяТарифнаяСтавка,
		|	НовыеКадровыеДанные.ТекущийСпособРасчетаАванса,
		|	НовыеКадровыеДанные.ТекущийАванс
		|ИЗ
		|	ВТНовыеКадровыеДанные КАК НовыеКадровыеДанные
		|ГДЕ
		|	(НовыеКадровыеДанные.ТекущаяТарифнаяСтавка <> НовыеКадровыеДанные.ТекущаяТарифнаяСтавкаПрежняя
		|			ИЛИ НовыеКадровыеДанные.ТекущийСпособРасчетаАванса <> НовыеКадровыеДанные.ТекущийСпособРасчетаАвансаПрежняя
		|			ИЛИ НовыеКадровыеДанные.ТекущийАванс <> НовыеКадровыеДанные.ТекущийАвансПрежний)";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			НаборЗаписей = РегистрыСведений.ТекущаяТарифнаяСтавкаСотрудников.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Сотрудник.Установить(Выборка.Сотрудник, Истина); 
			Запись = НаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(Запись, Выборка);
			
			НаборЗаписей.Записать();
			
		КонецЦикла;
	
	КонецЕсли;
	
КонецПроцедуры

Функция ЗапросВТТарифныеСтавкиСотрудников(ТолькоРазрешенные, ИмяВТТарифныеСтавкиСотрудников, ИмяВременнойТаблицыОтборовСотрудников, ПоляОтбораСотрудников, ПоляОтбораПериодическихДанных) Экспорт

	Запрос = Новый Запрос;
	ИмяВТПлановыеНачисленияСрезПоследних = ЗарплатаКадрыОбщиеНаборыДанных.УникальноеИмяТекстаЗапроса("ВТПлановыеНачисленияСрезПоследних");
	
	ОписаниеФильтра = ЗарплатаКадрыОбщиеНаборыДанных.ОписаниеФильтраДляСоздатьВТИмяРегистра(
		ИмяВременнойТаблицыОтборовСотрудников, "Период,Сотрудник");
	
	ОписаниеФильтра.СоответствиеИзмеренийРегистраИзмерениямФильтра.Вставить("Период", ПоляОтбораСотрудников.Период);
	ОписаниеФильтра.СоответствиеИзмеренийРегистраИзмерениямФильтра.Вставить("Сотрудник", ПоляОтбораСотрудников.Сотрудник);
	
	ПараметрыПостроения = ЗарплатаКадрыОбщиеНаборыДанных.ПараметрыПостроенияДляСоздатьВТИмяРегистраСрез();
	
	ПоляОтбора = Неопределено;
	Если ПоляОтбораПериодическихДанных <> Неопределено Тогда
		ПоляОтбораПериодическихДанных.Свойство("ПлановыеНачисления", ПоляОтбора);
	КонецЕсли;
	
	ПараметрыПостроения.Отборы = ПоляОтбора;
		 
	ЗапросВТИмяРегистраСрез = ЗарплатаКадрыОбщиеНаборыДанных.ЗапросВТИмяРегистраСрез(
		"ПлановыеНачисления",
		ТолькоРазрешенные,
		ОписаниеФильтра,
		ПараметрыПостроения,
		Истина,
		ИмяВТПлановыеНачисленияСрезПоследних);
		
	ЗарплатаКадрыОбщиеНаборыДанных.СкопироватьПараметрыЗапроса(Запрос, ЗапросВТИмяРегистраСрез);
	
	ТекстЗапросаВТТарифнаяСтавкаСотрудников =
		"ВЫБРАТЬ
		|	ПлановыеНачисленияСрезПоследних.Период,
		|	ПлановыеНачисленияСрезПоследних.Сотрудник,
		|	СУММА(ПлановыеНачисленияСрезПоследних.Размер) КАК ТарифнаяСтавка,
		|	0 КАК Надбавка,
		|	СУММА(ПлановыеНачисленияСрезПоследних.Размер) КАК ФОТ
		|ПОМЕСТИТЬ ВТТарифныеСтавкиСотрудников
		|ИЗ
		|	ВТПлановыеНачисленияСрезПоследних КАК ПлановыеНачисленияСрезПоследних
		|
		|СГРУППИРОВАТЬ ПО
		|	ПлановыеНачисленияСрезПоследних.Период,
		|	ПлановыеНачисленияСрезПоследних.Сотрудник";
		
	ТекстЗапросаВТТарифнаяСтавкаСотрудников = СтрЗаменить(ТекстЗапросаВТТарифнаяСтавкаСотрудников, "ВТПлановыеНачисленияСрезПоследних", ИмяВТПлановыеНачисленияСрезПоследних);
	ТекстЗапросаВТТарифнаяСтавкаСотрудников = СтрЗаменить(ТекстЗапросаВТТарифнаяСтавкаСотрудников, "ВТТарифныеСтавкиСотрудников", ИмяВТТарифныеСтавкиСотрудников);
		
	ТекстЗапроса = 
		ЗапросВТИмяРегистраСрез.Текст +
		ЗарплатаКадрыОбщиеНаборыДанных.РазделительЗапросов() +
		ТекстЗапросаВТТарифнаяСтавкаСотрудников;
	
	Запрос.Текст = ТекстЗапроса;
	
	Возврат Запрос;
	
КонецФункции

Процедура ПеренестиТекущиеКадровыеДанныеСотрудниковИзСправочникаВРегистры() Экспорт
	
	Запрос = Новый Запрос;
	
	// Проверка наличия записей
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ТекущиеКадровыеДанныеСотрудников.Сотрудник
		|ИЗ
		|	РегистрСведений.ТекущиеКадровыеДанныеСотрудников КАК ТекущиеКадровыеДанныеСотрудников";
		
	РезультатЗапроса = Запрос.Выполнить();
	
	// Заполнение регистра, если записей нет.
	Если РезультатЗапроса.Пустой() Тогда
		
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	Сотрудники.ФизическоеЛицо,
			|	Сотрудники.ГоловнаяОрганизация,
			|	Сотрудники.Ссылка,
			|	Сотрудники.УдалитьТекущаяОрганизация КАК ТекущаяОрганизация,
			|	Сотрудники.УдалитьТекущееПодразделение КАК ТекущееПодразделение,
			|	Сотрудники.УдалитьТекущаяДолжность КАК ТекущаяДолжность,
			|	Сотрудники.УдалитьТекущаяТарифнаяСтавка КАК ТекущаяТарифнаяСтавка,
			|	Сотрудники.УдалитьДатаПриема КАК ДатаПриема,
			|	Сотрудники.УдалитьДатаУвольнения КАК ДатаУвольнения,
			|	Сотрудники.УдалитьОсновноеРабочееМестоВОрганизации КАК ОсновноеРабочееМестоВОрганизации
			|ИЗ
			|	Справочник.Сотрудники КАК Сотрудники";
			
		Результат = Запрос.Выполнить();
		Если Не Результат.Пустой() Тогда
			
			Выборка = Результат.Выбрать();
			
			ТекущиеКадровыеДанныеНаборЗаписей = РегистрыСведений.ТекущиеКадровыеДанныеСотрудников.СоздатьНаборЗаписей();
			ТекущаяТарифнаяСтавкаНаборЗаписей = РегистрыСведений.ТекущаяТарифнаяСтавкаСотрудников.СоздатьНаборЗаписей();
			
			Пока Выборка.Следующий() Цикл
				
				Строка = ТекущиеКадровыеДанныеНаборЗаписей.Добавить();
				Строка.Сотрудник = Выборка.Ссылка; 
				Строка.ГоловнаяОрганизация = Выборка.ГоловнаяОрганизация; 
				Строка.ФизическоеЛицо = Выборка.ФизическоеЛицо; 
				Строка.ТекущаяОрганизация = Выборка.ТекущаяОрганизация; 
				Строка.ТекущееПодразделение = Выборка.ТекущееПодразделение; 
				Строка.ТекущаяДолжность = Выборка.ТекущаяДолжность; 
				Строка.ДатаПриема = Выборка.ДатаПриема; 
				Строка.ДатаУвольнения = Выборка.ДатаУвольнения;
				Строка.ОсновноеРабочееМестоВОрганизации = Выборка.ОсновноеРабочееМестоВОрганизации;
				
				Строка = ТекущаяТарифнаяСтавкаНаборЗаписей.Добавить();
				Строка.Сотрудник = Выборка.Ссылка; 
				Строка.ФизическоеЛицо = Выборка.ФизическоеЛицо; 
				Строка.ГоловнаяОрганизация = Выборка.ГоловнаяОрганизация; 
				Строка.ТекущаяОрганизация = Выборка.ТекущаяОрганизация; 
				Строка.ТекущаяТарифнаяСтавка = Выборка.ТекущаяТарифнаяСтавка; 
				
			КонецЦикла;	
			
			ТекущиеКадровыеДанныеНаборЗаписей.ОбменДанными.Загрузка = Истина;
			ТекущиеКадровыеДанныеНаборЗаписей.Записать();
			
			ТекущаяТарифнаяСтавкаНаборЗаписей.ОбменДанными.Загрузка = Истина;
			ТекущаяТарифнаяСтавкаНаборЗаписей.Записать();
			
		КонецЕсли;
		
	КонецЕсли; 
	
КонецПроцедуры

Процедура ПеренестиТекущиеВидыЗанятостиСправочниковВРегистр() Экспорт
	
	Запрос = Новый Запрос;
	
	// Проверка наличия записей с незаполненным видом занятости.
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ТекущиеКадровыеДанныеСотрудников.Сотрудник
		|ИЗ
		|	РегистрСведений.ТекущиеКадровыеДанныеСотрудников КАК ТекущиеКадровыеДанныеСотрудников
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Сотрудники КАК Сотрудники
		|		ПО ТекущиеКадровыеДанныеСотрудников.Сотрудник = Сотрудники.Ссылка
		|			И ТекущиеКадровыеДанныеСотрудников.ФизическоеЛицо = Сотрудники.ФизическоеЛицо
		|ГДЕ
		|	ТекущиеКадровыеДанныеСотрудников.ТекущийВидЗанятости = ЗНАЧЕНИЕ(Перечисление.ВидыЗанятости.ПустаяСсылка)
		|	И Сотрудники.УдалитьВидЗанятости <> ЗНАЧЕНИЕ(Перечисление.ВидыЗанятости.ПустаяСсылка)";
		
	РезультатЗапроса = Запрос.Выполнить();
	
	// Заполнение вида занятости, если есть записи с незаполненным видом занятости.
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ТекущиеКадровыеДанныеСотрудников.Сотрудник,
			|	ТекущиеКадровыеДанныеСотрудников.ФизическоеЛицо КАК ФизическоеЛицо,
			|	ТекущиеКадровыеДанныеСотрудников.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
			|	ТекущиеКадровыеДанныеСотрудников.ТекущаяОрганизация,
			|	ТекущиеКадровыеДанныеСотрудников.ТекущееПодразделение,
			|	ТекущиеКадровыеДанныеСотрудников.ТекущаяДолжность,
			|	ТекущиеКадровыеДанныеСотрудников.ДатаПриема,
			|	ТекущиеКадровыеДанныеСотрудников.ДатаУвольнения,
			|	ТекущиеКадровыеДанныеСотрудников.ОсновноеРабочееМестоВОрганизации,
			|	ВЫБОР
			|		КОГДА ТекущиеКадровыеДанныеСотрудников.ТекущийВидЗанятости = ЗНАЧЕНИЕ(Перечисление.ВидыЗанятости.ПустаяСсылка)
			|			ТОГДА Сотрудники.УдалитьВидЗанятости
			|		ИНАЧЕ ТекущиеКадровыеДанныеСотрудников.ТекущийВидЗанятости
			|	КОНЕЦ КАК ТекущийВидЗанятости
			|ИЗ
			|	РегистрСведений.ТекущиеКадровыеДанныеСотрудников КАК ТекущиеКадровыеДанныеСотрудников
			|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Сотрудники КАК Сотрудники
			|		ПО ТекущиеКадровыеДанныеСотрудников.Сотрудник = Сотрудники.Ссылка
			|			И ТекущиеКадровыеДанныеСотрудников.ФизическоеЛицо = Сотрудники.ФизическоеЛицо";
					   
		РезультатЗапроса = Запрос.Выполнить();
		
		Если Не РезультатЗапроса.Пустой() Тогда
			
			Набор = РегистрыСведений.ТекущиеКадровыеДанныеСотрудников.СоздатьНаборЗаписей();
			Набор.Загрузить(РезультатЗапроса.Выгрузить());
			
			Набор.ОбменДанными.Загрузка = Истина;
			Набор.Записать();
			
		КонецЕсли;
		
	КонецЕсли; 
	
КонецПроцедуры

Процедура ОбновитьСобытияУвольнений(МенеджерВременныхТаблиц) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Регистраторы.Период КАК Период,
		|	Регистраторы.Регистратор КАК Регистратор,
		|	КадроваяИсторияСотрудников.*
		|ИЗ
		|	РегистрСведений.КадроваяИсторияСотрудников КАК КадроваяИсторияСотрудников
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТРегистраторы КАК Регистраторы
		|		ПО КадроваяИсторияСотрудников.Регистратор = Регистраторы.Регистратор
		|
		|УПОРЯДОЧИТЬ ПО
		|	Регистратор";
		
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Набор = РегистрыСведений.КадроваяИсторияСотрудников.СоздатьНаборЗаписей();
		Набор.Отбор.Регистратор.Установить(Выборка.Регистратор);
		
		ЗаполнитьЗначенияСвойств(Набор.Добавить(), Выборка, , "Период1,Регистратор1");
		
		Набор.ДополнительныеСвойства.Вставить("ОтключитьПроверкуДатыЗапретаИзменения", Истина);
		Набор.ОбменДанными.Загрузка = Истина;
		Набор.Записать();
		
	КонецЦикла; 
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Регистраторы.Период КАК Период,
		|	Регистраторы.Регистратор КАК Регистратор,
		|	ПлановыеНачисления.*
		|ИЗ
		|	РегистрСведений.ПлановыеНачисления КАК ПлановыеНачисления
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТРегистраторы КАК Регистраторы
		|		ПО ПлановыеНачисления.Регистратор = Регистраторы.Регистратор
		|ИТОГИ ПО
		|	Регистратор";
	
	ВыборкаПоРегистраторам = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаПоРегистраторам.Следующий() Цикл
		
		Набор = РегистрыСведений.ПлановыеНачисления.СоздатьНаборЗаписей();
		Набор.Отбор.Регистратор.Установить(ВыборкаПоРегистраторам.Регистратор);
		
		Выборка = ВыборкаПоРегистраторам.Выбрать();
		Пока Выборка.Следующий() Цикл
			ЗаполнитьЗначенияСвойств(Набор.Добавить(), Выборка, , "Период1,Регистратор1");
		КонецЦикла;
		
		Набор.ДополнительныеСвойства.Вставить("ОтключитьПроверкуДатыЗапретаИзменения", Истина);
		Набор.ОбменДанными.Загрузка = Истина;
		Набор.Записать();
		
	КонецЦикла; 
	
КонецПроцедуры

#КонецОбласти

#Область КадровыеДанныеФизическихЛиц

Функция НеобходимыКадровыеДанныеФизическогоЛица(Знач ИмяПоля) Экспорт
	
	ИмяПоля = ВРег(ИмяПоля);
	
	ВозвращаемоеЗначение = Ложь;
	
	Возврат КадровыйУчет.ЭтоОбязательноеПолеКадровыхДанныхФизическогоЛица(ИмяПоля)
		ИЛИ КадровыйУчет.НеобходимыПостоянныеКадровыеДанныеФизическихЛиц(ИмяПоля)
		ИЛИ НеобходимыСведенияОСтатусахЗастрахованныхЛиц(ИмяПоля)
		ИЛИ НеобходимыСведенияОГражданстве(ИмяПоля)
		ИЛИ НеобходимыСведенияОбИнвалидности(ИмяПоля)
		ИЛИ НеобходимыСведенияОСтатусахНалогоплательщиков(ИмяПоля)
		ИЛИ НеобходимыСведенияДокументовУдостоверяющихЛичность(ИмяПоля)
		ИЛИ НеобходимыСведенияОФИОФизическихЛиц(ИмяПоля)
		ИЛИ НеобходимаКонтактнаяИнформацияФизическогоЛица(ИмяПоля);
		
КонецФункции
	
Функция НеобходимыПостоянныеКадровыеДанныеФизическихЛиц(Знач ИмяПоля) Экспорт
	
	Возврат КадровыйУчет.НеобходимыЗначенияРеквизитовСправочникаФизическиеЛица(ИмяПоля)
		ИЛИ КадровыйУчет.НеобходимаКонтактнаяИнформацияФизическогоЛица(ИмяПоля);
	
КонецФункции
	
Функция ЗапросВТКадровыеДанныеФизическихЛиц(ТолькоРазрешенные, ОписательВременнойТаблицыОтборов, КадровыеДанные, ПоляОтбораПериодическихДанных, ИмяВТКадровыеДанныеФизическихЛиц) Экспорт
	
	ТекстОписанияПолей = "";
	ИсточникиДанных = Новый Соответствие;
	
	Если ТипЗнч(КадровыеДанные) = Тип("Массив") Тогда
		ИменаКадровыхДанных = КадровыеДанные;
	Иначе
		ИменаКадровыхДанных = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(КадровыеДанные);
	КонецЕсли;
	
	Для каждого ИмяЗапрашиваемыхДанных Из ИменаКадровыхДанных Цикл
		
		ИмяКадровыхДанных = СокрЛП(ИмяЗапрашиваемыхДанных);
		
		Если КадровыйУчет.ЭтоОбязательноеПолеКадровыхДанныхФизическогоЛица(ИмяКадровыхДанных) Тогда
			
			 Продолжить;
			 
		ИначеЕсли ДобавитьПолеПостоянныхКадровыхДанных(ИмяКадровыхДанных, ТекстОписанияПолей, ИсточникиДанных)
			ИЛИ ДобавитьПолеСведенийОДокументахУдостоверяющихЛичность(ИмяКадровыхДанных, ТекстОписанияПолей, ИсточникиДанных)
			ИЛИ ДобавитьПолеСведенийОбИнвалидности(ИмяКадровыхДанных, ТекстОписанияПолей, ИсточникиДанных)
			ИЛИ ДобавитьПолеСведенийОГражданстве(ИмяКадровыхДанных, ТекстОписанияПолей, ИсточникиДанных)
			ИЛИ ДобавитьПолеСведенийОФИОФизическихЛиц(ИмяКадровыхДанных, ТекстОписанияПолей, ИсточникиДанных)
			ИЛИ ДобавитьПолеСведенийОСтатусахЗастрахованныхЛиц(ИмяКадровыхДанных, ТекстОписанияПолей, ИсточникиДанных)
			ИЛИ ДобавитьПолеСведенийОСтатусахНалогоплательщиков(ИмяКадровыхДанных, ТекстОписанияПолей, ИсточникиДанных) Тогда
				
			Продолжить;
			
		Иначе
			
			ТекстСообщенияИсключения = НСтр("ru='Среди кадровых данных физических лиц нет данных с именем'") + " """ + ИмяКадровыхДанных + """";
			ВызватьИсключение ТекстСообщенияИсключения;
			
		КонецЕсли; 
		
	КонецЦикла;
	
	Запрос = Новый Запрос;
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	ТаблицаОтборовФизическихЛиц.Период КАК Период,
		|	ТаблицаОтборовФизическихЛиц.ФизическоеЛицо КАК ФизическоеЛицо
		|ПОМЕСТИТЬ ВТКадровыеДанныеФизическихЛиц
		|ИЗ
		|	ВТОтборовФизическихЛиц КАК ТаблицаОтборовФизическихЛиц";
		
	КадровыйУчет.ДобавитьВТекстЗапросаОписаниеПолей(Запрос.Текст, ТекстОписанияПолей, "ПОМЕСТИТЬ ВТКадровыеДанныеФизическихЛиц");
		
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТОтборовФизическихЛиц", ОписательВременнойТаблицыОтборов.ИмяВременнойТаблицыОтборовФизическихЛиц);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ТаблицаОтборовФизическихЛиц.ФизическоеЛицо", "ТаблицаОтборовФизическихЛиц." + ОписательВременнойТаблицыОтборов.ИмяПоляФизическоеЛицо);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ТаблицаОтборовФизическихЛиц.Период", "ТаблицаОтборовФизическихЛиц." + ОписательВременнойТаблицыОтборов.ИмяПоляПериод);
	
	ЗарплатаКадрыОбщиеНаборыДанных.ЗаменитьИмяСоздаваемойВременнойТаблицы(Запрос.Текст, "ВТКадровыеДанныеФизическихЛиц", ИмяВТКадровыеДанныеФизическихЛиц);
	ЗарплатаКадрыОбщиеНаборыДанных.УстановитьВыборкуТолькоРазрешенныхДанных(Запрос.Текст, ТолькоРазрешенные);
	
	ДобавитьТекстЗапросаВТПостоянныеКадровыеДанныеФизическихЛиц(Запрос, ТолькоРазрешенные, ОписательВременнойТаблицыОтборов, ИсточникиДанных);
	ДобавитьТекстЗапросаВТДокументыУдостоверяющиеЛичность(Запрос, ТолькоРазрешенные, ОписательВременнойТаблицыОтборов, ИсточникиДанных);
	ДобавитьТекстЗапросаВТСведенияОбИнвалидности(Запрос, ТолькоРазрешенные, ОписательВременнойТаблицыОтборов, ПоляОтбораПериодическихДанных, ИсточникиДанных);
	ДобавитьТекстЗапросаВТСведенияОГражданстве(Запрос, ТолькоРазрешенные, ОписательВременнойТаблицыОтборов, ПоляОтбораПериодическихДанных, ИсточникиДанных);
	ДобавитьТекстЗапросаВТСведенияОФИОФизическихЛиц(Запрос, ТолькоРазрешенные, ОписательВременнойТаблицыОтборов, ПоляОтбораПериодическихДанных, ИсточникиДанных);
	ДобавитьТекстЗапросаВТСведенияОСтатусахЗастрахованныхЛиц(Запрос, ТолькоРазрешенные, ОписательВременнойТаблицыОтборов, ПоляОтбораПериодическихДанных, ИсточникиДанных);
	ДобавитьТекстЗапросаВТСведенияОСтатусахНалогоплательщиков(Запрос, ТолькоРазрешенные, ОписательВременнойТаблицыОтборов, ПоляОтбораПериодическихДанных, ИсточникиДанных);
	
	Возврат Запрос;
	
КонецФункции

Функция ЗапросВТПостоянныеКадровыеДанныеФизическихЛиц(ТолькоРазрешенные, ОписательВременнойТаблицыОтборов, КадровыеДанные, ИмяВТПостоянныеКадровыеДанныеФизическихЛиц) Экспорт
	
	ТекстОписанияПолей = "";
	ИсточникиДанных = Новый Соответствие;
	
	Если ТипЗнч(КадровыеДанные) = Тип("Массив") Тогда
		ИменаКадровыхДанных = КадровыеДанные;
	Иначе
		ИменаКадровыхДанных = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(КадровыеДанные);
	КонецЕсли;
	
	Для каждого ИмяЗапрашиваемыхДанных Из ИменаКадровыхДанных Цикл
		
		ИмяКадровыхДанных = СокрЛП(ИмяЗапрашиваемыхДанных);
		
		Если КадровыйУчет.ЭтоОбязательноеПолеКадровыхДанныхФизическогоЛица(ИмяКадровыхДанных) Тогда
			
			Продолжить;
			
		ИначеЕсли ДобавитьПолеРеквизитаСправочникаФизическиеЛица(ИмяКадровыхДанных, ТекстОписанияПолей, ИсточникиДанных)
			ИЛИ ДобавитьПолеКонтактнойИнформацииФизическихЛиц(ИмяКадровыхДанных, ТекстОписанияПолей, ИсточникиДанных) Тогда
			
			Продолжить;
			
		Иначе
			
			ТекстСообщенияИсключения = НСтр("ru='Среди кадровых данных физических лиц нет данных с именем'") + " """ + ИмяКадровыхДанных + """";
			ВызватьИсключение ТекстСообщенияИсключения;
			
		КонецЕсли; 
		
	КонецЦикла;
	
	Запрос = Новый Запрос;
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	ТаблицаОтборовФизическихЛиц.ФизическоеЛицо КАК ФизическоеЛицо
		|ПОМЕСТИТЬ ВТПостоянныеКадровыеДанныеФизическихЛиц
		|ИЗ
		|	ВТОтборовФизическихЛиц КАК ТаблицаОтборовФизическихЛиц";
		
	КадровыйУчет.ДобавитьВТекстЗапросаОписаниеПолей(Запрос.Текст, ТекстОписанияПолей, "ПОМЕСТИТЬ ВТПостоянныеКадровыеДанныеФизическихЛиц");
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТОтборовФизическихЛиц", ОписательВременнойТаблицыОтборов.ИмяВременнойТаблицыОтборовФизическихЛиц);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ТаблицаОтборовФизическихЛиц.ФизическоеЛицо", "ТаблицаОтборовФизическихЛиц." + ОписательВременнойТаблицыОтборов.ИмяПоляФизическоеЛицо);
	
	ЗарплатаКадрыОбщиеНаборыДанных.ЗаменитьИмяСоздаваемойВременнойТаблицы(Запрос.Текст, "ВТПостоянныеКадровыеДанныеФизическихЛиц", ИмяВТПостоянныеКадровыеДанныеФизическихЛиц);
	ЗарплатаКадрыОбщиеНаборыДанных.УстановитьВыборкуТолькоРазрешенныхДанных(Запрос.Текст, ТолькоРазрешенные);
	
	ДобавитьТекстЗапросаДанныхРеквизитовСправочникаФизическиеЛица(Запрос, ОписательВременнойТаблицыОтборов, ИсточникиДанных);
	ДобавитьТекстЗапросаВТКонтактнаяИнформация(Запрос, ТолькоРазрешенные, ОписательВременнойТаблицыОтборов, ИсточникиДанных);
	
	Возврат Запрос;
		
КонецФункции

Функция ДобавитьПолеПостоянныхКадровыхДанных(ИмяПоля, ТекстОписанияПолей, ИсточникиДанных)
	
	ДобавленоПолеСведений = Ложь;
	
	Если КадровыйУчет.НеобходимыПостоянныеКадровыеДанныеФизическихЛиц(ИмяПоля) Тогда
		
		ДобавленоПолеСведений = Истина;
		
		ПоляПостоянныхКадровыхДанных = ИсточникиДанных.Получить("ПостоянныеКадровыеДанныеФизическихЛиц");
		Если ПоляПостоянныхКадровыхДанных = Неопределено Тогда
			ПоляПостоянныхКадровыхДанных = Новый Массив;
		КонецЕсли;
		ПоляПостоянныхКадровыхДанных.Добавить(ИмяПоля);
		
		ИсточникиДанных.Вставить("ПостоянныеКадровыеДанныеФизическихЛиц", ПоляПостоянныхКадровыхДанных);
		
		ПутьКДанным = "	ПостоянныеКадровыеДанные." + ИмяПоля;
		ТекстОписанияПолей = ТекстОписанияПолей + "," + Символы.ПС + ПутьКДанным + " КАК " + ИмяПоля;
		
	КонецЕсли;
	
	Возврат ДобавленоПолеСведений;
	
КонецФункции

Процедура ДобавитьТекстЗапросаВТПостоянныеКадровыеДанныеФизическихЛиц(Запрос, ТолькоРазрешенные, ОписательВременнойТаблицыОтборов, ИсточникиДанных)
	
	ПоляПостоянныхКадровыхДанных = ИсточникиДанных.Получить("ПостоянныеКадровыеДанныеФизическихЛиц");
	Если ПоляПостоянныхКадровыхДанных = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	ИмяВТПостоянныеКадровыеДанныеФизическихЛиц = ЗарплатаКадрыОбщиеНаборыДанных.УникальноеИмяТекстаЗапроса("ВТПостоянныеКадровыеДанныеФизическихЛиц");
	
	ЗапросВТ = КадровыйУчет.ЗапросВТПостоянныеКадровыеДанныеФизическихЛиц(
		ТолькоРазрешенные, ОписательВременнойТаблицыОтборов, ПоляПостоянныхКадровыхДанных, ИмяВТПостоянныеКадровыеДанныеФизическихЛиц);
		
	ЗарплатаКадрыОбщиеНаборыДанных.СкопироватьПараметрыЗапроса(Запрос, ЗапросВТ);
		
	Запрос.Текст = 
		ЗапросВТ.Текст
		+ ЗарплатаКадрыОбщиеНаборыДанных.РазделительЗапросов()
		+ Запрос.Текст;
		
	Запрос.Текст = Запрос.Текст + Символы.ПС
		+ "		{ЛЕВОЕ СОЕДИНЕНИЕ " + ИмяВТПостоянныеКадровыеДанныеФизическихЛиц + " КАК ПостоянныеКадровыеДанные
			|		ПО ТаблицаОтборовФизическихЛиц." + ОписательВременнойТаблицыОтборов.ИмяПоляФизическоеЛицо + " = ПостоянныеКадровыеДанные.ФизическоеЛицо}";
			
КонецПроцедуры

// Значения реквизитов справочника ФизическиеЛица.

Функция ДобавитьПолеРеквизитаСправочникаФизическиеЛица(ИмяПоля, ТекстОписанияПолей, ИсточникиДанных)
	
	ДобавленоПолеСведений = Ложь;
	Если КадровыйУчет.НеобходимыЗначенияРеквизитовСправочникаФизическиеЛица(ИмяПоля) Тогда
		
		ДобавленоПолеСведений = Истина;
		ИсточникиДанных.Вставить("РеквизитыСправочникаФизическиеЛица", Истина);
		
		ПутьКДанным = ПутьКДаннымЗначенийРеквизитовСправочникаФизическиеЛица(ИмяПоля);
		ТекстОписанияПолей = ТекстОписанияПолей + "," + Символы.ПС + ПутьКДанным + " КАК " + ИмяПоля;
		
	КонецЕсли;
	
	Возврат ДобавленоПолеСведений;
	
КонецФункции

Процедура ДобавитьТекстЗапросаДанныхРеквизитовСправочникаФизическиеЛица(Запрос, ОписательВременнойТаблицыОтборов, ИсточникиДанных)
	
	Если ИсточникиДанных.Получить("РеквизитыСправочникаФизическиеЛица") = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	Запрос.Текст = Запрос.Текст + Символы.ПС
		+ "		{ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ФизическиеЛица КАК СправочникФизическиеЛица
		|		ПО ТаблицаОтборовФизическихЛиц." + ОписательВременнойТаблицыОтборов.ИмяПоляФизическоеЛицо + " = СправочникФизическиеЛица.Ссылка}";
		
КонецПроцедуры

Функция НеобходимыЗначенияРеквизитовСправочникаФизическиеЛица(ИмяПоля) Экспорт
	
	ИмяПоля = ВРег(ИмяПоля);
	
	Возврат Метаданные.Справочники.ФизическиеЛица.Реквизиты.Найти(ИмяПоля) <> Неопределено
		ИЛИ ИмяПоля = ВРег("Наименование") 
		ИЛИ ИмяПоля = ВРег("ФизическоеЛицоНаименование") 
		ИЛИ ИмяПоля = ВРег("Код") 
		ИЛИ ИмяПоля = ВРег("ФизическоеЛицоКод");
			
КонецФункции

Функция ПутьКДаннымЗначенийРеквизитовСправочникаФизическиеЛица(Знач ИмяПоля)
	
	ИмяПоляВВерхнемРегистре = ВРег(ИмяПоля);
	
	ПутьКДанным = "";
	Если ИмяПоляВВерхнемРегистре = ВРег("ФизическоеЛицоНаименование") Тогда
		ПутьКДанным = "	СправочникФизическиеЛица.Наименование";
	ИначеЕсли ИмяПоляВВерхнемРегистре = ВРег("ФизическоеЛицоКод") Тогда
		ПутьКДанным = "	СправочникФизическиеЛица.Код";
	Иначе
		ПутьКДанным = "	СправочникФизическиеЛица." + ИмяПоля;
	КонецЕсли;
	
	Возврат ПутьКДанным;
	
КонецФункции

// Контактная информация

Функция ДобавитьПолеКонтактнойИнформацииФизическихЛиц(ИмяПоля, ТекстОписанияПолей, ИсточникиДанных)
	
	ДобавленоПолеСведений = Ложь;
	Если КадровыйУчет.НеобходимаКонтактнаяИнформацияФизическогоЛица(ИмяПоля) Тогда
		
		ДобавленоПолеСведений = Истина;
		
		ИменаТаблицКонтактнойИнформации = ИсточникиДанных.Получить("КонтактнаяИнформацияФизическихЛиц");
		Если ИменаТаблицКонтактнойИнформации = Неопределено Тогда
			ИменаТаблицКонтактнойИнформации = Новый Массив;
		КонецЕсли; 
		
		ПутьКДанным = ПутьКДаннымКонтактнойИнформацииФизическихЛиц(ИмяПоля, ИменаТаблицКонтактнойИнформации);
		ТекстОписанияПолей = ТекстОписанияПолей + "," + Символы.ПС + ПутьКДанным + " КАК " + ИмяПоля;
		
		ИсточникиДанных.Вставить("КонтактнаяИнформацияФизическихЛиц", ИменаТаблицКонтактнойИнформации);
		
	КонецЕсли;
	
	Возврат ДобавленоПолеСведений;
	
КонецФункции

Процедура ДобавитьТекстЗапросаВТКонтактнаяИнформация(Запрос, ТолькоРазрешенные, ОписательВременнойТаблицыОтборов, ИсточникиДанных)
	
	ИменаТаблицКонтактнойИнформации = ИсточникиДанных.Получить("КонтактнаяИнформацияФизическихЛиц");
	Если ИменаТаблицКонтактнойИнформации = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	ИмяВТКонтактнаяИнформация = ЗарплатаКадрыОбщиеНаборыДанных.УникальноеИмяТекстаЗапроса("ВТКонтактнаяИнформация");
	ВидыКонтактнойИнформацииЗапроса = "";
	
	Для каждого ИмяТаблицы Из ИменаТаблицКонтактнойИнформации Цикл
		
		ПредставлениеВидаКонтактнойИнформации = "ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации." + ИмяТаблицы + "ФизическиеЛица)";
		ВидыКонтактнойИнформацииЗапроса = ?(ПустаяСтрока(ВидыКонтактнойИнформацииЗапроса), "", ВидыКонтактнойИнформацииЗапроса + ", ") + ПредставлениеВидаКонтактнойИнформации;
		
		Запрос.Текст = Запрос.Текст + Символы.ПС
			+ "		{ЛЕВОЕ СОЕДИНЕНИЕ " + ИмяВТКонтактнаяИнформация + " КАК " + ИмяТаблицы + "ФизическиеЛица
				|		ПО ТаблицаОтборовФизическихЛиц." + ОписательВременнойТаблицыОтборов.ИмяПоляФизическоеЛицо + " = " + ИмяТаблицы + "ФизическиеЛица.Объект
				|			И " + ИмяТаблицы + "ФизическиеЛица.Вид = " + ПредставлениеВидаКонтактнойИнформации + "}";
				
	КонецЦикла;
	
	// Подготовка запроса, создающего временную таблицу - отбор по видам контактной информации.
	ИмяВТОтборКонтактнойИнформации = ЗарплатаКадрыОбщиеНаборыДанных.УникальноеИмяТекстаЗапроса("ВТОтборКонтактнойИнформации");
	
	ТекстЗапросаФильтраПоВидамКонтактнойИнформации = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	ТаблицаОтборовФизическихЛиц.ФизическоеЛицо КАК ФизическоеЛицо,
		|	ВидыКонтактнойИнформации.Ссылка КАК Вид
		|ПОМЕСТИТЬ ВТОтборКонтактнойИнформации
		|ИЗ
		|	ВТОтборовФизическихЛиц КАК ТаблицаОтборовФизическихЛиц
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыКонтактнойИнформации КАК ВидыКонтактнойИнформации
		|		ПО (ВидыКонтактнойИнформации.Ссылка В (&СписокВидовКонтактнойИнформации))";
		
	ТекстЗапросаФильтраПоВидамКонтактнойИнформации = СтрЗаменить(
		ТекстЗапросаФильтраПоВидамКонтактнойИнформации,
		"ВТОтборовФизическихЛиц",
		ОписательВременнойТаблицыОтборов.ИмяВременнойТаблицыОтборовФизическихЛиц);
		
	ТекстЗапросаФильтраПоВидамКонтактнойИнформации = СтрЗаменить(
		ТекстЗапросаФильтраПоВидамКонтактнойИнформации,
		"ВТОтборКонтактнойИнформации",
		ИмяВТОтборКонтактнойИнформации);
		
	ТекстЗапросаФильтраПоВидамКонтактнойИнформации = СтрЗаменить(
		ТекстЗапросаФильтраПоВидамКонтактнойИнформации,
		"ТаблицаОтборовФизическихЛиц.ФизическоеЛицо",
		"ТаблицаОтборовФизическихЛиц." + ОписательВременнойТаблицыОтборов.ИмяПоляФизическоеЛицо);

	ТекстЗапросаФильтраПоВидамКонтактнойИнформации = СтрЗаменить(
		ТекстЗапросаФильтраПоВидамКонтактнойИнформации,
		"&СписокВидовКонтактнойИнформации",
		ВидыКонтактнойИнформацииЗапроса);
		
	ЗарплатаКадрыОбщиеНаборыДанных.УстановитьВыборкуТолькоРазрешенныхДанных(ТекстЗапросаФильтраПоВидамКонтактнойИнформации, ТолькоРазрешенные);
	
	// Подготовка запроса, создающего временную таблицу с контактной информацией.
	ОписаниеТаблицыОтборов = ОписаниеВременнойТаблицыОтборовКонтактнойИнформации(
		ИмяВТОтборКонтактнойИнформации,
		"ФизическоеЛицо",
		"Вид");
		
	ЗапросВТКонтактнаяИнформация = ЗапросВТКонтактнаяИнформация(
		ТолькоРазрешенные, ИмяВТКонтактнаяИнформация, "Справочник.ФизическиеЛица", ОписаниеТаблицыОтборов);
		
	ЗарплатаКадрыОбщиеНаборыДанных.СкопироватьПараметрыЗапроса(Запрос, ЗапросВТКонтактнаяИнформация);
		
	Запрос.Текст =
		ТекстЗапросаФильтраПоВидамКонтактнойИнформации
		+ ЗарплатаКадрыОбщиеНаборыДанных.РазделительЗапросов()
		+ ЗапросВТКонтактнаяИнформация.Текст
		+ ЗарплатаКадрыОбщиеНаборыДанных.РазделительЗапросов()
		+ Запрос.Текст;
			
КонецПроцедуры

Функция НеобходимаКонтактнаяИнформацияФизическогоЛица(ИмяПоля) Экспорт
	
	ИмяПоля = ВРег(ИмяПоля);
	
	Возврат ИмяПоля = ВРег("АдресДляИнформирования")
		ИЛИ ИмяПоля = ВРег("АдресДляИнформированияПредставление")
		ИЛИ ИмяПоля = ВРег("АдресЗаПределамиРФ")
		ИЛИ ИмяПоля = ВРег("АдресЗаПределамиРФПредставление")
		ИЛИ ИмяПоля = ВРег("АдресМестаПроживания")
		ИЛИ ИмяПоля = ВРег("АдресМестаПроживанияПредставление")
		ИЛИ ИмяПоля = ВРег("АдресПоПрописке")
		ИЛИ ИмяПоля = ВРег("АдресПоПропискеПредставление")
		ИЛИ ИмяПоля = ВРег("ТелефонДомашний")
		ИЛИ ИмяПоля = ВРег("ТелефонДомашнийПредставление")
		ИЛИ ИмяПоля = ВРег("ТелефонРабочий")
		ИЛИ ИмяПоля = ВРег("ТелефонРабочийПредставление");
		
КонецФункции

Функция ПутьКДаннымКонтактнойИнформацииФизическихЛиц(Знач ИмяПоля, ИменаТаблицКонтактнойИнформации)
	
	ПозицияПредставление = Найти(ВРег(ИмяПоля), ВРег("Представление"));
	Если ПозицияПредставление > 0 Тогда
					
		ИмяТаблицы = Лев(ИмяПоля, ПозицияПредставление - 1);
		ПутьКДанным = Символы.Таб + ИмяТаблицы + "ФизическиеЛица.Представление";
						
	Иначе
		ИмяТаблицы = ИмяПоля;
		ПутьКДанным = "	ВЫРАЗИТЬ(" + ИмяТаблицы + "ФизическиеЛица.ЗначенияПолей КАК СТРОКА(1000))";
	КонецЕсли; 
	
	Если ИменаТаблицКонтактнойИнформации.Найти(ИмяТаблицы) = Неопределено Тогда
		ИменаТаблицКонтактнойИнформации.Добавить(ИмяТаблицы);
	КонецЕсли; 
	
	Возврат ПутьКДанным;
	
КонецФункции

// Документы, удостоверяющие личность.

Функция ДобавитьПолеСведенийОДокументахУдостоверяющихЛичность(ИмяПоля, ТекстОписанияПолей, ИсточникиДанных)
	
	ДобавленоПолеСведений = Ложь;
	Если НеобходимыСведенияДокументовУдостоверяющихЛичность(ИмяПоля) Тогда
		
		ДобавленоПолеСведений = Истина;
		ИсточникиДанных.Вставить("ДокументыУдостоверяющиеЛичность", Истина);
		
		ПутьКДанным = "	ДокументыУдостоверяющиеЛичность." + ИмяПоля;
		ТекстОписанияПолей = ТекстОписанияПолей + "," + Символы.ПС + ПутьКДанным + " КАК " + ИмяПоля;
		
	КонецЕсли;
	
	Возврат ДобавленоПолеСведений;
	
КонецФункции

Процедура ДобавитьТекстЗапросаВТДокументыУдостоверяющиеЛичность(Запрос, ТолькоРазрешенные, ОписательВременнойТаблицыОтборов, ИсточникиДанных)
	
	Если ИсточникиДанных.Получить("ДокументыУдостоверяющиеЛичность") = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	ИмяВТДокументыУдостоверяющиеЛичность = ЗарплатаКадрыОбщиеНаборыДанных.УникальноеИмяТекстаЗапроса("ВТДокументыУдостоверяющиеЛичность");
	
	ЗапросВТ = КадровыйУчет.ЗапросВТДокументыУдостоверяющиеЛичность(ТолькоРазрешенные, ОписательВременнойТаблицыОтборов, ИмяВТДокументыУдостоверяющиеЛичность);
		
	ЗарплатаКадрыОбщиеНаборыДанных.СкопироватьПараметрыЗапроса(Запрос, ЗапросВТ);
	
	Запрос.Текст = 
		ЗапросВТ.Текст
		+ ЗарплатаКадрыОбщиеНаборыДанных.РазделительЗапросов()
		+ Запрос.Текст;
		
	Запрос.Текст = Запрос.Текст + Символы.ПС
		+ "		{ЛЕВОЕ СОЕДИНЕНИЕ " + ИмяВТДокументыУдостоверяющиеЛичность + " КАК ДокументыУдостоверяющиеЛичность
			|		ПО ТаблицаОтборовФизическихЛиц." + ОписательВременнойТаблицыОтборов.ИмяПоляФизическоеЛицо + " = ДокументыУдостоверяющиеЛичность.ФизическоеЛицо
			|			И ТаблицаОтборовФизическихЛиц." + ОписательВременнойТаблицыОтборов.ИмяПоляПериод + " = ДокументыУдостоверяющиеЛичность.Период}";
				
КонецПроцедуры

Функция НеобходимыСведенияДокументовУдостоверяющихЛичность(Знач ИмяПоля)
	
	ИмяПоля = ВРег(ИмяПоля);
	
	Возврат ИмяПоля = ВРег("ДокументВид")
		ИЛИ ИмяПоля = ВРег("ДокументКодМВД")
		ИЛИ ИмяПоля = ВРег("ДокументСерия")
		ИЛИ ИмяПоля = ВРег("ДокументНомер")
		ИЛИ ИмяПоля = ВРег("ДокументДатаВыдачи")
		ИЛИ ИмяПоля = ВРег("ДокументСрокДействия")
		ИЛИ ИмяПоля = ВРег("ДокументКемВыдан")
		ИЛИ ИмяПоля = ВРег("ДокументКодПодразделения")
		ИЛИ ИмяПоля = ВРег("ДокументПредставление");
		
КонецФункции

// Сведения об инвалидности

Функция ДобавитьПолеСведенийОбИнвалидности(ИмяПоля, ТекстОписанияПолей, ИсточникиДанных)
	
	ДобавленоПолеСведений = Ложь;
	Если НеобходимыСведенияОбИнвалидности(ИмяПоля) Тогда
		
		ДобавленоПолеСведений = Истина;
		ИсточникиДанных.Вставить("СведенияОбИнвалидности", Истина);
		
		ПутьКДанным = ПутьКДаннымСведенийОбИнвалидности(ИмяПоля);
		ТекстОписанияПолей = ТекстОписанияПолей + "," + Символы.ПС + ПутьКДанным + " КАК " + ИмяПоля;
		
	КонецЕсли;
	
	Возврат ДобавленоПолеСведений;
	
КонецФункции

Процедура ДобавитьТекстЗапросаВТСведенияОбИнвалидности(Запрос, ТолькоРазрешенные, ОписательВременнойТаблицыОтборов, ПоляОтбораПериодическихДанных, ИсточникиДанных)
	
	Если ИсточникиДанных.Получить("СведенияОбИнвалидности") = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	ИмяВТСведенияОбИнвалидности = ЗарплатаКадрыОбщиеНаборыДанных.УникальноеИмяТекстаЗапроса("ВТСведенияОбИнвалидности");
	
	ЗапросВТ = КадровыйУчет.ЗапросВТСведенияОбИнвалидности(ТолькоРазрешенные, ОписательВременнойТаблицыОтборов, ПоляОтбораПериодическихДанных, ИмяВТСведенияОбИнвалидности);
			
	ЗарплатаКадрыОбщиеНаборыДанных.СкопироватьПараметрыЗапроса(Запрос, ЗапросВТ);
	
	Запрос.Текст = 
		ЗапросВТ.Текст
		+ ЗарплатаКадрыОбщиеНаборыДанных.РазделительЗапросов()
		+ Запрос.Текст;
		
	Запрос.Текст = Запрос.Текст + Символы.ПС
		+ "		{ЛЕВОЕ СОЕДИНЕНИЕ " + ИмяВТСведенияОбИнвалидности + " КАК СведенияОбИнвалидности
			|		ПО ТаблицаОтборовФизическихЛиц." + ОписательВременнойТаблицыОтборов.ИмяПоляФизическоеЛицо + " = СведенияОбИнвалидности.ФизическоеЛицо
			|			И ТаблицаОтборовФизическихЛиц." + ОписательВременнойТаблицыОтборов.ИмяПоляПериод + " = СведенияОбИнвалидности.Период}";
				
КонецПроцедуры

Функция НеобходимыСведенияОбИнвалидности(Знач ИмяПоля)
	
	ИмяПоля = ВРег(ИмяПоля);
	
	Возврат ИмяПоля = ВРег("Инвалидность")
		ИЛИ ИмяПоля = ВРег("ИнвалидностьДатаВыдачи")
		ИЛИ ИмяПоля = ВРег("ИнвалидностьСрокДействияСправки");
	
КонецФункции

Функция ПутьКДаннымСведенийОбИнвалидности(Знач ИмяПоля)
	
	ИмяПоляВВерхнемРегистре = ВРег(ИмяПоля);
	
	ПутьКДанным = "";
	Если ИмяПоляВВерхнемРегистре = ВРег("ИнвалидностьДатаВыдачи") Тогда
		ПутьКДанным = "	СведенияОбИнвалидности.ДатаВыдачи";
	ИначеЕсли ИмяПоляВВерхнемРегистре = ВРег("ИнвалидностьСрокДействияСправки") Тогда
		ПутьКДанным = "	СведенияОбИнвалидности.СрокДействияСправки";
	Иначе
		ПутьКДанным = "	СведенияОбИнвалидности." + ИмяПоля;
	КонецЕсли;
	
	Возврат ПутьКДанным;
	
КонецФункции

// Сведения о гражданстве

Функция ДобавитьПолеСведенийОГражданстве(ИмяПоля, ТекстОписанияПолей, ИсточникиДанных)
	
	ДобавленоПолеСведений = Ложь;
	Если НеобходимыСведенияОГражданстве(ИмяПоля) Тогда
		
		ДобавленоПолеСведений = Истина;
		ИсточникиДанных.Вставить("СведенияОГражданстве", Истина);
		
		ПутьКДанным = "	ЕСТЬNULL(ГражданствоФизическихЛиц." + ИмяПоля + ", ЗНАЧЕНИЕ(Справочник.СтраныМира.Россия))";
		ТекстОписанияПолей = ТекстОписанияПолей + "," + Символы.ПС + ПутьКДанным + " КАК " + ИмяПоля;
		
	КонецЕсли;
	
	Возврат ДобавленоПолеСведений;
	
КонецФункции

Процедура ДобавитьТекстЗапросаВТСведенияОГражданстве(Запрос, ТолькоРазрешенные, ОписательВременнойТаблицыОтборов, ПоляОтбораПериодическихДанных, ИсточникиДанных)
	
	Если ИсточникиДанных.Получить("СведенияОГражданстве") = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	ИмяВТСведенияОГражданстве = ЗарплатаКадрыОбщиеНаборыДанных.УникальноеИмяТекстаЗапроса("ВТСведенияОГражданстве");
	
	ЗапросВТ = КадровыйУчет.ЗапросВТСведенияОГражданстве(ТолькоРазрешенные, ОписательВременнойТаблицыОтборов, ПоляОтбораПериодическихДанных, ИмяВТСведенияОГражданстве);
			
	ЗарплатаКадрыОбщиеНаборыДанных.СкопироватьПараметрыЗапроса(Запрос, ЗапросВТ);
	
	Запрос.Текст = 
		ЗапросВТ.Текст
		+ ЗарплатаКадрыОбщиеНаборыДанных.РазделительЗапросов()
		+ Запрос.Текст;
		
	Запрос.Текст = Запрос.Текст + Символы.ПС
		+ "		{ЛЕВОЕ СОЕДИНЕНИЕ " + ИмяВТСведенияОГражданстве + " КАК ГражданствоФизическихЛиц
			|		ПО ТаблицаОтборовФизическихЛиц." + ОписательВременнойТаблицыОтборов.ИмяПоляФизическоеЛицо + " = ГражданствоФизическихЛиц.ФизическоеЛицо
			|			И ТаблицаОтборовФизическихЛиц." + ОписательВременнойТаблицыОтборов.ИмяПоляПериод + " = ГражданствоФизическихЛиц.Период}";
				
КонецПроцедуры

Функция НеобходимыСведенияОГражданстве(Знач ИмяПоля)
	
	Возврат ВРег(ИмяПоля) = Врег("Страна");
	
КонецФункции

// Сведения о ФИО физических лиц.

Функция ДобавитьПолеСведенийОФИОФизическихЛиц(ИмяПоля, ТекстОписанияПолей, ИсточникиДанных)
	
	ДобавленоПолеСведений = Ложь;
	Если НеобходимыСведенияОФИОФизическихЛиц(ИмяПоля) Тогда
		
		ДобавленоПолеСведений = Истина;
		ИсточникиДанных.Вставить("СведенияОФИОФизическихЛиц", Истина);
		
		ПутьКДанным = ПутьКДаннымСведенийОФИОФизическихЛиц(ИмяПоля);
		ТекстОписанияПолей = ТекстОписанияПолей + "," + Символы.ПС + ПутьКДанным + " КАК " + ИмяПоля;
		
	КонецЕсли;
	
	Возврат ДобавленоПолеСведений;
	
КонецФункции

Процедура ДобавитьТекстЗапросаВТСведенияОФИОФизическихЛиц(Запрос, ТолькоРазрешенные, ОписательВременнойТаблицыОтборов, ПоляОтбораПериодическихДанных, ИсточникиДанных)
	
	Если ИсточникиДанных.Получить("СведенияОФИОФизическихЛиц") = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	ИмяВТСведенияОФИОФизическихЛиц = ЗарплатаКадрыОбщиеНаборыДанных.УникальноеИмяТекстаЗапроса("ВТСведенияОФИОФизическихЛиц");
	
	ЗапросВТ = КадровыйУчет.ЗапросВТСведенияОФИОФизическихЛиц(ТолькоРазрешенные, ОписательВременнойТаблицыОтборов, ПоляОтбораПериодическихДанных, ИмяВТСведенияОФИОФизическихЛиц);
			
	ЗарплатаКадрыОбщиеНаборыДанных.СкопироватьПараметрыЗапроса(Запрос, ЗапросВТ);
	
	Запрос.Текст = 
		ЗапросВТ.Текст
		+ ЗарплатаКадрыОбщиеНаборыДанных.РазделительЗапросов()
		+ Запрос.Текст;
		
	Запрос.Текст = Запрос.Текст + Символы.ПС
		+ "		{ЛЕВОЕ СОЕДИНЕНИЕ " + ИмяВТСведенияОФИОФизическихЛиц + " КАК СведенияОФИОФизическихЛиц
			|		ПО ТаблицаОтборовФизическихЛиц." + ОписательВременнойТаблицыОтборов.ИмяПоляФизическоеЛицо + " = СведенияОФИОФизическихЛиц.ФизическоеЛицо
			|			И ТаблицаОтборовФизическихЛиц." + ОписательВременнойТаблицыОтборов.ИмяПоляПериод + " = СведенияОФИОФизическихЛиц.Период}";
				
КонецПроцедуры

Функция НеобходимыСведенияОФИОФизическихЛиц(Знач ИмяПоля)
	
	ИмяПоля = ВРег(ИмяПоля);
	
	Возврат ИмяПоля = ВРег("Фамилия")
		ИЛИ ИмяПоля = ВРег("Имя")
		ИЛИ ИмяПоля = ВРег("Отчество")
		ИЛИ ИмяПоля = ВРег("ФамилияИО")
		ИЛИ ИмяПоля = ВРег("ИОФамилия")
		ИЛИ ИмяПоля = ВРег("ФИОПолные");

КонецФункции

Функция ПутьКДаннымСведенийОФИОФизическихЛиц(Знач ИмяПоля)
	
	ИмяПоляВВерхнемРегистре = ВРег(ИмяПоля);
	
	ПутьКДанным = "";
	
	Если ИмяПоляВВерхнемРегистре = ВРег("ФамилияИО") Тогда
		
		ПутьКДанным = 
			"	СведенияОФИОФизическихЛиц.Фамилия + "" "" + ВЫБОР
			|		КОГДА СведенияОФИОФизическихЛиц.Имя = """"
			|			ТОГДА """"
			|		ИНАЧЕ ПОДСТРОКА(СведенияОФИОФизическихЛиц.Имя, 1, 1) + ""."" + ВЫБОР
			|			КОГДА СведенияОФИОФизическихЛиц.Отчество = """"
			|				ТОГДА """"
			|			ИНАЧЕ ПОДСТРОКА(СведенияОФИОФизическихЛиц.Отчество, 1, 1) + "".""
			|		КОНЕЦ
			|	КОНЕЦ";
			
	ИначеЕсли ИмяПоляВВерхнемРегистре = ВРег("ИОФамилия") Тогда
				
		ПутьКДанным =
			"	ВЫБОР КОГДА СведенияОФИОФизическихЛиц.Имя = """"
			|		ТОГДА """"
			|		ИНАЧЕ ПОДСТРОКА(СведенияОФИОФизическихЛиц.Имя, 1, 1) + ""."" + ВЫБОР
			|			КОГДА СведенияОФИОФизическихЛиц.Отчество = """"
			|				ТОГДА """"
			|			ИНАЧЕ ПОДСТРОКА(СведенияОФИОФизическихЛиц.Отчество, 1, 1) + "".""
			|		КОНЕЦ + "" ""
			|	КОНЕЦ + СведенияОФИОФизическихЛиц.Фамилия";

	ИначеЕсли ИмяПоляВВерхнемРегистре = ВРег("ФИОПолные") Тогда
		ПутьКДанным = "	СведенияОФИОФизическихЛиц.Фамилия + "" "" + СведенияОФИОФизическихЛиц.Имя + "" "" + СведенияОФИОФизическихЛиц.Отчество";
	Иначе
		ПутьКДанным = "	СведенияОФИОФизическихЛиц." + ИмяПоля;
	КонецЕсли;
	
	Возврат ПутьКДанным;
	
КонецФункции

// Сведения о статусах застрахованных физических лиц.

Функция ДобавитьПолеСведенийОСтатусахЗастрахованныхЛиц(ИмяПоля, ТекстОписанияПолей, ИсточникиДанных)
	
	ДобавленоПолеСведений = Ложь;
	Если НеобходимыСведенияОСтатусахЗастрахованныхЛиц(ИмяПоля) Тогда
		
		ДобавленоПолеСведений = Истина;
		ИсточникиДанных.Вставить("СведенияОСтатусахЗастрахованныхЛиц", Истина);
		
		ПутьКДанным = "	СтатусыЗастрахованныхЛиц." + ИмяПоля;
		ТекстОписанияПолей = ТекстОписанияПолей + "," + Символы.ПС + ПутьКДанным + " КАК " + ИмяПоля;
		
	КонецЕсли;
	
	Возврат ДобавленоПолеСведений;
	
КонецФункции

Процедура ДобавитьТекстЗапросаВТСведенияОСтатусахЗастрахованныхЛиц(Запрос, ТолькоРазрешенные, ОписательВременнойТаблицыОтборов, ПоляОтбораПериодическихДанных, ИсточникиДанных)
	
	Если ИсточникиДанных.Получить("СведенияОСтатусахЗастрахованныхЛиц") = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	ИмяВТСведенияОСтатусахЗастрахованныхЛиц = ЗарплатаКадрыОбщиеНаборыДанных.УникальноеИмяТекстаЗапроса("ВТСведенияОСтатусахЗастрахованныхЛиц");
	
	ЗапросВТ = УчетСтраховыхВзносов.ЗапросВТСведенияОСтатусахЗастрахованныхЛиц(ТолькоРазрешенные, ОписательВременнойТаблицыОтборов, ПоляОтбораПериодическихДанных, ИмяВТСведенияОСтатусахЗастрахованныхЛиц);
			
	ЗарплатаКадрыОбщиеНаборыДанных.СкопироватьПараметрыЗапроса(Запрос, ЗапросВТ);
	
	Запрос.Текст = 
		ЗапросВТ.Текст
		+ ЗарплатаКадрыОбщиеНаборыДанных.РазделительЗапросов()
		+ Запрос.Текст;
		
	Запрос.Текст = Запрос.Текст + Символы.ПС
		+ "		{ЛЕВОЕ СОЕДИНЕНИЕ " + ИмяВТСведенияОСтатусахЗастрахованныхЛиц + " КАК СтатусыЗастрахованныхЛиц
			|		ПО ТаблицаОтборовФизическихЛиц." + ОписательВременнойТаблицыОтборов.ИмяПоляФизическоеЛицо + " = СтатусыЗастрахованныхЛиц.ФизическоеЛицо
			|			И ТаблицаОтборовФизическихЛиц." + ОписательВременнойТаблицыОтборов.ИмяПоляПериод + " = СтатусыЗастрахованныхЛиц.Период}";
				
КонецПроцедуры

Функция НеобходимыСведенияОСтатусахЗастрахованныхЛиц(Знач ИмяПоля)
	
	Возврат ВРег(ИмяПоля) = ВРег("ВидЗастрахованногоЛица");

КонецФункции

// Сведения о статусе налогоплательщика.

Функция ДобавитьПолеСведенийОСтатусахНалогоплательщиков(ИмяПоля, ТекстОписанияПолей, ИсточникиДанных)
	
	ДобавленоПолеСведений = Ложь;
	Если НеобходимыСведенияОСтатусахНалогоплательщиков(ИмяПоля) Тогда
		
		ДобавленоПолеСведений = Истина;
		ИсточникиДанных.Вставить("СведенияОСтатусахНалогоплательщиков", Истина);
		
		ПутьКДанным = "	СтатусыНалогоплательщиков." + ИмяПоля;
		ТекстОписанияПолей = ТекстОписанияПолей + "," + Символы.ПС + ПутьКДанным + " КАК " + ИмяПоля;
		
	КонецЕсли;
	
	Возврат ДобавленоПолеСведений;
	
КонецФункции

Процедура ДобавитьТекстЗапросаВТСведенияОСтатусахНалогоплательщиков(Запрос, ТолькоРазрешенные, ОписательВременнойТаблицыОтборов, ПоляОтбораПериодическихДанных, ИсточникиДанных)
	
	Если ИсточникиДанных.Получить("СведенияОСтатусахНалогоплательщиков") = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	ИмяВТСведенияОСтатусахНалогоплательщиков = ЗарплатаКадрыОбщиеНаборыДанных.УникальноеИмяТекстаЗапроса("ВТСведенияОСтатусахНалогоплательщиков");
	
	ЗапросВТ = УчетНДФЛ.ЗапросВТСведенияОСтатусахНалогоплательщиков(ТолькоРазрешенные, ОписательВременнойТаблицыОтборов, ПоляОтбораПериодическихДанных, ИмяВТСведенияОСтатусахНалогоплательщиков);
			
	ЗарплатаКадрыОбщиеНаборыДанных.СкопироватьПараметрыЗапроса(Запрос, ЗапросВТ);
	
	Запрос.Текст = 
		ЗапросВТ.Текст
		+ ЗарплатаКадрыОбщиеНаборыДанных.РазделительЗапросов()
		+ Запрос.Текст;
		
	Запрос.Текст = Запрос.Текст + Символы.ПС
		+ "		{ЛЕВОЕ СОЕДИНЕНИЕ " + ИмяВТСведенияОСтатусахНалогоплательщиков + " КАК СтатусыНалогоплательщиков
			|		ПО ТаблицаОтборовФизическихЛиц." + ОписательВременнойТаблицыОтборов.ИмяПоляФизическоеЛицо + " = СтатусыНалогоплательщиков.ФизическоеЛицо
			|			И ТаблицаОтборовФизическихЛиц." + ОписательВременнойТаблицыОтборов.ИмяПоляПериод + " = СтатусыНалогоплательщиков.Период}";
				
КонецПроцедуры

Функция НеобходимыСведенияОСтатусахНалогоплательщиков(Знач ИмяПоля)
	
	Возврат ВРег(ИмяПоля) = ВРег("СтатусНалогоплательщика");	
	
КонецФункции

#КонецОбласти

#Область КадровыеДанныеСотрудников

Функция ЗапросВТПостоянныеКадровыеДанныеСотрудников(ТолькоРазрешенные, ОписательВременнойТаблицыОтборов, КадровыеДанные, ИмяВТПостоянныеКадровыеДанныеСотрудников) Экспорт
	
	ТекстОписанияПолей = "";
	ИсточникиДанных = Новый Соответствие;
	
	Если ТипЗнч(КадровыеДанные) = Тип("Массив") Тогда
		ИменаКадровыхДанных = КадровыеДанные;
	Иначе
		ИменаКадровыхДанных = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(КадровыеДанные);
	КонецЕсли;
	
	Для каждого ИмяЗапрашиваемыхДанных Из ИменаКадровыхДанных Цикл
		
		ИмяКадровыхДанных = СокрЛП(ИмяЗапрашиваемыхДанных);
		
		Если КадровыйУчет.ЭтоОбязательноеПолеКадровыхДанныхСотрудника(ИмяКадровыхДанных) Тогда
			
			Продолжить;
			
		ИначеЕсли ДобавитьПолеРеквизитаСправочникаСотрудники(ИмяКадровыхДанных, ТекстОписанияПолей, ИсточникиДанных)
			ИЛИ ДобавитьПолеСведенийПостоянныхКадровыхДанныхФизическихЛиц(ИмяКадровыхДанных, ТекстОписанияПолей, ИсточникиДанных)
			ИЛИ ДобавитьПолеСведенийТекущихКадровыхДанныхСотрудников(ИмяКадровыхДанных, ТекстОписанияПолей, ИсточникиДанных)
			ИЛИ ДобавитьПолеСведенийТекущихТарифныхСтавокСотрудников(ИмяКадровыхДанных, ТекстОписанияПолей, ИсточникиДанных)
			ИЛИ ДобавитьПолеСведенийДанныхПриказаОПриемеОбУвольнении(ИмяКадровыхДанных, ТекстОписанияПолей, ИсточникиДанных) Тогда
			
			Продолжить;
			
		Иначе
			
			ТекстСообщенияИсключения = НСтр("ru='Среди кадровых данных сотрудников нет данных с именем'") + " """ + ИмяКадровыхДанных + """";
			ВызватьИсключение ТекстСообщенияИсключения;
			
		КонецЕсли; 
		
	КонецЦикла;
	
	Запрос = Новый Запрос;
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	ТаблицаОтборовСотрудников.Сотрудник КАК Сотрудник,
		|	ВЫРАЗИТЬ(ТаблицаОтборовСотрудников.Сотрудник КАК Справочник.Сотрудники).ФизическоеЛицо КАК ФизическоеЛицо
		|ПОМЕСТИТЬ ВТПостоянныеКадровыеДанныеСотрудников
		|ИЗ
		|	ВТОтборовСотрудников КАК ТаблицаОтборовСотрудников";
		
	КадровыйУчет.ДобавитьВТекстЗапросаОписаниеПолей(Запрос.Текст, ТекстОписанияПолей, "ПОМЕСТИТЬ ВТПостоянныеКадровыеДанныеСотрудников");
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТОтборовСотрудников", ОписательВременнойТаблицыОтборов.ИмяВременнойТаблицыОтборовСотрудников);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ТаблицаОтборовСотрудников.Сотрудник", "ТаблицаОтборовСотрудников." + ОписательВременнойТаблицыОтборов.ИмяПоляСотрудник);
	
	ЗарплатаКадрыОбщиеНаборыДанных.ЗаменитьИмяСоздаваемойВременнойТаблицы(Запрос.Текст, "ВТПостоянныеКадровыеДанныеСотрудников", ИмяВТПостоянныеКадровыеДанныеСотрудников);
	ЗарплатаКадрыОбщиеНаборыДанных.УстановитьВыборкуТолькоРазрешенныхДанных(Запрос.Текст, ТолькоРазрешенные);
	
	ДобавитьТекстЗапросаДанныхРеквизитовСправочникаСотрудники(Запрос, ТолькоРазрешенные, ОписательВременнойТаблицыОтборов, ИсточникиДанных);
	ДобавитьТекстЗапросаПостоянныхКадровыхДанныхФизическихЛиц(Запрос, ТолькоРазрешенные, ОписательВременнойТаблицыОтборов, ИсточникиДанных);
	ДобавитьТекстЗапросаТекущихКадровыхДанныхСотрудников(Запрос, ТолькоРазрешенные, ОписательВременнойТаблицыОтборов, ИсточникиДанных);
	ДобавитьТекстЗапросаТекущихТарифныхСтавок(Запрос, ТолькоРазрешенные, ОписательВременнойТаблицыОтборов, ИсточникиДанных);
	ДобавитьТекстЗапросаДанныхПриказовОПриемеОбУвольнении(Запрос, ТолькоРазрешенные, ОписательВременнойТаблицыОтборов, ИсточникиДанных);
	
	// В конце добавляется запрос функциональных опций организаций.
	ДобавитьТекстЗапросаВТФункциональныеОпцииОрганизаций(Запрос, ТолькоРазрешенные, ОписательВременнойТаблицыОтборов, ИсточникиДанных);
	
	Возврат Запрос;
		
КонецФункции

Функция ЗапросВТКадровыеДанныеСотрудников(ТолькоРазрешенные, ОписательВременнойТаблицыОтборов, КадровыеДанные, ПоляОтбораПериодическихДанных, ФормироватьСПериодичностьДень, ИмяВТКадровыеДанныеСотрудников) Экспорт
	
	ТекстОписанияПолей = "";
	ИсточникиДанных = Новый Соответствие;
	
	Если ТипЗнч(КадровыеДанные) = Тип("Массив") Тогда
		ИменаКадровыхДанных = КадровыеДанные;
	Иначе
		ИменаКадровыхДанных = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(КадровыеДанные);
	КонецЕсли;
	
	Для каждого ИмяЗапрашиваемыхДанных Из ИменаКадровыхДанных Цикл
		
		ИмяКадровыхДанных = СокрЛП(ИмяЗапрашиваемыхДанных);
		
		Если КадровыйУчет.ЭтоОбязательноеПолеКадровыхДанныхСотрудника(ИмяКадровыхДанных) Тогда
			
			 Продолжить;
			 
		 ИначеЕсли ДобавитьПолеПостоянныхКадровыхДанныхСотрудников(ИмяКадровыхДанных, ТекстОписанияПолей, ИсточникиДанных)
			 ИЛИ ДобавитьПолеКадровыхДанныхФизическихЛиц(ИмяКадровыхДанных, ТекстОписанияПолей, ИсточникиДанных)
			 ИЛИ ДобавитьПолеСведенийКадровойИсторииСотрудников(ИмяКадровыхДанных, ТекстОписанияПолей, ИсточникиДанных)
			 ИЛИ ДобавитьПолеСведенийОПлановыхАвансах(ИмяКадровыхДанных, ТекстОписанияПолей, ИсточникиДанных)
			 ИЛИ ДобавитьПолеСведенийОбУчетеЗатрат(ИмяКадровыхДанных, ТекстОписанияПолей, ИсточникиДанных)
			 ИЛИ ДобавитьПолеСведенийОбОплатеТруда(ИмяКадровыхДанных, ТекстОписанияПолей, ИсточникиДанных) Тогда
				
			Продолжить;
			
		Иначе
			
			ТекстСообщенияИсключения = НСтр("ru='Среди кадровых данных сотрудников нет данных с именем'") + " """ + ИмяКадровыхДанных + """";
			ВызватьИсключение ТекстСообщенияИсключения;
			
		КонецЕсли; 
		
	КонецЦикла;
	
	Запрос = Новый Запрос;
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	ТаблицаОтборовСотрудников.Период КАК Период,
		|	ТаблицаОтборовСотрудников.Сотрудник КАК Сотрудник,
		|	ВЫРАЗИТЬ(ТаблицаОтборовСотрудников.Сотрудник КАК Справочник.Сотрудники).ФизическоеЛицо КАК ФизическоеЛицо
		|ПОМЕСТИТЬ ВТКадровыеДанныеСотрудников
		|ИЗ
		|	ВТОтборовСотрудников КАК ТаблицаОтборовСотрудников";
		
	КадровыйУчет.ДобавитьВТекстЗапросаОписаниеПолей(Запрос.Текст, ТекстОписанияПолей, "ПОМЕСТИТЬ ВТКадровыеДанныеСотрудников");
		
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТОтборовСотрудников", ОписательВременнойТаблицыОтборов.ИмяВременнойТаблицыОтборовСотрудников);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ТаблицаОтборовСотрудников.Сотрудник", "ТаблицаОтборовСотрудников." + ОписательВременнойТаблицыОтборов.ИмяПоляСотрудник);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ТаблицаОтборовСотрудников.Период", "ТаблицаОтборовСотрудников." + ОписательВременнойТаблицыОтборов.ИмяПоляПериод);
	
	ЗарплатаКадрыОбщиеНаборыДанных.ЗаменитьИмяСоздаваемойВременнойТаблицы(Запрос.Текст, "ВТКадровыеДанныеСотрудников", ИмяВТКадровыеДанныеСотрудников);
	ЗарплатаКадрыОбщиеНаборыДанных.УстановитьВыборкуТолькоРазрешенныхДанных(Запрос.Текст, ТолькоРазрешенные);
	
	ДобавитьТекстЗапросаВТПостоянныеКадровыеДанныеСотрудников(Запрос, ТолькоРазрешенные, ОписательВременнойТаблицыОтборов, ИсточникиДанных);
	ДобавитьТекстЗапросаВТКадровыеДанныеФизическихЛиц(Запрос, ТолькоРазрешенные, ОписательВременнойТаблицыОтборов, ПоляОтбораПериодическихДанных, ИсточникиДанных);
	КадровыйУчет.ДобавитьТекстЗапросаВТСведенияКадроваяИсторияСотрудников(Запрос, ТолькоРазрешенные, ОписательВременнойТаблицыОтборов, ПоляОтбораПериодическихДанных, ИсточникиДанных);
	ДобавитьТекстЗапросаВТСведенийОПлановыхАвансах(Запрос, ТолькоРазрешенные, ОписательВременнойТаблицыОтборов, ПоляОтбораПериодическихДанных, ИсточникиДанных);
	ДобавитьТекстЗапросаВТСведенияОбУчетеЗатрат(Запрос, ТолькоРазрешенные, ОписательВременнойТаблицыОтборов, ПоляОтбораПериодическихДанных, ИсточникиДанных);
	ДобавитьТекстЗапросаВТСведенияОбОплатеТруда(Запрос, ТолькоРазрешенные, ОписательВременнойТаблицыОтборов, ПоляОтбораПериодическихДанных, ИсточникиДанных);
	
	// В конце добавляется запрос функциональных опций организаций.
	ДобавитьТекстЗапросаВТФункциональныеОпцииОрганизаций(Запрос, ТолькоРазрешенные, ОписательВременнойТаблицыОтборов, ИсточникиДанных);
	
	Возврат Запрос;
	
КонецФункции

// Данные реквизитов справочника сотрудников.

Функция ДобавитьПолеРеквизитаСправочникаСотрудники(ИмяПоля, ТекстОписанияПолей, ИсточникиДанных)
	
	ДобавленоПолеСведений = Ложь;
	Если НеобходимыЗначенияРеквизитовСправочникаСотрудники(ИмяПоля) Тогда
		
		ДобавленоПолеСведений = Истина;
		ИсточникиДанных.Вставить("РеквизитыСправочникаСотрудники", Истина);
		
		ПутьКДанным = ПутьКДаннымЗначенийРеквизитовСправочникаСотрудники(ИмяПоля);
		ТекстОписанияПолей = ТекстОписанияПолей + "," + Символы.ПС + ПутьКДанным + " КАК " + ИмяПоля;
		
	КонецЕсли;
	
	Возврат ДобавленоПолеСведений;
	
КонецФункции

Функция НеобходимыЗначенияРеквизитовСправочникаСотрудники(Знач ИмяПоля)
	
	ИмяПоля = ВРег(ИмяПоля);
	
	Возврат Метаданные.Справочники.Сотрудники.Реквизиты.Найти(ИмяПоля) <> Неопределено
		ИЛИ ИмяПоля = ВРег("ТабельныйНомер") 
		ИЛИ ИмяПоля = ВРег("Наименование") 
		ИЛИ ИмяПоля = ВРег("Код")
		ИЛИ ИмяПоля = ВРег("ПроцентСевернойНадбавки");
			
КонецФункции

Функция ПутьКДаннымЗначенийРеквизитовСправочникаСотрудники(ИмяПоля)
	
	ИмяПоляВВерхнемРегистре = ВРег(ИмяПоля);
	
	ПутьКДанным = "";
	Если ИмяПоляВВерхнемРегистре = ВРег("Наименование") Тогда
		ПутьКДанным = "	СправочникСотрудники.Наименование";
	ИначеЕсли ИмяПоляВВерхнемРегистре = ВРег("Код") ИЛИ ИмяПоляВВерхнемРегистре = ВРег("ТабельныйНомер") Тогда
		ПутьКДанным = "	СправочникСотрудники.Код";
	ИначеЕсли ИмяПоляВВерхнемРегистре = ВРег("ПроцентСевернойНадбавки") Тогда
		ПутьКДанным = "	СправочникСотрудники.ТекущийПроцентСевернойНадбавки";
	Иначе
		ПутьКДанным = "	СправочникСотрудники." + ИмяПоля;
	КонецЕсли;
	
	Возврат ПутьКДанным;
	
КонецФункции

Процедура ДобавитьТекстЗапросаДанныхРеквизитовСправочникаСотрудники(Запрос, ТолькоРазрешенные, ОписательВременнойТаблицыОтборов, ИсточникиДанных)
	
	Если ИсточникиДанных.Получить("РеквизитыСправочникаСотрудники") = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	Запрос.Текст = Запрос.Текст + Символы.ПС
		+ "		{ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Сотрудники КАК СправочникСотрудники
		|		ПО ТаблицаОтборовСотрудников." + ОписательВременнойТаблицыОтборов.ИмяПоляСотрудник + " = СправочникСотрудники.Ссылка}";
		
КонецПроцедуры

// Постоянные кадровые данные физических лиц.

Функция ДобавитьПолеСведенийПостоянныхКадровыхДанныхФизическихЛиц(ИмяПоля, ТекстОписанияПолей, ИсточникиДанных)
	
	ДобавленоПолеСведений = Ложь;
	Если КадровыйУчет.НеобходимыПостоянныеКадровыеДанныеФизическихЛиц(ИмяПоля) Тогда
		
		ДобавленоПолеСведений = Истина;
		КадровыеДанные = ИсточникиДанных.Получить("ПостоянныеКадровыеДанныеФизическихЛиц");
		Если КадровыеДанные = Неопределено Тогда
			КадровыеДанные = Новый Массив;
		КонецЕсли; 
		КадровыеДанные.Добавить(ИмяПоля);
		
		ИсточникиДанных.Вставить("ПостоянныеКадровыеДанныеФизическихЛиц", КадровыеДанные);
		
		ПутьКДанным = "	ПостоянныеКадровыеДанныеФизическихЛиц." + ИмяПоля;
		ТекстОписанияПолей = ТекстОписанияПолей + "," + Символы.ПС + ПутьКДанным + " КАК " + ИмяПоля;
		
	КонецЕсли;
	
	Возврат ДобавленоПолеСведений;
	
КонецФункции

Процедура ДобавитьТекстЗапросаПостоянныхКадровыхДанныхФизическихЛиц(Запрос, ТолькоРазрешенные, ОписательВременнойТаблицыОтборов, ИсточникиДанных)
	
	КадровыеДанные = ИсточникиДанных.Получить("ПостоянныеКадровыеДанныеФизическихЛиц");
	Если КадровыеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИмяВТПостоянныеКадровыеДанныеФизическихЛиц = ЗарплатаКадрыОбщиеНаборыДанных.УникальноеИмяТекстаЗапроса("ВТПостоянныеКадровыеДанныеФизическихЛиц");
	ОписательТаблиц = КадровыйУчет.ОписаниеВременнойТаблицыОтборовФизическихЛиц(
		ОписательВременнойТаблицыОтборов.ИмяВременнойТаблицыОтборовСотрудников,
		ОписательВременнойТаблицыОтборов.ИмяПоляСотрудник + ".ФизическоеЛицо");
	
	ЗапросВТПостоянныеКадровыеДанныеФЛ = КадровыйУчет.ЗапросВТПостоянныеКадровыеДанныеФизическихЛиц(ТолькоРазрешенные, ОписательТаблиц, КадровыеДанные, ИмяВТПостоянныеКадровыеДанныеФизическихЛиц);
	ЗарплатаКадрыОбщиеНаборыДанных.СкопироватьПараметрыЗапроса(Запрос, ЗапросВТПостоянныеКадровыеДанныеФЛ);
	
	Запрос.Текст = ЗапросВТПостоянныеКадровыеДанныеФЛ.Текст
		+ ЗарплатаКадрыОбщиеНаборыДанных.РазделительЗапросов()
		+ Запрос.Текст
		+ Символы.ПС
		+ "		{ЛЕВОЕ СОЕДИНЕНИЕ " + ИмяВТПостоянныеКадровыеДанныеФизическихЛиц + " КАК ПостоянныеКадровыеДанныеФизическихЛиц
			|		ПО ВЫРАЗИТЬ(ТаблицаОтборовСотрудников." + ОписательВременнойТаблицыОтборов.ИмяПоляСотрудник + " КАК Справочник.Сотрудники).ФизическоеЛицо = ПостоянныеКадровыеДанныеФизическихЛиц.ФизическоеЛицо}";
		
КонецПроцедуры

// Текущие кадровые данные сотрудников.

Функция ДобавитьПолеСведенийТекущихКадровыхДанныхСотрудников(ИмяПоля, ТекстОписанияПолей, ИсточникиДанных)
	
	ДобавленоПолеСведений = Ложь;
	Если КадровыйУчет.НеобходимыСведенияТекущихКадровыхДанныхСотрудников(ИмяПоля) Тогда
		
		ДобавленоПолеСведений = Истина;
		ИсточникиДанных.Вставить("ТекущиеКадровыеДанныеСотрудников", Истина);
		
		Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьКадровыйУчет") Тогда
			
			ИмяФО = ИмяФункциональнойОпцииОрганизацииПоИмениДанныхСотрудника(ИмяПоля);
			Если НЕ ПустаяСтрока(ИмяФО) Тогда
				
				СписокФО = ИсточникиДанных.Получить("ФункциональныеОпцииОрганизаций");
				Если СписокФО = Неопределено Тогда
					СписокФО = Новый Массив;
				КонецЕсли; 
				СписокФО.Добавить(ИмяФО);
				ИсточникиДанных.Вставить("ФункциональныеОпцииОрганизаций", СписокФО);
				
			КонецЕсли;
			
		КонецЕсли; 
		
		ПутьКДанным = КадровыйУчет.ПутьКДаннымСведенийТекущихКадровыхДанныхСотрудников(ИмяПоля);
		ТекстОписанияПолей = ТекстОписанияПолей + "," + Символы.ПС + ПутьКДанным + " КАК " + ИмяПоля;
		
	КонецЕсли;
	
	Возврат ДобавленоПолеСведений;
	
КонецФункции

Функция НеобходимыСведенияТекущихКадровыхДанныхСотрудников(ИмяПоля) Экспорт
	
	ИмяПоля = ВРег(ИмяПоля);
	
	СведенияНеобходимы = 
		(ИмяПоля = ВРег("ДатаПриема") 
		ИЛИ ИмяПоля = ВРег("ДатаУвольнения") 
		ИЛИ ИмяПоля = ВРег("ТекущаяОрганизация") 
		ИЛИ ИмяПоля = ВРег("ТекущееПодразделение") 
		ИЛИ ИмяПоля = ВРег("ТекущаяДолжность") 
		ИЛИ ИмяПоля = ВРег("ТекущийВидЗанятости") 
		ИЛИ ИмяПоля = ВРег("ОсновноеРабочееМестоВОрганизации") 
		ИЛИ ИмяПоля = ВРег("ОформленПоТрудовомуДоговору"));
		
	Если НЕ СведенияНеобходимы И НЕ ПолучитьФункциональнуюОпцию("ИспользоватьКадровыйУчет") Тогда
		СведенияНеобходимы = КадровыйУчет.НеобходимыСведенияКадровойИсторииСотрудников(ИмяПоля);
	КонецЕсли; 
	
	Возврат СведенияНеобходимы;
			
КонецФункции

Функция ПутьКДаннымСведенийТекущихКадровыхДанныхСотрудников(ИмяПоля) Экспорт
	
	ИмяПоляВВерхнемРегистре = ВРег(ИмяПоля);
	
	ПутьКДанным = "";
	
	ФОИспользоватьКадровыйУчет = ПолучитьФункциональнуюОпцию("ИспользоватьКадровыйУчет");
	
	Если ИмяПоляВВерхнемРегистре = ВРег("ОформленПоТрудовомуДоговору") Тогда
		
		Если ФОИспользоватьКадровыйУчет Тогда
		
			ПутьКДанным =
				"ВЫБОР
				|	КОГДА ЕСТЬNULL(ТекущиеКадровыеДанныеСотрудников.ТекущийВидЗанятости, ЗНАЧЕНИЕ(Перечисление.ВидыЗанятости.ПустаяСсылка)) = ЗНАЧЕНИЕ(Перечисление.ВидыЗанятости.ПустаяСсылка)
				|		ТОГДА ЛОЖЬ
				|	ИНАЧЕ ИСТИНА
				|КОНЕЦ
				|";
				
		Иначе
			
			ПутьКДанным = 
				"ВЫБОР
				|	КОГДА ЕСТЬNULL(ТекущиеКадровыеДанныеСотрудников.ДатаПриема, ДАТАВРЕМЯ(1, 1, 1)) = ДАТАВРЕМЯ(1, 1, 1)
				|		ИЛИ ЕСТЬNULL(ТекущиеКадровыеДанныеСотрудников.ТекущаяОрганизация, ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)) = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
				|		ИЛИ ЕСТЬNULL(ТекущиеКадровыеДанныеСотрудников.ТекущийВидЗанятости, ЗНАЧЕНИЕ(Перечисление.ВидыЗанятости.ПустаяСсылка)) = ЗНАЧЕНИЕ(Перечисление.ВидыЗанятости.ПустаяСсылка)
				|		ТОГДА ЛОЖЬ
				|	ИНАЧЕ ИСТИНА
				|КОНЕЦ
				|";
				
		КонецЕсли;

	ИначеЕсли НЕ ФОИспользоватьКадровыйУчет
		И НеобходимыСведенияЗависящиеОтФункциональныхОпцийОрганизации(ИмяПоляВВерхнемРегистре) Тогда
		
		ПутьКДанным = ПутьКДаннымСведенийЗависящихОтФункциональныхОпцийОрганизаций(ИмяПоля, Ложь);
		
	ИначеЕсли НЕ ФОИспользоватьКадровыйУчет И ИмяПоляВВерхнемРегистре = ВРег("Организация") Тогда
		
		ПутьКДанным =
			"	ВЫБОР КОГДА ТекущиеКадровыеДанныеСотрудников.ТекущаяОрганизация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
			|		ТОГДА NULL
			|		ИНАЧЕ ТекущиеКадровыеДанныеСотрудников.ТекущаяОрганизация
			|	КОНЕЦ";
		
	ИначеЕсли НЕ ФОИспользоватьКадровыйУчет И ИмяПоляВВерхнемРегистре = ВРег("Подразделение") Тогда
		
		ПутьКДанным =
			"	ВЫБОР КОГДА ТекущиеКадровыеДанныеСотрудников.ТекущееПодразделение = ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)
			|		ТОГДА NULL
			|		ИНАЧЕ ТекущиеКадровыеДанныеСотрудников.ТекущееПодразделение
			|	КОНЕЦ";
		
	ИначеЕсли НЕ ФОИспользоватьКадровыйУчет И ИмяПоляВВерхнемРегистре = ВРег("Должность") Тогда
		
		ПутьКДанным =
			"	ВЫБОР КОГДА ТекущиеКадровыеДанныеСотрудников.ТекущаяДолжность = ЗНАЧЕНИЕ(Справочник.Должности.ПустаяСсылка)
			|		ТОГДА NULL
			|		ИНАЧЕ ТекущиеКадровыеДанныеСотрудников.ТекущаяДолжность
			|	КОНЕЦ";
		
	ИначеЕсли НЕ ФОИспользоватьКадровыйУчет И ИмяПоляВВерхнемРегистре = ВРег("ВидЗанятости") Тогда
		
		ПутьКДанным =
			"	ВЫБОР КОГДА ТекущиеКадровыеДанныеСотрудников.ТекущийВидЗанятости = ЗНАЧЕНИЕ(Перечисление.ВидыЗанятости.ПустаяСсылка)
			|		ТОГДА NULL
			|		ИНАЧЕ ТекущиеКадровыеДанныеСотрудников.ТекущийВидЗанятости
			|	КОНЕЦ";
		
	ИначеЕсли ИмяПоляВВерхнемРегистре = ВРег("КоличествоСтавок") Тогда
		
		ПутьКДанным = "	1";
		
	ИначеЕсли ИмяПоляВВерхнемРегистре = ВРег("ДатаПриема") Тогда
		
		ПутьКДанным = "	ЕСТЬNULL(ТекущиеКадровыеДанныеСотрудников.ДатаПриема, ДАТАВРЕМЯ(1, 1, 1))";
		
	ИначеЕсли ИмяПоляВВерхнемРегистре = ВРег("ДатаУвольнения") Тогда
		
		ПутьКДанным = "	ЕСТЬNULL(ТекущиеКадровыеДанныеСотрудников.ДатаУвольнения, ДАТАВРЕМЯ(1, 1, 1))";
		
	ИначеЕсли ИмяПоляВВерхнемРегистре = ВРег("ТекущаяОрганизация") Тогда
		
		ПутьКДанным = "	ЕСТЬNULL(ТекущиеКадровыеДанныеСотрудников.ТекущаяОрганизация, ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка))";
		
	ИначеЕсли ИмяПоляВВерхнемРегистре = ВРег("ТекущееПодразделение") Тогда
		
		ПутьКДанным = "	ЕСТЬNULL(ТекущиеКадровыеДанныеСотрудников.ТекущееПодразделение, ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка))";
		
	ИначеЕсли ИмяПоляВВерхнемРегистре = ВРег("ТекущаяДолжность") Тогда
		
		ПутьКДанным = "	ЕСТЬNULL(ТекущиеКадровыеДанныеСотрудников.ТекущаяДолжность, ЗНАЧЕНИЕ(Справочник.Должности.ПустаяСсылка))";
		
	ИначеЕсли ИмяПоляВВерхнемРегистре = ВРег("ТекущийВидЗанятости") Тогда
		
		ПутьКДанным = "	ЕСТЬNULL(ТекущиеКадровыеДанныеСотрудников.ТекущийВидЗанятости, ЗНАЧЕНИЕ(Перечисление.ВидыЗанятости.ПустаяСсылка))";
		
	ИначеЕсли ИмяПоляВВерхнемРегистре = ВРег("ОсновноеРабочееМестоВОрганизации") Тогда
		
		ПутьКДанным = "	ЕСТЬNULL(ТекущиеКадровыеДанныеСотрудников.ОсновноеРабочееМестоВОрганизации, ЛОЖЬ)";
			
	КонецЕсли;
		
	Возврат ПутьКДанным;
	
КонецФункции

Процедура ДобавитьТекстЗапросаТекущихКадровыхДанныхСотрудников(Запрос, ТолькоРазрешенные, ОписательВременнойТаблицыОтборов, ИсточникиДанных)
	
	Если ИсточникиДанных.Получить("ТекущиеКадровыеДанныеСотрудников") = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	Запрос.Текст = Запрос.Текст + Символы.ПС
		+ "		{ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТекущиеКадровыеДанныеСотрудников КАК ТекущиеКадровыеДанныеСотрудников
		|		ПО ТаблицаОтборовСотрудников." + ОписательВременнойТаблицыОтборов.ИмяПоляСотрудник + " = ТекущиеКадровыеДанныеСотрудников.Сотрудник}";
		
КонецПроцедуры

// Текущие тарифные ставки

Функция ДобавитьПолеСведенийТекущихТарифныхСтавокСотрудников(ИмяПоля, ТекстОписанияПолей, ИсточникиДанных)
	
	ДобавленоПолеСведений = Ложь;
	Если КадровыйУчет.НеобходимыСведенияТекущихТарифныхСтавкахСотрудников(ИмяПоля) Тогда
		
		ДобавленоПолеСведений = Истина;
		ИсточникиДанных.Вставить("ТекущаяТарифнаяСтавкаСотрудников", Истина);
		
		ПутьКДанным = КадровыйУчет.ПутьКДаннымСведенийТекущихТарифныхСтавокСотрудников(ИмяПоля);
		ТекстОписанияПолей = ТекстОписанияПолей + "," + Символы.ПС + ПутьКДанным + " КАК " + ИмяПоля;
		
	КонецЕсли;
	
	Возврат ДобавленоПолеСведений;
	
КонецФункции

Функция НеобходимыСведенияТекущихТарифныхСтавкахСотрудников(ИмяПоля) Экспорт
	
	ИмяПоля = ВРег(ИмяПоля);
	
	СведенияНеобходимы = (ИмяПоля = ВРег("ТекущаяТарифнаяСтавка")
		ИЛИ ИмяПоля = ВРег("ТекущийСпособРасчетаАванса")
		ИЛИ ИмяПоля = ВРег("ТекущийАванс"));
		
	Если НЕ СведенияНеобходимы И НЕ ПолучитьФункциональнуюОпцию("ИспользоватьКадровыйУчет") Тогда
		
		СведенияНеобходимы = КадровыйУчет.НеобходимыСведенияОбОплатеТруда(ИмяПоля);
		Если НЕ СведенияНеобходимы Тогда
			СведенияНеобходимы = НеобходимыСведенияОПлановыхАвансах(ИмяПоля);
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат СведенияНеобходимы;
	
КонецФункции

Функция ПутьКДаннымСведенийТекущихТарифныхСтавокСотрудников(ИмяПоля) Экспорт
	
	ИмяПоляВВерхнемРегистре = ВРег(ИмяПоля);
	
	ПутьКДанным = "";
	
	Если ИмяПоляВВерхнемРегистре = ВРег("ТекущаяТарифнаяСтавка") Тогда
		ПутьКДанным = "	ЕСТЬNULL(ТекущаяТарифнаяСтавкаСотрудников.ТекущаяТарифнаяСтавка, 0)";
	ИначеЕсли ИмяПоляВВерхнемРегистре = ВРег("ТекущийСпособРасчетаАванса") Тогда
		ПутьКДанным = "	ЕСТЬNULL(ТекущаяТарифнаяСтавкаСотрудников.ТекущийСпособРасчетаАванса, ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаАванса.ПустаяСсылка))";
	ИначеЕсли ИмяПоляВВерхнемРегистре = ВРег("ТекущийАванс") Тогда
		ПутьКДанным = "	ЕСТЬNULL(ТекущаяТарифнаяСтавкаСотрудников.ТекущийАванс, 0)";
	ИначеЕсли НЕ ПолучитьФункциональнуюОпцию("ИспользоватьКадровыйУчет") Тогда
		
		Если КадровыйУчет.НеобходимыСведенияОбОплатеТруда(ИмяПоляВВерхнемРегистре) Тогда
			
			Если ИмяПоляВВерхнемРегистре = ВРег("Надбавка") Тогда
				ПутьКДанным = "	NULL";
			Иначе
				ПутьКДанным = "	ТекущаяТарифнаяСтавкаСотрудников.ТекущаяТарифнаяСтавка";
			КонецЕсли;
			
		ИначеЕсли НеобходимыСведенияОПлановыхАвансах(ИмяПоля) Тогда
			
			Если ИмяПоляВВерхнемРегистре = ВРег("СпособРасчетаАванса") Тогда
				ПутьКДанным = "	ТекущаяТарифнаяСтавкаСотрудников.ТекущийСпособРасчетаАванса";;
			ИначеЕсли ИмяПоляВВерхнемРегистре = ВРег("Аванс") Тогда
				ПутьКДанным = "	ТекущаяТарифнаяСтавкаСотрудников.ТекущийАванс";
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
		
	Возврат ПутьКДанным;
	
КонецФункции

Процедура ДобавитьТекстЗапросаТекущихТарифныхСтавок(Запрос, ТолькоРазрешенные, ОписательВременнойТаблицыОтборов, ИсточникиДанных);
	
	Если ИсточникиДанных.Получить("ТекущаяТарифнаяСтавкаСотрудников") = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	Запрос.Текст = Запрос.Текст + Символы.ПС
		+ "		{ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТекущаяТарифнаяСтавкаСотрудников КАК ТекущаяТарифнаяСтавкаСотрудников
		|		ПО ТаблицаОтборовСотрудников." + ОписательВременнойТаблицыОтборов.ИмяПоляСотрудник + " = ТекущаяТарифнаяСтавкаСотрудников.Сотрудник}";
		
КонецПроцедуры

// Данные приказов о приеме об увольнении.

Функция ДобавитьПолеСведенийДанныхПриказаОПриемеОбУвольнении(ИмяПоля, ТекстОписанияПолей, ИсточникиДанных)
	
	ДобавленоПолеСведений = Ложь;
	
	НеобходимыСведенияОПриеме = КадровыйУчет.НеобходимыСведенияПриказаОПриеме(ИмяПоля);
	Если НеобходимыСведенияОПриеме
		ИЛИ НеобходимыСведенияПриказаОбУвольнении(ИмяПоля) Тогда
		
		ДобавленоПолеСведений = Истина;
		
		НеобходимыеСведения = ИсточникиДанных.Получить("ДанныеПриказаОПриемеОбУвольнении");
		Если НеобходимыеСведения = Неопределено Тогда
			НеобходимыеСведения = Новый Структура("НеобходимыСведенияПриказаОПриеме,НеобходимыСведенияПриказаОбУвольнении", Ложь, Ложь);
		КонецЕсли; 
		
		Если НеобходимыСведенияОПриеме Тогда
			НеобходимыеСведения.НеобходимыСведенияПриказаОПриеме = Истина;
		Иначе
			НеобходимыеСведения.НеобходимыСведенияПриказаОбУвольнении = Истина;
		КонецЕсли;
		ИсточникиДанных.Вставить("ДанныеПриказаОПриемеОбУвольнении", НеобходимыеСведения);
		
		ПутьКДанным = КадровыйУчет.ПутьКДаннымСведенийПриказаОПриемеОбУвольнении(ИмяПоля);
		ТекстОписанияПолей = ТекстОписанияПолей + "," + Символы.ПС + ПутьКДанным + " КАК " + ИмяПоля;
		
	КонецЕсли;
	
	Возврат ДобавленоПолеСведений;
	
КонецФункции

Функция НеобходимыСведенияПриказаОПриеме(ИмяПоля) Экспорт
	
	ИмяПоля = ВРег(ИмяПоля);
	
	Возврат ИмяПоля = ВРег("ПриказОПриеме")
		ИЛИ ИмяПоля = ВРег("ПриказОПриемеДата")
		ИЛИ ИмяПоля = ВРег("ПриказОПриемеНомер");
	
КонецФункции

Функция НеобходимыСведенияПриказаОбУвольнении(Знач ИмяПоля)
	
	ИмяПоля = ВРег(ИмяПоля);
	
	Возврат ИмяПоля = ВРег("ПриказОбУвольнении")
		ИЛИ ИмяПоля = ВРег("ПриказОбУвольненииДата")
		ИЛИ ИмяПоля = ВРег("ПриказОбУвольненииНомер")
		ИЛИ ИмяПоля = ВРег("ПриказОбУвольненииСтатьяТКРФ");
	
КонецФункции

Функция ЗапросВТСведенийПриказовОПриеме(ИмяВТКадроваяИсторияСотрудников, ТолькоРазрешенные, ИмяВТСведенийПриказовОПриеме) Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ПриемНаРаботу.Ссылка,
		|	ПриемНаРаботу.Номер,
		|	ПриемНаРаботу.Дата,
		|	ПриемНаРаботу.Организация,
		|	ПриемНаРаботу.Подразделение,
		|	ПриемНаРаботу.Сотрудник,
		|	ПриемНаРаботу.ФизическоеЛицо,
		|	ПриемНаРаботу.Должность,
		|	ПриемНаРаботу.ДатаПриема,
		|	ПриемНаРаботу.ВидЗанятости,
		|	ПриемНаРаботу.УсловияПриема,
		|	ПриемНаРаботу.СпособРасчетаАванса,
		|	ПриемНаРаботу.Аванс,
		|	ПриемНаРаботу.Руководитель,
		|	ПриемНаРаботу.ДолжностьРуководителя,
		|	ПриемНаРаботу.Проведен
		|ПОМЕСТИТЬ ВТСведенийПриказовОПриеме
		|ИЗ
		|	Документ.ПриемНаРаботу КАК ПриемНаРаботу
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТКадроваяИсторияСотрудников КАК КадроваяИстория
		|		ПО ПриемНаРаботу.Сотрудник = КадроваяИстория.Сотрудник
		|			И ПриемНаРаботу.Ссылка = КадроваяИстория.Регистратор
		|			И (КадроваяИстория.ВидСобытия = ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Прием))";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТКадроваяИсторияСотрудников", ИмяВТКадроваяИсторияСотрудников);
	
	ЗарплатаКадрыОбщиеНаборыДанных.ЗаменитьИмяСоздаваемойВременнойТаблицы(Запрос.Текст, "ВТСведенийПриказовОПриеме", ИмяВТСведенийПриказовОПриеме);
	
	ЗарплатаКадрыОбщиеНаборыДанных.УстановитьВыборкуТолькоРазрешенныхДанных(Запрос.Текст, ТолькоРазрешенные);
	
	Возврат Запрос;
	
КонецФункции

Функция ЗапросВТСведенийПриказовОбУвольнении(ИмяВТКадроваяИсторияСотрудников, ТолькоРазрешенные, ИмяВТСведенийПриказовОбУвольнении) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Увольнение.Ссылка,
		|	Увольнение.Номер,
		|	Увольнение.Дата,
		|	Увольнение.Организация,
		|	Увольнение.ДатаУвольнения,
		|	Увольнение.Сотрудник,
		|	Увольнение.ФизическоеЛицо,
		|	Увольнение.СтатьяТКРФ,
		|	Увольнение.УдалитьСтатьяТКРФ,
		|	Увольнение.ОснованиеУвольнения,
		|	Увольнение.Руководитель,
		|	Увольнение.ДолжностьРуководителя,
		|	Увольнение.Проведен
		|ПОМЕСТИТЬ ВТСведенийПриказовОбУвольнении
		|ИЗ
		|	Документ.Увольнение КАК Увольнение
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТКадроваяИсторияСотрудников КАК КадроваяИстория
		|		ПО Увольнение.Сотрудник = КадроваяИстория.Сотрудник
		|			И Увольнение.Ссылка = КадроваяИстория.Регистратор
		|			И (КадроваяИстория.ВидСобытия = ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Увольнение))";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТКадроваяИсторияСотрудников", ИмяВТКадроваяИсторияСотрудников);
	
	ЗарплатаКадрыОбщиеНаборыДанных.ЗаменитьИмяСоздаваемойВременнойТаблицы(Запрос.Текст, "ВТСведенийПриказовОбУвольнении", ИмяВТСведенийПриказовОбУвольнении);
	
	ЗарплатаКадрыОбщиеНаборыДанных.УстановитьВыборкуТолькоРазрешенныхДанных(Запрос.Текст, ТолькоРазрешенные);
	
	Возврат Запрос;
	
КонецФункции

Функция ПутьКДаннымСведенийПриказаОПриемеОбУвольнении(ИмяПоля) Экспорт
	
	ИмяПоляВВерхнемРегистре = ВРег(ИмяПоля);
	
	ПутьКДанным = "";
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьКадровыйУчет") Тогда
		
		Если ИмяПоляВВерхнемРегистре = ВРег("ПриказОПриеме") Тогда
			ПутьКДанным = "	ПриказыОПриеме.Ссылка";
		ИначеЕсли ИмяПоляВВерхнемРегистре = ВРег("ПриказОПриемеДата") Тогда
			ПутьКДанным = "	ПриказыОПриеме.Дата";
		ИначеЕсли ИмяПоляВВерхнемРегистре = ВРег("ПриказОПриемеНомер") Тогда
			ПутьКДанным = "	ПриказыОПриеме.Номер";
		ИначеЕсли ИмяПоляВВерхнемРегистре = ВРег("ПриказОбУвольнении") Тогда
			ПутьКДанным = "	ПриказыОбУвольнении.Ссылка";
		ИначеЕсли ИмяПоляВВерхнемРегистре = ВРег("ПриказОбУвольненииДата") Тогда
			ПутьКДанным = "	ПриказыОбУвольнении.Дата";
		ИначеЕсли ИмяПоляВВерхнемРегистре = ВРег("ПриказОбУвольненииНомер") Тогда
			ПутьКДанным = "	ПриказыОбУвольнении.Номер";
		ИначеЕсли ИмяПоляВВерхнемРегистре = ВРег("ПриказОбУвольненииСтатьяТКРФ") Тогда
			ПутьКДанным =
				"ВЫБОР
				|	КОГДА ПриказыОбУвольнении.СтатьяТКРФ = ЗНАЧЕНИЕ(Справочник.ОснованияУвольнения.ПустаяСсылка) ТОГДА """"
				|	ИНАЧЕ ПриказыОбУвольнении.СтатьяТКРФ.ТекстОснования + "", ""+ ПриказыОбУвольнении.СтатьяТКРФ.Наименование
				|КОНЕЦ";
		КонецЕсли;
		
	Иначе
		ПутьКДанным = "	NULL";
	КонецЕсли;
	
	Возврат ПутьКДанным;
	
КонецФункции

Процедура ДобавитьТекстЗапросаДанныхПриказовОПриемеОбУвольнении(Запрос, ТолькоРазрешенные, ОписательВременнойТаблицыОтборов, ИсточникиДанных)
	
	НеобходимыеСведения = ИсточникиДанных.Получить("ДанныеПриказаОПриемеОбУвольнении");
	Если НеобходимыеСведения = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьКадровыйУчет") Тогда
		Возврат;
	КонецЕсли;
	
	ИмяВТИзмеренияДатыДляТаблицыПриказов = ЗарплатаКадрыОбщиеНаборыДанных.УникальноеИмяТекстаЗапроса("ВТИзмеренияДатыДляТаблицыПриказов");
	ИмяВТКадроваяИсторияСотрудников = ЗарплатаКадрыОбщиеНаборыДанных.УникальноеИмяТекстаЗапроса("ВТКадроваяИсторияСотрудников");
			
	ОписаниеФильтра = ЗарплатаКадрыОбщиеНаборыДанных.ОписаниеФильтраДляСоздатьВТИмяРегистра(
		ИмяВТИзмеренияДатыДляТаблицыПриказов,
		"Сотрудник");
				
	ПараметрыПостроения = ЗарплатаКадрыОбщиеНаборыДанных.ПараметрыПостроенияДляСоздатьВТИмяРегистра();
	ЗарплатаКадрыОбщиеНаборыДанных.ДобавитьВКоллекциюОтбор(ПараметрыПостроения.Отборы,
		"ВидСобытия", "В", "ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Прием), ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Увольнение)");
			
	ЗапросВТИмяРегистра = ЗарплатаКадрыОбщиеНаборыДанных.ЗапросВТИмяРегистра(
		"КадроваяИсторияСотрудников",
		ТолькоРазрешенные,
		ОписаниеФильтра,
		ПараметрыПостроения,
		ИмяВТКадроваяИсторияСотрудников);
		
	ЗарплатаКадрыОбщиеНаборыДанных.СкопироватьПараметрыЗапроса(Запрос, ЗапросВТИмяРегистра);
	
	Если НеобходимыеСведения.НеобходимыСведенияПриказаОПриеме Тогда
		
		ИмяВТСведенийПриказовОПриеме = ЗарплатаКадрыОбщиеНаборыДанных.УникальноеИмяТекстаЗапроса("ВТСведенийПриказовОПриеме");
		ЗапросСведенийПриказовОПриеме = КадровыйУчет.ЗапросВТСведенийПриказовОПриеме(ИмяВТКадроваяИсторияСотрудников, ТолькоРазрешенные, ИмяВТСведенийПриказовОПриеме);
		
		ЗарплатаКадрыОбщиеНаборыДанных.ОбъединитьТекстыЗапросов(ЗапросВТИмяРегистра.Текст, ЗапросСведенийПриказовОПриеме.Текст);
		
		Запрос.Текст = Запрос.Текст +
			"
			|{ЛЕВОЕ СОЕДИНЕНИЕ " + ИмяВТСведенийПриказовОПриеме + " КАК ПриказыОПриеме
			| 	ПО ТаблицаОтборовСотрудников." + ОписательВременнойТаблицыОтборов.ИмяПоляСотрудник + " = ПриказыОПриеме.Сотрудник}";
		
	КонецЕсли;
	
	Если НеобходимыеСведения.НеобходимыСведенияПриказаОбУвольнении Тогда
		
		ИмяВТСведенийПриказовОбУвольнении = ЗарплатаКадрыОбщиеНаборыДанных.УникальноеИмяТекстаЗапроса("ВТСведенийПриказовОбУвольнении");
		ЗапросСведенийПриказовОбУвольнении = КадровыйУчет.ЗапросВТСведенийПриказовОбУвольнении(ИмяВТКадроваяИсторияСотрудников, ТолькоРазрешенные, ИмяВТСведенийПриказовОбУвольнении);
		
		ЗарплатаКадрыОбщиеНаборыДанных.ОбъединитьТекстыЗапросов(ЗапросВТИмяРегистра.Текст, ЗапросСведенийПриказовОбУвольнении.Текст);
		
		Запрос.Текст = Запрос.Текст +
			"
			|{ЛЕВОЕ СОЕДИНЕНИЕ " + ИмяВТСведенийПриказовОбУвольнении + " КАК ПриказыОбУвольнении
			| 	ПО ТаблицаОтборовСотрудников." + ОписательВременнойТаблицыОтборов.ИмяПоляСотрудник + " = ПриказыОбУвольнении.Сотрудник}";
		
	КонецЕсли; 
				
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ИзмеренияДаты." + ОписательВременнойТаблицыОтборов.ИмяПоляСотрудник + " КАК Сотрудник,
		|	ДАТАВРЕМЯ(1,1,1) КАК ДатаНачала,
		|	ДАТАВРЕМЯ(1,1,1) КАК ДатаОкончания
		|ПОМЕСТИТЬ " + ИмяВТИзмеренияДатыДляТаблицыПриказов + "
		|ИЗ
		|	" + ОписательВременнойТаблицыОтборов.ИмяВременнойТаблицыОтборовСотрудников + " КАК ИзмеренияДаты"
		+ ЗарплатаКадрыОбщиеНаборыДанных.РазделительЗапросов()
		+ ЗапросВТИмяРегистра.Текст
		+ ЗарплатаКадрыОбщиеНаборыДанных.РазделительЗапросов()
		+ Запрос.Текст;
		
КонецПроцедуры

// Постоянные кадровые данные

Функция ДобавитьПолеПостоянныхКадровыхДанныхСотрудников(ИмяПоля, ТекстОписанияПолей, ИсточникиДанных)
	
	ДобавленоПолеСведений = Ложь;
	Если КадровыйУчет.НеобходимыПостоянныеКадровыеДанныеСотрудников(ИмяПоля) Тогда
		
		ДобавленоПолеСведений = Истина;
		
		КадровыеДанные = ИсточникиДанных.Получить("ПостоянныеКадровыеДанныеСотрудников");
		Если КадровыеДанные = Неопределено Тогда
			КадровыеДанные = Новый Массив;
		КонецЕсли; 
		КадровыеДанные.Добавить(ИмяПоля);
		ИсточникиДанных.Вставить("ПостоянныеКадровыеДанныеСотрудников", КадровыеДанные);
		
		ПутьКДанным = "	ПостоянныеКадровыеДанныеСотрудников." + ИмяПоля;
		ТекстОписанияПолей = ТекстОписанияПолей + "," + Символы.ПС + ПутьКДанным + " КАК " + ИмяПоля;
		
	КонецЕсли;
	
	Возврат ДобавленоПолеСведений;
	
КонецФункции

Функция НеобходимыПостоянныеКадровыеДанныеСотрудников(ИмяПоля) Экспорт
	
	Возврат НеобходимыЗначенияРеквизитовСправочникаСотрудники(ИмяПоля)
		ИЛИ КадровыйУчет.НеобходимыСведенияТекущихКадровыхДанныхСотрудников(ИмяПоля)
		ИЛИ КадровыйУчет.НеобходимыСведенияТекущихТарифныхСтавкахСотрудников(ИмяПоля)
		ИЛИ КадровыйУчет.НеобходимыСведенияПриказаОПриеме(ИмяПоля)
		ИЛИ НеобходимыСведенияПриказаОбУвольнении(ИмяПоля);
	
КонецФункции

Процедура ДобавитьТекстЗапросаВТПостоянныеКадровыеДанныеСотрудников(Запрос, ТолькоРазрешенные, ОписательВременнойТаблицыОтборов, ИсточникиДанных);
	
	КадровыеДанные = ИсточникиДанных.Получить("ПостоянныеКадровыеДанныеСотрудников");
	Если КадровыеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	ИмяВТПостоянныеКадровыеДанныеСотрудников = ЗарплатаКадрыОбщиеНаборыДанных.УникальноеИмяТекстаЗапроса("ВТПостоянныеКадровыеДанныеСотрудников");
	
	ЗапросВТПостоянныеКадровыеДанные = КадровыйУчет.ЗапросВТПостоянныеКадровыеДанныеСотрудников(ТолькоРазрешенные,ОписательВременнойТаблицыОтборов, КадровыеДанные, ИмяВТПостоянныеКадровыеДанныеСотрудников); 
	
	ЗарплатаКадрыОбщиеНаборыДанных.СкопироватьПараметрыЗапроса(Запрос, ЗапросВТПостоянныеКадровыеДанные);
	
	Запрос.Текст =
		ЗапросВТПостоянныеКадровыеДанные.Текст
		+ ЗарплатаКадрыОбщиеНаборыДанных.РазделительЗапросов()
		+ Запрос.Текст
		+ Символы.ПС
		+ "		{ЛЕВОЕ СОЕДИНЕНИЕ " + ИмяВТПостоянныеКадровыеДанныеСотрудников + " КАК ПостоянныеКадровыеДанныеСотрудников
			|		ПО ТаблицаОтборовСотрудников." + ОписательВременнойТаблицыОтборов.ИмяПоляСотрудник + " = ПостоянныеКадровыеДанныеСотрудников.Сотрудник}";
		
КонецПроцедуры

// Кадровые данные физических лиц.

Функция ДобавитьПолеКадровыхДанныхФизическихЛиц(ИмяПоля, ТекстОписанияПолей, ИсточникиДанных)
	
	ДобавленоПолеСведений = Ложь;
	Если КадровыйУчет.НеобходимыКадровыеДанныеФизическогоЛица(ИмяПоля) Тогда
		
		ДобавленоПолеСведений = Истина;
		
		КадровыеДанные = ИсточникиДанных.Получить("КадровыеДанныеФизическихЛиц");
		Если КадровыеДанные = Неопределено Тогда
			КадровыеДанные = Новый Массив;
		КонецЕсли; 
		КадровыеДанные.Добавить(ИмяПоля);
		ИсточникиДанных.Вставить("КадровыеДанныеФизическихЛиц", КадровыеДанные);
		
		ПутьКДанным = "	КадровыеДанныеФизическихЛиц." + ИмяПоля;
		ТекстОписанияПолей = ТекстОписанияПолей + "," + Символы.ПС + ПутьКДанным + " КАК " + ИмяПоля;
		
	КонецЕсли;
	
	Возврат ДобавленоПолеСведений;
	
КонецФункции

Процедура ДобавитьТекстЗапросаВТКадровыеДанныеФизическихЛиц(Запрос, ТолькоРазрешенные, ОписательВременнойТаблицыОтборов, ПоляОтбораПериодическихДанных, ИсточникиДанных)
	
	КадровыеДанные = ИсточникиДанных.Получить("КадровыеДанныеФизическихЛиц");
	Если КадровыеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	ИмяВТКадровыеДанныеФизическихЛиц = ЗарплатаКадрыОбщиеНаборыДанных.УникальноеИмяТекстаЗапроса("ВТКадровыеДанныеФизическихЛиц");
	
	ОписательВременнойТаблицыОтборовФизическихЛиц = КадровыйУчет.ОписаниеВременнойТаблицыОтборовФизическихЛиц(
		ОписательВременнойТаблицыОтборов.ИмяВременнойТаблицыОтборовСотрудников,
		ОписательВременнойТаблицыОтборов.ИмяПоляСотрудник + ".ФизическоеЛицо",
		ОписательВременнойТаблицыОтборов.ИмяПоляПериод);
	
	ЗапросВТКадровыеДанныеФЛ = КадровыйУчет.ЗапросВТКадровыеДанныеФизическихЛиц(ТолькоРазрешенные, ОписательВременнойТаблицыОтборовФизическихЛиц, КадровыеДанные, ПоляОтбораПериодическихДанных, ИмяВТКадровыеДанныеФизическихЛиц); 
	
	ЗарплатаКадрыОбщиеНаборыДанных.СкопироватьПараметрыЗапроса(Запрос, ЗапросВТКадровыеДанныеФЛ);
	
	Запрос.Текст =
		ЗапросВТКадровыеДанныеФЛ.Текст
		+ ЗарплатаКадрыОбщиеНаборыДанных.РазделительЗапросов()
		+ Запрос.Текст
		+ Символы.ПС
		+ "		{ЛЕВОЕ СОЕДИНЕНИЕ " + ИмяВТКадровыеДанныеФизическихЛиц + " КАК КадровыеДанныеФизическихЛиц
			|		ПО ВЫРАЗИТЬ(ТаблицаОтборовСотрудников." + ОписательВременнойТаблицыОтборов.ИмяПоляСотрудник + " КАК Справочник.Сотрудники).ФизическоеЛицо = КадровыеДанныеФизическихЛиц.ФизическоеЛицо
			|			И ТаблицаОтборовСотрудников." + ОписательВременнойТаблицыОтборов.ИмяПоляПериод + " = КадровыеДанныеФизическихЛиц.Период}";
		
КонецПроцедуры

// Значения зависящие от функциональных опций.

Функция НеобходимыСведенияЗависящиеОтФункциональныхОпцийОрганизации(Знач ИмяПоля)
	
	ИмяПоля = ВРег(ИмяПоля);
	
	Возврат ИмяПоля = ВРег("ЯвляетсяЧленомЛетногоЭкипажа")
		ИЛИ ИмяПоля = ВРег("ЯвляетсяШахтером")
		ИЛИ ИмяПоля = ВРег("ЯвляетсяФармацевтом")
		ИЛИ ИмяПоля = ВРег("ЯвляетсяПрокурором")
		ИЛИ ИмяПоля = ВРег("ЯвляетсяВоеннослужащим")
		ИЛИ ИмяПоля = ВРег("РаботаетВСтуденческомОтряде")
		ИЛИ ИмяПоля = ВРег("ЯвляетсяРаботникомСДосрочнойПенсией")
		ИЛИ ИмяПоля = ВРег("ЯвляетсяЧленомЭкипажаСуднаПодФлагомРФ");
	
КонецФункции

Функция ИмяФункциональнойОпцииОрганизацииПоИмениДанныхСотрудника(Знач ИмяПоля)
	
	ИмяПоля = ВРег(ИмяПоля);
	
	Если ИмяПоля = ВРег("ЯвляетсяЧленомЛетногоЭкипажа") Тогда
		
		Возврат "ИспользуетсяТрудЧленовЛетныхЭкипажей";
		
	ИначеЕсли ИмяПоля = ВРег("ЯвляетсяШахтером") Тогда
		
		Возврат "ИспользуетсяТрудШахтеров";
		
	ИначеЕсли ИмяПоля = ВРег("ЯвляетсяФармацевтом") Тогда
		
		Возврат "ИспользуетсяТрудФармацевтов";
		
	ИначеЕсли ИмяПоля = ВРег("ЯвляетсяЧленомЭкипажаСуднаПодФлагомРФ") Тогда
		
		Возврат "ИспользуетсяТрудЧленовЭкипажейМорскихСудов";
		
	ИначеЕсли ИмяПоля = ВРег("ЯвляетсяРаботникомСДосрочнойПенсией") Тогда
		
		Возврат "ИспользуютсяРаботыСДосрочнойПенсией";
		
	КонецЕсли; 
	
	Возврат "";
	
КонецФункции

Функция ПутьКДаннымСведенийЗависящихОтФункциональныхОпцийОрганизаций(ИмяПоля, ИспользоватьКадровуюИсторию) Экспорт
	
	ИмяПоляВВерхнемРегистре = ВРег(ИмяПоля);
	
	ПутьКДанным = "";
	
	Если ИспользоватьКадровуюИсторию Тогда
		ПутьКДолжности = "ВЫРАЗИТЬ(КадроваяИсторияСотрудников.Должность КАК Справочник.Должности)";
		ПутьКПодразделению = "ВЫРАЗИТЬ(КадроваяИсторияСотрудников.Подразделение КАК Справочник.ПодразделенияОрганизаций)";
	Иначе
		ПутьКДолжности = "ВЫРАЗИТЬ(ТекущиеКадровыеДанныеСотрудников.ТекущаяДолжность КАК Справочник.Должности)";
		ПутьКПодразделению = "ВЫРАЗИТЬ(ТекущиеКадровыеДанныеСотрудников.ТекущееПодразделение КАК Справочник.ПодразделенияОрганизаций)";
	КонецЕсли;
	
	Если ИмяПоляВВерхнемРегистре = ВРег("ЯвляетсяЧленомЛетногоЭкипажа") Тогда
		
		ПутьКДанным =
			"	ВЫБОР 
			|		КОГДА ФункциональныеОпции.ИспользуетсяТрудЧленовЛетныхЭкипажей 
			|			ТОГДА ЕСТЬNULL(" + ПутьКДолжности + ".ЯвляетсяДолжностьюЛетногоЭкипажа, ЛОЖЬ) 
			|		ИНАЧЕ ЛОЖЬ 
			|	КОНЕЦ";
		
	ИначеЕсли ИмяПоляВВерхнемРегистре = ВРег("ЯвляетсяШахтером") Тогда
		
		ПутьКДанным =
			"	ВЫБОР 
			|		КОГДА ФункциональныеОпции.ИспользуетсяТрудШахтеров 
			|			ТОГДА ЕСТЬNULL(" + ПутьКДолжности + ".ЯвляетсяШахтерскойДолжностью, ЛОЖЬ) 
			|		ИНАЧЕ ЛОЖЬ 
			|	КОНЕЦ";
		
	ИначеЕсли ИмяПоляВВерхнемРегистре = ВРег("ЯвляетсяФармацевтом") Тогда
		
		ПутьКДанным =
			"	ВЫБОР 
			|		КОГДА ФункциональныеОпции.ИспользуетсяТрудФармацевтов 
			|			ТОГДА ЕСТЬNULL(" + ПутьКДолжности + ".ЯвляетсяФармацевтическойДолжностью, ЛОЖЬ) 
			|		ИНАЧЕ ЛОЖЬ 
			|	КОНЕЦ";
	
	ИначеЕсли ИмяПоляВВерхнемРегистре = ВРег("ЯвляетсяРаботникомСДосрочнойПенсией") Тогда
		
		ПутьКДанным =
			"	ВЫБОР 
			|		КОГДА ФункциональныеОпции.ИспользуютсяРаботыСДосрочнойПенсией 
			|			ТОГДА ЕСТЬNULL(" + ПутьКДолжности + ".ВзимаютсяВзносыЗаЗанятыхНаРаботахСДосрочнойПенсией, ЗНАЧЕНИЕ(Перечисление.ВидыРаботСДосрочнойПенсией.ПустаяСсылка)) 
			|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыРаботСДосрочнойПенсией.ПустаяСсылка) 
			|	КОНЕЦ";
		
	ИначеЕсли ИмяПоляВВерхнемРегистре = ВРег("ЯвляетсяПрокурором")
		ИЛИ ИмяПоляВВерхнемРегистре = ВРег("ЯвляетсяВоеннослужащим") 
		ИЛИ ИмяПоляВВерхнемРегистре = ВРег("РаботаетВСтуденческомОтряде") Тогда
		
		ПутьКДанным = "	ЛОЖЬ";
	
	ИначеЕсли ИмяПоляВВерхнемРегистре = ВРег("ЯвляетсяЧленомЭкипажаСуднаПодФлагомРФ") Тогда
		
		ПутьКДанным =
			"	ВЫБОР
			|		КОГДА ФункциональныеОпции.ИспользуетсяТрудЧленовЭкипажейМорскихСудов 
			|			ТОГДА ЕСТЬNULL(" + ПутьКПодразделению + ".СоответствуетСудамЗарегистрированнымВРоссийскомМеждународномРеестреСудов, ЛОЖЬ)
			|		ИНАЧЕ ЛОЖЬ
			|	КОНЕЦ";
		
	КонецЕсли;
	
	Возврат ПутьКДанным;
	
КонецФункции

Процедура ДобавитьТекстЗапросаВТФункциональныеОпцииОрганизаций(Запрос, ТолькоРазрешенные, ОписательВременнойТаблицыОтборов, ИсточникиДанных)
	
	СписокОпций = ИсточникиДанных.Получить("ФункциональныеОпцииОрганизаций");
	Если СписокОпций = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	ИмяВТФункциональныеОпцииОрганизаций = ЗарплатаКадрыОбщиеНаборыДанных.УникальноеИмяТекстаЗапроса("ВТФункциональныеОпцииОрганизаций");
	
	ЗапросВТФункциональныеОпции = ЗапросВТФункциональныеОпцииОрганизаций(ТолькоРазрешенные, ИмяВТФункциональныеОпцииОрганизаций, СписокОпций);
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьКадровыйУчет") Тогда
		ПутьКПолюОрганизация = "КадроваяИсторияСотрудников.Организация";
	Иначе
		ПутьКПолюОрганизация = "ТекущиеКадровыеДанныеСотрудников.ТекущаяОрганизация";
	КонецЕсли;
	
	Запрос.Текст = 
		ЗапросВТФункциональныеОпции.Текст
		+ ЗарплатаКадрыОбщиеНаборыДанных.РазделительЗапросов()
		+ Запрос.Текст
		+ "
			|{ЛЕВОЕ СОЕДИНЕНИЕ " + ИмяВТФункциональныеОпцииОрганизаций + " КАК ФункциональныеОпции
			| 	ПО " + ПутьКПолюОрганизация + " = ФункциональныеОпции.Организация}";
	
КонецПроцедуры

// Кадровая история сотрудников.

Функция ДобавитьПолеСведенийКадровойИсторииСотрудников(ИмяПоля, ТекстОписанияПолей, ИсточникиДанных)
	
	ДобавленоПолеСведений = Ложь;
	Если ПолучитьФункциональнуюОпцию("ИспользоватьКадровыйУчет") И КадровыйУчет.НеобходимыСведенияКадровойИсторииСотрудников(ИмяПоля) Тогда
		
		ДобавленоПолеСведений = Истина;
		
		ПоляКадровойИстории = ИсточникиДанных.Получить("КадроваяИсторияСотрудников");
		Если ПоляКадровойИстории = Неопределено Тогда
			ПоляКадровойИстории = Новый Массив;
		КонецЕсли; 
		
		ПоляКадровойИстории.Добавить(ВРег(ИмяПоля));
		ИсточникиДанных.Вставить("КадроваяИсторияСотрудников", ПоляКадровойИстории);
		
		ИмяФО = ИмяФункциональнойОпцииОрганизацииПоИмениДанныхСотрудника(ИмяПоля);
		Если НЕ ПустаяСтрока(ИмяФО) Тогда
			
			СписокФО = ИсточникиДанных.Получить("ФункциональныеОпцииОрганизаций");
			Если СписокФО = Неопределено Тогда
				СписокФО = Новый Массив;
			КонецЕсли; 
			СписокФО.Добавить(ИмяФО);
			ИсточникиДанных.Вставить("ФункциональныеОпцииОрганизаций", СписокФО);
			
		КонецЕсли; 
		
		ПутьКДанным = КадровыйУчет.ПутьКДаннымСведенийКадровойИсторииСотрудников(ИмяПоля);
		ТекстОписанияПолей = ТекстОписанияПолей + "," + Символы.ПС + ПутьКДанным + " КАК " + ИмяПоля;
		
	КонецЕсли;
	
	Возврат ДобавленоПолеСведений;
	
КонецФункции

Функция НеобходимыСведенияКадровойИсторииСотрудников(ИмяПоля) Экспорт
	
	ИмяПоля = ВРег(ИмяПоля);
	
	Возврат ИмяПоля = ВРег("Организация")
		ИЛИ ИмяПоля = ВРег("Подразделение")
		ИЛИ ИмяПоля = ВРег("Должность")
		ИЛИ ИмяПоля = ВРег("ВидЗанятости")
		ИЛИ ИмяПоля = ВРег("ВидСобытия")
		ИЛИ ИмяПоля = ВРег("КоличествоСтавок")
		ИЛИ НеобходимыСведенияЗависящиеОтФункциональныхОпцийОрганизации(ИмяПоля)
		ИЛИ ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ГосударственнаяСлужба")
			И ОбщегоНазначения.ОбщийМодуль("ГосударственнаяСлужба").НеобходимыСвойстваДолжностейГосударственнойСлужбы(ИмяПоля);
	
КонецФункции

Функция ПутьКДаннымСведенийКадровойИсторииСотрудников(ИмяПоля) Экспорт
	
	ИмяПоляВВерхнемРегистре = ВРег(ИмяПоля);
	
	ПутьКДанным = "";
	
	Если ИмяПоляВВерхнемРегистре = ВРег("КоличествоСтавок") Тогда
		ПутьКДанным = "	1";
	ИначеЕсли НеобходимыСведенияЗависящиеОтФункциональныхОпцийОрганизации(ИмяПоляВВерхнемРегистре) Тогда
		ПутьКДанным = ПутьКДаннымСведенийЗависящихОтФункциональныхОпцийОрганизаций(ИмяПоля, Истина);
	ИначеЕсли ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ГосударственнаяСлужба")
			И ОбщегоНазначения.ОбщийМодуль("ГосударственнаяСлужба").НеобходимыСвойстваДолжностейГосударственнойСлужбы(ИмяПоля) Тогда
		ПутьКДанным = ОбщегоНазначения.ОбщийМодуль("ГосударственнаяСлужба").ПутьКДаннымСвойствДолжностейГосударственнойСлужбы(ИмяПоля);
	Иначе
		ПутьКДанным = "	КадроваяИсторияСотрудников." + ИмяПоля;
	КонецЕсли;
	
	Возврат ПутьКДанным;
	
КонецФункции

Функция ЗапросВТСведенияКадровойИсторияСотрудников(ТолькоРазрешенные, ОписательВременнойТаблицыОтборов, ПоляОтбораПериодическихДанных, ИмяВТСведенияКадровойИсторияСотрудников = "ВТСведенияКадровойИсторияСотрудников") Экспорт
	
	ПоляОтбора = Неопределено;
	Если ПоляОтбораПериодическихДанных <> Неопределено Тогда
		ПоляОтбораПериодическихДанных.Свойство("КадроваяИсторияСотрудников", ПоляОтбора);
	КонецЕсли;
	 
	ОписаниеФильтра = ЗарплатаКадрыОбщиеНаборыДанных.ОписаниеФильтраДляСоздатьВТИмяРегистра(
		ОписательВременнойТаблицыОтборов.ИмяВременнойТаблицыОтборовСотрудников, "Сотрудник");
		
	ОписаниеФильтра.СоответствиеИзмеренийРегистраИзмерениямФильтра.Вставить("Период", ОписательВременнойТаблицыОтборов.ИмяПоляПериод);
	ОписаниеФильтра.СоответствиеИзмеренийРегистраИзмерениямФильтра.Вставить("Сотрудник", ОписательВременнойТаблицыОтборов.ИмяПоляСотрудник);
		
	ПараметрыПостроения = ЗарплатаКадрыОбщиеНаборыДанных.ПараметрыПостроенияДляСоздатьВТИмяРегистраСрез();
	ПараметрыПостроения.Отборы = ПоляОтбора;
		
	ЗапросВТИмяРегистраСрез = ЗарплатаКадрыОбщиеНаборыДанных.ЗапросВТИмяРегистраСрез(
		"КадроваяИсторияСотрудников", 
		ТолькоРазрешенные,
		ОписаниеФильтра,
		ПараметрыПостроения,
		Истина,
		ИмяВТСведенияКадровойИсторияСотрудников);
	
	Возврат ЗапросВТИмяРегистраСрез;
	
КонецФункции

Процедура ДобавитьТекстЗапросаВТСведенияКадроваяИсторияСотрудников(Запрос, ТолькоРазрешенные, ОписательВременнойТаблицыОтборов, ПоляОтбораПериодическихДанных, ИсточникиДанных) Экспорт
	
	Если ИсточникиДанных.Получить("КадроваяИсторияСотрудников") = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИмяВТСведенияКадровойИсторияСотрудников = ЗарплатаКадрыОбщиеНаборыДанных.УникальноеИмяТекстаЗапроса("ВТСведенияКадровойИсторияСотрудников");
	
	ЗапросВТ = ЗапросВТСведенияКадровойИсторияСотрудников(ТолькоРазрешенные, ОписательВременнойТаблицыОтборов, ПоляОтбораПериодическихДанных, ИмяВТСведенияКадровойИсторияСотрудников);
			
	ЗарплатаКадрыОбщиеНаборыДанных.СкопироватьПараметрыЗапроса(Запрос, ЗапросВТ);
	
	Запрос.Текст = 
		ЗапросВТ.Текст
		+ ЗарплатаКадрыОбщиеНаборыДанных.РазделительЗапросов()
		+ Запрос.Текст;
		
	Запрос.Текст = Запрос.Текст + Символы.ПС
		+ "		{ЛЕВОЕ СОЕДИНЕНИЕ " + ИмяВТСведенияКадровойИсторияСотрудников + " КАК КадроваяИсторияСотрудников
			|		ПО ТаблицаОтборовСотрудников." + ОписательВременнойТаблицыОтборов.ИмяПоляСотрудник + " = КадроваяИсторияСотрудников.Сотрудник
			|			И ТаблицаОтборовСотрудников." + ОписательВременнойТаблицыОтборов.ИмяПоляПериод + " = КадроваяИсторияСотрудников.Период}";
				
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ГосударственнаяСлужба") Тогда
		ОбщегоНазначения.ОбщийМодуль("ГосударственнаяСлужба").ДобавитьТекстЗапросаВТСвойстваДолжностейГосударственнойСлужбы(Запрос, ТолькоРазрешенные, ОписательВременнойТаблицыОтборов, ПоляОтбораПериодическихДанных, ИсточникиДанных);
	КонецЕсли; 
	
КонецПроцедуры

// Сведения об учете затрат

Функция ДобавитьПолеСведенийОбУчетеЗатрат(ИмяПоля, ТекстОписанияПолей, ИсточникиДанных)
	
	ДобавленоПолеСведений = Ложь;
	Если НеобходимыСведенияОбУчетеЗатрат(ИмяПоля) Тогда
		
		ДобавленоПолеСведений = Истина;
		ИсточникиДанных.Вставить("СведенияОбУчетеЗатрат", Истина);
		
		ПутьКДанным = ПутьКДаннымСведенийОбУчетеЗатрат(ИмяПоля);
		ТекстОписанияПолей = ТекстОписанияПолей + "," + Символы.ПС + ПутьКДанным + " КАК " + ИмяПоля;
		
	КонецЕсли;
	
	Возврат ДобавленоПолеСведений;
	
КонецФункции

Функция НеобходимыСведенияОбУчетеЗатрат(Знач ИмяПоля)
	
	ИмяПоля = ВРег(ИмяПоля);
	
	Возврат ИмяПоля = ВРег("СпособОтраженияЗарплатыВБухучете")
		ИЛИ ИмяПоля = ВРег("ОтношениеКЕНВД")
		ИЛИ ИмяПоля = ВРег("СтатьяФинансирования")
		ИЛИ ИмяПоля = ВРег("УчетЗатратДокументОснование");
		
КонецФункции
	
Функция ПутьКДаннымСведенийОбУчетеЗатрат(Знач ИмяПоля)
	
	ИмяПоляВВерхнемРегистре = ВРег(ИмяПоля);
	
	ПутьКДанным = "";
	
	Если ИмяПоляВВерхнемРегистре = ВРег("УчетЗатратДокументОснование") Тогда
		ПутьКДанным = "	БухучетЗарплатыСотрудников.ДокументОснование";
	Иначе
		ПутьКДанным = "	БухучетЗарплатыСотрудников." + ИмяПоля;
	КонецЕсли;
	
	Возврат ПутьКДанным;
	
КонецФункции

Функция ЗапросВТСведенияОбУчетеЗатрат(ТолькоРазрешенные, ОписательВременнойТаблицыОтборов, ПоляОтбораПериодическихДанных, ИмяВТСведенияОбУчетеЗатрат = "ВТСведенияОбУчетеЗатрат") Экспорт
	
	ПоляОтбора = Неопределено;
	Если ПоляОтбораПериодическихДанных <> Неопределено Тогда
		ПоляОтбораПериодическихДанных.Свойство("БухучетЗарплатыСотрудников", ПоляОтбора);
	КонецЕсли;
	 
	ОписаниеФильтра = ЗарплатаКадрыОбщиеНаборыДанных.ОписаниеФильтраДляСоздатьВТИмяРегистра(
		ОписательВременнойТаблицыОтборов.ИмяВременнойТаблицыОтборовСотрудников, "Сотрудник");
		
	ОписаниеФильтра.СоответствиеИзмеренийРегистраИзмерениямФильтра.Вставить("Период", ОписательВременнойТаблицыОтборов.ИмяПоляПериод);
	ОписаниеФильтра.СоответствиеИзмеренийРегистраИзмерениямФильтра.Вставить("Сотрудник", ОписательВременнойТаблицыОтборов.ИмяПоляСотрудник);
		
	ПараметрыПостроения = ЗарплатаКадрыОбщиеНаборыДанных.ПараметрыПостроенияДляСоздатьВТИмяРегистраСрез();
	ПараметрыПостроения.Отборы = ПоляОтбора;
	
	ИмяВТБухучетЗарплатыСотрудников = ЗарплатаКадрыОбщиеНаборыДанных.УникальноеИмяТекстаЗапроса("ВТБухучетЗарплатыСотрудников");
		
	ЗапросВТИмяРегистраСрез = ЗарплатаКадрыОбщиеНаборыДанных.ЗапросВТИмяРегистраСрез(
		"БухучетЗарплатыСотрудников", 
		ТолькоРазрешенные,
		ОписаниеФильтра,
		ПараметрыПостроения,
		Истина,
		ИмяВТБухучетЗарплатыСотрудников);
		
	ИмяВТПоследниеПериодыВТСведенияОбУчетеЗатрат = ЗарплатаКадрыОбщиеНаборыДанных.УникальноеИмяТекстаЗапроса("ВТПоследниеПериодыВТСведенияОбУчетеЗатрат");
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	БухучетЗарплатыСотрудников.Сотрудник,
		|	МАКСИМУМ(БухучетЗарплатыСотрудников.ПериодЗаписи) КАК ПериодЗаписи
		|ПОМЕСТИТЬ ВТПоследниеПериодыВТСведенияОбУчетеЗатрат
		|ИЗ
		|	ВТБухучетЗарплатыСотрудников КАК БухучетЗарплатыСотрудников
		|
		|СГРУППИРОВАТЬ ПО
		|	БухучетЗарплатыСотрудников.Сотрудник
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	БухучетЗарплатыСотрудников.Период,
		|	БухучетЗарплатыСотрудников.Сотрудник,
		|	БухучетЗарплатыСотрудников.ДокументОснование,
		|	БухучетЗарплатыСотрудников.СпособОтраженияЗарплатыВБухучете,
		|	БухучетЗарплатыСотрудников.ОтношениеКЕНВД,
		|	БухучетЗарплатыСотрудников.СтатьяФинансирования
		|ПОМЕСТИТЬ ВТСведенияОбУчетеЗатрат
		|ИЗ
		|	ВТПоследниеПериодыВТСведенияОбУчетеЗатрат КАК ПоследниеПериодыВТСведенияОбУчетеЗатрат
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТБухучетЗарплатыСотрудников КАК БухучетЗарплатыСотрудников
		|		ПО ПоследниеПериодыВТСведенияОбУчетеЗатрат.Сотрудник = БухучетЗарплатыСотрудников.Сотрудник
		|			И ПоследниеПериодыВТСведенияОбУчетеЗатрат.ПериодЗаписи = БухучетЗарплатыСотрудников.ПериодЗаписи";
		
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ВТБухучетЗарплатыСотрудников", ИмяВТБухучетЗарплатыСотрудников);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ВТПоследниеПериодыВТСведенияОбУчетеЗатрат", ИмяВТПоследниеПериодыВТСведенияОбУчетеЗатрат);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ВТСведенияОбУчетеЗатрат", ИмяВТСведенияОбУчетеЗатрат);
	
	ЗапросВТИмяРегистраСрез.Текст = 
		ЗапросВТИмяРегистраСрез.Текст
		+ ЗарплатаКадрыОбщиеНаборыДанных.РазделительЗапросов()
		+ ТекстЗапроса;
	
	Возврат ЗапросВТИмяРегистраСрез;
	
КонецФункции

Процедура ДобавитьТекстЗапросаВТСведенияОбУчетеЗатрат(Запрос, ТолькоРазрешенные, ОписательВременнойТаблицыОтборов, ПоляОтбораПериодическихДанных, ИсточникиДанных)
	
	Если ИсточникиДанных.Получить("СведенияОбУчетеЗатрат") = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИмяВТСведенияОбУчетеЗатрат = ЗарплатаКадрыОбщиеНаборыДанных.УникальноеИмяТекстаЗапроса("ВТСведенияОбУчетеЗатрат");
	
	ЗапросВТ = ЗапросВТСведенияОбУчетеЗатрат(ТолькоРазрешенные, ОписательВременнойТаблицыОтборов, ПоляОтбораПериодическихДанных, ИмяВТСведенияОбУчетеЗатрат);
			
	ЗарплатаКадрыОбщиеНаборыДанных.СкопироватьПараметрыЗапроса(Запрос, ЗапросВТ);
	
	Запрос.Текст = 
		ЗапросВТ.Текст
		+ ЗарплатаКадрыОбщиеНаборыДанных.РазделительЗапросов()
		+ Запрос.Текст;
		
	Запрос.Текст = Запрос.Текст + Символы.ПС
		+ "		{ЛЕВОЕ СОЕДИНЕНИЕ " + ИмяВТСведенияОбУчетеЗатрат + " КАК БухучетЗарплатыСотрудников
			|		ПО ТаблицаОтборовСотрудников." + ОписательВременнойТаблицыОтборов.ИмяПоляСотрудник + " = БухучетЗарплатыСотрудников.Сотрудник
			|			И ТаблицаОтборовСотрудников." + ОписательВременнойТаблицыОтборов.ИмяПоляПериод + " = БухучетЗарплатыСотрудников.Период}";
				
КонецПроцедуры

// Сведения об оплате труда

Функция ДобавитьПолеСведенийОбОплатеТруда(ИмяПоля, ТекстОписанияПолей, ИсточникиДанных)
	
	ДобавленоПолеСведений = Ложь;
	Если ПолучитьФункциональнуюОпцию("ИспользоватьКадровыйУчет") И КадровыйУчет.НеобходимыСведенияОбОплатеТруда(ИмяПоля) Тогда
		
		ДобавленоПолеСведений = Истина;
		ИсточникиДанных.Вставить("СведенияОбОплатеТруда", Истина);
		
		ПутьКДанным = КадровыйУчет.ПутьКДаннымСведенийОбОплатеТруда(ИмяПоля);
		ТекстОписанияПолей = ТекстОписанияПолей + "," + Символы.ПС + ПутьКДанным + " КАК " + ИмяПоля;
		
	КонецЕсли;
	
	Возврат ДобавленоПолеСведений;
	
КонецФункции

Функция НеобходимыСведенияОбОплатеТруда(ИмяПоля) Экспорт
	
	ИмяПоля = ВРег(ИмяПоля);
	
	Возврат ИмяПоля = ВРег("ТарифнаяСтавка")
		ИЛИ ИмяПоля = ВРег("ФОТ")
		ИЛИ ИмяПоля = ВРег("Надбавка");
		
КонецФункции

Функция ПутьКДаннымСведенийОбОплатеТруда(ИмяПоля) Экспорт
	
	ИмяПоляВВерхнемРегистре = ВРег(ИмяПоля);
	
	ПутьКДанным = "";
	
	Если ИмяПоляВВерхнемРегистре = ВРег("Надбавка") Тогда
		ПутьКДанным = "	0";
	Иначе
		ПутьКДанным = "	ТарифныеСтавкиСотрудников." + ИмяПоля;
	КонецЕсли;

	Возврат ПутьКДанным;
	
КонецФункции

Процедура ДобавитьТекстЗапросаВТСведенияОбОплатеТруда(Запрос, ТолькоРазрешенные, ОписательВременнойТаблицыОтборов, ПоляОтбораПериодическихДанных, ИсточникиДанных)
	
	Если ИсточникиДанных.Получить("СведенияОбОплатеТруда") = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИмяВТСведенияОбОплатеТруда = ЗарплатаКадрыОбщиеНаборыДанных.УникальноеИмяТекстаЗапроса("ВТСведенияОбОплатеТруда");
		
	ЗапросВТТарифныеСтавкиСотрудников = КадровыйУчет.ЗапросВТТарифныеСтавкиСотрудников(
		ТолькоРазрешенные,
		ИмяВТСведенияОбОплатеТруда,
		ОписательВременнойТаблицыОтборов.ИмяВременнойТаблицыОтборовСотрудников,
		КадровыйУчет.ПоляОтбораСотрудников(ОписательВременнойТаблицыОтборов.ИмяПоляСотрудник + "," + ОписательВременнойТаблицыОтборов.ИмяПоляПериод),
		ПоляОтбораПериодическихДанных);
	
	ЗарплатаКадрыОбщиеНаборыДанных.СкопироватьПараметрыЗапроса(Запрос, ЗапросВТТарифныеСтавкиСотрудников);
	
	Запрос.Текст = 
		ЗапросВТТарифныеСтавкиСотрудников.Текст
		+ ЗарплатаКадрыОбщиеНаборыДанных.РазделительЗапросов()
		+ Запрос.Текст	
		+ "
			|	{ЛЕВОЕ СОЕДИНЕНИЕ " + ИмяВТСведенияОбОплатеТруда + " КАК ТарифныеСтавкиСотрудников
			| 	ПО ТаблицаОтборовСотрудников." + ОписательВременнойТаблицыОтборов.ИмяПоляСотрудник + " = ТарифныеСтавкиСотрудников.Сотрудник
			| 		И ТаблицаОтборовСотрудников." + ОписательВременнойТаблицыОтборов.ИмяПоляПериод + " = ТарифныеСтавкиСотрудников.Период}";
		
КонецПроцедуры

// Сведения о плановых авансах.

Функция ДобавитьПолеСведенийОПлановыхАвансах(ИмяПоля, ТекстОписанияПолей, ИсточникиДанных)
	
	ДобавленоПолеСведений = Ложь;
	Если ПолучитьФункциональнуюОпцию("ИспользоватьКадровыйУчет") И НеобходимыСведенияОПлановыхАвансах(ИмяПоля) Тогда
		
		ДобавленоПолеСведений = Истина;
		ИсточникиДанных.Вставить("СведенияОПлановыхАвансах", Истина);
		
		ПутьКДанным = "	СведенияОПлановыхАвансах." + ИмяПоля;
		ТекстОписанияПолей = ТекстОписанияПолей + "," + Символы.ПС + ПутьКДанным + " КАК " + ИмяПоля;
		
	КонецЕсли;
	
	Возврат ДобавленоПолеСведений;
	
КонецФункции

Функция НеобходимыСведенияОПлановыхАвансах(Знач ИмяПоля)
	
	ИмяПоля = ВРег(ИмяПоля);
	
	Возврат ИмяПоля = ВРег("СпособРасчетаАванса")
		ИЛИ ИмяПоля = ВРег("Аванс");
		
КонецФункции
	
Функция ЗапросВТСведенияОПлановыхАвансах(ТолькоРазрешенные, ОписательВременнойТаблицыОтборов, ПоляОтбораПериодическихДанных, ИмяВТСведенияОПлановыхАвансах = "ВТСведенияОПлановыхАвансах") Экспорт
	
	ПоляОтбора = Неопределено;
	Если ПоляОтбораПериодическихДанных <> Неопределено Тогда
		ПоляОтбораПериодическихДанных.Свойство("ПлановыеАвансы", ПоляОтбора);
	КонецЕсли;
	 
	ОписаниеФильтра = ЗарплатаКадрыОбщиеНаборыДанных.ОписаниеФильтраДляСоздатьВТИмяРегистра(
		ОписательВременнойТаблицыОтборов.ИмяВременнойТаблицыОтборовСотрудников, "Сотрудник");
		
	ОписаниеФильтра.СоответствиеИзмеренийРегистраИзмерениямФильтра.Вставить("Период", ОписательВременнойТаблицыОтборов.ИмяПоляПериод);
	ОписаниеФильтра.СоответствиеИзмеренийРегистраИзмерениямФильтра.Вставить("Сотрудник", ОписательВременнойТаблицыОтборов.ИмяПоляСотрудник);
		
	ПараметрыПостроения = ЗарплатаКадрыОбщиеНаборыДанных.ПараметрыПостроенияДляСоздатьВТИмяРегистраСрез();
	ПараметрыПостроения.Отборы = ПоляОтбора;
		
	ЗапросВТИмяРегистраСрез = ЗарплатаКадрыОбщиеНаборыДанных.ЗапросВТИмяРегистраСрез(
		"ПлановыеАвансы", 
		ТолькоРазрешенные,
		ОписаниеФильтра,
		ПараметрыПостроения,
		Истина,
		ИмяВТСведенияОПлановыхАвансах);
	
	Возврат ЗапросВТИмяРегистраСрез;
	
КонецФункции

Процедура ДобавитьТекстЗапросаВТСведенийОПлановыхАвансах(Запрос, ТолькоРазрешенные, ОписательВременнойТаблицыОтборов, ПоляОтбораПериодическихДанных, ИсточникиДанных)
	
	Если ИсточникиДанных.Получить("СведенияОПлановыхАвансах") = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИмяВТСведенияОПлановыхАвансах = ЗарплатаКадрыОбщиеНаборыДанных.УникальноеИмяТекстаЗапроса("ВТСведенияОПлановыхАвансах");
	
	ЗапросВТ = ЗапросВТСведенияОПлановыхАвансах(ТолькоРазрешенные, ОписательВременнойТаблицыОтборов, ПоляОтбораПериодическихДанных, ИмяВТСведенияОПлановыхАвансах);
			
	ЗарплатаКадрыОбщиеНаборыДанных.СкопироватьПараметрыЗапроса(Запрос, ЗапросВТ);
	
	ЗапросВТ.Текст = СтрЗаменить(ЗапросВТ.Текст, 
		"{ГДЕ
			|	РегистрСведений.Сотрудник.*}", "");
			
	Запрос.Текст = 
		ЗапросВТ.Текст
		+ ЗарплатаКадрыОбщиеНаборыДанных.РазделительЗапросов()
		+ Запрос.Текст;
		
	Запрос.Текст = Запрос.Текст + Символы.ПС
		+ "		{ЛЕВОЕ СОЕДИНЕНИЕ " + ИмяВТСведенияОПлановыхАвансах + " КАК СведенияОПлановыхАвансах
			|		ПО ТаблицаОтборовСотрудников." + ОписательВременнойТаблицыОтборов.ИмяПоляСотрудник + " = СведенияОПлановыхАвансах.Сотрудник
			|			И ТаблицаОтборовСотрудников." + ОписательВременнойТаблицыОтборов.ИмяПоляПериод + " = СведенияОПлановыхАвансах.Период}";
				
КонецПроцедуры

#КонецОбласти

#Область СобытияРегистровСведений

Процедура КадроваяИсторияСотрудниковПередЗаписью(НаборЗаписей, Отказ) Экспорт
	
	КадровыйУчет.ЗапомнитьСписокСотрудниковДляОбновления(НаборЗаписей, Отказ);
	
КонецПроцедуры

Процедура КадроваяИсторияСотрудниковПриЗаписи(НаборЗаписей, Отказ) Экспорт
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьКадровыйУчет") Тогда
		
		// Обновлять записи необходимо, только при записи набора, содержащего первичные данные.
		Если НаборЗаписей.ДополнительныеСвойства.Свойство("ЭтоВторичныйНабор") 
			И НаборЗаписей.ДополнительныеСвойства.ЭтоВторичныйНабор Тогда
			Возврат;
		КонецЕсли;
		
		Если НЕ НаборЗаписей.ДополнительныеСвойства.Свойство("МассивСотрудниковДляОбновления") 
			ИЛИ НаборЗаписей.ДополнительныеСвойства.МассивСотрудниковДляОбновления.Количество() = 0 Тогда
			Возврат;
		КонецЕсли;
		
		Если НаборЗаписей.ДополнительныеСвойства.Свойство("СохранитьДатыПриема") 
			И НаборЗаписей.ДополнительныеСвойства.СохранитьДатыПриема = Истина Тогда
			СохранитьДатыПриема = Истина;
		Иначе
			СохранитьДатыПриема = Ложь;
		КонецЕсли;
		
		КадровыйУчет.ОбновитьТекущиеКадровыеДанныеСотрудников(НаборЗаписей.ДополнительныеСвойства.МассивСотрудниковДляОбновления, СохранитьДатыПриема);
		КадровыйУчет.ОбновитьЗначенияДоступаСотрудников(НаборЗаписей.ДополнительныеСвойства.МассивСотрудниковДляОбновления);
		
	КонецЕсли;
	
	Для Каждого СтрокаНабора Из НаборЗаписей Цикл
		УправлениеДоступомСлужебный.ЗаписатьНаборыЗначенийДоступа(СтрокаНабора.Сотрудник);
	КонецЦикла;
	
КонецПроцедуры

Процедура ПлановыеНачисленияПередЗаписью(НаборЗаписей, Отказ) Экспорт
	
	КадровыйУчет.ЗапомнитьСписокСотрудниковДляОбновления(НаборЗаписей, Отказ);
	
КонецПроцедуры

Процедура ПлановыеНачисленияПриЗаписи(НаборЗаписей, Отказ) Экспорт
	
	Если НаборЗаписей.ДополнительныеСвойства.Свойство("МассивСотрудниковДляОбновления") Тогда
		КадровыйУчет.ОбновитьТекущиеТарифныеСтавки(НаборЗаписей.ДополнительныеСвойства.МассивСотрудниковДляОбновления);
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
