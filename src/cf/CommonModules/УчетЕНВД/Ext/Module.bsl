#Область ПрограммныйИнтерфейс

// Возвращает вмененный доход по данным деклараций по ЕНВД за заданный период.
// Поддерживается только период кратный кварталу!
//
// Параметры:
//  Организация - СправочникСсылка.Организации
//  НачалоПериода - Дата - начало периода
//  КонецПериода - Дата - конец периода
//  СообщатьОбОшибках - Булево - Истина, если необходимо выводить сообщения об ошибках
//
Функция ВмененныйДоход(Организация, НачалоПериода, КонецПериода, СообщатьОбОшибках = Истина) Экспорт
	
	ВмененныйДоход = 0;
	
	Если НачалоПериода > КонецПериода Тогда
		Возврат ВмененныйДоход;
	КонецЕсли;
	
	ПериодДекларации = КонецКвартала(НачалоПериода);
	Пока ПериодДекларации <= КонецКвартала(КонецПериода) Цикл
		
		ПлательщикЕНВД = УчетнаяПолитика.ПлательщикЕНВДЗаПериод(
			Организация, НачалоКвартала(ПериодДекларации), КонецКвартала(ПериодДекларации));
			
		Если ПлательщикЕНВД Тогда
			
			ДанныеДекларации = ДанныеДекларации(Организация, ПериодДекларации);
			
			Если ДанныеДекларации <> Неопределено Тогда
				ВмененныйДоход = ВмененныйДоход + ?(ДанныеДекларации.Свойство("НалоговаяБаза"), ДанныеДекларации.НалоговаяБаза, 0);
			ИначеЕсли СообщатьОбОшибках Тогда
				ПредставлениеПериода =
					ПредставлениеПериода(НачалоКвартала(ПериодДекларации), КонецКвартала(ПериодДекларации), "ФП=Истина");
				СообщениеОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Не заполнена декларация по ЕНВД за %1'"), ПредставлениеПериода);
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СообщениеОбОшибке);
			КонецЕсли;
			
		КонецЕсли;
		
		ПериодДекларации = КонецКвартала(ДобавитьМесяц(ПериодДекларации, 3)); // Следующий квартал
		
	КонецЦикла;
	
	Возврат ВмененныйДоход;
	
КонецФункции

Функция ДанныеДекларации(Организация, ПериодДекларации) Экспорт
	
	ИмяРеглОтчета = "РегламентированныйОтчетЕдиныйНалогНаВмененныйДоход";
	
	ЭкземплярыРеглОтчета = РегламентированнаяОтчетность.ПолучитьСсылкуНаРеглОтчет(
		ИмяРеглОтчета, Организация, НачалоДня(ПериодДекларации));
	
	Если ЭкземплярыРеглОтчета.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ЭкземплярРеглОтчета = ЭкземплярыРеглОтчета[0];
	
	Попытка
		ДанныеРасчета = Отчеты[ИмяРеглОтчета].ДанныеРеглОтчета(ЭкземплярРеглОтчета, "Раздел3");
	Исключение
		ДанныеРасчета = Неопределено;
	КонецПопытки;
	
	Возврат ДанныеРасчета;
	
КонецФункции

#КонецОбласти
