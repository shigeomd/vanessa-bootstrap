Функция Идентификатор(Имя) Экспорт
	
	Цифры = "1234567890";
	НомерСтрокой = "";
	ДлинаИмени = СтрДлина(Имя);
	Для Индекс = 0 По ДлинаИмени - 1 Цикл
		Позиция = ДлинаИмени - Индекс;
		Символ = Сред(Имя,Позиция,1);
		Если Найти(Цифры, Символ) = 0 Тогда
			Прервать;
		КонецЕсли;
		НомерСтрокой = Символ + НомерСтрокой;
	КонецЦикла;
	
	Если ПустаяСтрока(НомерСтрокой) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Попытка
		Возврат Число(НомерСтрокой) - 1;
	Исключение
		Возврат Неопределено;
	КонецПопытки;
	
КонецФункции

Функция НомерКонтейнера(Индекс) Экспорт
	
	Возврат Формат(Индекс + 1, "ЧГ=0");
	
КонецФункции

Функция СсылкаНаИТС(Срок = Неопределено, ИдентификаторЗадачи  = Неопределено, ИдентификаторПравила  = Неопределено) Экспорт
	
	Если Срок = Неопределено Тогда
		Параметры = "";
	Иначе
		Параметры = "/?date="
		+ Формат(Срок, "ДФ=yyyyMMdd")
		+ "&task=" + ИдентификаторЗадачи
		+ "&rule=" + ИдентификаторПравила;
	КонецЕсли;
	
	Возврат "http://its.1c.ru/calendar" + Параметры;
	
КонецФункции

Функция ОписаниеДействия(ПараметрыКоманды) Экспорт
	
	Если ТипЗнч(ПараметрыКоманды.Правило) = Тип("ПеречислениеСсылка.ЗадачиНачалаРаботы") Тогда
		
		Если ПараметрыКоманды.Правило = ПредопределенноеЗначение("Перечисление.ЗадачиНачалаРаботы.ДоступКОрганизациям") Тогда
			
			ОписаниеДействия = Новый Структура;
			ОписаниеДействия.Вставить("Предупреждение", Строка(ПараметрыКоманды.Правило));
			
		ИначеЕсли ПараметрыКоманды.Правило = ПредопределенноеЗначение("Перечисление.ЗадачиНачалаРаботы.РеквизитыОрганизации") Тогда
			
			ОписаниеДействия = Новый Структура;
			ОписаниеДействия.Вставить("ИмяФормы",       "Справочник.Организации.ФормаОбъекта");
			ОписаниеДействия.Вставить("ПараметрыФормы", Новый Структура);
			
		ИначеЕсли ПараметрыКоманды.Правило = ПредопределенноеЗначение("Перечисление.ЗадачиНачалаРаботы.ПараметрыУчета") Тогда
			
			ОписаниеДействия = Новый Структура;
			ОписаниеДействия.Вставить("ИмяФормы",       "ОбщаяФорма.ПараметрыУчета");
			ОписаниеДействия.Вставить("ПараметрыФормы", Новый Структура);
			
		ИначеЕсли ПараметрыКоманды.Правило = ПредопределенноеЗначение("Перечисление.ЗадачиНачалаРаботы.УчетнаяПолитика") Тогда
			
			ОписаниеДействия = Новый Структура;
			Отбор = Новый Структура("Организация", ПараметрыКоманды.Организация);
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("Отбор", Отбор);
			
			ОписаниеДействия.Вставить("ИмяФормы",      "РегистрСведений.УчетнаяПолитикаОрганизаций.ФормаСписка");
			ОписаниеДействия.Вставить("ПараметрыФормы", ПараметрыФормы);
			
		ИначеЕсли ПараметрыКоманды.Правило = ПредопределенноеЗначение("Перечисление.ЗадачиНачалаРаботы.НачальныеОстатки") Тогда
			
			ОписаниеДействия = Новый Структура;
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("Организация", ПараметрыКоманды.Организация);
			
			ОписаниеДействия.Вставить("ИмяФормы",       "Обработка.ВводНачальныхОстатков.Форма");
			ОписаниеДействия.Вставить("ПараметрыФормы", ПараметрыФормы);
			
		ИначеЕсли ПараметрыКоманды.Правило = ПредопределенноеЗначение("Перечисление.ЗадачиНачалаРаботы.СписокНалоговОтчетов") Тогда
			
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("Организация", ПараметрыКоманды.Организация);
			
			ОписаниеДействия = Новый Структура;
			ОписаниеДействия.Вставить("ИмяФормы",       "РегистрСведений.НалогиОтчеты.Форма.Настройка");
			ОписаниеДействия.Вставить("ПараметрыФормы", ПараметрыФормы);
			
		ИначеЕсли ПараметрыКоманды.Правило = ПредопределенноеЗначение("Перечисление.ЗадачиНачалаРаботы.Подключение1СОтчетности") Тогда
			
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("Организация", ПараметрыКоманды.Организация);
			
			ОписаниеДействия = Новый Структура;
			ОписаниеДействия.Вставить("ИмяФормы",       "Обработка.ОбщиеОбъектыРегламентированнойОтчетности.Форма.ПредложениеОформитьЗаявлениеНаПодключение");
			ОписаниеДействия.Вставить("ПараметрыФормы", ПараметрыФормы);
			
		ИначеЕсли ПараметрыКоманды.Правило = ПредопределенноеЗначение("Перечисление.ЗадачиНачалаРаботы.НастройкаЗаполненияФормСтатистики") Тогда
			
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("Организация", ПараметрыКоманды.Организация);
			
			ОписаниеДействия = Новый Структура;
			ОписаниеДействия.Вставить("ИмяФормы",       "РегистрСведений.НастройкаЗаполненияФормСтатистики.Форма.НастройкаЗаполненияФормСтатистики");
			ОписаниеДействия.Вставить("ПараметрыФормы", ПараметрыФормы);
			
		Иначе
			
			Возврат Неопределено;
			
		КонецЕсли
		
	Иначе
		
		ОписаниеДействия = ВыполнениеЗадачБухгалтераВызовСервера.ОписаниеДействия(
			ПараметрыКоманды.Организация, 
			ПараметрыКоманды.РегистрацияВНалоговомОргане, 
			ПараметрыКоманды.Срок, 
			ПараметрыКоманды.Правило, 
			ПараметрыКоманды.ПериодСобытия, 
			ПараметрыКоманды.Периодичность,
			ПараметрыКоманды.Наименование,
			ПараметрыКоманды.СпособОплаты);
		
	КонецЕсли;
	
	Возврат ОписаниеДействия;
	
КонецФункции
