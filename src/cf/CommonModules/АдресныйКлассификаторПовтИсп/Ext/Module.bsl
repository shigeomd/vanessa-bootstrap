////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Подсистема "Адресный классификатор".
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

#Область СлужебныеПроцедурыИФункции

// Набор уровней для запросов в режиме совместимости с КЛАДР.
//
// Возвращаемое значение:
//     ФиксированныйМассив - набор числовых уровней.
//
Функция УровниКлассификатораКЛАДР() Экспорт
	
	Возврат АдресныйКлассификаторКлиентСервер.УровниКлассификатораКЛАДР();
	
КонецФункции

// Набор уровней для запросов ФИАС.
//
// Возвращаемое значение:
//     ФиксированныйМассив - набор числовых уровней.
//
Функция УровниКлассификатораФИАС() Экспорт
	
	Возврат АдресныйКлассификаторКлиентСервер.УровниКлассификатораФИАС();
	
КонецФункции

// WSПрокси для вызова веб-сервиса 1С.
//
// Возвращаемое значение:
//     WSПрокси - объект для вызовов сервиса.
//
Функция СервисКлассификатора1С() Экспорт

	Авторизация = СтандартныеПодсистемыСервер.ПараметрыАутентификацииНаСайте();
	Если Авторизация = Неопределено Тогда
		Логин  = Неопределено;
		Пароль = Неопределено;
	Иначе
		Логин  = Авторизация.Логин;
		Пароль = Авторизация.Пароль;
	КонецЕсли;
	
	ТекущийURLВебСервиса = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("АдресныйКлассификатор", "URLСервисаКлассификатора1С");
	Если ТекущийURLВебСервиса = Неопределено Тогда
		ТекущийURLВебСервиса = "https://api.orgaddress.1c.ru/orgaddress/v1?wsdl";
	КонецЕсли;
	
	Возврат ОбщегоНазначения.WSПрокси(ТекущийURLВебСервиса,
		"http://www.v8.1c.ru/ssl/AddressSystem", "AddressSystem", "AddressSystemSoap12", Логин, Пароль, 10);
		
КонецФункции

// Возвращает данные о загруженных регионах
// 
// Возвращаемое значение:
//  Соответствие - Ключ - код субъекта РФ, Значение - Истина, если регион загружен
//
Функция СведенияОЗагруженныхРегионах() Экспорт
	
	Результат = Новый Соответствие;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РодительскийОбъект.КодСубъектаРФ КАК КодСубъектаРФ,
		|	РодительскийОбъект.Наименование КАК Наименование,
		|	РодительскийОбъект.Сокращение КАК Сокращение,
		|	ВЫБОР
		|		КОГДА 1 В
		|				(ВЫБРАТЬ ПЕРВЫЕ 1
		|					1
		|				ИЗ
		|					РегистрСведений.АдресныеОбъекты
		|				ГДЕ
		|					РегистрСведений.АдресныеОбъекты.Уровень В (2, 3, 4, 5, 6)
		|					И РегистрСведений.АдресныеОбъекты.КодСубъектаРФ = Регион.КодСубъектаРФ)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК РегионЗагружен
		|ИЗ
		|	РегистрСведений.АдресныеОбъекты КАК РодительскийОбъект
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АдресныеОбъекты КАК Регион
		|		ПО (Регион.Уровень = 1)
		|			И (Регион.КодСубъектаРФ = РодительскийОбъект.КодСубъектаРФ)
		|			И (Регион.КодОкруга = 0)
		|			И (Регион.КодРайона = 0)
		|			И (Регион.КодГорода = 0)
		|			И (Регион.КодВнутригородскогоРайона = 0)
		|			И (Регион.КодНаселенногоПункта = 0)
		|			И (Регион.КодУлицы = 0)
		|			И (Регион.КодДополнительногоЭлемента = 0)
		|			И (Регион.КодПодчиненногоЭлемента = 0)
		|ГДЕ
		|	РодительскийОбъект.Уровень = 1";
	
	РезультатЗапроса = Запрос.Выполнить().Выбрать();
	
	Пока РезультатЗапроса.Следующий() Цикл
		Результат.Вставить(РезультатЗапроса.КодСубъектаРФ, РезультатЗапроса.РегионЗагружен);
	КонецЦикла;
	
	Возврат Результат;

КонецФункции

#КонецОбласти