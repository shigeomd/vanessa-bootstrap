////////////////////////////////////////////////////////////////////////////////
// Проверка контрагентов: запуск проверок, сохранение настроек
//  
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

Функция НужноПоказатьПредложениеВключитьПроверку(ДополнительныеПараметры) Экспорт
	
	ПоказатьВЛюбомСлучае = ДополнительныеПараметры.Свойство("ЭтоЗапускПроверкиКомандой");
	
	ПредложениеИзДекларацииНиРазуНеПоказывалось = ХранилищеОбщихНастроек.Загрузить("ПроверкаКонтрагентов_ПредложениеНаВключениеПроверкиИзДекларацииПоНДСУжеПоказывалось") = Неопределено;
	
	ЭтоЗапускПроверкиПослеЗаполнения	= ДополнительныеПараметры.Свойство("ЭтоЗапускПроверкиПослеЗаполнения");
	ЭтоЗапускПроверкиПриОткрытии		= ДополнительныеПараметры.Свойство("ЭтоЗапускПроверкиПриОткрытии");
	
	НужноПоказатьПредложение =
		// В модели сервисов предложение не показываем
		НЕ ОбщегоНазначенияПовтИсп.РазделениеВключено()
		// Проверяем наличие прав
		И (ПроверкаКонтрагентовБРО.ЕстьПравоНаРедактированиеНастроек()
		ИЛИ ПроверкаКонтрагентовБРО.ЕстьПравоНаИспользованиеПроверки())
		// Определяем, включен ли сервис
		И НЕ ПроверкаКонтрагентовБРО.ПроверкаКонтрагентовВключена()
		// Проверяем, не нажал ли пользователь кнопку "Больше не показывать" в предложении на включение сервиса
		И ПредложениеНиРазуНеПоказывалось(ДополнительныеПараметры);
		
	Возврат НужноПоказатьПредложение;
		
КонецФункции

Процедура ВключитьСервисСейчас() Экспорт
	
	ПроверкаКонтрагентовБРО.ВключитьВыключитьПроверкуКонтрагентов(Истина);
	ПроверкаКонтрагентовБРО.ПроверитьКонтрагентовПослеВключенияПроверкиФоновоеЗадание();
	
КонецПроцедуры

Функция ЗаданиеВыполнено(ИдентификаторЗадания) Экспорт
	
	Возврат ДлительныеОперации.ЗаданиеВыполнено(ИдентификаторЗадания);
	
КонецФункции

#Область ОболочкаДляПодсистемыБСП

Функция ИспользованиеПроверкиВозможно() Экспорт
	
	Если ПроверкаКонтрагентовБРОКлиентСервер.РаботаСКонтрагентамиСуществует() Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("ПроверкаКонтрагентовВызовСервера");
		Возврат Модуль.ИспользованиеПроверкиВозможно();
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПредложениеНиРазуНеПоказывалось(ДополнительныеПараметры)
	
	// Уже показывали при открытии 
	ЭтоЗапускПроверкиПриОткрытии = ДополнительныеПараметры.Свойство("ЭтоЗапускПроверкиПриОткрытии");
	Если ЭтоЗапускПроверкиПриОткрытии Тогда
		НиРазуНеПоказывалось = ХранилищеОбщихНастроек.Загрузить("ПроверкаКонтрагентов_ЗапускПроверкиПриОткрытии_ПредложениеУжеПоказывалось") = Неопределено;
		Если НиРазуНеПоказывалось Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЕсли;
	
	// Уже показывали после заполнения
	ЭтоЗапускПроверкиПослеЗаполнения = ДополнительныеПараметры.Свойство("ЭтоЗапускПроверкиПослеЗаполнения");
	Если ЭтоЗапускПроверкиПослеЗаполнения Тогда
		НиРазуНеПоказывалось = ХранилищеОбщихНастроек.Загрузить("ПроверкаКонтрагентов_ЗапускПроверкиПослеЗаполнения_ПредложениеУжеПоказывалось") = Неопределено;
		Если НиРазуНеПоказывалось Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЕсли;
	
	// Если запуск из команды, то требуется показывать всегда
	ЭтоЗапускПроверкиКомандой = ДополнительныеПараметры.Свойство("ЭтоЗапускПроверкиКомандой");
	Если ЭтоЗапускПроверкиКомандой Тогда
		Возврат Истина;
	КонецЕсли;

	Возврат Ложь;
	
КонецФункции

#КонецОбласти

