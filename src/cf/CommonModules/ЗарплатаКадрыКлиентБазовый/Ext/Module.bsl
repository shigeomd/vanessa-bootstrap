
#Область ПрограммныйИнтерфейс

// Открывает в качестве расшифровки переданной ячейки предварительно подготовленный вариант отчета.
// Параметры:
//	ИДОтчета - Строка - идентификатор отчета (совпадает с именем объекта метаданных).
// 	ИДРедакцииОтчета - Строка - идентификатор редакции формы отчета (совпадает с именем формы объекта метаданных).
//  ИДИменПоказателей - Массив - массив идентификаторов имен показателей, по которым формируется расшифровка.
//  ПараметрыОтчета - Структура - структура параметров отчета, необходимых для формирования расшифровки.
// 
Процедура ОткрытьРасшифровкуРегламентированногоОтчета(ИДОтчета, ИДРедакцииОтчета, ИДИменПоказателей, ПараметрыОтчета) Экспорт
	
	ФормаРасшифровки = Неопределено;
	
	Если ИДОтчета = "РегламентированныйОтчетРСВ1" Тогда
		Если ИДРедакцииОтчета = "ФормаОтчета2013Кв1" 
			Или ИДРедакцииОтчета = "ФормаОтчета2014Кв1" 
			Или ИДРедакцииОтчета = "ФормаОтчета2015Кв1" Тогда
			ФормаРасшифровки = УчетСтраховыхВзносовКлиент.ФормаРасшифровкиРегламентированногоОтчета(ИДОтчета, ИДРедакцииОтчета, ИДИменПоказателей, ПараметрыОтчета)
		Иначе
			Возврат
		КонецЕсли;
	ИначеЕсли ИДОтчета = "РегламентированныйОтчет4ФСС" Тогда
		Если ИДРедакцииОтчета = "ФормаОтчета2013Кв2" 
			Или ИДРедакцииОтчета = "ФормаОтчета2014Кв1" 
			Или ИДРедакцииОтчета = "ФормаОтчета2015Кв1" Тогда
			ФормаРасшифровки = УчетСтраховыхВзносовКлиент.ФормаРасшифровкиРегламентированногоОтчета(ИДОтчета, ИДРедакцииОтчета, ИДИменПоказателей, ПараметрыОтчета);
			Если ФормаРасшифровки = Неопределено Тогда
				ФормаРасшифровки = УчетПособийСоциальногоСтрахованияКлиент.ФормаРасшифровкиРегламентированногоОтчета(ИДОтчета, ИДРедакцииОтчета, ИДИменПоказателей, ПараметрыОтчета)
			КонецЕсли;
		Иначе
			Возврат
		КонецЕсли;
	Иначе
		Возврат
	КонецЕсли;		
	
	Если ФормаРасшифровки = Неопределено Тогда
		
		ПоказатьПредупреждение(, НСтр("ru='Для выбранной ячейки расшифровка не существует.'"),,
			Нстр("ru = 'Расшифровка регламентированных отчетов'"));
		
	Иначе
		
		ФормаРасшифровки.Открыть();
		
	КонецЕсли;
	
КонецПроцедуры

// Процедура реализует печать объектов, отображаемых на закладке Отчеты и Уведомления формы Отчетность.
// Параметры - (см. РегламентированнаяОтчетностьКлиентПереопределяемый.Печать) 
//	 
Процедура ПечатьДокументаОтчетности(Ссылка, ИмяМакетаДляПечати, СтандартнаяОбработка) Экспорт
	ПерсонифицированныйУчетКлиент.ПечатьДокументаОтчетности(Ссылка, ИмяМакетаДляПечати, СтандартнаяОбработка);
	УчетНДФЛКлиент.ПечатьДокументаОтчетности(Ссылка, ИмяМакетаДляПечати, СтандартнаяОбработка);
КонецПроцедуры	

// Процедура реализует печать объектов, отображаемых на закладке Отчеты и Уведомления формы Отчетность.
// Параметры - (см. РегламентированнаяОтчетностьКлиентПереопределяемый.Выгрузить) 
//	 
Процедура ВыгрузитьДокументОтчетности(Ссылка) Экспорт
	ПерсонифицированныйУчетКлиент.ВыгрузитьДокументОтчетности(Ссылка);
	УчетНДФЛКлиент.ВыгрузитьДокументОтчетности(Ссылка);	
КонецПроцедуры	

// Процедура реализует печать объектов, отображаемых на закладке Отчеты и Уведомления формы Отчетность.
// Параметры - (см. РегламентированнаяОтчетностьКлиентПереопределяемый.СоздатьНовыйОбъект) 
//	 
Процедура СоздатьНовыйДокументОтчетности(Организация, Тип, СтандартнаяОбработка) Экспорт
	ПерсонифицированныйУчетКлиент.СоздатьНовыйДокументОтчетности(Организация, Тип, СтандартнаяОбработка);
	УчетНДФЛКлиент.СоздатьНовыйДокументОтчетности(Организация, Тип, СтандартнаяОбработка);	
КонецПроцедуры	

#КонецОбласти
