Процедура УстановитьПараметрСеансаТекущийСеансДокументооборотаСКО(ИмяПараметра = Неопределено, УстановленныеПараметры = Неопределено) Экспорт
	
	ПараметрыСеанса.ТекущийСеансДокументооборотаСКО = Справочники.СеансыСвязиСКонтролирующимиОрганами.ПустаяСсылка();
	
КонецПроцедуры

Процедура НачатьСеансДокументооборотаСКО(ИнициаторСеанса, УчетнаяЗапись = Неопределено) Экспорт
	
	НовыйСеанс = Справочники.СеансыСвязиСКонтролирующимиОрганами.СоздатьЭлемент();
	НовыйСеанс.Начало = ТекущаяУниверсальнаяДата();
	НовыйСеанс.ИнициаторСеанса = ИнициаторСеанса;
	Если ТипЗнч(УчетнаяЗапись) = Тип("СправочникСсылка.УчетныеЗаписиДокументооборота") Тогда
		НовыйСеанс.УчетнаяЗапись = УчетнаяЗапись;
	КонецЕсли;
	
	НовыйСеанс.Записать();
	
	ПараметрыСеанса.ТекущийСеансДокументооборотаСКО = НовыйСеанс.Ссылка;
	
КонецПроцедуры


Процедура ЗакончитьСеансДокументооборотаСКО(УчетнаяЗапись = Неопределено, Успешно = Истина) Экспорт
	
	ТекущийСеанс = ТекущийСеансДокументооборотаСКО();
	Если ЗначениеЗаполнено(ТекущийСеанс) Тогда
		СеансОбъект = ТекущийСеанс.ПолучитьОбъект();
		
		СеансОбъект.Окончание = ТекущаяУниверсальнаяДата();
		СеансОбъект.Успешно = Успешно;
		СеансОбъект.Записать();
		
		ПараметрыСеанса.ТекущийСеансДокументооборотаСКО = Справочники.СеансыСвязиСКонтролирующимиОрганами.ПустаяСсылка();
		
	КонецЕсли;
	
КонецПроцедуры

Функция ТекущийСеансДокументооборотаСКО() Экспорт
	
	Возврат ПараметрыСеанса.ТекущийСеансДокументооборотаСКО;
	
КонецФункции

Процедура УстановитьРасписаниеРегламентногоЗаданияАвтоматическогоОбмена() Экспорт
	
	НайденныеЗадания = РегламентныеЗадания.ПолучитьРегламентныеЗадания(Новый Структура("Метаданные", Метаданные.РегламентныеЗадания.ОбменСообщениямиПоУчетнымЗаписямДокументооборота));
	
	Для Каждого Задание Из НайденныеЗадания Цикл
		НовоеРасписание = Новый РасписаниеРегламентногоЗадания;
		НовоеРасписание.ПериодПовтораДней = 1;
		НовоеРасписание.ПериодПовтораВТечениеДня = 10800;
		Задание.Расписание = НовоеРасписание;
		Задание.Записать();
	КонецЦикла;
	
КонецПроцедуры