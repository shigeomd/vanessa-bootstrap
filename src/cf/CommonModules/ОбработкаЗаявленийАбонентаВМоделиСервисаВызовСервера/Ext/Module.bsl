////////////////////////////////////////////////////////////////////////////////
// Подсистема "Обработка заявлений абонента на подключение".
//
////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

Функция ЕстьЗаявленияНаПодключениеТребующиеРеакцииПользователя() Экспорт
	
	Результат = Новый Структура("Ссылка,Статус,Комментарий,Организация");
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	ЗаявлениеАбонентаСпецоператораСвязи.Ссылка,
	|	ЗаявлениеАбонентаСпецоператораСвязи.Статус,
	|	ЗаявлениеАбонентаСпецоператораСвязи.СтатусКомментарий,
	|	ЗаявлениеАбонентаСпецоператораСвязи.Организация,
	|	ВЫБОР
	|		КОГДА ЗаявлениеАбонентаСпецоператораСвязи.ТипЗаявления = ЗНАЧЕНИЕ(Перечисление.ТипыЗаявленияАбонентаСпецоператораСвязи.Изменение)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоВторичноеЗаявление
	|ИЗ
	|	Документ.ЗаявлениеАбонентаСпецоператораСвязи КАК ЗаявлениеАбонентаСпецоператораСвязи
	|ГДЕ
	|	(ЗаявлениеАбонентаСпецоператораСвязи.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЗаявленияАбонентаСпецоператораСвязи.Одобрено)
	|			ИЛИ ЗаявлениеАбонентаСпецоператораСвязи.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЗаявленияАбонентаСпецоператораСвязи.Отклонено))
	|	И (ЗаявлениеАбонентаСпецоператораСвязи.ТипЗаявления = ЗНАЧЕНИЕ(Перечисление.ТипыЗаявленияАбонентаСпецоператораСвязи.Первичное)
	|				И ЗаявлениеАбонентаСпецоператораСвязи.УчетнаяЗапись = ЗНАЧЕНИЕ(Справочник.УчетныеЗаписиДокументооборота.ПустаяСсылка)
	|			ИЛИ ЗаявлениеАбонентаСпецоператораСвязи.ТипЗаявления = ЗНАЧЕНИЕ(Перечисление.ТипыЗаявленияАбонентаСпецоператораСвязи.Изменение)
	|				И НЕ ЗаявлениеАбонентаСпецоператораСвязи.Обработана)
	|	И ЗаявлениеАбонентаСпецоператораСвязи.Ответственный = &Ответственный
	|	И ЗаявлениеАбонентаСпецоператораСвязи.ЭлектроннаяПодписьВМоделиСервиса
	|	И НЕ ЗаявлениеАбонентаСпецоператораСвязи.ПометкаУдаления";
	Запрос.УстановитьПараметр("Ответственный", Пользователи.ТекущийПользователь());
	
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		
		Результат.Вставить("Статус", Выборка.Статус);
		Результат.Вставить("Комментарий", Выборка.СтатусКомментарий);
		Результат.Вставить("Ссылка", Выборка.Ссылка);
		Результат.Вставить("Организация", Выборка.Организация);
		Результат.Вставить("ЭтоВторичноеЗаявление", Выборка.ЭтоВторичноеЗаявление);
		
		Возврат Результат;
	Иначе
		Возврат Результат;
	КонецЕсли;
	
КонецФункции