
////////////////////////////////////////////////////////////////////////////////
// Подсистема "Интернет-поддержка пользователей".
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Возвращает версию подсистемы ИнтернетПоддержкаПользователей.
// Возвращаемое значение:
//	Строка - версия текущей библиотеки.
//
Функция ВерсияБиблиотеки() Экспорт
	
	Возврат "2.1.3.1";
	
КонецФункции

// Возвращает версию поддерживаемого бибилотекой API
// взаимодействия с серверной частью.
//
// Возвращаемое значение:
//	Строка - версия поддерживаемого библиотекой API
//
Функция ВерсияAPIВзаимодействия() Экспорт
	
	Возврат "1.0.1.1";
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Экспортные процедуры и функции для работы с веб-сервисом Интернет-поддержки

// Возвращает URL веб-сервиса Интернет-поддержки пользователей.
//
// Возвращаемое значение:
//	Строка - URL веб-сервиса ИПП.
//
Функция ИмяWSОпределения() Экспорт
	
	Возврат "https://webits.1c.ru/services/WebItsSimpleService?wsdl";
	
КонецФункции

// Определение имени URI веб-сервиса ИПП.
//
// Возвращаемое значение:
//	Строка - URI сервиса.
//
Функция ИмяURIСервиса() Экспорт
	
	Возврат "https://ws.webits.onec.ru";
	
КонецФункции

// Возвращает структуру-описатель ресурса Интернет-поддержки пользователей.
// Используется для заполнения разрешений доступа к внешним ресурсам.
//
// Возвращаемое значение:
// Структура - описание ресурса Интернет-поддержки пользователей:
//	* Протокол - Строка - протокол соединения (http или https);
//	* Адрес    - Строка - адрес сервера;
//	* Порт     - Число  - порт на сервере Интернет-поддержки;
//	* Описание - Строка - строковое описание ресурса;
//
Функция ОписаниеРесурсаИнтернетПоддержки() Экспорт
	
	Возврат Новый Структура("Протокол, Адрес, Порт, Описание",
		"HTTPS",
		"webits.1c.ru",
		443,
		НСтр("ru = 'Интернет-поддержка пользователей'"));
	
КонецФункции

// Возвращает структуру с полями-параметрами, передаваемыми в процедуры
// ИнтернетПоддержкаПользователейПереопределяемый.ПриОпределенииДанныхПользователяИнтернетПоддержки()
// и ИнтернетПоддержкаПользователейПереопределяемый.ПриАвторизацииПользователяВИнтернетПоддержке()
//
// Возвращаемое значение:
// Структура - с полями-параметрами:
//	* Логин  - Строка - логин пользователя;
//	* Пароль - Строка - пароль пользователя;
//
Функция НовыйДанныеПользователяИнтернетПоддержки() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("Логин" , "");
	Результат.Вставить("Пароль", "");
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Проверка возможности запуска Интернет-поддержки в соответствии с параметрами
// и местом (кнопкой) запуска. Возвращает управляющую структуру, описывающую
// действия, которые необходимо выполнить.
//
// Параметры:
//	МестоЗапуска - Строка - место запуска (имя кнопки) механизма ИПП;
//	ПараметрыИнтернетПоддержки - Структура - параметры работы механизма
//		Интернет-поддержки (см. функцию
//		ИнтернетПоддержкаПользователей.ПараметрыСозданияКонтекста()).
//
// Возвращаемое значение:
// Структура - если запуск в текущем режиме работы запрещен:
//	* Действие  - Строка - действие, которое необходимо выполнить;
//	* Сообщение - Строка - если выполнение действия подразумевает показ сообщения
//						   пользователю;
// Неопределено - если запуск разрешен.
//
Функция ОпределитьВозможностьЗапускаПоМестуИПараметрам(МестоЗапуска, ПараметрыИнтернетПоддержки) Экспорт
	
	// Проверка возможности запуска
	ПриНачалеРаботыСистемы = ПараметрыИнтернетПоддержки.ПриНачалеРаботыСистемы;
	
	Результат = Неопределено;
	
	// Стандартная обработка при начале работы с программой
	Если ПриНачалеРаботыСистемы Тогда
		
		Если НЕ ПараметрыИнтернетПоддержки.ИспользоватьИнтернетПоддержку
			ИЛИ НЕ ПараметрыИнтернетПоддержки.ЗапускРазрешен Тогда
			
			Результат = Новый Структура("Действие", "Возврат");
			
		КонецЕсли;
		
	ИначеЕсли НЕ ПараметрыИнтернетПоддержки.ИспользоватьИнтернетПоддержку Тогда
		
		Результат = Новый Структура;
		Результат.Вставить("Действие", "ПоказатьСообщение");
		Результат.Вставить("Сообщение",
			НСтр("ru = 'Использование Интернет-поддержки пользователей запрещено в текущем режиме работы.'"));
		
	ИначеЕсли НЕ ПараметрыИнтернетПоддержки.ЗапускРазрешен Тогда
		
		Результат = Новый Структура;
		Результат.Вставить("Действие" , "ПоказатьСообщение");
		Результат.Вставить("Сообщение",
			НСтр("ru = 'Недостаточно прав для запуска Интернет-поддержки пользователей. Обратитесь к администратору.'"));
		
	КонецЕсли;
	
	Если Результат = Неопределено Тогда
		// Обработка обработчиком текущего бизнес-процесса
		Обработчик = ОбработчикБизнесПроцесса(МестоЗапуска, "ОпределитьВозможностьЗапуска");
		Если Обработчик <> Неопределено Тогда
			
			ОписаниеДействия = Новый Структура;
			Обработчик.ОпределитьВозможностьЗапуска(
				МестоЗапуска,
				ПараметрыИнтернетПоддержки,
				ОписаниеДействия);
			
			Если ОписаниеДействия.Количество() > 0 Тогда
				Результат = ОписаниеДействия;
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;
	
	Если Результат <> Неопределено Тогда
		Результат.Вставить("ПриНачалеРаботыСистемы", ПриНачалеРаботыСистемы);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#Если Не ВебКлиент Тогда

// Проверка доступности сервиса Интернет-поддержки пользователей посредством
// вызова метода isReady().
//
// Параметры:
//	ОписаниеСервисаИПП - Структура - описатель соединения с веб-сервисом ИПП:
//		см. функцию НовыйОписаниеСервисаИПП().
//
// Возвращаемое значение:
//	Булево - Истина, если сервис доступен, Ложь - если при обращении к сервису
//		возникло исключение;
//	Строка - описание причины недоступности сервиса, возвращенной сервисом;
//
Функция ЕстьДоступКВебCервису(ОписаниеСервисаИПП) Экспорт
	
	Попытка
		
		ОтветСервера = СервисИПП_IsReady(ОписаниеСервисаИПП);
		
		Если НРег(СокрЛП(ОтветСервера)) = "ready" Тогда
			Возврат Истина;
		Иначе
			Возврат ОтветСервера;
		КонецЕсли;
		
	Исключение
		
		ИнтернетПоддержкаПользователейВызовСервера.ЗаписатьОшибкуВЖурналРегистрации(
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		Возврат Ложь;
		
	КонецПопытки;
	
КонецФункции

// Проверка, зарегистрирована ли конфигурация в сервисе Интернет-поддержки
// пользователей.
//
// Параметры:
//	ОшибкаДоступаКВебСервису - Булево - в параметре возвращается значение Истина,
//		если в процессе обращения к веб-сервису возникло исключение, ошибка
//		записывается в журнал регистрации;
//	ОписаниеСервисаИПП - Структура - описатель сервиса ИПП (см. функцию
//		НовыйОписаниеСервисаИПП());
//	ПараметрыИПП - Структура - параметры для работы с сервисом ИПП, полученные
//		ранее;
//
// Возвращаемое значение:
//	Булево - Истина, если конфигурация зарегистрирована в сервисе ИПП,
//		Ложь - если конфигурация не зарегистрирована или возникло исключение
//		при обращении к сервису ИПП.
//
Функция КонфигурацияЗарегистрированаВСервисеИПП(
	ОшибкаОбращенияКВебСервису = Ложь,
	ОписаниеСервисаИПП = Неопределено,
	ПараметрыИПП = Неопределено) Экспорт
	
	Попытка
		
		Если ОписаниеСервисаИПП = Неопределено Тогда
			ОписаниеСервисаИПП = НовыйОписаниеСервисаИПП(, ПараметрыИПП);
		КонецЕсли;
		
		// В качестве параметра метода передается имя конфигурации
		ОтветСервера = СервисИПП_isConfigurationSupported(
			ИмяКонфигурации(),
			ОписаниеСервисаИПП);
		
		Возврат (ОтветСервера = Истина ИЛИ ОтветСервера = "true");
		
	Исключение
		ОшибкаОбращенияКВебСервису = Истина;
		ИнтернетПоддержкаПользователейВызовСервера.ЗаписатьОшибкуВЖурналРегистрации(
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		Возврат Ложь;
	КонецПопытки;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Процедуры и функции для работы с веб-сервисом Интернет-поддержки
// пользователей на "низком уровне".

// Формирует описание веб-сервиса из WSDL-документа для дальнейшей работы
// с веб-сервисом ИПП.
//
// Параметры:
// АдресWSDL - Строка, Неопределено - URL расположения WSDL-документа;
//		Если не задан, тогда используется ИмяWSОпределения();
// СетевыеПараметрыИПП - Структура - сетевые параметры Интернет-поддержки:
//		(см. ИнтернетПоддержкаПользователейВызовСервера.СетевыеПараметрыИнтернетПоддержки());
// КэшWSDLОписаний - Соответствие, Неопределено - кэш WSDL-описаний сервисов;
//		используется для сохранения WSDL-описаний в процессе взаимодействия с
//		сервисами ИПП. Ключ - URL WSDL-документа, значение - Строка - адрес
//		текста WSDL-описания во временном хранилище;
//
// Возвращаемое значение:
// Структура - описание соединения с сервисом ИПП:
//	* АдресWSDL - Строка - URL WSDL-документа;
//	* ТаймаутСети - Число - таймаут сетевого соединения;
//	* ФабрикаXDTO - ФабрикаXDTO - Фабрика XDTO веб-сервиса;
//	* URIСервиса - Строка - URI веб-сервиса ИПП;
//	* СоединениеПорта - HTTPСоединение - соединение с портом сервиса
//		для вызовов методов веб-сервиса;
//	* ИнтернетПрокси - ИнтернетПрокси - соединение прокси-сервера;
//	* ПараметрыПодключенияКПорту -Структура - см. функцию
//		НовыйПараметрыПолученияДокумента();
//	
Функция НовыйОписаниеСервисаИПП(
	АдресWSDL = Неопределено,
	СетевыеПараметры = Неопределено,
	КэшWSDLОписаний = Неопределено) Экспорт
	
	Если АдресWSDL = Неопределено Тогда
		АдресWSDL = ИмяWSОпределения();
	КонецЕсли;
	
	Результат = Новый Структура("АдресWSDL", АдресWSDL);
	
	Если СетевыеПараметры = Неопределено Тогда
		СетевыеПараметры = ИнтернетПоддержкаПользователейВызовСервера.СетевыеПараметрыИнтернетПоддержки();
	КонецЕсли;
	
	Результат.Вставить("ТаймаутСети", СетевыеПараметры.ТаймаутСети);
	
	ТекстWSDL     = Неопределено;
	АдресОписания = Неопределено;
	Если КэшWSDLОписаний <> Неопределено Тогда
		АдресОписания = КэшWSDLОписаний.Получить(АдресWSDL);
		Если АдресОписания <> Неопределено Тогда
			ТекстWSDL = ПолучитьИзВременногоХранилища(АдресОписания);
		КонецЕсли;
	КонецЕсли;
	
	Если ТекстWSDL = Неопределено Тогда
		
		// Получение и разбор WSDL-документа
		ПараметрыПолученияWSDL = НовыйПараметрыПолученияДокумента(АдресWSDL);
		
		ИнтернетПрокси = ПолучениеФайловИзИнтернетаКлиентСервер.ПолучитьПрокси(
			?(ПараметрыПолученияWSDL.БезопасноеСоединение, "https", "http"));
		
		HTTP = Новый HTTPСоединение(
			ПараметрыПолученияWSDL.Сервер,
			ПараметрыПолученияWSDL.Порт,
			ПараметрыПолученияWSDL.ИмяПользователя,
			ПараметрыПолученияWSDL.Пароль,
			ИнтернетПрокси,
			Результат.ТаймаутСети,
			?(ПараметрыПолученияWSDL.БезопасноеСоединение,
				Новый ЗащищенноеСоединениеOpenSSL,
				Неопределено));
		
		Попытка
			
			HTTPЗапрос = Новый HTTPЗапрос(ПараметрыПолученияWSDL.Путь);
			Ответ = HTTP.Получить(HTTPЗапрос);
			ТекстWSDL = Ответ.ПолучитьТелоКакСтроку();
			
		Исключение
			ТекстИсключения = СтрЗаменить(
				НСтр("ru = 'Ошибка при создании описания веб-сервиса.
							|Не удалось получить WSDL-описание с сервера Интернет-поддержки пользователей (%1).'"),
					"%1",
					АдресWSDL)
				+ " " + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			ВызватьИсключение ТекстИсключения;
		КонецПопытки;
		
	КонецЕсли;
	
	Если КэшWSDLОписаний <> Неопределено И АдресОписания = Неопределено Тогда
		// Помещение описания WSDL во временное хранилище
		АдресОписания = ПоместитьВоВременноеХранилище(ТекстWSDL, Новый УникальныйИдентификатор);
		КэшWSDLОписаний.Вставить(АдресWSDL, АдресОписания);
		// Данные из временного хранилища удаляются при завершении бизнес-процесса
		// Интернет-поддержки пользователей
	КонецЕсли;
	
	ЧтениеXML = Новый ЧтениеXML;
	ЧтениеXML.УстановитьСтроку(ТекстWSDL);
	
	ПостроительDOM = Новый ПостроительDOM;
	Попытка
		ДокументDOM = ПостроительDOM.Прочитать(ЧтениеXML);
	Исключение
		ТекстИсключения = СтрЗаменить(
				НСтр("ru = 'Ошибка при создании описания веб-сервиса (%1).
							|Ошибка чтения WSDL-описания веб-сервиса Интернет-поддержки пользователей:'"),
				"%1",
				АдресWSDL)
			+ " " + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ВызватьИсключение ТекстИсключения;
	КонецПопытки;
	
	// Создание фабрики XDTO веб-сервиса ИПП
	
	УзлыСхемы = ДокументDOM.ПолучитьЭлементыПоИмени("wsdl:types");
	Если УзлыСхемы.Количество() = 0 Тогда
		
		ТекстИсключения = СтрЗаменить(НСтр("ru = 'Ошибка при создании описания веб-сервиса (%1).
				|Ошибка чтения WSDL-описания веб-сервиса Интернет-поддержки пользователей:'"),
				"%1",
				АдресWSDL)
			+ Символы.ПС
			+ НСтр("ru = 'Отсутствует элемент описания типов данных (<wsdl:types ...>).'");
		
		ВызватьИсключение ТекстИсключения;
		
	КонецЕсли;
	
	УзелОписанияСхемы = УзлыСхемы[0].ПервыйДочерний;
	Если УзелОписанияСхемы = Неопределено Тогда
		
		ТекстИсключения = СтрЗаменить(НСтр("ru = 'Ошибка при создании описания веб-сервиса (%1).
				|Ошибка чтения WSDL-описания веб-сервиса Интернет-поддержки пользователей:'"),
				"%1",
				АдресWSDL)
			+ " " + НСтр("ru = 'Отсутствует элемент описания типов данных (<xs:schema ...>)'");
		
		ВызватьИсключение ТекстИсключения;
		
	КонецЕсли;
	
	ПостроительСхемы = Новый ПостроительСхемXML;
	
	Попытка
		СхемаДанныхСервиса = ПостроительСхемы.СоздатьСхемуXML(УзелОписанияСхемы);
	Исключение
		ТекстИсключения = СтрЗаменить(НСтр("ru = 'Ошибка при создании описания веб-сервиса (%1).
				|Ошибка при создании схемы данных из WSDL-описания веб-сервиса Интернет-поддержки пользователей:'"),
				"%1",
				АдресWSDL)
			+ " " + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ВызватьИсключение ТекстИсключения;
	КонецПопытки;
	
	НаборСхем = Новый НаборСхемXML;
	НаборСхем.Добавить(СхемаДанныхСервиса);
	
	Попытка
		ФабрикаСервиса = Новый ФабрикаXDTO(НаборСхем);
	Исключение
		ТекстИсключения = СтрЗаменить(НСтр("ru = 'Ошибка при создании описания веб-сервиса (%1).
				|Ошибка при создании фабрики XDTO из WSDL-описания веб-сервиса Интернет-поддержки пользователей:'"),
				"%1",
				АдресWSDL)
			+ " " + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ВызватьИсключение ТекстИсключения;
	КонецПопытки;
	
	// Определение параметров подключения к порту сервиса
	
	КорневойЭлемент = ДокументDOM.ПервыйДочерний;
	
	Результат.Вставить("ФабрикаXDTO", ФабрикаСервиса);
	
	URIСервиса = ЗначениеАтрибутаУзлаDOM(КорневойЭлемент, "targetNamespace");
	Если НЕ ЗначениеЗаполнено(URIСервиса) Тогда
		
		ТекстИсключения = СтрЗаменить(НСтр("ru = 'Ошибка при создании описания веб-сервиса (%1).
				|Ошибка чтения WSDL-описания веб-сервиса Интернет-поддержки пользователей:'"),
				"%1",
				АдресWSDL)
			+ " " + НСтр("ru = 'Отсутствует URI пространства имен в WSDL-описании.'");
		
		ВызватьИсключение ТекстИсключения;
		
	КонецЕсли;
	
	Результат.Вставить("URIСервиса", URIСервиса);
	
	// Определение адреса порта веб-сервиса
	УзлыСервисов = КорневойЭлемент.ПолучитьЭлементыПоИмени("wsdl:service");
	Если УзлыСервисов.Количество() = 0 Тогда
		ТекстИсключения = СтрЗаменить(НСтр("ru = 'Ошибка при создании описания веб-сервиса (%1).
				|Ошибка чтения WSDL-описания веб-сервиса Интернет-поддержки пользователей:'"),
				"%1",
				АдресWSDL)
			+ " " + НСтр("ru = 'Отсутствует описание веб-сервисов в WSDL-описании (<wsdl:service ...>).'");
		
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	УзелСервиса = УзлыСервисов[0];
	
	ИмяСервиса = ЗначениеАтрибутаУзлаDOM(УзелСервиса, "name");
	
	УзлыПортов = УзелСервиса.ПолучитьЭлементыПоИмени("wsdl:port");
	
	Если УзлыПортов.Количество() = 0 Тогда
		ТекстИсключения = СтрЗаменить(НСтр("ru = 'Ошибка при создании описания веб-сервиса (%1).
				|Ошибка чтения WSDL-описания веб-сервиса Интернет-поддержки пользователей:'"),
				"%1",
				АдресWSDL)
			+ " " + НСтр("ru = 'Отсутствует описание портов в WSDL-описании (<wsdl:port ...>).'");
		
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	УзелПорта = УзлыПортов[0];
	ИмяПорта  = ЗначениеАтрибутаУзлаDOM(УзелПорта, "name");
	
	Если НЕ ЗначениеЗаполнено(ИмяПорта) Тогда
		
		ТекстИсключения = СтрЗаменить(НСтр("ru = 'Ошибка при создании описания веб-сервиса (%1).
				|Ошибка чтения WSDL-описания веб-сервиса Интернет-поддержки пользователей:'"),
				"%1",
				АдресWSDL)
			+ " " + СтрЗаменить(НСтр("ru = 'Не удалось определить имя порта сервиса (%1).'"),
				"%1",
				ИмяСервиса);
		
		ВызватьИсключение ТекстИсключения;
		
	КонецЕсли;
	
	АдресПорта = Неопределено;
	УзлыАдреса = УзелПорта.ПолучитьЭлементыПоИмени("soap:address");
	Если УзлыАдреса.Количество() > 0 Тогда
		АдресПорта = ЗначениеАтрибутаУзлаDOM(УзлыАдреса[0], "location");
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(АдресПорта) Тогда
		
		ТекстИсключения = СтрЗаменить(НСтр("ru = 'Ошибка при создании описания веб-сервиса (%1).
				|Ошибка чтения WSDL-описания веб-сервиса Интернет-поддержки пользователей:'"),
				"%1",
				АдресWSDL)
			+ " " + СтрЗаменить(НСтр("ru = 'Не удалось определить URL заданного порта сервиса (%1).'"),
				"%1",
				ИмяПорта);
		
		ВызватьИсключение ТекстИсключения;
		
	КонецЕсли;
	
	// Создание подключения к порту веб-сервиса
	ПараметрыПодключенияКПорту = НовыйПараметрыПолученияДокумента(АдресПорта);
	
	ИнтернетПрокси = ПолучениеФайловИзИнтернетаКлиентСервер.ПолучитьПрокси(
		?(ПараметрыПодключенияКПорту.БезопасноеСоединение, "https", "http"));
	
	СоединениеПорта = Новый HTTPСоединение(
		ПараметрыПодключенияКПорту.Сервер,
		ПараметрыПодключенияКПорту.Порт,
		ПараметрыПодключенияКПорту.ИмяПользователя,
		ПараметрыПодключенияКПорту.Пароль,
		ИнтернетПрокси,
		Результат.ТаймаутСети,
		?(ПараметрыПодключенияКПорту.БезопасноеСоединение,
			Новый ЗащищенноеСоединениеOpenSSL,
			Неопределено));
	
	Результат.Вставить("ИнтернетПрокси"            , ИнтернетПрокси);
	Результат.Вставить("ПараметрыПодключенияКПорту", ПараметрыПодключенияКПорту);
	Результат.Вставить("СоединениеПорта"           , СоединениеПорта);
	
	Возврат Результат;
	
КонецФункции

// Изменяет таймаут обращения к сервису в текущем соединении с сервисом ИПП.
//
// Параметры:
//	ОписаниеСервисаИПП - Структура - см. функцию НовыйОписаниеСервисаИПП();
//	ЗначениеТаймаута - Число - значение устанавливаемого таймаута в секундах;
//
Процедура ИзменитьТаймаутОбращения(ОписаниеСервисаИПП, ЗначениеТаймаута) Экспорт
	
	ОписаниеСервисаИПП.ТаймаутСети = ЗначениеТаймаута;
	
	// Назначить таймаут в HTTP-соединии возможно только
	// в конструкторе, поэтому необходимо повторно создать
	// подключение порта
	
	ПараметрыПодключенияКПорту = ОписаниеСервисаИПП.ПараметрыПодключенияКПорту;
	СоединениеПорта = Новый HTTPСоединение(
		ПараметрыПодключенияКПорту.Сервер,
		ПараметрыПодключенияКПорту.Порт,
		ПараметрыПодключенияКПорту.ИмяПользователя,
		ПараметрыПодключенияКПорту.Пароль,
		ОписаниеСервисаИПП.ИнтернетПрокси,
		ЗначениеТаймаута,
		?(ПараметрыПодключенияКПорту.БезопасноеСоединение,
			Новый ЗащищенноеСоединениеOpenSSL,
			Неопределено));
	
	ОписаниеСервисаИПП.СоединениеПорта = СоединениеПорта;
	
КонецПроцедуры

// Прокси-функция для вызова метода isReady() веб-сервиса ИПП
//
// Параметры:
//	ОписаниеСервисаИПП - Структура - описание веб-сервиса ИПП,
//		см. НовыйОписаниеСервисаИПП();
//
// Возвращаемое значение:
// Строка - значение, возвращенное методом isReady() веб-сервиса ИПП;
//
Функция СервисИПП_isReady(ОписаниеСервисаИПП) Экспорт
	
	ЗаписьКонверта = НовыйЗаписьКонвертаSOAP();
	ТекстКонверта  = ТекстВКонвертеSOAP(ЗаписьКонверта);
	
	Попытка
		ТелоОтвета = ОтправитьЗапросSOAP(ТекстКонверта, ОписаниеСервисаИПП);
	Исключение
		
		ТекстИсключения = СтрЗаменить(НСтр("ru = 'Ошибка при вызове операции isReady сервиса (%1):'"),
				"%1",
				ОписаниеСервисаИПП.АдресWSDL)
			+ " " + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ВызватьИсключение ТекстИсключения;
		
	КонецПопытки;
	
	ТипОбъекта = ТипЗначенияКорневогоСвойстваФабрикиСервисаИПП("isReadyResponse", ОписаниеСервисаИПП);
	Если ТипОбъекта = Неопределено Тогда
		ТекстИсключения = СтрЗаменить(НСтр("ru = 'Ошибка при вызове операции isReady сервиса (%1).
			|Не удалось определить тип корневого свойства isReadyResponse.'"),
			"%1",
			ОписаниеСервисаИПП.АдресWSDL);
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	Попытка
		Значение = ПрочитатьОтветВКонвертеSOAP(ТелоОтвета, ОписаниеСервисаИПП, ТипОбъекта);
	Исключение
		ТекстИсключения = СтрЗаменить(НСтр("ru = 'Ошибка при вызове операции isReady сервиса (%1).'"),
			"%1",
			ОписаниеСервисаИПП.АдресWSDL)
			+ Символы.ПС
			+ ПодробноеПредставлениеОшибки(ИнформацияОбОшибке())
			+ Символы.ПС + Символы.ПС
			+ НСтр("ru = 'Тело запроса:'")
			+ Символы.ПС
			+ ТекстКонверта;
		
		ВызватьИсключение ТекстИсключения;
	КонецПопытки;
	
	Если ТипЗнч(Значение) = Тип("Структура") Тогда
		
		// Возвращено описание исключения SOAP
		ТекстИсключения = СтрЗаменить(НСтр("ru = 'Ошибка при вызове операции isReady сервиса (%1).
				|Ошибка SOAP:'"),
			"%1",
			ОписаниеСервисаИПП.АдресWSDL)
			+ " " + ОписаниеИсключенияSOAPВСтроку(Значение);
		
		ВызватьИсключение ТекстИсключения;
		
	ИначеЕсли ТипЗнч(Значение) = Тип("ЗначениеXDTO") Тогда
		Возврат Значение.Значение;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

// Прокси-функция для вызова метода isConfigurationSupported() веб-сервиса ИПП.
//
// Параметры:
// ИмяКонфигурации - Строка - имя текущей конфигурации;
// ОписаниеСервисаИПП - Структура - описание веб-сервиса ИПП,
//		см. НовыйОписаниеСервисаИПП();
//
// Возвращаемое значение:
// Булево - значение, возвращенное методом isConfigurationSupported()
//		веб-сервиса ИПП;
//
Функция СервисИПП_isConfigurationSupported(ИмяКонфигурации, ОписаниеСервисаИПП) Экспорт
	
	ЗаписьКонверта = НовыйЗаписьКонвертаSOAP();
	
	ТипЗначенияСвойства = ТипЗначенияКорневогоСвойстваФабрикиСервисаИПП("isConfigurationSupported",
		ОписаниеСервисаИПП);
	
	Если ТипЗначенияСвойства = Неопределено Тогда
		ТекстИсключения = СтрЗаменить(НСтр("ru = 'Ошибка при вызове операции isConfigurationSupported сервиса (%1).
			|Не удалось определить тип корневого свойства isConfigurationSupported.'"),
			"%1",
			ОписаниеСервисаИПП.АдресWSDL);
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	ЗначениеXDTO = ОписаниеСервисаИПП.ФабрикаXDTO.Создать(ТипЗначенияСвойства, ИмяКонфигурации);
	
	ОписаниеСервисаИПП.ФабрикаXDTO.ЗаписатьXML(ЗаписьКонверта,
		ЗначениеXDTO,
		"isConfigurationSupported",
		,
		ФормаXML.Элемент,
		НазначениеТипаXML.Явное);
	
	ТекстКонверта = ТекстВКонвертеSOAP(ЗаписьКонверта);
	
	Попытка
		ТелоОтвета = ОтправитьЗапросSOAP(ТекстКонверта, ОписаниеСервисаИПП);
	Исключение
		
		ТекстИсключения = СтрЗаменить(НСтр("ru = 'Ошибка при вызове операции isConfigurationSupported сервиса (%1):'"),
			"%1",
			ОписаниеСервисаИПП.АдресWSDL)
			+ " " + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		
		ВызватьИсключение ТекстИсключения;
		
	КонецПопытки;
	
	ТипОбъекта = ТипЗначенияКорневогоСвойстваФабрикиСервисаИПП("isConfigurationSupportedResponse", ОписаниеСервисаИПП);
	Если ТипОбъекта = Неопределено Тогда
		ТекстИсключения = СтрЗаменить(НСтр("ru = 'Ошибка при вызове операции isConfigurationSupported сервиса (%1).
			|Не удалось определить тип корневого свойства isConfigurationSupportedResponse.'"),
			"%1",
			ОписаниеСервисаИПП.АдресWSDL);
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	Попытка
		Значение = ПрочитатьОтветВКонвертеSOAP(ТелоОтвета, ОписаниеСервисаИПП, ТипОбъекта);
	Исключение
		ТекстИсключения = СтрЗаменить(НСтр("ru = 'Ошибка при вызове операции isConfigurationSupported сервиса (%1).'"),
			"%1",
			ОписаниеСервисаИПП.АдресWSDL)
			+ Символы.ПС
			+ ПодробноеПредставлениеОшибки(ИнформацияОбОшибке())
			+ Символы.ПС + Символы.ПС
			+ НСтр("ru = 'Тело запроса:'")
			+ Символы.ПС
			+ ТекстКонверта;
		
		ВызватьИсключение ТекстИсключения;
	КонецПопытки;
	
	Если ТипЗнч(Значение) = Тип("ЗначениеXDTO") Тогда
		Возврат Значение.Значение;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

// Прокси-функция для вызова метода process() веб-сервиса ИПП.
//
// Параметры:
//	ПараметрыЗапроса - ОбъектXDTO - параметры запроса метода process();
//	ОписаниеСервисаИПП - Структура - описание веб-сервиса ИПП,
//		см. НовыйОписаниеСервисаИПП();
//
// Возвращаемое значение:
//	ОбъектXDTO - значение, возвращенное методом process() веб-сервиса ИПП;
//
Функция СервисИПП_process(ПараметрыЗапроса, ОписаниеСервисаИПП) Экспорт
	
	ЗаписьКонверта = НовыйЗаписьКонвертаSOAP();
	
	ЗаписьКонверта.ЗаписатьНачалоЭлемента("m:processRequest");
	ЗаписьКонверта.ЗаписатьАтрибут("xmlns:m", ОписаниеСервисаИПП.URIСервиса);
	
	ОписаниеСервисаИПП.ФабрикаXDTO.ЗаписатьXML(
		ЗаписьКонверта,
		ПараметрыЗапроса,
		"parameters",
		,
		ФормаXML.Элемент,
		НазначениеТипаXML.Явное);
	
	ЗаписьКонверта.ЗаписатьКонецЭлемента(); // </m:processRequest>
	
	ТекстКонверта = ТекстВКонвертеSOAP(ЗаписьКонверта);
	
	Попытка
		ТелоОтвета = ОтправитьЗапросSOAP(ТекстКонверта, ОписаниеСервисаИПП);
	Исключение
		
		ТекстИсключения = СтрЗаменить(НСтр("ru = 'Ошибка при вызове операции process сервиса (%1):'"),
			"%1",
			ОписаниеСервисаИПП.АдресWSDL)
			+ " " + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		
		ВызватьИсключение ТекстИсключения;
		
	КонецПопытки;
	
	ТипОбъекта = ТипЗначенияКорневогоСвойстваФабрикиСервисаИПП("processResponse", ОписаниеСервисаИПП);
	Если ТипОбъекта = Неопределено Тогда
		ТекстИсключения = СтрЗаменить(НСтр("ru = 'Ошибка при вызове операции process сервиса (%1).
			|Не удалось определить тип корневого свойства processResponse.'"),
			"%1",
			ОписаниеСервисаИПП.АдресWSDL);
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	Попытка
		Значение = ПрочитатьОтветВКонвертеSOAP(ТелоОтвета, ОписаниеСервисаИПП, ТипОбъекта);
	Исключение
		
		ТекстИсключения = СтрЗаменить(НСтр("ru = 'Ошибка при вызове операции process сервиса (%1).'"),
			"%1",
			ОписаниеСервисаИПП.АдресWSDL)
			+ Символы.ПС
			+ ПодробноеПредставлениеОшибки(ИнформацияОбОшибке())
			+ Символы.ПС + Символы.ПС
			+ НСтр("ru = 'Тело запроса:'")
			+ Символы.ПС
			+ ТекстКонверта;
		
		ВызватьИсключение ТекстИсключения;
		
	КонецПопытки;
	
	Если ТипЗнч(Значение) = Тип("ОбъектXDTO") Тогда
		Возврат Значение.commands;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

// Прокси-функция для вызова метода sendmailtonet() веб-сервиса ИПП
//
// Параметры:
//	ПараметрыЗапроса - ОбъектXDTO - параметры запроса метода sendmailtonet();
//	ОписаниеСервисаИПП - Структура - описание веб-сервиса ИПП,
//		см. НовыйОписаниеСервисаИПП();
//
// Возвращаемое значение:
//	ОбъектXDTO - значение, возвращенное методом process() веб-сервиса ИПП;
//
Функция СервисИПП_sendmailtonet(ПараметрыЗапроса, ОписаниеСервисаИПП) Экспорт
	
	ЗаписьКонверта = НовыйЗаписьКонвертаSOAP();
	
	ОписаниеСервисаИПП.ФабрикаXDTO.ЗаписатьXML(
		ЗаписьКонверта,
		ПараметрыЗапроса,
		"sendmailParams",
		ОписаниеСервисаИПП.URIСервиса,
		ФормаXML.Элемент,
		НазначениеТипаXML.Явное);
	
	ТекстКонверта = ТекстВКонвертеSOAP(ЗаписьКонверта);
	
	Попытка
		ТелоОтвета = ОтправитьЗапросSOAP(ТекстКонверта, ОписаниеСервисаИПП);
	Исключение
		
		ТекстИсключения = СтрЗаменить(НСтр("ru = 'Ошибка при вызове операции sendmailtonet сервиса (%1).'"),
			"%1",
			ОписаниеСервисаИПП.АдресWSDL)
			+ " " + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		
		ВызватьИсключение ТекстИсключения;
		
	КонецПопытки;
	
	ТипОбъекта = ТипЗначенияКорневогоСвойстваФабрикиСервисаИПП("processResponse", ОписаниеСервисаИПП);
	Если ТипОбъекта = Неопределено Тогда
		ТекстИсключения = СтрЗаменить(НСтр("ru = 'Ошибка при вызове операции sendmailtonet сервиса (%1).
			|Не удалось определить тип корневого свойства processResponse.'"),
			"%1",
			ОписаниеСервисаИПП.АдресWSDL);
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	Попытка
		Значение = ПрочитатьОтветВКонвертеSOAP(ТелоОтвета, ОписаниеСервисаИПП, ТипОбъекта);
	Исключение
		
		ТекстИсключения = СтрЗаменить(НСтр("ru = 'Ошибка при вызове операции process сервиса (%1).'"),
			"%1",
			ОписаниеСервисаИПП.АдресWSDL)
			+ Символы.ПС
			+ ПодробноеПредставлениеОшибки(ИнформацияОбОшибке())
			+ Символы.ПС + Символы.ПС
			+ НСтр("ru = 'Тело запроса:'")
			+ Символы.ПС
			+ ТекстКонверта;
		
		ВызватьИсключение ТекстИсключения;
		
	КонецПопытки;
	
	Если ТипЗнч(Значение) = Тип("ОбъектXDTO") Тогда
		Возврат Значение.commands;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

// Возвращает строковое значение атрибута узла DOM-документа.
//
// Параметры:
//	УзелDOM - УзелDOM - узел DOM-документа;
//	ИмяАтрибута - Строка - полное имя атрибута;
//	ЗначениеЕслиНеНайдено - Произвольный - значение, которое необходимо
//		возвратить, если атрибут не найден;
//
// Возвращаемое значение:
//	Строка - строковое значение атрибута узла;
//
Функция ЗначениеАтрибутаУзлаDOM(УзелDOM, ИмяАтрибута, ЗначениеЕслиНеНайдено = Неопределено)
	
	Атрибут = УзелDOM.Атрибуты.ПолучитьИменованныйЭлемент(ИмяАтрибута);
	
	Если Атрибут = Неопределено Тогда
		Возврат ЗначениеЕслиНеНайдено;
	Иначе
		Возврат Атрибут.Значение;
	КонецЕсли;
	
КонецФункции

// Возвращает тип значения корневого свойства пакета фабрики XDTO
// веб-сервиса ИПП.
//
// Параметры:
//	ИмяСвойства - Строка - имя корневого свойства;
//	ОписаниеСервисаИПП - Структура - описание веб-сервиса ИПП,
//		см. НовыйОписаниеСервисаИПП();
//
// Возвращаемое значение:
//	ТипЗначенияXDTO, ТипОбъектаXDTO, Неопределено - возвращаемый тип корневого
//		свойства, Неопределено - если корневое свойство отсутствует.
//
Функция ТипЗначенияКорневогоСвойстваФабрикиСервисаИПП(ИмяСвойства, ОписаниеСервисаИПП)
	
	Пакет            = ОписаниеСервисаИПП.ФабрикаXDTO.Пакеты.Получить(ОписаниеСервисаИПП.URIСервиса);
	КорневоеСвойство = Пакет.КорневыеСвойства.Получить(ИмяСвойства);
	Если КорневоеСвойство = Неопределено Тогда
		Возврат Неопределено;
	Иначе
		Возврат КорневоеСвойство.Тип;
	КонецЕсли;
	
КонецФункции

// Формирует объект тип ЗаписьXML с записанными SOAP-заголовками;
//
// Возвращаемое значение:
//	ЗаписьXML - объект записи XML с записанными SOAP-заголовками;
//
Функция НовыйЗаписьКонвертаSOAP()
	
	ЗаписьXML = Новый ЗаписьXML;
	ЗаписьXML.УстановитьСтроку();
	
	ЗаписьXML.ЗаписатьНачалоЭлемента("soap:Envelope");
	ЗаписьXML.ЗаписатьАтрибут("xmlns:soap", "http://schemas.xmlsoap.org/soap/envelope/");
	ЗаписьXML.ЗаписатьНачалоЭлемента("soap:Header");
	ЗаписьXML.ЗаписатьКонецЭлемента(); // </soap:Header>
	ЗаписьXML.ЗаписатьНачалоЭлемента("soap:Body");
	
	Возврат ЗаписьXML;
	
КонецФункции

// Финализирует запись конверта SOAP и возвращает текст конверта.
//
// Параметры:
//	ЗаписьКонверта - ЗаписьXML - объект, в который выполнялась запись конверта;
//
// Возвращаемое значение:
//	Строка - текст конверта SOAP;
//
Функция ТекстВКонвертеSOAP(ЗаписьКонверта)
	
	ЗаписьКонверта.ЗаписатьКонецЭлемента(); // </soap:Body>
	ЗаписьКонверта.ЗаписатьКонецЭлемента(); // </soap:Envelope>
	
	Возврат ЗаписьКонверта.Закрыть();
	
КонецФункции

// Отправку SOAP-конверта веб-сервису ИПП и получение ответного SOAP-конверта.
//
// Параметры:
//	ТекстКонверта - Строка - текст конверта-запроса;
//	ОписаниеСервисаИПП - Структура - описание веб-сервиса ИПП,
//		см. НовыйОписаниеСервисаИПП();
//
// Возвращаемое значение:
//	Строка - текст SOAP-конверта-ответа;
//
Функция ОтправитьЗапросSOAP(ТекстКонверта, ОписаниеСервисаИПП)
	
	ПараметрыКонструктора = Новый Массив;
	ПараметрыКонструктора.Добавить(ОписаниеСервисаИПП.ПараметрыПодключенияКПорту.Путь);
	HTTPЗапрос = Новый(Тип("HTTPЗапрос"), ПараметрыКонструктора);
	HTTPЗапрос.Заголовки["Content-Type"] = "text/xml;charset=UTF-8";
	
	HTTPЗапрос.УстановитьТелоИзСтроки(ТекстКонверта);
	
	Попытка
		HTTPОтвет = ОписаниеСервисаИПП.СоединениеПорта.ОтправитьДляОбработки(HTTPЗапрос);
	Исключение
		ТекстИсключения = НСтр("ru = 'Ошибка сетевого соединения при отправке запроса.'")
			+ Символы.ПС
			+ ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ВызватьИсключение ТекстИсключения;
	КонецПопытки;
	
	ТелоОтвета = HTTPОтвет.ПолучитьТелоКакСтроку();
	
	Возврат ТелоОтвета;
	
КонецФункции

// Чтение объекта или значения в ответном SOAP-конверте в
// соответствии с фабрикой типов XDTO веб-сервиса.
//
// Параметры:
//	ТелоОтвета - Строка - телов SOAP-конверта-ответа;
//	ОписаниеСервисаИПП - Структура - описание веб-сервиса ИПП,
//		см. НовыйОписаниеСервисаИПП();
//	ТипЗначения - ТипЗначенияXDTO, ТипОбъектаXDTO - тип читаемого значения;
//
// Возвращаемое значение:
//	ЗначениеXDTO, ОбъектXDTO - прочитанный ответ сервиса.
//
Функция ПрочитатьОтветВКонвертеSOAP(ТелоОтвета, ОписаниеСервисаИПП, ТипЗначения)
	
	ЧтениеОтвета = Новый ЧтениеXML;
	ЧтениеОтвета.УстановитьСтроку(ТелоОтвета);
	
	URISOAP = "http://schemas.xmlsoap.org/soap/envelope/";
	
	Попытка
		
		// Переход к телу ответа
		Пока НЕ (НРег(ЧтениеОтвета.ЛокальноеИмя) = "body"
			И ЧтениеОтвета.URIПространстваИмен = URISOAP) Цикл
			Если НЕ ЧтениеОтвета.Прочитать() Тогда
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		// Переход к описанию объекта ответа
		ЧтениеОтвета.Прочитать();
		
	Исключение
		
		ТекстИсключения = НСтр("ru = 'Ошибка чтения ответа SOAP:'")
			+ " " + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке())
			+ Символы.ПС
			+ НСтр("ru = 'Тело ответа:'")
			+ Символы.ПС
			+ ТелоОтвета;
		
		ВызватьИсключение ТекстИсключения;
		
	КонецПопытки;
	
	Если ЧтениеОтвета.ТипУзла = ТипУзлаXML.НачалоЭлемента
		И ВРег(ЧтениеОтвета.ЛокальноеИмя) = "FAULT"
		И ЧтениеОтвета.URIПространстваИмен = URISOAP Тогда
		
		// Это исключение веб-сервиса
		Попытка
			ДеталиИсключения = ПрочитатьОписаниеИсключенияСервиса(ЧтениеОтвета);
		Исключение
			
			ТекстИсключения = НСтр("ru = 'Ошибка чтения ответа SOAP:'")
				+ " " + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке())
				+ Символы.ПС
				+ НСтр("ru = 'Тело ответа:'")
				+ Символы.ПС
				+ ТелоОтвета;
			
			ВызватьИсключение ТекстИсключения;
			
		КонецПопытки;
		
		ТекстИсключения = НСтр("ru = 'Ошибка SOAP-Сервера при обработке запроса:'")
			+ " " + ОписаниеИсключенияSOAPВСтроку(ДеталиИсключения);
		
		ВызватьИсключение ТекстИсключения;
		
	КонецЕсли;
	
	Попытка
		Значение = ОписаниеСервисаИПП.ФабрикаXDTO.ПрочитатьXML(ЧтениеОтвета, ТипЗначения);
	Исключение
		
		ТекстИсключения = СтрЗаменить(НСтр("ru = 'Ошибка чтения объекта (%1) в конверте SOAP:'"),
				"%1",
				Строка(ТипЗначения))
			+ " " + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке())
			+ Символы.ПС
			+ НСтр("ru = 'Тело ответа:'")
			+ Символы.ПС
			+ ТелоОтвета;
			
		ВызватьИсключение ТекстИсключения;
		
	КонецПопытки;
	
	Возврат Значение;
	
КонецФункции

// Если в ответном SOAP-конверте содержится описание ошибки,
// то выполняется чтение описания ошибки.
//
// Параметры:
//	ЧтениеОтвета - ЧтениеXML - объект, используемый для чтения
//		ответного SOAP-конверта. На момент вызова спозиционирован на описании
//		исключения SOAP;
//
// Возвращаемое значение:
// Структура - описание исключения SOAP-сервера:
//	* FaultCode - Строка - код ошибки;
//	* FaultString - Строка - строковое описание ошибки;
//	* FaultActor - Строка - источник ошибки;
//
Функция ПрочитатьОписаниеИсключенияСервиса(ЧтениеОтвета)
	
	ОписаниеИсключения = Новый Структура("FaultCode, FaultString, FaultActor", "", "", "");
	
	URISOAP = "http://schemas.xmlsoap.org/soap/envelope/";
	
	Пока НЕ (ВРег(ЧтениеОтвета.ЛокальноеИмя) = "BODY"
		И ЧтениеОтвета.URIПространстваИмен = URISOAP
		И ЧтениеОтвета.ТипУзла = ТипУзлаXML.КонецЭлемента) Цикл
		
		Если ЧтениеОтвета.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
			ИмяУзлаВРег = ВРег(ЧтениеОтвета.ЛокальноеИмя);
			
			Если ИмяУзлаВРег = "FAULTCODE"
				ИЛИ ИмяУзлаВРег = "FAULTSTRING"
				ИЛИ ИмяУзлаВРег = "FAULTACTOR" Тогда
				
				ЧтениеОтвета.Прочитать(); // Прочитать текст узла
				
				Если ЧтениеОтвета.ТипУзла = ТипУзлаXML.Текст Тогда
					ОписаниеИсключения[ИмяУзлаВРег] = ЧтениеОтвета.Значение;
				КонецЕсли;
				
				ЧтениеОтвета.Прочитать(); // Прочитать конец элемента
				
			КонецЕсли;
			
		КонецЕсли;
		
		Если НЕ ЧтениеОтвета.Прочитать() Тогда
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ОписаниеИсключения;
	
КонецФункции

// Преобразование структуры-описателя исключения SOAP
// в строку для пользовательского представления;
//
// Параметры:
//	ИсключениеSOAP - Структура - см. ПрочитатьОписаниеИсключенияСервиса();
//
// Возвращаемое значение:
//	Строка - пользовательское представление исключения SOAP;
//
Функция ОписаниеИсключенияSOAPВСтроку(ИсключениеSOAP)
	
	Результат = "";
	Если НЕ ПустаяСтрока(ИсключениеSOAP.FaultCode) Тогда
		Результат = ИсключениеSOAP.FaultCode;
	КонецЕсли;
	
	Если НЕ ПустаяСтрока(ИсключениеSOAP.FaultString) Тогда
		Результат = Результат
			+ ?(ПустаяСтрока(Результат), "", " - ")
			+ ИсключениеSOAP.FaultString;
	КонецЕсли;
	
	Если НЕ ПустаяСтрока(ИсключениеSOAP.FaultActor) Тогда
		Результат = Результат + ?(ПустаяСтрока(Результат), "", Символы.ПС + НСтр("ru = 'Источник ошибки:'") + " ")
			+ ИсключениеSOAP.FaultActor;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Процедуры и функции общего назначения для работы с Интернет

// Раскладывает URL веб-документа на необходимые параметры.
// Параметры:
//	URL (Строка) - URL веб-документа;
//
// Возвращаемое значение:
//	Структура со свойствами-значениями;
//
Функция НовыйПараметрыПолученияДокумента(URL)
	
	Результат    = Новый Структура;
	СтруктураURI = ОбщегоНазначенияКлиентСервер.СтруктураURI(URL);
	
	Результат.Вставить("Сервер", СтруктураURI.Хост);
	Результат.Вставить("Путь"  , СтруктураURI.ПутьНаСервере);
	
	Если ПустаяСтрока(СтруктураURI.Схема) Тогда
		Результат.Вставить("БезопасноеСоединение", Ложь);
	Иначе
		Результат.Вставить("БезопасноеСоединение", (ВРег(СтруктураURI.Схема) = "HTTPS"));
	КонецЕсли;
	
	Если СтруктураURI.Порт = Неопределено ИЛИ ПустаяСтрока(СтруктураURI.Порт) Тогда
		Результат.Вставить("Порт", ?(Результат.БезопасноеСоединение, 443, 80));
	Иначе
		Результат.Вставить("Порт", Число(СтруктураURI.Порт));
	КонецЕсли;
	
	Результат.Вставить("ИмяПользователя",
		?(ПустаяСтрока(СтруктураURI.Логин),
			Неопределено,
			СтруктураURI.Логин));
	
	Результат.Вставить("Пароль",
		?(ПустаяСтрока(СтруктураURI.Пароль),
			Неопределено,
			СтруктураURI.Пароль));
	
	Возврат Результат;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Процедуры и функции для преобразования команд сервиса во внутренние структуры

// Преобразует ответ операции process() сервиса ИПП в последовательность команд
// во внутреннем представлении.
//
// Параметры:
// ОсновныеПараметры - Структура - основные параметры контекста взаимодействия;
// ОтветСервера - ОбъектXDTO - ответ сервиса, возвращенный операцией process();
// КонтекстОбработчика - - Структура - контекст клиент-серверного обработчика
//		команд (см. функцию НовыйКонтекстОбработчикаКоманд());
//
// Возвращаемое значение:
//	Массив - массив команд сервиса во внутреннем представлении.
//
Функция СтруктурироватьОтветСервера(
	ОсновныеПараметры,
	ОтветСервера,
	КонтекстОбработчика) Экспорт
	
	МассивОтвета = Новый Массив;
	
	Попытка
		
		Для каждого КомандаСервера Из ОтветСервера.command Цикл
			
			СтруктураКоманды = Неопределено;
			
			ИмяТекКоманды = НРег(СокрЛП(КомандаСервера.name));
			
			Если ИмяТекКоманды = "ui.open" Тогда
				СтруктураКоманды = СтруктурироватьОткрытиеФормы(
					ОсновныеПараметры,
					КомандаСервера);
					
			ИначеЕсли ИмяТекКоманды = "store.put" Тогда
				СтруктураКоманды = СтруктурироватьЗаписьПараметров(КомандаСервера);
			
			ИначеЕсли ИмяТекКоманды = "store.get" Тогда
				СтруктураКоманды = СтруктурироватьЧтениеПараметров(КомандаСервера);
				
			ИначеЕсли ИмяТекКоманды = "store.delete" Тогда
				СтруктураКоманды = СтруктурироватьУдалениеПараметров(КомандаСервера);
				
			ИначеЕсли ИмяТекКоманды = "ui.close" Тогда
				СтруктураКоманды = СтруктурироватьЗакрытиеФормы(
				ОсновныеПараметры,
				КомандаСервера);
				
			ИначеЕсли ИмяТекКоманды = "system.halt" Тогда
				СтруктураКоманды = СтруктурироватьОстановкуМеханизма(КомандаСервера);
				
			ИначеЕсли ИмяТекКоманды = "launchservice" Тогда
				СтруктураКоманды = СтруктурироватьОтветСервераОПереходеБизнеспроцесса(КомандаСервера);
				
			ИначеЕсли ИмяТекКоманды = "message.show" ИЛИ ИмяТекКоманды = "question.show" Тогда
				СтруктураКоманды = СтруктурироватьСообщениеИлиВопросПользователю(КомандаСервера);
				
			ИначеЕсли ИмяТекКоманды = "input.field" Тогда
				СтруктураКоманды = СтруктурироватьВводДанных(КомандаСервера);
				
			ИначеЕсли ИмяТекКоманды = "store.putorganizations" Тогда
				СтруктураКоманды = СтруктурироватьЗаписьОрганизацийПользователя(КомандаСервера);
				
			ИначеЕсли ИмяТекКоманды = "store.putadressclassifier" Тогда
				СтруктураКоманды = СтруктурироватьЗаписьАдресногоКлассификатора(КомандаСервера);
				
			Иначе
				
				ДопОбработчик = ОбработчикБизнесПроцесса(
					ОсновныеПараметры.МестоЗапуска,
					"СтруктурироватьКомандуСервиса");
				Если ДопОбработчик <> Неопределено Тогда
					
					ДопОбработчик.СтруктурироватьКомандуСервиса(
						ИмяТекКоманды,
						КомандаСервера,
						СтруктураКоманды);
					
				КонецЕсли;
				
			КонецЕсли;
			
			Если СтруктураКоманды <> Неопределено Тогда
				
				Если НЕ СтруктураКоманды.Свойство("ИмяКоманды") Тогда
					СтруктураКоманды.Вставить("ИмяКоманды", ИмяТекКоманды);
				КонецЕсли;
				
				МассивОтвета.Добавить(СтруктураКоманды);
				
			КонецЕсли;
			
			Если КонтекстОбработчика.ПроизошлаОшибка Тогда
				Возврат Неопределено;
			КонецЕсли;
			
		КонецЦикла;
		
	Исключение
		
		КонтекстОбработчика.ПроизошлаОшибка = Истина;
		КонтекстОбработчика.ПолноеОписаниеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		КонтекстОбработчика.ДействияПриОшибкеДляСервера.Добавить("СоздатьЗаписьВЖурналеРегистрации");
		
		КонтекстОбработчика.ПользовательскоеОписаниеОшибки =
			НСтр("ru = 'Неизвестная ошибка. См. подробности в журнале регистрации.'");
		КонтекстОбработчика.ДействиеПриОшибкеДляКлиента = "ПоказатьСообщение";
		
		Возврат Неопределено;
		
	КонецПопытки;
	
	Если МассивОтвета.Количество() > 0 Тогда
		
		Для каждого СтруктураКоманды Из МассивОтвета Цикл
			СтруктураКоманды.ИмяКоманды = НРег(СокрЛП(СтруктураКоманды.ИмяКоманды));
		КонецЦикла;
		
		Возврат МассивОтвета;
		
	Иначе
		
		Возврат Неопределено;
		
	КонецЕсли;
	
КонецФункции

// Преобразование команд "Сообщение пользователю" и "Вопрос пользователю" во
// внутреннее представление.
//
Функция СтруктурироватьСообщениеИлиВопросПользователю(КомандаСервера) Экспорт
	
	СтруктураКоманды = Новый Структура;
	
	СписокКнопок = Новый СписокЗначений;
	ОписаниеТекКнопкиОтвета = Новый Структура;
	Для каждого Параметр Из КомандаСервера.parameters.parameter Цикл
		
		ИмяТекПараметра = НРег(СокрЛП(Параметр.name));
		
		Если ИмяТекПараметра = "caption" Тогда
			СтруктураКоманды.Вставить("Заголовок", Строка(Параметр.value));
			
		ИначеЕсли ИмяТекПараметра = "formmessage"
			ИЛИ ИмяТекПараметра = "messagetext" Тогда
			СтруктураКоманды.Вставить("ТекстСообщения", Строка(Параметр.value));
			
		ИначеЕсли ИмяТекПараметра = "messagetype" ИЛИ ИмяТекПараметра = "questiontype" Тогда
			СтруктураКоманды.Вставить("Тип", НРег(СокрЛП(Строка(Параметр.value))));
			
		ИначеЕсли ИмяТекПараметра = "button" Тогда
			СписокКнопок.Добавить(НРег(СокрЛП(Строка(Параметр.value))));
			
		ИначеЕсли ИмяТекПараметра = "buttonvalue" Тогда
			ОписаниеТекКнопкиОтвета.Вставить("ЗначениеКнопки", Строка(Параметр.value));
			
		ИначеЕсли ИмяТекПараметра = "buttontext" Тогда
			ОписаниеТекКнопкиОтвета.Вставить("ТекстКнопки", Строка(Параметр.value));
			
		КонецЕсли;
		
		Если ОписаниеТекКнопкиОтвета.Свойство("ЗначениеКнопки")
			И ОписаниеТекКнопкиОтвета.Свойство("ТекстКнопки") Тогда
			// Если получено описание очередной кнопки, тогда добавить ее в список кнопок
			СписокКнопок.Добавить(
				ОписаниеТекКнопкиОтвета.ЗначениеКнопки,
				ОписаниеТекКнопкиОтвета.ТекстКнопки);
			ОписаниеТекКнопкиОтвета = новый Структура;
		КонецЕсли;
		
	КонецЦикла;
	
	Если НЕ СтруктураКоманды.Свойство("Заголовок") ИЛИ ПустаяСтрока(СтруктураКоманды.Заголовок) Тогда
		СтруктураКоманды.Вставить("Заголовок", НСтр("ru = 'Интернет-поддержка пользователей'"));
	КонецЕсли;
	
	Если СписокКнопок.Количество() > 0 Тогда
		СтруктураКоманды.Вставить("Кнопки", СписокКнопок);
	КонецЕсли;
	
	Возврат СтруктураКоманды;
	
КонецФункции

// Преобразование команды "Ввод данных" во внутреннее представление.
//
Функция СтруктурироватьВводДанных(КомандаСервера) Экспорт
	
	СтруктураКоманды = Новый Структура;
	
	Если КомандаСервера.parameters = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	Для каждого Параметр Из КомандаСервера.parameters.parameter Цикл
		
		ИмяТекПараметра = НРег(СокрЛП(Параметр.name));
		
		Если ИмяТекПараметра = "caption" Тогда
			ПараметрыФормы.Вставить("ТекстЗаголовка", Строка(Параметр.value));
			
		ИначеЕсли ИмяТекПараметра = "explanationtext" Тогда
			ПараметрыФормы.Вставить("ПоясняющийТекст", Строка(Параметр.value));
			
		ИначеЕсли ИмяТекПараметра = "datatype" Тогда
			ПараметрыФормы.Вставить("ТипДанных", Строка(Параметр.value));
			
		ИначеЕсли ИмяТекПараметра = "precision" Тогда
			ПараметрыФормы.Вставить("ТочностьЧисла", Строка(Параметр.value));
			
		КонецЕсли;
		
	КонецЦикла;
	
	СтруктураКоманды.Вставить("ПараметрыФормы", ПараметрыФормы);
	
	Возврат СтруктураКоманды;
	
КонецФункции

// Преобразование команды "Записать параметры" во внутреннее представление.
//
Функция СтруктурироватьЗаписьПараметров(КомандаСервера) Экспорт
	
	СтруктураКоманды = Новый Структура;
	
	Если КомандаСервера.parameters.parameter.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	МассивПараметров = Новый Массив;
	
	Для каждого Параметр из КомандаСервера.parameters.parameter Цикл
		СтруктураПараметра = Новый Структура("Имя, БизнесПроцесс, Значение, ОбластьВидимости",
												СокрЛП(Параметр.name),
												СокрЛП(Параметр.bp),
												СокрЛП(Параметр.value),
												СокрЛП(Параметр.type));
		
		МассивПараметров.Добавить(СтруктураПараметра);
	КонецЦикла;
	
	СтруктураКоманды.Вставить("Параметры" , МассивПараметров);
	СтруктураКоманды.Вставить("ИмяКоманды", КомандаСервера.name);
	
	Возврат СтруктураКоманды;
	
КонецФункции

// Преобразование команды "Прочитать параметры" во внутреннее представление.
//
Функция СтруктурироватьЧтениеПараметров(КомандаСервера) Экспорт
	
	СтруктураКоманды = Новый Структура;
	
	Если КомандаСервера.parameters.parameter.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	МассивПараметров = Новый Массив;
	
	Для каждого Параметр из КомандаСервера.parameters.parameter Цикл
		
		Если НРег(СокрЛП(Параметр.type)) = "startup" Тогда
			СтруктураПараметра = Новый Структура("Имя, ОбластьВидимости",
				СокрЛП(Параметр.name),
				СокрЛП(Параметр.type));
		Иначе
			СтруктураПараметра = Новый Структура("Имя, БизнесПроцесс, ОбластьВидимости",
				СокрЛП(Параметр.name),
				СокрЛП(Параметр.bp),
				СокрЛП(Параметр.type));
		КонецЕсли;
		
		МассивПараметров.Добавить(СтруктураПараметра);
		
	КонецЦикла;
	
	СтруктураКоманды.Вставить("Параметры", 	МассивПараметров);
	СтруктураКоманды.Вставить("ИмяКоманды", КомандаСервера.name);
	
	Возврат СтруктураКоманды;
	
КонецФункции

// Преобразование команды "Удалить параметры" во внутреннее представление.
//
Функция СтруктурироватьУдалениеПараметров(КомандаСервера) Экспорт
	
	СтруктураКоманды = Новый Структура;
	
	Если КомандаСервера.parameters.parameter.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	МассивПараметров = Новый Массив;
	
	Для каждого Параметр из КомандаСервера.parameters.parameter Цикл
	
		СтруктураПараметра = Новый Структура("Имя, ОбластьВидимости",
			СокрЛП(Параметр.name),
			СокрЛП(Параметр.type));
		
		МассивПараметров.Добавить(СтруктураПараметра);
		
	КонецЦикла;
	
	СтруктураКоманды.Вставить("Параметры" , МассивПараметров);
	СтруктураКоманды.Вставить("ИмяКоманды", КомандаСервера.name);
	
	Возврат СтруктураКоманды;
	
КонецФункции

// Преобразование команды "Открыть форму" во внутреннее представление.
//
Функция СтруктурироватьОткрытиеФормы(ОсновныеПараметры, КомандаСервера) Экспорт
	
	СтруктураКоманды = Новый Структура;
	
	// Чтение общих параметров открытия формы
	Для каждого Параметр из КомандаСервера.parameters.parameter Цикл
		
		Если НРег(СокрЛП(Параметр.name)) = "indexform" Тогда
			ПараметрыФормы = ПараметрыВнутреннейФормы(
				СокрЛП(Параметр.value),
				ОсновныеПараметры.МестоЗапуска);
			
			Если ПараметрыФормы.Количество() = 0 Тогда
				Возврат Неопределено;
			КонецЕсли;
			
			СтруктураКоманды.Вставить("ПараметрыФормы", ПараметрыФормы);
		КонецЕсли;
		
		Если НРег(СокрЛП(Параметр.name)) = "caption" Тогда
			СтруктураКоманды.Вставить("Заголовок", СокрЛП(Параметр.value));
		КонецЕсли;
		
		Если НРег(СокрЛП(Параметр.name)) = "text" Тогда
			СтруктураКоманды.Вставить("Текст", СокрЛП(Параметр.value));
		КонецЕсли;
		
		Если НРег(СокрЛП(Параметр.name)) = "formmessage" Тогда
			СтруктураКоманды.Вставить("Текст", СокрЛП(Параметр.value));
		КонецЕсли;
		
		Если НРег(СокрЛП(Параметр.name)) = "url" Тогда
			СтруктураКоманды.Вставить("УРЛ", СокрЛП(Параметр.value));
		КонецЕсли;
		
	КонецЦикла;
	
	Если СтруктураКоманды.Количество() > 0 Тогда
		СтруктураКоманды.Вставить("ИмяКоманды", КомандаСервера.name); 
		Возврат СтруктураКоманды;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

// Преобразование команды "Закрыть форму" во внутреннее представление.
//
Функция СтруктурироватьЗакрытиеФормы(ОсновныеПараметры, КомандаСервера) Экспорт
	
	СтруктураКоманды = Новый Структура;
	
	Для каждого Параметр из КомандаСервера.parameters.parameter Цикл
		
		Если НРег(СокрЛП(Параметр.name)) = "indexform" Тогда
			ПараметрыФормы = ПараметрыВнутреннейФормы(
				СокрЛП(Параметр.value),
				ОсновныеПараметры.МестоЗапуска);
			
			Если ПараметрыФормы.Количество() = 0 Тогда 
				Возврат Неопределено;
			КонецЕсли;
			
			СтруктураКоманды.Вставить("ПараметрыФормы", ПараметрыФормы);
		КонецЕсли;
		
	КонецЦикла;
	
	Если СтруктураКоманды.Количество() > 0 Тогда 
		СтруктураКоманды.Вставить("ИмяКоманды", КомандаСервера.name);
		Возврат СтруктураКоманды;
	Иначе
		Возврат Неопределено;
	КонецЕсли;

КонецФункции

// Вспомогательная функция для формирования параметров внутренней формы при
// выполнении команды "Открыть внутреннюю форму" и "Закрыть внутреннюю форму".
//
Функция ПараметрыВнутреннейФормы(ИндексФормы, МестоЗапуска) Экспорт
	
	ПараметрыФормы = Новый Структура;
	
	Если ИндексФормы = "f2" Тогда
		ПараметрыФормы.Вставить("ИмяОткрываемойФормы",
			"Обработка.БазовыеФункцииИнтернетПоддержки.Форма.ИнтернетПоддержкаПродуктаНеОказывается");
		
	ИначеЕсли ИндексФормы = "f3" Тогда
		ПараметрыФормы.Вставить("ИмяОткрываемойФормы",
			"Обработка.БазовыеФункцииИнтернетПоддержки.Форма.ОтправкаЭлектронногоПисьма");
	
	ИначеЕсли ИндексФормы = "f4" ИЛИ ИндексФормы = "1" Тогда
		ПараметрыФормы.Вставить("ИмяОткрываемойФормы",
			"Обработка.БазовыеФункцииИнтернетПоддержки.Форма.ОбщаяАвторизация");
		
	ИначеЕсли ИндексФормы = "f5" ИЛИ ИндексФормы = "13" Тогда
		ПараметрыФормы.Вставить("ИмяОткрываемойФормы",
			"Обработка.БазовыеФункцииИнтернетПоддержки.Форма.ВосстановлениеПароля");
		
	ИначеЕсли ИндексФормы = "f6" ИЛИ ИндексФормы = "2" Тогда
		ПараметрыФормы.Вставить("ИмяОткрываемойФормы",
			"Обработка.БазовыеФункцииИнтернетПоддержки.Форма.ОбщаяРегНомер");
		
	ИначеЕсли ИндексФормы = "f7" Тогда
		ПараметрыФормы.Вставить("ИмяОткрываемойФормы",
			"Обработка.БазовыеФункцииИнтернетПоддержки.Форма.ОбщаяПинкод");
		
	ИначеЕсли ИндексФормы = "f9" ИЛИ ИндексФормы = "18" Тогда
		ПараметрыФормы.Вставить("ИмяОткрываемойФормы",
			"Обработка.БазовыеФункцииИнтернетПоддержки.Форма.РегистрацияНовогоПользователя");
		
	ИначеЕсли ИндексФормы = "f10" ИЛИ ИндексФормы = "19" Тогда
		ПараметрыФормы.Вставить("ИмяОткрываемойФормы",
			"Обработка.БазовыеФункцииИнтернетПоддержки.Форма.ДополнительнаяИнформация");
		
	ИначеЕсли ИндексФормы = "f11" Тогда
		ПараметрыФормы.Вставить("ИмяОткрываемойФормы",
			"Обработка.БазовыеФункцииИнтернетПоддержки.Форма.АвторизацияУпрощенная");
		
	ИначеЕсли ИндексФормы = "c20" Тогда
		ПараметрыФормы.Вставить("ИмяОткрываемойФормы",
			"Обработка.БазовыеФункцииИнтернетПоддержки.Форма.ДействиеНеПоддерживается");
		
	ИначеЕсли ИндексФормы = "bh1" Тогда
		ПараметрыФормы.Вставить("Заголовок", НСтр("ru = 'Интернет-поддержка пользователей'"));
		ПараметрыФормы.Вставить("ИмяОткрываемойФормы", "ВсплывающаяПодсказка");
		ПараметрыФормы.Вставить("УспешноеЗавершениеБизнесПроцесса", Истина);
		
	Иначе
		
		ДопОбработчик = ОбработчикБизнесПроцесса(МестоЗапуска, "ЗаполнитьПараметрыВнутреннейФормы");
		
		Если ДопОбработчик <> Неопределено Тогда
			ДопОбработчик.ЗаполнитьПараметрыВнутреннейФормы(ИндексФормы, ПараметрыФормы);
		КонецЕсли;
		
		Если НЕ ПараметрыФормы.Свойство("ИмяОткрываемойФормы") Тогда
			ПараметрыФормы.Вставить("ИмяОткрываемойФормы", Неопределено);
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ПараметрыФормы;
	
КонецФункции

// Преобразование команды "Остановить механизм" во внутреннее представление.
//
Функция СтруктурироватьОстановкуМеханизма(КомандаСервера) Экспорт
	
	СтруктураКоманды = Новый Структура;
	
	МассивПараметров = Новый Массив;
	Попытка
		
		Если КомандаСервера.parameters <> Неопределено
			И КомандаСервера.parameters.parameter.Количество() > 0 Тогда
			
			Для каждого Параметр из КомандаСервера.parameters.parameter Цикл 
				
				СтруктураПараметра = Неопределено;
				
				Если НРег(СокрЛП(Параметр.name)) = "errorcode" Тогда 
					СтруктураПараметра = Новый Структура("errorCode", СокрЛП(Параметр.value));
					МассивПараметров.Добавить(СтруктураПараметра);
				КонецЕсли;
				
				Если СтруктураПараметра <> Неопределено Тогда
					МассивПараметров.Добавить(СтруктураПараметра);
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;
		
	Исключение
	КонецПопытки;
	
	СтруктураКоманды.Вставить("Параметры" , МассивПараметров);
	СтруктураКоманды.Вставить("ИмяКоманды", КомандаСервера.name);
	
	Возврат СтруктураКоманды;
	
КонецФункции

// Преобразование команды "Изменить бизнес-процесс" во внутреннее представление.
//
Функция СтруктурироватьОтветСервераОПереходеБизнеспроцесса(КомандаСервера) Экспорт
	
	СтруктураКоманды = Новый Структура;
	
	Если КомандаСервера.parameters.parameter.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	МассивПараметров = Новый Массив;
	
	Для каждого Параметр из КомандаСервера.parameters.parameter Цикл
		
		СтруктураПараметра = Новый Структура(Параметр.name, Параметр.value);
		МассивПараметров.Добавить(СтруктураПараметра);
		
	КонецЦикла;
	
	СтруктураКоманды.Вставить("Параметры" , МассивПараметров);
	СтруктураКоманды.Вставить("ИмяКоманды", КомандаСервера.name); 
	
	Возврат СтруктураКоманды;
	
КонецФункции

// Преобразование команды "Записать адресный классификатор" во
// внутреннее представление.
//
Функция СтруктурироватьЗаписьАдресногоКлассификатора(КомандаСервера) Экспорт
	
	Если КомандаСервера.parameters = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	СтруктураКоманды = Новый Структура;
	
	СписокСтран  = Новый СписокЗначений;
	РегионыСтран = Новый Соответствие;
	
	ПараметрыКоманды = КомандаСервера.parameters.parameter;
	
	Если ПараметрыКоманды.Количество() > 0 Тогда
		ПараметрыСписокСтран = ПараметрыКоманды[0].parameters.parameter;
	Иначе
		ПараметрыСписокСтран = Неопределено;
	КонецЕсли;
	
	Если ПараметрыСписокСтран <> Неопределено Тогда
		
		Для каждого Параметр Из ПараметрыСписокСтран Цикл
			
			ИмяТекПараметра = НРег(СокрЛП(Параметр.name));
			Если ИмяТекПараметра = "country" Тогда
				
				НазваниеСтраны       = Строка(Параметр.value);
				ВложенныеПараметры   = Параметр.parameters.parameter;
				ИдентификаторСтраны  = Неопределено;
				СписокРегионовСтраны = Новый СписокЗначений;
				
				Если ВложенныеПараметры = Неопределено Тогда
					Продолжить;
				КонецЕсли;
				
				Для каждого ВложенныйПараметр Из ВложенныеПараметры Цикл
					
					ИмяТекВлПараметра = НРег(СокрЛП(ВложенныйПараметр.name));
					Если ИмяТекВлПараметра = "id" Тогда
						
						ИдентификаторСтраны = Строка(ВложенныйПараметр.value);
						
					ИначеЕсли ИмяТекВлПараметра = "region" Тогда
						
						НазваниеРегиона            = Строка(ВложенныйПараметр.value);
						ВложенныеПараметрыРегионов = ВложенныйПараметр.parameters.parameter;
						ИдентификаторРегиона       = Неопределено;
						
						Если ВложенныеПараметрыРегионов = Неопределено Тогда
							Продолжить;
						КонецЕсли;
						
						Для каждого ВлПараметрРегиона Из ВложенныеПараметрыРегионов Цикл
							ИмяТекВлПараметраРегиона = НРег(СокрЛП(ВлПараметрРегиона.name));
							Если ИмяТекВлПараметраРегиона = "id" Тогда
								ИдентификаторРегиона = Строка(ВлПараметрРегиона.value);
								Прервать;
							КонецЕсли;
						КонецЦикла;
						
						Если ИдентификаторРегиона <> Неопределено Тогда
							СписокРегионовСтраны.Добавить(ИдентификаторРегиона, НазваниеРегиона);
						КонецЕсли;
						
					КонецЕсли;
					
				КонецЦикла;
				
				Если ИдентификаторСтраны <> Неопределено Тогда
					СписокСтран.Добавить(ИдентификаторСтраны, НазваниеСтраны);
					СписокРегионовСтраны.Вставить(0, "-1", НСтр("ru = '<не выбран>'"));
					РегионыСтран[ИдентификаторСтраны] = СписокРегионовСтраны;
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	СписокСтран.Вставить(0, "-1", НСтр("ru = '<не выбрана>'"));
	
	СтруктураКоманды.Вставить("Страны"      , СписокСтран);
	СтруктураКоманды.Вставить("РегионыСтран", РегионыСтран);
	
	Возврат СтруктураКоманды;
	
КонецФункции

// Преобразование команды "Записать данные организаций" во внутреннее
// представление.
//
Функция СтруктурироватьЗаписьОрганизацийПользователя(КомандаСервера) Экспорт
	
	Если КомандаСервера.parameters = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	СтруктураКоманды = Новый Структура;
	
	СписокОрганизаций = Новый СписокЗначений;
	ДанныеОрганизаций = Новый Соответствие;
	ПараметрыКоманды  = КомандаСервера.parameters.parameter;
	
	ПараметрыСписокОрганизаций = Неопределено;
	Если ПараметрыКоманды.Количество() > 0 Тогда
		ВложенныеПараметрыОбъект = ПараметрыКоманды[0].parameters;
		Если ВложенныеПараметрыОбъект <> Неопределено Тогда
			ПараметрыСписокОрганизаций = ВложенныеПараметрыОбъект.parameter;
		КонецЕсли;
	КонецЕсли;
	
	Если ПараметрыСписокОрганизаций <> Неопределено Тогда
		
		Для каждого Параметр Из ПараметрыСписокОрганизаций Цикл
			
			ИмяТекПараметра = НРег(СокрЛП(Параметр.name));
			Если ИмяТекПараметра = "organization" Тогда
				
				НазваниеОрганизации      = Строка(Параметр.value);
				ИдентификаторОрганизации = Неопределено;
				ДанныеТекОрганизации     = Новый Структура;
				
				Если Параметр.parameters = Неопределено Тогда
					Продолжить;
				КонецЕсли;
				
				ВложенныеПараметры = Параметр.parameters.parameter;
				
				Для каждого ВложенныйПараметр Из ВложенныеПараметры Цикл
					
					ИмяТекВлПараметра = НРег(СокрЛП(ВложенныйПараметр.name));
					Если ИмяТекВлПараметра = "id" Тогда
						ИдентификаторОрганизации = Строка(ВложенныйПараметр.value);
					Иначе
						ДанныеТекОрганизации.Вставить(ИмяТекВлПараметра, Строка(ВложенныйПараметр.value));
					КонецЕсли;
					
				КонецЦикла;
				
				Если ИдентификаторОрганизации <> Неопределено Тогда
					СписокОрганизаций.Добавить(ИдентификаторОрганизации, НазваниеОрганизации);
					ДанныеТекОрганизации.Вставить("НазваниеОрганизации", НазваниеОрганизации);
					ДанныеОрганизаций[ИдентификаторОрганизации] = ДанныеТекОрганизации;
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	СписокОрганизаций.Вставить(0, "-1", НСтр("ru = '<добавить новую организацию>'"));
	
	СтруктураКоманды.Вставить("СписокОрганизаций", СписокОрганизаций);
	СтруктураКоманды.Вставить("ДанныеОрганизаций", ДанныеОрганизаций);
	
	Возврат СтруктураКоманды;
	
КонецФункции

#КонецЕсли

////////////////////////////////////////////////////////////////////////////////
// Процедуры и функции для обработки команд сервиса

// Выполнение команды "Записать параметры".
//
Процедура ВыполнениеКомандыЗаписьПараметров(КСКонтекст, СтруктураКоманды, КонтекстОбработчика) Экспорт
	
	Если ТипЗнч(СтруктураКоманды) = Тип("Структура") Тогда
		ЗаписываемыеПараметры = СтруктураКоманды.Параметры;
	Иначе
		ЗаписываемыеПараметры = СтруктураКоманды;
	КонецЕсли;
	
	ОбщиеСтартовыеПараметры = Новый Соответствие; // Параметры, записываемые в РС
	Для каждого Параметр Из ЗаписываемыеПараметры Цикл
		
		// Сохранение параметров как сессионных
		ЗаписатьПараметрКонтекста(
			КСКонтекст,
			Параметр.Имя,
			Параметр.Значение,
			Параметр.ОбластьВидимости,
			ОбщиеСтартовыеПараметры);
		
	КонецЦикла;
	
	Если ОбщиеСтартовыеПараметры.Количество() > 0 Тогда
		ИнтернетПоддержкаПользователейВызовСервера.ЗаписатьОбщиеСтартовыеПараметрыВРСИПП(ОбщиеСтартовыеПараметры);
		ЛогинАвторизованногоПользователя = ОбщиеСтартовыеПараметры.Получить("login");
		Если ЛогинАвторизованногоПользователя <> Неопределено Тогда
			КонтекстОбработчика.ДопСвойства.Вставить("ИзмененЛогинПользователя",
				ЛогинАвторизованногоПользователя);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Выполнение команды "Прочитать параметры". Выполняется чтение параметров
// и отправка их сервису посредством вызова метода process().
//
Процедура ВыполнениеКомандыПрочитатьПараметры(
	КонтекстВзаимодействия,
	СтруктураКоманды,
	КонтекстОбработчика,
	ВыполнениеНаСервере) Экспорт
	
	МассивПараметров = СтруктураКоманды.Параметры;
	ПараметрыЗапроса = Новый Массив;
	
	КСКонтекст        = КонтекстВзаимодействия.КСКонтекст;
	ОсновныеПараметры = КСКонтекст.ОсновныеПараметры;
	
	Для каждого Параметр Из МассивПараметров Цикл
		
		Если Параметр.Имя = "session_id" Тогда
			НомерСессииУжеЕсть = Истина;
		КонецЕсли;
		
		ЗначениеПараметра = ЗначениеСессионногоПараметра(КСКонтекст, Параметр.Имя);
		
		ПередаваемыйПапаметр = Новый Структура("Имя, Значение, БизнесПроцесс, ОбластьВидимости",
			Параметр.Имя,
			ЗначениеПараметра,
			ОсновныеПараметры.ИмяWSОпределения,
			Параметр.ОбластьВидимости);
		
		ПараметрыЗапроса.Добавить(ПередаваемыйПапаметр);
		
	КонецЦикла;
	
	ДобавитьСессионныеПараметрыКЗапросу(КСКонтекст, , ПараметрыЗапроса);
	
	Если ВыполнениеНаСервере Тогда
		ИнтернетПоддержкаПользователейВызовСервера.ДобавитьКомандыСервиса(
			ОсновныеПараметры,
			ПараметрыЗапроса,
			КонтекстОбработчика);
	Иначе
		#Если Клиент Тогда
		// Обратиться к веб-сервису за командами, и записать их в стек команд
		ИнтернетПоддержкаПользователейКлиент.ДобавитьКомандыСервиса(
			КонтекстВзаимодействия,
			ПараметрыЗапроса,
			КонтекстОбработчика);
		#КонецЕсли
	КонецЕсли;
	
КонецПроцедуры

// Выполнение команды "Записать адресный классификатор".
//
Процедура ЗаписатьАдресныйКлассификатор(КСКонтекст, СтруктураКоманды) Экспорт
	
	КСКонтекст.КонтекстРегистрации.Вставить("Страны"      , СтруктураКоманды.Страны);
	КСКонтекст.КонтекстРегистрации.Вставить("РегионыСтран", СтруктураКоманды.РегионыСтран);
	
КонецПроцедуры

// Выполнение команды "Записать данные организаций".
//
Процедура ЗаписатьСписокОрганизаций(КСКонтекст, СтруктураКоманды) Экспорт
	
	КСКонтекст.КонтекстРегистрации = Новый Структура;
	КСКонтекст.КонтекстРегистрации.Вставить("СписокОрганизаций", СтруктураКоманды.СписокОрганизаций);
	КСКонтекст.КонтекстРегистрации.Вставить("ДанныеОрганизаций", СтруктураКоманды.ДанныеОрганизаций);
	
КонецПроцедуры

// Возвращает тип команды - клиентская или серверная.
//
// Параметры:
// СтруктураКоманды - Структура - сервиса ИПП во внутреннем представлении.
//
// Возвращаемое значение:
// Число - тип команды: -1 - неизвестный тип команды, 0 - выполняется на
//		сервере, 1 - выполняется на клиенте.
//
Функция ТипКоманды(СтруктураКоманды, ОбращениеССервера, МестоЗапуска) Экспорт
	
	Если СтруктураКоманды = Неопределено Тогда
		Возврат -1; // Неизвестный тип команды
	КонецЕсли;
	
	ИмяКоманды = НРег(СокрЛП(СтруктураКоманды.ИмяКоманды));
	
	Если ИмяКоманды = "store.put"
		ИЛИ ИмяКоманды = "store.get"
		ИЛИ ИмяКоманды = "store.delete"
		ИЛИ ИмяКоманды = "launchservice"
		ИЛИ ИмяКоманды = "store.putorganizations"
		ИЛИ ИмяКоманды = "store.putadressclassifier" Тогда
		
		Возврат ?(ОбращениеССервера, 0, 1);
		
	ИначеЕсли СтруктураКоманды.ИмяКоманды = "ui.open"
		ИЛИ ИмяКоманды = "ui.close"
		ИЛИ ИмяКоманды = "performtheaction.decode"
		ИЛИ ИмяКоманды = "message.show"
		ИЛИ ИмяКоманды = "question.show"
		ИЛИ ИмяКоманды = "input.field"
		ИЛИ ИмяКоманды = "system.halt" Тогда
		
		Возврат 1;
		
	Иначе
		
		Результат = -1;
		Обработчик = ОбработчикБизнесПроцесса(МестоЗапуска, "КонтекстВыполненияКоманды");
		Если Обработчик <> Неопределено Тогда
			Обработчик.КонтекстВыполненияКоманды(ИмяКоманды, ОбращениеССервера, Результат);
		КонецЕсли;
		
		Возврат Результат;
		
	КонецЕсли;
	
КонецФункции

// Функция создает и возвращает описатель контекста обработчика команд сервиса.
//
// Возвращаемое значение:
// Структура - структура со свойствами:
//	* Команды - Массив - стек команд сервиса во внутреннем представлении;
//	* ВыполнитьОстановку - Булево - если Истина, то требуется выполнить
//		остановку механизма ИПП;
//	* ПроизошлаОшибка - Булево - в процессе цикла обращения к веб-сервису ИПП
//		произошла ошибка;
//	* ПолноеОписаниеОшибки - Строка - полное описание ошибки для журнала
//		регистрации;
//	* ПользовательскоеОписаниеОшибки - Строка - представление ошибки
//		для пользователя;
//	* ДействияПриОшибкеДляСервера - Массив - массив элементов типа Строка -
//		имена действий, которые необходимо выполнить на сервере 1С:Предприятия;
//	ДействиеПриОшибкеДляКлиента - Строка - действие, которое нужно выполнить на
//		стороне клиента 1С:Предприятия при возникновении ошибки;
//	* ДопСвойства - Структура - структура с дополнительными данными обработчика
//		команд;
//
Функция НовыйКонтекстОбработчикаКоманд() Экспорт
	
	КонтекстОбработчика = Новый Структура;
	КонтекстОбработчика.Вставить("Команды"                       , Новый Массив);
	КонтекстОбработчика.Вставить("ВыполнитьОстановку"            , Ложь);
	КонтекстОбработчика.Вставить("ПроизошлаОшибка"               , Ложь);
	КонтекстОбработчика.Вставить("ПолноеОписаниеОшибки"          , "");
	КонтекстОбработчика.Вставить("ПользовательскоеОписаниеОшибки", "");
	КонтекстОбработчика.Вставить("ДействияПриОшибкеДляСервера"   , Новый Массив);
	КонтекстОбработчика.Вставить("ДействиеПриОшибкеДляКлиента"   , "");
	КонтекстОбработчика.Вставить("ПротоколОбмена"                , "");
	КонтекстОбработчика.Вставить("ДопСвойства"                   , Новый Структура);
	
	Возврат КонтекстОбработчика;
	
КонецФункции

#Если Не ВебКлиент Тогда

// Выполняет вызов операции process() сервиса ИПП. При вызове передаются
// необходимые параметры запроса.
//
// Параметры:
// ОпределениеWS - Структура - см. функцию НовыйОписаниеСервисаИПП().
// ПередаваемыеПараметрыЗапроса - Массив - массив элементов типа Структура:
//	* Значение - Строка, ДвоичныеДанные, Неопределено - значение параметра;
//	* ОбластьВидимости - Строка - область видимости параметра;
//	* БизнесПроцесс - Строка - имя бизнес-процесса;
// КонтекстОбработчика - Структура - см. функцию НовыйКонтекстОбработчикаКоманд();
// ОсновныеПараметры - Структура - основные параметры контекста взаимодействия;
//
Процедура ДобавитьКомандыСервиса(
	ОпределениеWS,
	ПередаваемыеПараметрыЗапроса,
	КонтекстОбработчика,
	ОсновныеПараметры) Экспорт
	
	URIСервиса = ОпределениеWS.URIСервиса;
	
	ТипЗапроса       = ОпределениеWS.ФабрикаXDTO.Тип(URIСервиса, "Parameters");
	ПараметрыЗапроса = ОпределениеWS.ФабрикаXDTO.Создать(ТипЗапроса);
	
	ТипОтвета        = ОпределениеWS.ФабрикаXDTO.Тип(URIСервиса, "ProcessResponseType");
	ОтветСервера     = ОпределениеWS.ФабрикаXDTO.Создать(ТипОтвета);
	
	ТипПараметр = ОпределениеWS.ФабрикаXDTO.Тип(URIСервиса, "Parameter");
	
	// Добавление параметров запроса
	Если ПередаваемыеПараметрыЗапроса <> Неопределено Тогда
		
		ИндексПараметра = 0;
		ТипДвоичныеДанные = Тип("ДвоичныеДанные");
		Для каждого ПередаваемыйПараметр Из ПередаваемыеПараметрыЗапроса Цикл
			
			Если ТипЗнч(ПередаваемыйПараметр.Значение) <> ТипДвоичныеДанные Тогда
				ЗначениеПараметра = СокрЛП(Строка(ПередаваемыйПараметр.Значение));
			Иначе
				Попытка
					ЗначениеПараметра = ТекстВДвоичныхДанных(ПередаваемыйПараметр.Значение);
				Исключение
					ИнфОшибка = ИнформацияОбОшибке();
					ТекстИсключения = СтрЗаменить(НСтр("ru = 'Ошибка при преобразовании передаваемых данных. %1'"),
						"%1",
						ПодробноеПредставлениеОшибки(ИнфОшибка));
					ВызватьИсключение ТекстИсключения;
				КонецПопытки;
			КонецЕсли;
			
			// Определение объекта параметра (Объект XDTO).
			Параметр = ОпределениеWS.ФабрикаXDTO.Создать(ТипПараметр);
			
			Параметр.name  = СокрЛП(ПередаваемыйПараметр.Имя);
			Параметр.value = ЗначениеПараметра;
			Параметр.index = ИндексПараметра;
			
			БизнесПроцесс = Неопределено;
			ПередаваемыйПараметр.Свойство("БизнесПроцесс", БизнесПроцесс);
			Если БизнесПроцесс <> Неопределено Тогда 
				Параметр.bp = СокрЛП(БизнесПроцесс);
			КонецЕсли;
			
			Если ПередаваемыйПараметр.Свойство("ВложенныеПараметры")
				И ТипЗнч(ПередаваемыйПараметр.ВложенныеПараметры) = Тип("Массив") Тогда
				ДобавитьВложенныеПараметры(
					Параметр,
					ПередаваемыйПараметр.ВложенныеПараметры,
					ОпределениеWS,
					ТипПараметр,
					ТипЗапроса);
			КонецЕсли;
			
			ПараметрыЗапроса.parameter.Добавить(Параметр);
			
			ИндексПараметра = ИндексПараметра + 1;
			
		КонецЦикла;
		
	КонецЕсли;
	
	ОтветСервера = Неопределено;
	
	// Выполнение метода "process" WEB-Сервиса.
	ОтветСервера = СервисИПП_process(ПараметрыЗапроса, ОпределениеWS);
	
	// Если контекста нет, то ничего не структурировать, т.к. выполнение команд не последует
	// в связи с тем, что обратная связь не требуется (используется, например, для закрытия
	// бизнес-процесса, чтобы освободить ресурсы на сервере).
	Если КонтекстОбработчика = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	// Преобразование ответа сервера из объекта XDTO в массив структур
	МассивСтруктурыКоманд = СтруктурироватьОтветСервера(
		ОсновныеПараметры,
		ОтветСервера,
		КонтекстОбработчика);
	
	Если КонтекстОбработчика.ПроизошлаОшибка Тогда
		Возврат;
	КонецЕсли;
	
	Если МассивСтруктурыКоманд = Неопределено ИЛИ МассивСтруктурыКоманд.Количество() = 0 Тогда
		ВызватьИсключение НСтр("ru = 'Пустой ответ сервера.'");
	КонецЕсли;
	
	// Вставка команд в начало стека команд
	КоличествоКомандСервера = МассивСтруктурыКоманд.Количество();
	Для ОбратныйИндекс = 1 По КоличествоКомандСервера Цикл
		КонтекстОбработчика.Команды.Вставить(0, МассивСтруктурыКоманд[КоличествоКомандСервера - ОбратныйИндекс]);
	КонецЦикла;
	
КонецПроцедуры

#КонецЕсли

////////////////////////////////////////////////////////////////////////////////
// Процедуры и функции для работы с параметрами в контексте взаимодействия

// Создает описание параметра ИПП.
// Параметры:
// Имя - Строка - имя параметра;
// Значение - Строка - значение параметра;
// ОбластьВидимости - Строка - область видимости параметра (сессионный
//		или стартовый);
//
// Возвращаемое значение:
// Структура - описание параметра с полями:
//	* Имя - Строка - имя параметра;
//	* Значение - Строка - значение параметра;
//	* ОбластьВидимости - Строка - область видимости параметра;
//
Функция НовыйПараметрИПП(Имя, Значение, ОбластьВидимости) Экспорт
	
	Возврат Новый Структура("Имя, Значение, ОбластьВидимости",
		Имя,
		Значение,
		ОбластьВидимости);
	
КонецФункции

// Запись сессионного или стартового параметра. Стартовые параметры записываются
// также регистр сведений ПараметрыИнтернетПоддержкиПользователей.
//
Процедура ЗаписатьПараметрКонтекста(
	КСКонтекст,
	ИмяПараметра,
	ЗначениеПараметра,
	ОбластьВидимости = "",
	ОбщиеСтартовыеПараметры = Неопределено) Экспорт
	
	ЭтоСтартовыйПараметр = (НРег(ОбластьВидимости) = "startup");
	СессионныеПараметры = КСКонтекст.СессионныеПараметры;
	
	Параметр = СессионныеПараметры.Получить(ИмяПараметра);
	
	ОбластьВидимостиЗаписываемого = ?(ЭтоСтартовыйПараметр, "startUp", "");
	
	Если Параметр <> Неопределено Тогда
		Параметр.Значение = ЗначениеПараметра;
		Параметр.ОбластьВидимости = ОбластьВидимостиЗаписываемого;
	Иначе
		Параметр = НовыйПараметрИПП(ИмяПараметра, ЗначениеПараметра, ОбластьВидимостиЗаписываемого);
		СессионныеПараметры.Вставить(ИмяПараметра, Параметр);
	КонецЕсли;
	
	// Если параметр является стартовым, то он сохраняется для
	// всех пользователей в регистре сведений
	Если ЭтоСтартовыйПараметр И ОбщиеСтартовыеПараметры <> Неопределено Тогда
		ОбщиеСтартовыеПараметры.Вставить(ИмяПараметра, ЗначениеПараметра);
	КонецЕсли;
	
КонецПроцедуры

// Удаление параметров контекста. Стартовые параметры удаляются также из
// регистра сведений ПараметрыИнтернетПоддержкиПользователей.
//
Процедура УдалитьПараметрыКонтекста(КСКонтекст, СтруктураКоманды, КонтекстОбработчика) Экспорт
	
	СессионныеПараметры = КСКонтекст.СессионныеПараметры;
	
	Если ТипЗнч(СтруктураКоманды) = Тип("Структура") Тогда
		Если СтруктураКоманды.Свойство("Параметры") Тогда
			МассивПараметров = СтруктураКоманды.Параметры;
		Иначе
			МассивПараметров = Новый Массив;
			МассивПараметров.Добавить(СтруктураКоманды);
		КонецЕсли;
	ИначеЕсли ТипЗнч(СтруктураКоманды) = Тип("Массив") Тогда
		МассивПараметров = СтруктураКоманды;
	Иначе
		Возврат;
	КонецЕсли;
	
	УдаляемыеИзРС = Новый Соответствие;
	
	Для каждого ПараметрКоманды Из МассивПараметров Цикл
		
		Если ПараметрКоманды = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		СессионныеПараметры.Удалить(ПараметрКоманды.Имя);
		
		// Если параметр стартовый, то он удаляется из регистра параметров ИПП
		Если ПараметрКоманды.Свойство("ОбластьВидимости") И НРег(ПараметрКоманды.ОбластьВидимости) = "startup" Тогда
			УдаляемыеИзРС.Вставить(ПараметрКоманды.Имя, Истина);
		КонецЕсли;
		
	КонецЦикла;
	
	Если УдаляемыеИзРС.Количество() > 0 Тогда
		
		Если УдаляемыеИзРС.Получить("login") = Истина Тогда
			КонтекстОбработчика.ДопСвойства.Вставить("ИзмененЛогинПользователя", "");
		КонецЕсли;
		
		ИнтернетПоддержкаПользователейВызовСервера.УдалитьПараметрыИзРегистра(УдаляемыеИзРС);
		
	КонецЕсли;
	
КонецПроцедуры

// Возвращает значение параметра контекста.
//
Функция ЗначениеСессионногоПараметра(КСКонтекст, ИмяПараметра) Экспорт
	
	СессионныеПараметры = КСКонтекст.СессионныеПараметры;
	ОсновныеПараметры   = КСКонтекст.ОсновныеПараметры;
	
	ЗначениеПараметра = Неопределено;
	
	Если ИмяПараметра = "libraryVersion" Тогда
		ЗначениеПараметра = ВерсияБиблиотеки();
		
	ИначеЕсли ИмяПараметра = "APIVersion" Тогда
		ЗначениеПараметра = ВерсияAPIВзаимодействия();
		
	ИначеЕсли ИмяПараметра = "versionPlatform" Тогда
		СистИнфо = Новый СистемнаяИнформация;
		ЗначениеПараметра = СистИнфо.ВерсияПриложения;
		
	ИначеЕсли ИмяПараметра = "nameConfiguration" Тогда
		ЗначениеПараметра = ИнтернетПоддержкаПользователейКлиентСервер.ИмяКонфигурации();
		
	ИначеЕсли ИмяПараметра = "versionConfiguration" Тогда
		ЗначениеПараметра = ИнтернетПоддержкаПользователейКлиентСервер.ВерсияКонфигурации();
		
	ИначеЕсли ИмяПараметра = "language" Тогда
		ЗначениеПараметра = ИнтернетПоддержкаПользователейКлиентСервер.ТекКодЛокализации();
		
	ИначеЕсли ИмяПараметра = "enterPoint" Тогда
		ЗначениеПараметра = СокрЛП(ОсновныеПараметры.МестоЗапуска);
		
	ИначеЕсли ИмяПараметра = "versionUpdateConfiguration" Тогда
		
		ЗначениеПараметра = ИнтернетПоддержкаПользователейКлиентСервер.ВерсияОбработкиОбновления();
		
	Иначе
		
		ОписательПараметра = СессионныеПараметры.Получить(ИмяПараметра);
		Если ОписательПараметра <> Неопределено Тогда
			Возврат ОписательПараметра.Значение;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ЗначениеПараметра;
	
КонецФункции

// Добавляет вложенные параметры к параметрам запроса.
//
Процедура ДобавитьВложенныеПараметры(
	Параметр,
	ВложенныеПараметрыМассив,
	ОпределениеWS,
	ТипПараметр,
	ТипПараметры) Экспорт
	
	Параметр.parameters = ОпределениеWS.ФабрикаXDTO.Создать(ТипПараметры);
	
	Индекс = 0;
	Для каждого ПередаваемыйПараметр Из ВложенныеПараметрыМассив Цикл
		
		ВложенныйПараметр = ОпределениеWS.ФабрикаXDTO.Создать(ТипПараметр);
		
		ВложенныйПараметр.name  = СокрЛП(ПередаваемыйПараметр.Имя);
		ВложенныйПараметр.value = СокрЛП(ПередаваемыйПараметр.Значение);
		ВложенныйПараметр.index = Индекс;
		
		Если ПередаваемыйПараметр.Свойство("БизнесПроцесс") Тогда
			Параметр.bp = СокрЛП(ПередаваемыйПараметр.БизнесПроцесс);
		КонецЕсли;
		
		Параметр.parameters.parameter.Добавить(ВложенныйПараметр);
		
		Если ПередаваемыйПараметр.Свойство("ВложенныеПараметры")
			И ТипЗнч(ПередаваемыйПараметр.ВложенныеПараметры) = Тип("Массив") Тогда
			
			ДобавитьВложенныеПараметры(
				ВложенныйПараметр,
				ПередаваемыйПараметр.ВложенныеПараметры,
				ОпределениеWS,
				ТипПараметр,
				ТипПараметры);
			
		КонецЕсли;
		
		Индекс = Индекс + 1;
		
	КонецЦикла;
	
КонецПроцедуры

// Возвращает сессионные параметры, необходимые для расшифрования
// контрольного маркера ЭП.
//
Функция СессионныеПараметрыДляРасшифрования(КСКонтекст) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("markerED",
		ЗначениеСессионногоПараметра(КСКонтекст, "markerED"));
	Результат.Вставить("IDCertificateED_Dop",
		ЗначениеСессионногоПараметра(КСКонтекст, "IDCertificateED_Dop"));
	Результат.Вставить("IDCertificateED",
		ЗначениеСессионногоПараметра(КСКонтекст, "IDCertificateED"));
	
	Возврат Результат;
	
КонецФункции

// Добавление сессионных параметров к параметрам запроса при вызове операции
// process() сервиса ИПП.
//
Процедура ДобавитьСессионныеПараметрыКЗапросу(
	КСКонтекст,
	ИменаСессионныхПараметров = Неопределено,
	ПараметрыЗапроса) Экспорт
	
	Если ПараметрыЗапроса = Неопределено Тогда
		ПараметрыЗапроса = Новый Массив;
	КонецЕсли;
	
	ОсновныеПараметры = КСКонтекст.ОсновныеПараметры;
	
	ИдентификаторСеансаДобавлен = Ложь;
	Если ИменаСессионныхПараметров <> Неопределено Тогда
		
		Для каждого ИмяПараметра Из ИменаСессионныхПараметров Цикл
			
			ЗначениеПараметра = ЗначениеСессионногоПараметра(КСКонтекст, ИмяПараметра);
			
			СессионныйПараметр = Новый Структура("Имя, БизнесПроцесс, Значение, ОбластьВидимости",
				ИмяПараметра,
				ОсновныеПараметры.ИмяWSОпределения,
				ЗначениеПараметра,
				"sessionParameter");
			
			ПараметрыЗапроса.Добавить(СессионныйПараметр);
			
			Если ИмяПараметра = "session_id" Тогда
				ИдентификаторСеансаДобавлен = Истина;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если НЕ ИдентификаторСеансаДобавлен Тогда
		
		ИдентификаторСеансаИПП = ЗначениеСессионногоПараметра(КСКонтекст, "session_id");
		
		СессионныйПараметр = Новый Структура("Имя, БизнесПроцесс, Значение, ОбластьВидимости",
			"session_id",
			ОсновныеПараметры.ИмяWSОпределения,
			ИдентификаторСеансаИПП,
			"sessionParameter");
		
		ПараметрыЗапроса.Добавить(СессионныйПараметр);
		
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Другие процедуры и функции для взаимодействия с сервисом ИПП

#Если Не ВебКлиент Тогда

// Отправка электронного письма в службу технической поддержки.
//
Функция ОтправитьЭлектронноеПисьмоЧерезСервис(СтруктураСообщения, СетевыеПараметрыИПП) Экспорт
	
	Попытка
		ОписаниеСервисаИПППочты = НовыйОписаниеСервисаИПП(ИмяWSОпределения(), СетевыеПараметрыИПП);
		
		ТипЗапроса       = ОписаниеСервисаИПППочты.ФабрикаXDTO.Тип(ОписаниеСервисаИПППочты.URIСервиса, "Parameters");
		ПараметрыЗапроса = ОписаниеСервисаИПППочты.ФабрикаXDTO.Создать(ТипЗапроса);
		
		ТипПараметр = ОписаниеСервисаИПППочты.ФабрикаXDTO.Тип(ОписаниеСервисаИПППочты.URIСервиса, "Parameter");
		
		// Инициализация параметров электронного письма
		Параметр2 = ОписаниеСервисаИПППочты.ФабрикаXDTO.Создать(ТипПараметр);
		Параметр3 = ОписаниеСервисаИПППочты.ФабрикаXDTO.Создать(ТипПараметр);
		Параметр4 = ОписаниеСервисаИПППочты.ФабрикаXDTO.Создать(ТипПараметр);
		
		Параметр2.name  = "subject";
		Параметр2.value = СтруктураСообщения.Тема;
		ПараметрыЗапроса.parameter.Добавить(Параметр2);
		
		Параметр3.name  = "text";
		Параметр3.value = СтруктураСообщения.Сообщение;
		ПараметрыЗапроса.parameter.Добавить(Параметр3);
		
		Параметр4.name  = "from";
		Параметр4.value = СтруктураСообщения.ОтКого;
		ПараметрыЗапроса.parameter.Добавить(Параметр4);
		
		УсловноеИмяПолучателя = Неопределено;
		Если СтруктураСообщения.Свойство("УсловноеИмяПолучателя", УсловноеИмяПолучателя) Тогда
			Параметр5 = ОписаниеСервисаИПППочты.ФабрикаXDTO.Создать(ТипПараметр);
			Параметр5.name  = "aliasto";
			Параметр5.value = УсловноеИмяПолучателя;
			ПараметрыЗапроса.parameter.Добавить(Параметр5);
		КонецЕсли;
		
		Ответ = СервисИПП_sendmailtonet(ПараметрыЗапроса, ОписаниеСервисаИПППочты);
	Исключение
		ИнтернетПоддержкаПользователейВызовСервера.ЗаписатьОшибкуВЖурналРегистрации(
			НСтр("ru = 'Ошибка при отправке электронного письма.'")
				+ " " + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		Возврат Ложь;
	КонецПопытки;
	
	Возврат Истина;
	
КонецФункции

#КонецЕсли

////////////////////////////////////////////////////////////////////////////////
// Прочие служебные процедуры и функции

// Возвращает имя конфигурации.
Функция ИмяКонфигурации() Экспорт
	
	#Если Клиент Тогда
	ПараметрыИПП = СтандартныеПодсистемыКлиентПовтИсп.ПараметрыРаботыКлиента().ИнтернетПоддержкаПользователей;
	Возврат ПараметрыИПП.ИмяКонфигурации;
	#Иначе
	Возврат Метаданные.Имя;
	#КонецЕсли
	
КонецФункции

// Возвращает версию конфигурации.
Функция ВерсияКонфигурации() Экспорт
	
	#Если Клиент Тогда
	ПараметрыИПП = СтандартныеПодсистемыКлиентПовтИсп.ПараметрыРаботыКлиента().ИнтернетПоддержкаПользователей;
	Возврат ПараметрыИПП.ВерсияКонфигурации;
	#Иначе
	Возврат Метаданные.Версия;
	#КонецЕсли
	
КонецФункции

// Возвращает текущий код локализации.
Функция ТекКодЛокализации() Экспорт
	
	#Если Клиент Тогда
	ПараметрыИПП = СтандартныеПодсистемыКлиентПовтИсп.ПараметрыРаботыКлиента().ИнтернетПоддержкаПользователей;
	Возврат ПараметрыИПП.КодЛокализации;
	#Иначе
	Возврат ТекущийКодЛокализации();
	#КонецЕсли
	
КонецФункции

// Возвращает версию обработки обновления конфигурации.
Функция ВерсияОбработкиОбновления() Экспорт
	
	#Если Клиент Тогда
	ПараметрыИПП = СтандартныеПодсистемыКлиентПовтИсп.ПараметрыРаботыКлиента().ИнтернетПоддержкаПользователей;
	Возврат ПараметрыИПП.ВерсияОбработкиОбновления;
	#Иначе
	Возврат ИнтернетПоддержкаПользователей.ВерсияОбработкиОбновления();
	#КонецЕсли
	
КонецФункции

// Определяет, обрабатывается ли бизнес-процесс по заданной точке входа
// базовой функциональностью БИП.
//
// Параметры:
//	МестоЗапуска - Строка - точка входа в бизнес-процесс.
//
// Возвращаемое значение:
//	Булево - Истина, если бизнес-процесс обрабатывает базовой функциональностью
//		БИП, Ложь - в противном случае.
//
Функция ЭтоБазовыйБизнесПроцесс(МестоЗапуска) Экспорт
	
	Возврат (МестоЗапуска = "connectIPP");
	
КонецФункции

// Возвращает заданный обработчик бизнес-процесса в текущем контексте
// (клиент или сервер)
//
Функция ОбработчикБизнесПроцесса(МестоЗапуска, ИмяСобытия)
	
	#Если Клиент Тогда
	Возврат ИнтернетПоддержкаПользователейКлиент.КлиентскийОбработчикБизнесПроцесса(
		МестоЗапуска,
		ИмяСобытия);
	#Иначе
	Возврат ИнтернетПоддержкаПользователей.СерверныйОбработчикБизнесПроцесса(
		МестоЗапуска,
		ИмяСобытия);
	#КонецЕсли
	
КонецФункции

#Если Не ВебКлиент Тогда

// Выполняет получение содержимого двоичных данных в виде текста.
// Параметры:
// ДвоичныеДанные - ДвоичныеДанные - двоичные данные, содержимое которых
//	необходимо получить в виде текста.
//
// Возвращаемое значение:
//	Строка - текст в двоичных данных;
//
Функция ТекстВДвоичныхДанных(ДвоичныеДанные) Экспорт
	
	Результат = "";
	
	Если ТипЗнч(ДвоичныеДанные) <> Тип("ДвоичныеДанные") Тогда
		Возврат "";
	КонецЕсли;
	
	ИмяВремФайла = ПолучитьИмяВременногоФайла("txt");
	ДвоичныеДанные.Записать(ИмяВремФайла);
	ТекДок = Новый ТекстовыйДокумент;
	ТекДок.Прочитать(ИмяВремФайла, , "");
	Результат = ТекДок.ПолучитьТекст();
	
	Попытка
		УдалитьФайлы(ИмяВремФайла);
	Исключение
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции

#КонецЕсли

#КонецОбласти
