////////////////////////////////////////////////////////////////////////////////
// Определение наличия прав работы с механизмом проверки контрагентов
//  
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

Процедура ОпределитьНаличиеПраваНаРедактированиеНастроек(ЕстьПраво) Экспорт
	
	ЕстьПраво = Пользователи.РолиДоступны("АдминистраторСистемы");
	
КонецПроцедуры

Процедура ОпределитьНаличиеПраваНаИспользованиеПроверки(ЕстьПраво) Экспорт
	
	ЕстьПраво = Пользователи.РолиДоступны("ДобавлениеИзменениеДанныхБухгалтерии")
		ИЛИ Пользователи.РолиДоступны("ЧтениеДанныхБухгалтерии");
	
КонецПроцедуры

#Область ДокументыОснование

Процедура ПолучитьОписаниеДокументовСоСчетомФактуройВПодвале(Форма, Описание) Экспорт
	
	ДокументОбъект = Форма.Объект;
	ДокументСсылка = ДокументОбъект.Ссылка;
	
	Если ПроверкаКонтрагентовКлиентСервер.ЭтоДокументСоСчетомФактуройВПодвале(Форма) Тогда
		
		СчетФактура = ПроверкаКонтрагентовКлиентСервер.СчетФактура(Форма);
		
		Если ЗначениеЗаполнено(СчетФактура) Тогда
			Если ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ОтчетКомиссионераОПродажах") Тогда
				Элемент 		= Форма.Элементы.НадписьСчетФактураНаВознаграждение; 
				ЭлементРодитель = Форма.Элементы.ГруппаСчетФактураСсылка;
			ИначеЕсли ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.КорректировкаПоступления")
				ИЛИ ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ВозвратТоваровПоставщику")
				ИЛИ ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ПередачаНМА")
				ИЛИ ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ПоступлениеДопРасходов")
				ИЛИ ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ПоступлениеТоваровУслуг") Тогда
				Элемент 		= Форма.Элементы.НадписьСчетФактура; 
				ЭлементРодитель = Форма.Элементы.ГруппаСФ;
			ИначеЕсли ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ОтчетКомитентуОПродажах") Тогда
				Элемент 		= Форма.Элементы.НадписьСчетФактураНаВознаграждение; 
				ЭлементРодитель = Форма.Элементы.ГруппаСФ;
			Иначе
				Элемент 		= Форма.Элементы.НадписьСчетФактура; 
				ЭлементРодитель = Форма.Элементы.ГруппаСчетФактураСсылка;
			КонецЕсли;
			
			ПроверкаКонтрагентов.ДополнитьОписание(
				Описание,
				ТипЗнч(СчетФактура),
				СчетФактура,
				НЕ ЗначениеЗаполнено(СчетФактура), 
				Элемент, 
				ЭлементРодитель, 
				Неопределено, 
				ПроверкаКонтрагентов.ВСчетеФактуреЗаполненХотяБыОдинКонтрагент(СчетФактура), 
				Форма.СостояниеКонтрагентовВСчетеФактуре,
				Истина,
				Истина);
				
		КонецЕсли;
			
	КонецЕсли;
		
КонецПроцедуры

Процедура ПолучитьОписаниеДокументовСКонтрагентомВШапке(Форма, Описание) Экспорт
	
	ДокументОбъект = Форма.Объект;
	ДокументСсылка = ДокументОбъект.Ссылка;
	
	// Заполнение данных по контрагенту в шапке
	Если ПроверкаКонтрагентовКлиентСервер.ЭтоДокументСКонтрагентомВШапке(Форма) Тогда
		
		ПроверкаКонтрагентов.ДополнитьОписание(
			Описание,
			ТипЗнч(ДокументСсылка), 
			ДокументСсылка,
			Ложь, 
			Форма.Элементы.Контрагент, 
			Форма.Элементы.ГруппаКонтрагент, 
			ДокументОбъект.Контрагент,
			ЗначениеЗаполнено(ДокументОбъект.Контрагент),
			Форма.СостояниеКонтрагента,
			Ложь,
			Ложь);
			
	КонецЕсли;
		
КонецПроцедуры

Процедура ПолучитьОписаниеСчетовФактур(Форма, Описание) Экспорт
	
	ДокументОбъект = Форма.Объект;
	ДокументСсылка = ДокументОбъект.Ссылка;
	
	// Заполнение данных для счета-фактуры
	Если ПроверкаКонтрагентовКлиентСервер.ЭтоСчетФактура(Форма) Тогда
		
		ДокументПустой	= Ложь;
		Ссылка 			= ДокументСсылка;
		ТипДокумента	= ТипЗнч(Ссылка);
		ЭтоСчетФактура	= Истина;
		
		// Контрагент
		Контрагент = ДокументОбъект.Контрагент;
		
		Если ЗначениеЗаполнено(Контрагент) Тогда
			ПроверкаКонтрагентов.ДополнитьОписание(
				Описание, 
				ТипДокумента, 
				Ссылка,
				ДокументПустой, 
				Форма.Элементы.Контрагент, 
				Форма.Элементы.ГруппаКонтрагент, 
				Контрагент, 
				ЗначениеЗаполнено(Контрагент), 
				Форма.СостояниеКонтрагента,
				Ложь,
				ЭтоСчетФактура);
		КонецЕсли;
			
		// Комитент
		Контрагент = ДокументОбъект.Комитент;
		
		Если ЗначениеЗаполнено(Контрагент) Тогда
			
			ЭлементРодитель = Форма.Элементы.ГруппаКомитент;
			
			ПроверкаКонтрагентов.ДополнитьОписание(
				Описание, 
				ТипДокумента, 
				Ссылка,
				ДокументПустой, 
				Форма.Элементы.Комитент, 
				ЭлементРодитель, 
				Контрагент, 
				ЗначениеЗаполнено(Контрагент), 
				Форма.СостояниеКомитента,
				Ложь,
				ЭтоСчетФактура);
		КонецЕсли;
				
		// Продавец
		Контрагент = ДокументОбъект.Продавец;
		
		Если ЗначениеЗаполнено(Контрагент) Тогда
			
			ЭлементРодитель = Форма.Элементы.ГруппаПродавец;
			
			ПроверкаКонтрагентов.ДополнитьОписание(
				Описание, 
				ТипДокумента, 
				Ссылка,
				ДокументПустой, 
				Форма.Элементы.Продавец, 
				ЭлементРодитель, 
				Контрагент, 
				ЗначениеЗаполнено(Контрагент), 
				Форма.СостояниеПродавца,
				Ложь,
				ЭтоСчетФактура);
		КонецЕсли;
			
		// Субкомиссионер
		Если ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.СчетФактураПолученный") Тогда
			
			Контрагент = ДокументОбъект.Субкомиссионер;
			
			Если ЗначениеЗаполнено(Контрагент) Тогда
						
				ПроверкаКонтрагентов.ДополнитьОписание(
					Описание, 
					ТипДокумента, 
					Ссылка,
					ДокументПустой, 
					Форма.Элементы.Субкомиссионер, 
					Форма.Элементы.ГруппаСубкомиссионер, 
					Контрагент, 
					ЗначениеЗаполнено(Контрагент), 
					Форма.СостояниеСубкомиссионера,
					Ложь,
					ЭтоСчетФактура);
					
			КонецЕсли;
				
		КонецЕсли;
	
	КонецЕсли;
		
КонецПроцедуры

Процедура ПолучитьОписаниеДокументовСТабличнымиЧастями(Форма, Описание) Экспорт
	
	ДокументОбъект = Форма.Объект;
	ДокументСсылка = ДокументОбъект.Ссылка;
	
	Если ПроверкаКонтрагентовКлиентСервер.ЭтоДокументСКонтрагентомВТабличнойЧасти(Форма) Тогда
		
		Если ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ОтчетКомиссионераОПродажах") Тогда
			
			ПроверкаКонтрагентов.ДополнитьОписаниеТабличныхЧастей(
				Форма, Описание,"Покупатели","ПокупателиТоварыПокупатель","ПокупателиТоварыДатаСФ");
				
			ПроверкаКонтрагентов.ДополнитьОписаниеТабличныхЧастей(
				Форма, Описание,"Возвраты","ВозвратыПокупатель","ВозвратыДатаСФ");
				
			ПроверкаКонтрагентов.ДополнитьОписаниеТабличныхЧастей(
				Форма, Описание, "ДенежныеСредства","ДенежныеСредстваПокупатель","ДенежныеСредстваДатаСобытия");
				
		ИначеЕсли ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ОказаниеУслуг") Тогда
			
			ПроверкаКонтрагентов.ДополнитьОписаниеТабличныхЧастей(
				Форма, Описание,"Контрагенты","КонтрагентыКонтрагент","");
				
			ПроверкаКонтрагентов.ДополнитьОписаниеТабличныхЧастей(
				Форма, Описание,"СчетаФактуры","СчетаФактурыКонтрагент","");
				
		ИначеЕсли ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ПоступлениеТоваровУслуг") И Найти(Форма.ИмяФормы, "ФормаДокументаОбщая") <> 0 Тогда
			
			ПроверкаКонтрагентов.ДополнитьОписаниеТабличныхЧастей(
				Форма, Описание,"Товары","ТоварыКонтрагент","");
				
			ПроверкаКонтрагентов.ДополнитьОписаниеТабличныхЧастей(
				Форма, Описание,"АгентскиеУслуги","АгентскиеУслугиКонтрагент","");
				
		ИначеЕсли ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.РеализацияТоваровУслуг") И Найти(Форма.ИмяФормы, "ФормаДокументаОбщая") <> 0 Тогда
			
			ПроверкаКонтрагентов.ДополнитьОписаниеТабличныхЧастей(
				Форма, Описание,"АгентскиеУслуги","АгентскиеУслугиКонтрагент","");
				
		ИначеЕсли ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.КорректировкаПоступления") Тогда
			
			ПроверкаКонтрагентов.ДополнитьОписаниеТабличныхЧастей(
				Форма, Описание,"Товары", "ТоварыКонтрагент","");
				
			ПроверкаКонтрагентов.ДополнитьОписаниеТабличныхЧастей(
				Форма, Описание,"АгентскиеУслуги","АгентскиеУслугиКонтрагент","");
				
		ИначеЕсли ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.АвансовыйОтчет") Тогда
				
			ПроверкаКонтрагентов.ДополнитьОписаниеТабличныхЧастей(
				Форма, Описание,"Товары", "ТоварыПоставщик","ТоварыДатаСФ");
				
			ПроверкаКонтрагентов.ДополнитьОписаниеТабличныхЧастей(
				Форма, Описание, "ВозвратнаяТара", "ВозвратнаяТараКонтрагент","");
				
			ПроверкаКонтрагентов.ДополнитьОписаниеТабличныхЧастей(
				Форма, Описание, "ОплатаПоставщикам", "ОплатаПоставщикамКонтрагент","");
				
			ПроверкаКонтрагентов.ДополнитьОписаниеТабличныхЧастей(
				Форма, Описание, "Прочее", "ПрочееПоставщик","ПрочееДатаСФ");
				
		ИначеЕсли ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ВозвратТоваровПоставщику") Тогда
				
			ПроверкаКонтрагентов.ДополнитьОписаниеТабличныхЧастей(
				Форма, Описание, "Товары", "ТоварыКонтрагент","");
				
		ИначеЕсли ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ОтчетКомитентуОПродажах") Тогда
			
			ЭтоФормаДокументаОПродажах = Найти(Форма.ИмяФормы, "ФормаДокументаОПродажах") <> 0;
			Если ЭтоФормаДокументаОПродажах Тогда
				
				ПроверкаКонтрагентов.ДополнитьОписаниеТабличныхЧастей(
					Форма, Описание, "Товары", "ТоварыПокупатель","ТоварыДатаРеализации");
					
				ПроверкаКонтрагентов.ДополнитьОписаниеТабличныхЧастей(
					Форма, Описание, "ДенежныеСредства", "ДенежныеСредстваПокупатель", "ДенежныеСредстваДатаСобытия");
					
			Иначе
				
				ПроверкаКонтрагентов.ДополнитьОписаниеТабличныхЧастей(
					Форма, Описание, "Поставщики", "ПоставщикиПоставщик", "ПоставщикиДатаСФ");
					
			КонецЕсли;
				
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОпределитьДатуВДокументе(ДокументОбъект, Дата) Экспорт
	
	Дата = НачалоДня(ДокументОбъект.Дата);
	
КонецПроцедуры

#КонецОбласти

#Область СчетаФактуры

Процедура ПолучитьОписаниеКонтрагентовВСчетеФактуре(Тип, Описание) Экспорт
	
	// Контрагент
	ПроверкаКонтрагентов.ДополнитьОписаниеКонтрагентовВСчетеФактуре(
		Описание,
		"Контрагент", 
		"СостояниеКонтрагента");
	
	// Комитент
	ПроверкаКонтрагентов.ДополнитьОписаниеКонтрагентовВСчетеФактуре(
		Описание,
		"Комитент", 
		"СостояниеКомитента");
	
	// Продавец
	ПроверкаКонтрагентов.ДополнитьОписаниеКонтрагентовВСчетеФактуре(
		Описание,
		"Продавец", 
		"СостояниеПродавца");
		
	// Субкомиссионер
	Если Тип = Тип("ДокументСсылка.СчетФактураПолученный") Тогда
		
		// Субкомиссионер
		ПроверкаКонтрагентов.ДополнитьОписаниеКонтрагентовВСчетеФактуре(
			Описание,
			"Субкомиссионер", 
			"СостояниеСубкомиссионера");
			
	КонецЕсли;

КонецПроцедуры

Процедура ОпределитьДатуВСчетеФактуре(СчетФактураОбъект, Дата) Экспорт
	
	ДокументСсылка = СчетФактураОбъект.Ссылка;
		
	// Дата, на которую будет выполняться проверка
	Если ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.СчетФактураПолученный") Тогда
		Если СчетФактураОбъект.Исправление Тогда
			Дата = СчетФактураОбъект.ДатаИсправления;
		Иначе
			Дата = СчетФактураОбъект.ДатаВходящегоДокумента;
		КонецЕсли;
	Иначе
		// Все время берем Дата
		Дата = СчетФактураОбъект.Дата;
	КонецЕсли;
	
	Дата = НачалоДня(Дата);
	
КонецПроцедуры

#КонецОбласти

Процедура ОпределитьИННиКППВДанныхКонтрагента(Строка) Экспорт
	
	СведенияОКонтрагенте = Справочники.Контрагенты.СведенияОКонтрагенте(Строка.Контрагент, Строка.Дата);
	ЗаполнитьЗначенияСвойств(Строка, СведенияОКонтрагенте);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#КонецОбласти