////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Параметры.ВидДокумента) Тогда
		ОтборВидДокумента = Параметры.ВидДокумента;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.Организация) Тогда
		ОтборОрганизация = Параметры.Организация;
	КонецЕсли;
	
	ЗаполнитьСпискиВыбора();
	ПодготовитьФормуНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСпискиВыбора()
	
	СписокВыбораВидДокумента = Элементы.ОтборВидДокумента.СписокВыбора;
	
	СписокВыбораВидДокумента.Добавить("КнигаПокупок", НСтр("ru='Книга покупок'"));
	СписокВыбораВидДокумента.Добавить("КнигаПродаж", НСтр("ru='Книга продаж'"));
	СписокВыбораВидДокумента.Добавить("ДополнительныйЛистКнигиПокупок", НСтр("ru='Дополнительный лист книги покупок'"));
	СписокВыбораВидДокумента.Добавить("ДополнительныйЛистКнигиПродаж", НСтр("ru='Дополнительный лист книги продаж'"));
	СписокВыбораВидДокумента.Добавить("ЖурналПолученныхИВыставленныхСчетовФактур", НСтр("ru='Журнал полученных и выставленных счетов-фактур'"));
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНД ФОРМЫ

&НаКлиенте
Процедура Выбрать(Команда)
	
	МассивВозврата = Новый Массив;
		
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("Выбрать", Истина);
	
	СтрокиТаблицы = ТаблицаДокументов.НайтиСтроки(СтруктураОтбора);
	
	Если СтрокиТаблицы.Количество() = 0 Тогда
		ПоказатьПредупреждение(, НСтр("ru='Действие отменено. Не выбрано ни одного документа'"));
		Возврат;
	КонецЕсли;
	
	Для Каждого Строка Из СтрокиТаблицы Цикл
		СтруктураРезультат = Новый Структура("СсылкаДокументИБ, ВидДокумента", Строка.Ссылка, Строка.ВидДокумента);
		МассивВозврата.Добавить(СтруктураРезультат)
	КонецЦикла;
	
	Оповестить("ЗакрытаФормаВыбораДокументовИБДляПередачиФНС", МассивВозврата);
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтметитьВсеСтроки(Команда)
	
	Для каждого СтрокаКоллекции Из ТаблицаДокументов Цикл
		Если НЕ СтрокаКоллекции.Выбрать Тогда
			СтрокаКоллекции.Выбрать = Истина;
		КонецЕсли;
	КонецЦикла;
	
	//МассивСтрок = Элементы.ТаблицаДокументов.ВыделенныеСтроки;
	//Для Каждого НомерСтроки Из МассивСтрок Цикл
	//	СтрокаТаблицы = ТаблицаДокументов.НайтиПоИдентификатору(НомерСтроки);
	//	Если СтрокаТаблицы <> Неопределено Тогда
	//		СтрокаТаблицы.Выбрать = Истина;
	//	КонецЕсли;
	//КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьОтметкуСоВсехСтрок(Команда)
	
	Для Каждого Строка Из ТаблицаДокументов Цикл
		Если Строка.Выбрать Тогда
			Строка.Выбрать = Ложь;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьДокумент(Команда)
	
	ТекущаяСтрока = Элементы.ТаблицаДокументов.ТекущиеДанные;
	Если ТекущаяСтрока <> Неопределено Тогда
		ПоказатьЗначение( , ТекущаяСтрока.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ПОЛЕЙ ФОРМЫ

&НаКлиенте
Процедура ТаблицаДокументовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ТекущаяСтрока = Элементы.ТаблицаДокументов.ТекущиеДанные;
	Если ТекущаяСтрока <> Неопределено Тогда
		ПоказатьЗначение(, ТекущаяСтрока.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

&НаСервере
Процедура ПодготовитьФормуНаСервере()
	
	ЗаполнитьТаблицуДокументов();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуДокументов()
	
	Запрос = Новый Запрос;
	
	ЗначениеОтбора = Неопределено;
	
	Если ОтборВидДокумента = "КнигаПокупок" Тогда
		ЗначениеОтбора = Тип("ДокументСсылка.КнигаПокупокДляПередачиВЭлектронномВиде");
	ИначеЕсли ОтборВидДокумента = "КнигаПродаж" Тогда
		ЗначениеОтбора = Тип("ДокументСсылка.КнигаПродажДляПередачиВЭлектронномВиде");
	ИначеЕсли ОтборВидДокумента = "ДополнительныйЛистКнигиПокупок" Тогда
		ЗначениеОтбора = Тип("ДокументСсылка.ДопЛистКнигиПокупокДляПередачиВЭлектронномВиде");
	ИначеЕсли ОтборВидДокумента = "ДополнительныйЛистКнигиПродаж" Тогда
		ЗначениеОтбора = Тип("ДокументСсылка.ДопЛистКнигиПродажДляПередачиВЭлектронномВиде");
	ИначеЕсли ОтборВидДокумента = "ЖурналПолученныхИВыставленныхСчетовФактур" Тогда
		ЗначениеОтбора = Тип("ДокументСсылка.ЖурналУчетаСчетовФактурДляПередачиВЭлектронномВиде");
	КонецЕсли;	
	
	Если ЗначениеОтбора <> Неопределено Тогда
		Запрос.УстановитьПараметр("ТипМетаданных", ЗначениеОтбора);
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Организация",  ОтборОрганизация);
	
	Запрос.Текст =
	
	"ВЫБРАТЬ
	|	ЖурналДокументовДокументыПоУчетуНДСДляПередачиВЭлектронномВиде.Ссылка,
	|	ЖурналДокументовДокументыПоУчетуНДСДляПередачиВЭлектронномВиде.НалоговыйПериод,
	|	ЖурналДокументовДокументыПоУчетуНДСДляПередачиВЭлектронномВиде.Организация,
	|	ЖурналДокументовДокументыПоУчетуНДСДляПередачиВЭлектронномВиде.Комментарий,
	|	ЖурналДокументовДокументыПоУчетуНДСДляПередачиВЭлектронномВиде.Тип
	|ИЗ
	|	ЖурналДокументов.ДокументыПоУчетуНДСДляПередачиВЭлектронномВиде КАК ЖурналДокументовДокументыПоУчетуНДСДляПередачиВЭлектронномВиде
	|ГДЕ
	|	ЖурналДокументовДокументыПоУчетуНДСДляПередачиВЭлектронномВиде.Организация = &Организация
	|   " + ?(ЗначениеОтбора <> Неопределено,"И ЖурналДокументовДокументыПоУчетуНДСДляПередачиВЭлектронномВиде.Тип = &ТипМетаданных", "") + "
	|	И ЖурналДокументовДокументыПоУчетуНДСДляПередачиВЭлектронномВиде.Проведен = ИСТИНА
	|	И ЖурналДокументовДокументыПоУчетуНДСДляПередачиВЭлектронномВиде.ПометкаУдаления = ЛОЖЬ";
	
		
	Результат = Запрос.Выполнить().Выбрать();
	
	ТаблицаДокументов.Очистить();
	
	Пока Результат.Следующий() Цикл
		СтрокаТаблицы = ТаблицаДокументов.Добавить();
		СтрокаТаблицы.Ссылка      			= Результат.Ссылка;
		СтрокаТаблицы.Организация         	= Результат.Организация;
		СтрокаТаблицы.ТипДокумента			= Строка(Результат.Тип);
		СтрокаТаблицы.НалоговыйПериод 		= Результат.НалоговыйПериод;
		СтрокаТаблицы.Комментарий 			= Результат.Комментарий;
		
		ВидДокумента = "";
		Если Результат.Тип = Тип("ДокументСсылка.КнигаПокупокДляПередачиВЭлектронномВиде") Тогда
			ВидДокумента = "КнигаПокупок";
		ИначеЕсли Результат.Тип = Тип("ДокументСсылка.КнигаПродажДляПередачиВЭлектронномВиде") Тогда
			ВидДокумента = "КнигаПродаж";
		ИначеЕсли Результат.Тип = Тип("ДокументСсылка.ДопЛистКнигиПокупокДляПередачиВЭлектронномВиде") Тогда
			ВидДокумента = "ДополнительныйЛистКнигиПокупок";
		ИначеЕсли Результат.Тип = Тип("ДокументСсылка.ДопЛистКнигиПродажДляПередачиВЭлектронномВиде") Тогда
			ВидДокумента = "ДополнительныйЛистКнигиПродаж";
		ИначеЕсли Результат.Тип = Тип("ДокументСсылка.ЖурналУчетаСчетовФактурДляПередачиВЭлектронномВиде") Тогда
			ВидДокумента = "ЖурналПолученныхИВыставленныхСчетовФактур";	
		КонецЕсли;		
		
		СтрокаТаблицы.ВидДокумента			= ВидДокумента;

	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция  ПолучитьТекущиеРеквизитыВыбора()
	
	Результат = Новый Структура("ТекущаяСсылка, МассивОтмеченныхСсылок");
	ТекущаяСтрока = Элементы.ТаблицаДокументов.ТекущаяСтрока;
	Если ТекущаяСтрока <> Неопределено Тогда
		ТекущаяСсылка = ТаблицаДокументов.НайтиПоИдентификатору(ТекущаяСтрока).Ссылка;
	Иначе
		ТекущаяСсылка = Неопределено;
	КонецЕсли;
	
	МассивОтмеченныхСсылок = Новый Массив;
	
	МассивОтмеченныхСсылок = Новый Массив;
	
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("Выбрать", Истина);
	
	СтрокиТаблицы = ТаблицаДокументов.НайтиСтроки(СтруктураОтбора);
	Для Каждого Строка Из СтрокиТаблицы Цикл
		МассивОтмеченныхСсылок.Добавить(Строка.Ссылка)
	КонецЦикла;
	
	Результат.Вставить("ТекущаяСсылка", ТекущаяСсылка);
	Результат.Вставить("МассивОтмеченныхСсылок", МассивОтмеченныхСсылок);
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура УстановитьРеквизитыВыбора(РеквизитыВыбора)
	
	//установим текущую строку
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("Ссылка", РеквизитыВыбора.ТекущаяСсылка);

	МассивСтрок = ТаблицаДокументов.НайтиСтроки(СтруктураОтбора);
	Если МассивСтрок.Количество() > 0 Тогда
		ДанныеТекущейСтроки = МассивСтрок[0];
	    Элементы.ТаблицаДокументов.ТекущаяСтрока = ДанныеТекущейСтроки.ПолучитьИдентификатор();
	КонецЕсли;
	
	//восстановим отмеченные строки по ссылкам
	МассивОтмеченныхСсылок = РеквизитыВыбора.МассивОтмеченныхСсылок;
	Для каждого СтрокаКоллекции Из ТаблицаДокументов Цикл
		Если МассивОтмеченныхСсылок.Найти(СтрокаКоллекции.Ссылка) <> Неопределено Тогда
			СтрокаКоллекции.Выбрать = Истина;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьТаблицуДокументов()
	
	ТекущиеРеквизитыВыбора = ПолучитьТекущиеРеквизитыВыбора();
	ЗаполнитьТаблицуДокументов();
	УстановитьРеквизитыВыбора(ТекущиеРеквизитыВыбора);
	
КонецПроцедуры

&НаКлиенте
Процедура Обновить(Команда)
	
	ОбновитьТаблицуДокументов();
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиВЖурнал(Команда)
	
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("ПерейтиВЖурналЗавершение", ЭтотОбъект);
	
	ОткрытьФорму("ЖурналДокументов.ДокументыПоУчетуНДСДляПередачиВЭлектронномВиде.ФормаСписка",,,,,, ОповещениеОЗакрытии, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиВЖурналЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	ОбновитьТаблицуДокументов();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборВидДокументаПриИзменении(Элемент)
	ОбновитьТаблицуДокументов();
КонецПроцедуры   