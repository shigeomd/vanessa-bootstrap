#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ДокументыЖурнала = ЖурналыДокументов.ЖурналОпераций.СоставДокументов();
	Если Параметры.КлючНазначения = "ДокументыПоКонтрагенту" Тогда
		ДокументыПоКонтрагенту = КритерииОтбора.ДокументыПоКонтрагенту.СоставДокументов();
		Для Каждого ОбъектМетаданных Из ДокументыПоКонтрагенту Цикл
			Если ДокументыЖурнала.Найти(ОбъектМетаданных) <> Неопределено И ПравоДоступа("Изменение", ОбъектМетаданных) Тогда
				ТипыДокументов.Добавить(ОбъектМетаданных.Имя, ОбъектМетаданных.Синоним);
			КонецЕсли;
		КонецЦикла;
	ИначеЕсли Параметры.КлючНазначения = "ДокументыПоДоговоруКонтрагента" Тогда
		ДокументыПоКонтрагенту = КритерииОтбора.ДокументыПоДоговоруКонтрагента.СоставДокументов();
		Для Каждого ОбъектМетаданных Из ДокументыПоКонтрагенту Цикл
			Если ДокументыЖурнала.Найти(ОбъектМетаданных) <> Неопределено И ПравоДоступа("Изменение", ОбъектМетаданных) Тогда
				ТипыДокументов.Добавить(ОбъектМетаданных.Имя, ОбъектМетаданных.Синоним);
			КонецЕсли;
		КонецЦикла;
	Иначе
		Для Каждого ОбъектМетаданных Из ДокументыЖурнала Цикл
			Если ПравоДоступа("Изменение", ОбъектМетаданных) Тогда
				ТипыДокументов.Добавить(ОбъектМетаданных.Имя, ОбъектМетаданных.Синоним);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если ТипыДокументов.Количество() = 0 Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Нарушение прав доступа!'"), , , , Отказ);
		Возврат;
	КонецЕсли;
	
	ТипыДокументов.СортироватьПоПредставлению();
	
	// Установка текущей строки списка
	Если ЗначениеЗаполнено(Параметры.НачальноеЗначение) Тогда
		ОбъектМетаданных = Метаданные.НайтиПоТипу(Параметры.НачальноеЗначение);
		Если ОбъектМетаданных <> Неопределено Тогда
			ТекущаяСтрока = ТипыДокументов.НайтиПоЗначению(ОбъектМетаданных.Имя);
			Если ТекущаяСтрока <> Неопределено Тогда
				Элементы.ТипыДокументов.ТекущаяСтрока = ТекущаяСтрока.ПолучитьИдентификатор();
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТипыДокументов

&НаКлиенте
Процедура ТипыДокументовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.ТипыДокументов.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ОповеститьОВыборе(ТекущиеДанные.Значение);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	ТекущиеДанные = Элементы.ТипыДокументов.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ОповеститьОВыборе(ТекущиеДанные.Значение);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
