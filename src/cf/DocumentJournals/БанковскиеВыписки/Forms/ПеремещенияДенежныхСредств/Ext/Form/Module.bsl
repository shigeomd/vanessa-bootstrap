
////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ
//

&НаСервере
Процедура ОбновитьСписокДокументов()
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачалоДня",            НачалоДня(ЗначенияПараметров.ДатаВыписки));
	Запрос.УстановитьПараметр("КонецДня",             КонецДня(ЗначенияПараметров.ДатаВыписки));
	Запрос.УстановитьПараметр("БанковскийСчетИтогов", ЗначенияПараметров.БанковскийСчет);
	Запрос.УстановитьПараметр("Организация",          ЗначенияПараметров.Организация);
	Запрос.УстановитьПараметр("СинонимСписание",      Метаданные.Документы.СписаниеСРасчетногоСчета.Синоним);
	Запрос.УстановитьПараметр("СинонимПКО",           Метаданные.Документы.ПриходныйКассовыйОрдер.Синоним);
	Запрос.УстановитьПараметр("СинонимРКО",           Метаданные.Документы.РасходныйКассовыйОрдер.Синоним);
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СписаниеСРасчетногоСчета.Дата,
	|	СписаниеСРасчетногоСчета.Номер,
	|	0 КАК Списано,
	|	СписаниеСРасчетногоСчета.СуммаДокумента КАК Поступило,
	|	СписаниеСРасчетногоСчета.Ссылка,
	|	ВЫБОР
	|		КОГДА СписаниеСРасчетногоСчета.РучнаяКорректировка = ИСТИНА
	|			ТОГДА ВЫБОР
	|					КОГДА СписаниеСРасчетногоСчета.ПометкаУдаления = ИСТИНА
	|						ТОГДА 10
	|					КОГДА СписаниеСРасчетногоСчета.Проведен = ЛОЖЬ
	|						ТОГДА 9
	|					ИНАЧЕ 8
	|				КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА СписаниеСРасчетногоСчета.ПометкаУдаления = ИСТИНА
	|					ТОГДА 2
	|				КОГДА СписаниеСРасчетногоСчета.Проведен = ИСТИНА
	|					ТОГДА 1
	|				ИНАЧЕ 0
	|			КОНЕЦ
	|	КОНЕЦ КАК НомерКартинки,
	|	СписаниеСРасчетногоСчета.ПометкаУдаления КАК ПометкаУдаления,
	|	&СинонимСписание КАК ВидДокумента
	|ИЗ
	|	Документ.СписаниеСРасчетногоСчета КАК СписаниеСРасчетногоСчета
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.БанковскиеСчета КАК БанковскиеСчета
	|		ПО СписаниеСРасчетногоСчета.СчетКонтрагента = БанковскиеСчета.Ссылка
	|			И (БанковскиеСчета.Ссылка = &БанковскийСчетИтогов)
	|			И (СписаниеСРасчетногоСчета.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийСписаниеДенежныхСредств.ПереводНаДругойСчет))
	|			И (СписаниеСРасчетногоСчета.Организация = &Организация)
	|			И (СписаниеСРасчетногоСчета.Дата МЕЖДУ &НачалоДня И &КонецДня)
	|			И (СписаниеСРасчетногоСчета.Проведен = ИСТИНА)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПриходныйКассовыйОрдер.Дата,
	|	ПриходныйКассовыйОрдер.Номер,
	|	ПриходныйКассовыйОрдер.СуммаДокумента,
	|	0,
	|	ПриходныйКассовыйОрдер.Ссылка,
	|	ВЫБОР
	|		КОГДА ПриходныйКассовыйОрдер.РучнаяКорректировка = ИСТИНА
	|			ТОГДА ВЫБОР
	|					КОГДА ПриходныйКассовыйОрдер.ПометкаУдаления = ИСТИНА
	|						ТОГДА 10
	|					КОГДА ПриходныйКассовыйОрдер.Проведен = ЛОЖЬ
	|						ТОГДА 9
	|					ИНАЧЕ 8
	|				КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ПриходныйКассовыйОрдер.ПометкаУдаления = ИСТИНА
	|					ТОГДА 2
	|				КОГДА ПриходныйКассовыйОрдер.Проведен = ИСТИНА
	|					ТОГДА 1
	|				ИНАЧЕ 0
	|			КОНЕЦ
	|	КОНЕЦ,
	|	ПриходныйКассовыйОрдер.ПометкаУдаления,
	|	&СинонимПКО
	|ИЗ
	|	Документ.ПриходныйКассовыйОрдер КАК ПриходныйКассовыйОрдер
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.БанковскиеСчета КАК БанковскиеСчета
	|		ПО ПриходныйКассовыйОрдер.Контрагент = БанковскиеСчета.Ссылка
	|			И (БанковскиеСчета.Ссылка = &БанковскийСчетИтогов)
	|			И (ПриходныйКассовыйОрдер.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПКО.ПолучениеНаличныхВБанке))
	|			И (ПриходныйКассовыйОрдер.Организация = &Организация)
	|			И (ПриходныйКассовыйОрдер.Дата МЕЖДУ &НачалоДня И &КонецДня)
	|			И (ПриходныйКассовыйОрдер.Проведен = ИСТИНА)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РасходныйКассовыйОрдер.Дата,
	|	РасходныйКассовыйОрдер.Номер,
	|	0,
	|	РасходныйКассовыйОрдер.СуммаДокумента,
	|	РасходныйКассовыйОрдер.Ссылка,
	|	ВЫБОР
	|		КОГДА РасходныйКассовыйОрдер.РучнаяКорректировка = ИСТИНА
	|			ТОГДА ВЫБОР
	|					КОГДА РасходныйКассовыйОрдер.ПометкаУдаления = ИСТИНА
	|						ТОГДА 10
	|					КОГДА РасходныйКассовыйОрдер.Проведен = ЛОЖЬ
	|						ТОГДА 9
	|					ИНАЧЕ 8
	|				КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА РасходныйКассовыйОрдер.ПометкаУдаления = ИСТИНА
	|					ТОГДА 2
	|				КОГДА РасходныйКассовыйОрдер.Проведен = ИСТИНА
	|					ТОГДА 1
	|				ИНАЧЕ 0
	|			КОНЕЦ
	|	КОНЕЦ,
	|	РасходныйКассовыйОрдер.ПометкаУдаления,
	|	&СинонимРКО
	|ИЗ
	|	Документ.РасходныйКассовыйОрдер КАК РасходныйКассовыйОрдер
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.БанковскиеСчета КАК БанковскиеСчета
	|		ПО РасходныйКассовыйОрдер.СчетОрганизации = БанковскиеСчета.Ссылка
	|			И (БанковскиеСчета.Ссылка = &БанковскийСчетИтогов)
	|			И (РасходныйКассовыйОрдер.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРКО.ВзносНаличнымиВБанк))
	|			И (РасходныйКассовыйОрдер.Организация = &Организация)
	|			И (РасходныйКассовыйОрдер.Дата МЕЖДУ &НачалоДня И &КонецДня)
	|			И (РасходныйКассовыйОрдер.Проведен = ИСТИНА)";
	
	СписокДокументов.Загрузить(Запрос.Выполнить().Выгрузить());
	Элементы.СписокДокументовСписано.ТекстПодвала   = СписокДокументов.Итог("Списано");
	Элементы.СписокДокументовПоступило.ТекстПодвала = СписокДокументов.Итог("Поступило");
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ИзменитьПометкуНаУдаление(Знач СсылкаНаДокумент, ТекущееЗначениеПометкиНаУдаление)
	
	ДокументОбъект = СсылкаНаДокумент.ПолучитьОбъект();
	
	Попытка
	
		ДокументОбъект.УстановитьПометкуУдаления(НЕ ТекущееЗначениеПометкиНаУдаление);
		ТекущееЗначениеПометкиНаУдаление = НЕ ТекущееЗначениеПометкиНаУдаление;
		Возврат Истина;
	
	Исключение
		
		Возврат ОписаниеОшибки();
		
	КонецПопытки;
	
КонецФункции


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ
//

&НаКлиенте
Процедура СписокДокументовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтрокаДокумента = СписокДокументов.НайтиПоИдентификатору(ВыбраннаяСтрока);
	Если СтрокаДокумента <> Неопределено И ЗначениеЗаполнено(СтрокаДокумента.Ссылка) Тогда
		ПоказатьЗначение( , СтрокаДокумента.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокДокументовПередНачаломИзменения(Элемент, Отказ)
	
	Отказ = истина;
	СтрокаДокумента = Элементы.СписокДокументов.ТекущиеДанные;
	Если СтрокаДокумента <> Неопределено И ЗначениеЗаполнено(СтрокаДокумента.Ссылка) Тогда
		ПоказатьЗначение( , СтрокаДокумента.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокДокументовПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокДокументовПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	ТекущаяСтрока = Элементы.СписокДокументов.ТекущиеДанные;
	Если ЗначениеЗаполнено(ТекущаяСтрока.Ссылка) Тогда
		Если ТекущаяСтрока.ПометкаУдаления Тогда
			ТекстВопроса = НСтр("ru = 'Снять с ""%1"" пометку на удаление?'");
		Иначе
			ТекстВопроса = НСтр("ru = 'Пометить ""%1"" на удаление?'");
		КонецЕсли;
		
		ОбозначениеДокумента = ТекущаяСтрока.ВидДокумента + " " + ТекущаяСтрока.Номер + " от " + ТекущаяСтрока.Дата;
		ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстВопроса, ОбозначениеДокумента);
		
		Оповещение = Новый ОписаниеОповещения("ВопросСнятьПометитьНаУдалениеЗавершение", ЭтотОбъект, ТекущаяСтрока);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросСнятьПометитьНаУдалениеЗавершение(Результат, ТекущаяСтрока) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		РезультатУстановки = ИзменитьПометкуНаУдаление(ТекущаяСтрока.Ссылка, ТекущаяСтрока.ПометкаУдаления);
		Если РезультатУстановки = Истина Тогда
			ТекущаяСтрока.НомерКартинки = ?(ТекущаяСтрока.ПометкаУдаления, 2, 0);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСписок(Команда)
	
	ОбновитьСписокДокументов();
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ
//

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗначенияПараметров = Новый Структура("Организация, БанковскийСчет, ДатаВыписки");
	Параметры.Свойство("Организация",    ЗначенияПараметров.Организация);
	Параметры.Свойство("БанковскийСчет", ЗначенияПараметров.БанковскийСчет);
	Параметры.Свойство("ДатаВыписки",    ЗначенияПараметров.ДатаВыписки);
	
	Если НЕ ЗначениеЗаполнено(ЗначенияПараметров.Организация) И ЗначениеЗаполнено(ЗначенияПараметров.БанковскийСчет) Тогда
		ЗначенияПараметров.Организация = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЗначенияПараметров.БанковскийСчет, "Владелец");
	КонецЕсли;
	
	НадписьПеремещения = НСтр("ru = 'Перемещения денежных средств в организации %1'");
	НадписьПеремещения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НадписьПеремещения,
		?(ЗначениеЗаполнено(ЗначенияПараметров.Организация), ЗначенияПараметров.Организация, НСтр("ru = '<не указана>'")));
	
	Если ЗначениеЗаполнено(ЗначенияПараметров.БанковскийСчет) Тогда
		ТекстПеремещения = НСтр("ru = '%1: р/с ""%2""'");
		НадписьПеремещения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстПеремещения,
			НадписьПеремещения, ЗначенияПараметров.БанковскийСчет);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЗначенияПараметров.ДатаВыписки) Тогда
		ТекстПеремещения = НСтр("ru = '%1: на дату %2'");
		НадписьПеремещения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстПеремещения,
			НадписьПеремещения, Формат(ЗначенияПараметров.ДатаВыписки, "ДЛФ=DD"));
	КонецЕсли;
	
	ОбновитьСписокДокументов();
	
КонецПроцедуры
