#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Подсистема "Управление доступом".

// Процедура ЗаполнитьНаборыЗначенийДоступа по свойствам объекта заполняет наборы значений доступа
// в таблице с полями:
//    НомерНабора     - Число                                     (необязательно, если набор один),
//    ВидДоступа      - ПланВидовХарактеристикСсылка.ВидыДоступа, (обязательно),
//    ЗначениеДоступа - Неопределено, СправочникСсылка или др.    (обязательно),
//    Чтение          - Булево (необязательно, если набор для всех прав) устанавливается для одной строки набора,
//    Добавление      - Булево (необязательно, если набор для всех прав) устанавливается для одной строки набора,
//    Изменение       - Булево (необязательно, если набор для всех прав) устанавливается для одной строки набора,
//    Удаление        - Булево (необязательно, если набор для всех прав) устанавливается для одной строки набора,
//
//  Вызывается из процедуры УправлениеДоступомСлужебный.ЗаписатьНаборыЗначенийДоступа(),
// если объект зарегистрирован в "ПодпискаНаСобытие.ЗаписатьНаборыЗначенийДоступа" и
// из таких же процедур объектов, у которых наборы значений доступа зависят от наборов этого
// объекта (в этом случае объект зарегистрирован в "ПодпискаНаСобытие.ЗаписатьЗависимыеНаборыЗначенийДоступа").
//
// Параметры:
//  Таблица      - ТабличнаяЧасть,
//                 РегистрСведенийНаборЗаписей.НаборыЗначенийДоступа,
//                 ТаблицаЗначений, возвращаемая УправлениеДоступом.ТаблицаНаборыЗначенийДоступа().
//
Процедура ЗаполнитьНаборыЗначенийДоступа(Таблица) Экспорт
	
	ЗарплатаКадры.ЗаполнитьНаборыПоОрганизацииИФизическимЛицам(ЭтотОбъект, Таблица, "Организация", "ФизическиеЛица.ФизическоеЛицо");
	
КонецПроцедуры

// Подсистема "Управление доступом".

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	ДанныеДляПроведения = ДанныеДляПроведения();
	
	УчетСтраховыхВзносов.СформироватьДоходыСтраховыеВзносы(Движения, Отказ, Организация, ПериодРегистрации, ДанныеДляПроведения.СведенияОДоходах, Истина);
	УчетСтраховыхВзносов.СформироватьИсчисленныеВзносы(Движения, Отказ, Организация, КонецМесяца(ПериодРегистрации), ДанныеДляПроведения.СтраховыеВзносы);
	Если РегистрироватьДляКорректирующегоРасчета Тогда
		Для каждого ПрошлыйПериод Из ДанныеДляПроведения.ПрошлыеПериоды Цикл
			СтрокиПериода = ДанныеДляПроведения.СтраховыеВзносы.Скопировать(ДанныеДляПроведения.СтраховыеВзносы.НайтиСтроки(Новый Структура("Период",ПрошлыйПериод)));
			УчетСтраховыхВзносов.СформироватьСтраховыеВзносыПоФизическимЛицам(Движения, Отказ, Организация, ПрошлыйПериод, Ссылка, СтрокиПериода);
		КонецЦикла;
	Иначе
		УчетСтраховыхВзносов.СформироватьСтраховыеВзносыПоФизическимЛицам(Движения, Отказ, Организация, ПериодРегистрации, Ссылка, ДанныеДляПроведения.СтраховыеВзносы);
	КонецЕсли;
	
	ДанныеДляПроведения = ОтражениеЗарплатыВУчете.НоваяСтруктураРезультатыРасчетаЗарплаты();
	ДанныеДляПроведения.СтраховыеВзносы = Движения.СтраховыеВзносыПоФизическимЛицам.Выгрузить();
	СтрокаСписокТаблиц = "НачисленнаяЗарплатаИВзносы";
	ОтражениеЗарплатыВБухучете.СформироватьДвиженияПоДокументу(Движения, Отказ, Организация, НачалоМесяца(ПериодРегистрации), ДанныеДляПроведения, СтрокаСписокТаблиц);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ДанныеДляПроведения()
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДанныеОДоходах.ФизическоеЛицо,
	|	ВЫБОР
	|		КОГДА ДанныеОДоходах.Ссылка.РегистрироватьДляКорректирующегоРасчета
	|			ТОГДА ДанныеОДоходах.ДатаПолученияДохода
	|		ИНАЧЕ ДанныеОДоходах.Ссылка.ПериодРегистрации
	|	КОНЕЦ КАК Период,
	|	ДанныеОДоходах.ВидДохода,
	|	СУММА(ДанныеОДоходах.Сумма) КАК Сумма,
	|	СУММА(ДанныеОДоходах.Скидка) КАК Скидка,
	|	ДанныеОДоходах.ОблагаетсяЕНВД,
	|	ДанныеОДоходах.ОблагаетсяВзносамиНаДоплатуЛетчикам,
	|	ДанныеОДоходах.ОблагаетсяВзносамиНаДоплатуШахтерам,
	|	ДанныеОДоходах.ЯвляетсяДоходомФармацевта,
	|	ДанныеОДоходах.ЯвляетсяДоходомЧленаЭкипажаСуднаПодФлагомРФ,
	|	ДанныеОДоходах.ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией,
	|	ДанныеОДоходах.КлассУсловийТруда,
	|	ДанныеОДоходах.ДатаПолученияДохода
	|ИЗ
	|	Документ.ПерерасчетСтраховыхВзносов.СведенияОДоходах КАК ДанныеОДоходах
	|ГДЕ
	|	ДанныеОДоходах.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеОДоходах.ВидДохода,
	|	ДанныеОДоходах.ФизическоеЛицо,
	|	ДанныеОДоходах.ОблагаетсяЕНВД,
	|	ДанныеОДоходах.ОблагаетсяВзносамиНаДоплатуЛетчикам,
	|	ДанныеОДоходах.ОблагаетсяВзносамиНаДоплатуШахтерам,
	|	ДанныеОДоходах.ЯвляетсяДоходомФармацевта,
	|	ДанныеОДоходах.ЯвляетсяДоходомЧленаЭкипажаСуднаПодФлагомРФ,
	|	ДанныеОДоходах.ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией,
	|	ДанныеОДоходах.КлассУсловийТруда,
	|	ВЫБОР
	|		КОГДА ДанныеОДоходах.Ссылка.РегистрироватьДляКорректирующегоРасчета
	|			ТОГДА ДанныеОДоходах.ДатаПолученияДохода
	|		ИНАЧЕ ДанныеОДоходах.Ссылка.ПериодРегистрации
	|	КОНЕЦ,
	|	ДанныеОДоходах.ДатаПолученияДохода";
	
	ДанныеДляПроведения = Новый Структура;
	ДанныеДляПроведения.Вставить("СведенияОДоходах", Запрос.Выполнить().Выгрузить());
	ДанныеДляПроведения.Вставить("СтраховыеВзносы", УчетСтраховыхВзносов.ДанныеОВзносахИзДокумента(Ссылка,,,,Истина));
	
	Если РегистрироватьДляКорректирующегоРасчета Тогда
		ПрошлыеПериоды = Новый Массив;
		ДанныеДляПроведения.СтраховыеВзносы.Колонки.Добавить("Период", Новый ОписаниеТипов("Дата"));
		Для каждого СтрокаТЗ Из ДанныеДляПроведения.СтраховыеВзносы Цикл
			СтрокаТЗ.Период = СтрокаТЗ.ДатаПолученияДохода;
			ПрошлыйПериод = НачалоМесяца(СтрокаТЗ.ДатаПолученияДохода);
			Если ПрошлыеПериоды.Найти(ПрошлыйПериод) = Неопределено Тогда
				ПрошлыеПериоды.Добавить(ПрошлыйПериод)
			КонецЕсли;
		КонецЦикла;
		ДанныеДляПроведения.Вставить("ПрошлыеПериоды", ПрошлыеПериоды);
	КонецЕсли;
	
	Возврат ДанныеДляПроведения;
	
КонецФункции

#КонецОбласти

#КонецЕсли