&НаСервере
Функция  ПолучитьСтруктуруРеквизитовДляОбработки(ДокументСсылка)
	
	СтруктураРекизитов = Новый Структура;
	
	РеквизитыСчетаФактуры =  ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументСсылка, "ВидСчетаФактуры, ДокументОснование");
	
	ВидСчетаФактуры = РеквизитыСчетаФактуры.ВидСчетаФактуры;
	ДокументОснование = РеквизитыСчетаФактуры.ДокументОснование;
	
	ПоследнееИсправление = УчетНДСПереопределяемый.ПолучитьПоследнееИсправлениеСчетаФактурыВыданного(ДокументСсылка);
	
	Основание = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПоследнееИсправление, "ДокументОснование");
	
	КорректироватьСчетФактуру = Ложь;
	Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ОказаниеУслуг")
		Или ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ОтчетКомитентуОПродажах")
		Или (ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ОтчетКомиссионераОПродажах")
		И НЕ Документы.КорректировкаРеализации.ОтчетКомиссионераЗаполненПоСтарому(ДокументОснование))Тогда 
		
		КорректироватьСчетФактуру = Истина;
		
	КонецЕсли;
	
	ПорядокИсправления = Новый ФиксированноеСоответствие(Документы.СчетФактураВыданный.ПолучитьСоответствиеВидовСчетаФактурыПорядкуИсправления());
	
	ВозможноКорректировать = Истина;
	Если Не ПорядокИсправления[ВидСчетаФактуры] 
		Или ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.НачислениеНДСпоСМРхозспособом") Тогда 
		ВозможноКорректировать = Ложь;	
	КонецЕсли;
	
	СтруктураРекизитов.Вставить("ВидСчетаФактуры", ВидСчетаФактуры);
	СтруктураРекизитов.Вставить("ПоследнееИсправление", ПоследнееИсправление);
	СтруктураРекизитов.Вставить("Основание", Основание);
	СтруктураРекизитов.Вставить("ВозможноКорректировать", ВозможноКорректировать);
	СтруктураРекизитов.Вставить("КорректироватьСчетФактуру", КорректироватьСчетФактуру);
	
	Возврат СтруктураРекизитов;
	
КонецФункции

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	РеквизитыДляОбработки 		= ПолучитьСтруктуруРеквизитовДляОбработки(ПараметрКоманды);
	ВозможноКорректировать 		= РеквизитыДляОбработки.ВозможноКорректировать;
	КорректироватьСчетФактуру	= РеквизитыДляОбработки.КорректироватьСчетФактуру;
	ОснованиеКорректировки 		= ?(КорректироватьСчетФактуру, РеквизитыДляОбработки.ПоследнееИсправление, РеквизитыДляОбработки.Основание);
	
	Если НЕ ВозможноКорректировать Тогда
		
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Для счета-фактуры с видом ""%1"" возможность корректировки не предусмотрена'"), РеквизитыДляОбработки.ВидСчетаФактуры);
		ВызватьИсключение ТекстСообщения;
			
	Иначе
		
			ПараметрыЗаполнения = Новый Структура("ДокументОснование, Видоперации", ОснованиеКорректировки, 
				ПредопределенноеЗначение("Перечисление.ВидыОперацийИсправленияПоступленияРеализации.СогласованноеИзменение"));
			ПараметрыФормы      = Новый Структура("ЗначенияЗаполнения", ПараметрыЗаполнения);
			
			ОткрытьФорму("Документ.КорректировкаРеализации.ФормаОбъекта", ПараметрыФормы, ПараметрыВыполненияКоманды.Источник, ПараметрыВыполненияКоманды.Уникальность, ПараметрыВыполненияКоманды.Окно);
			
	КонецЕсли;
	
КонецПроцедуры
