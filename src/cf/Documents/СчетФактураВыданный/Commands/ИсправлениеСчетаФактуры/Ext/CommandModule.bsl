
&НаСервере
Функция  ПолучитьСтруктуруРеквизитовДляОбработки(ДокументСсылка)
	
	СтруктураРекизитов = Новый Структура;
	
	РеквизитыСчетаФактуры =  ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументСсылка, "ВидСчетаФактуры, ДокументОснование");
	
	ВидСчетаФактуры = РеквизитыСчетаФактуры.ВидСчетаФактуры;
	ДокументОснование = РеквизитыСчетаФактуры.ДокументОснование;
	
	ПоследнееИсправление = УчетНДСПереопределяемый.ПолучитьПоследнееИсправлениеСчетаФактурыВыданного(ДокументСсылка);
	
	Основание = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПоследнееИсправление, "ДокументОснование");
	
	ИсправлятьСчетФактуру = Ложь;
	Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ОказаниеУслуг")
		Или ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ОтчетКомитентуОПродажах")
		Или (ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ОтчетКомиссионераОПродажах")
		И НЕ Документы.КорректировкаРеализации.ОтчетКомиссионераЗаполненПоСтарому(ДокументОснование))Тогда
		
		ИсправлятьСчетФактуру = Истина;
		
	КонецЕсли;

	ВозможноИсправить = Истина;
	ПорядокИсправления = Новый ФиксированноеСоответствие(Документы.СчетФактураВыданный.ПолучитьСоответствиеВидовСчетаФактурыПорядкуИсправления());
	
	Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.НачислениеНДСпоСМРхозспособом") Тогда 
		ВозможноИсправить = Ложь;	
	КонецЕсли;
		
	СтруктураРекизитов.Вставить("ВидСчетаФактуры", ВидСчетаФактуры);
	СтруктураРекизитов.Вставить("ПоследнееИсправление", ПоследнееИсправление);
	СтруктураРекизитов.Вставить("Основание", Основание);
	СтруктураРекизитов.Вставить("ПорядокИсправления", ПорядокИсправления);
	СтруктураРекизитов.Вставить("ВозможноИсправить", ВозможноИсправить);
	СтруктураРекизитов.Вставить("ИсправлятьСчетФактуру", ИсправлятьСчетФактуру);
	
	Возврат СтруктураРекизитов;
	
КонецФункции

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	РеквизитыДляОбработки 		= ПолучитьСтруктуруРеквизитовДляОбработки(ПараметрКоманды);
	ПорядокИсправления 			= РеквизитыДляОбработки.ПорядокИсправления;
	ВозможноИсправить 			= РеквизитыДляОбработки.ВозможноИсправить;
	ИсправлятьСчетФактуру	= РеквизитыДляОбработки.ИсправлятьСчетФактуру;
	ОснованиеИсправления 		= ?(ИсправлятьСчетФактуру, РеквизитыДляОбработки.ПоследнееИсправление, РеквизитыДляОбработки.Основание);
	
	Если Не ВозможноИсправить Тогда
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Для счета-фактуры с видом ""%1"" возможность исправления не предусмотрена'"), РеквизитыДляОбработки.ВидСчетаФактуры);
		ВызватьИсключение ТекстСообщения;
	КонецЕсли;
	
	Если НЕ ПорядокИсправления[РеквизитыДляОбработки.ВидСчетаФактуры] Тогда
		
		ПараметрыФормы = Новый Структура("Основание", РеквизитыДляОбработки.ПоследнееИсправление);
		ОткрытьФорму("Документ.СчетФактураВыданный.ФормаОбъекта", ПараметрыФормы, ПараметрыВыполненияКоманды.Источник, ПараметрыВыполненияКоманды.Уникальность, ПараметрыВыполненияКоманды.Окно);
		
	Иначе
		
		ПараметрыЗаполнения = Новый Структура("ДокументОснование, Видоперации", ОснованиеИсправления,
			ПредопределенноеЗначение("Перечисление.ВидыОперацийИсправленияПоступленияРеализации.ИсправлениеОшибки"));
		ПараметрыФормы      = Новый Структура("ЗначенияЗаполнения", ПараметрыЗаполнения);
		
		ОткрытьФорму("Документ.КорректировкаРеализации.ФормаОбъекта", ПараметрыФормы, ПараметрыВыполненияКоманды.Источник, ПараметрыВыполненияКоманды.Уникальность, ПараметрыВыполненияКоманды.Окно); 
		
	КонецЕсли;
	
КонецПроцедуры
