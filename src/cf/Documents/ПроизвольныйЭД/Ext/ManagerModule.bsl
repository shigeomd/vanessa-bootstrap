#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Функция СтатусОбъектаГотов(Объект) Экспорт
	
	Результат = (Объект.СтатусДокумента = Перечисления.СтатусыЭД.ПодготовленоПодтверждение
		ИЛИ Объект.СтатусДокумента = Перечисления.СтатусыЭД.ПодготовленКОтправке
		ИЛИ Объект.СтатусДокумента = Перечисления.СтатусыЭД.Подписан);
		
	Возврат Результат;
	
КонецФункции

Функция СтатусОбъектаНеГотов(Объект) Экспорт
	
	Результат = (Объект.СтатусДокумента = Перечисления.СтатусыЭД.Получен
		ИЛИ Объект.СтатусДокумента = Перечисления.СтатусыЭД.Сформирован);
		
	Возврат Результат;
		
КонецФункции

Функция СтатусОбъектаПередан(Объект) Экспорт
	
	Результат = (Объект.СтатусДокумента = Перечисления.СтатусыЭД.ПереданОператору
		ИЛИ Объект.СтатусДокумента = Перечисления.СтатусыЭД.Отправлен
		ИЛИ Объект.СтатусДокумента = Перечисления.СтатусыЭД.ПолученоПодтверждение
		ИЛИ Объект.СтатусДокумента = Перечисления.СтатусыЭД.ОтправленоПодтверждение
		ИЛИ Объект.СтатусДокумента = Перечисления.СтатусыЭД.ДоставленоПодтверждение);
		
	Возврат Результат;
	
КонецФункции

Функция СтатусОбъектаОтклонен(Объект) Экспорт
	
	Результат = (Объект.СтатусДокумента = Перечисления.СтатусыЭД.Отклонен
		ИЛИ Объект.СтатусДокумента = Перечисления.СтатусыЭД.ОтклоненПолучателем);
		
	Возврат Результат;
	
КонецФункции

Функция МожноПодписывать(ОбъектЭД) Экспорт
	
	НаправлениеИсходящий = (ОбъектЭД.НаправлениеЭД = Перечисления.НаправленияЭД.Исходящий);
	ТребуетсяПодтверждение = ОбъектЭД.ВладелецФайла.ТребуетсяПодтверждение;
	СтатусПередан = СтатусОбъектаПередан(ОбъектЭД.ВладелецФайла);
	СтатусОтклонен = СтатусОбъектаОтклонен(ОбъектЭД.ВладелецФайла);
	
	Результат = (НЕ (СтатусПередан ИЛИ СтатусОтклонен) И (НаправлениеИсходящий ИЛИ ТребуетсяПодтверждение));
	
	Возврат Результат;
	
КонецФункции

Функция МожноОтправлять(ОбъектЭД) Экспорт
	
	НаправлениеИсходящий = (ОбъектЭД.НаправлениеЭД = Перечисления.НаправленияЭД.Исходящий);
	ТребуетсяПодтверждение = ОбъектЭД.ВладелецФайла.ТребуетсяПодтверждение;
	СтатусГотов = СтатусОбъектаГотов(ОбъектЭД.ВладелецФайла);
	
	Результат = (СтатусГотов И (НаправлениеИсходящий ИЛИ ТребуетсяПодтверждение));
	
	Возврат Результат;
	
КонецФункции

// Обработчик обновления БЭД 1.1.14.2
// Заполняет тип документа
//
Процедура ЗаполнитьТипДокумента() Экспорт
	
	ЭлементСсылка = Документы.ПроизвольныйЭД.Выбрать();
	
	Пока ЭлементСсылка.Следующий() Цикл
		Если НЕ ЗначениеЗаполнено(ЭлементСсылка.ТипДокумента) Тогда
			Попытка
				ЭлементОбъект = ЭлементСсылка.ПолучитьОбъект();
				ЭлементОбъект.ТипДокумента = Перечисления.ТипыЭД.Прочее;
				ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ЭлементОбъект);
			Исключение
			КонецПопытки;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецЕсли