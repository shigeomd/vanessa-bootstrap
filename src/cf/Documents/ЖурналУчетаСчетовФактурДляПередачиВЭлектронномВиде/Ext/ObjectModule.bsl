#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

// Функция выгружает документ и возвращает свойства файла выгрузки.
//
// Параметры:
//  Ссылка - ссылка на отчет (документ).
//
// Результат:
//	Массив структур или Неопределено, если не удалось сформировать файл выгрузки.
//	Ключи структуры:
//		- АдресФайлаВыгрузки - адрес двоичных данных файла выгрузки во временном хранилище
//		- ИмяФайлаВыгрузки - короткое имя файла выгрузки (с расширением)
//
Функция ВыгрузитьДокумент(УникальныйИдентификатор = Неопределено) Экспорт
	
	Результат = Неопределено;
	
	УстановитьПривилегированныйРежим(Истина);
	
	СодержаниеВыгрузки = ЭлектронноеПредставление();
	
	Для Каждого ЭлементВыгрузки Из СодержаниеВыгрузки Цикл
		
		Если Результат = Неопределено Тогда
			Результат = Новый Массив;
		КонецЕсли;
		
		ИмяВременногоФайла = ПолучитьИмяВременногоФайла();
		СохраняемыйФайл = Новый ТекстовыйДокумент;
		СохраняемыйФайл.УстановитьТекст(ЭлементВыгрузки.ТекстФайла);
		СохраняемыйФайл.Записать(ИмяВременногоФайла, ЭлементВыгрузки.КодировкаТекста);
		
		ФайлВыгрузки = Новый ДвоичныеДанные(ИмяВременногоФайла);
		Если УникальныйИдентификатор <> Неопределено Тогда
			СсылкаНаДвоичныеДанныеФайла = ПоместитьВоВременноеХранилище(ФайлВыгрузки, УникальныйИдентификатор);
		Иначе
			СсылкаНаДвоичныеДанныеФайла = ПоместитьВоВременноеХранилище(ФайлВыгрузки);
		КонецЕсли;
		
		СтруктураВыгрузки = Новый Структура;
		СтруктураВыгрузки.Вставить("АдресФайлаВыгрузки", СсылкаНаДвоичныеДанныеФайла);
		СтруктураВыгрузки.Вставить("ИмяФайлаВыгрузки", ЭлементВыгрузки.ИмяФайла);
		
		Результат.Добавить(СтруктураВыгрузки);
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	ЗаполнениеДокументов.Заполнить(ЭтотОбъект, ДанныеЗаполнения);
	НалоговыйПериод = НачалоКвартала(Дата); 
	ПериодПоСКНП 	= УчетНДСКлиентСервер.ПолучитьКодПоСКНП(НалоговыйПериод, Реорганизация);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ПометкаУдаления Тогда
		Возврат;
	КонецЕсли;
	
	НалоговыйПериод		= НачалоКвартала(НалоговыйПериод);
	ПериодСоставления	= НалоговыйПериод;
	
	НайденныеДокументы	= Документы.ЖурналУчетаСчетовФактурДляПередачиВЭлектронномВиде.НайтиДокументыЗаНалоговыйПериод(Организация, НалоговыйПериод);
	Если НЕ НайденныеДокументы = Неопределено Тогда
		
		Для каждого Документ Из НайденныеДокументы Цикл
			
			Если НЕ Документ = Ссылка Тогда
				ТекстСообщения	= СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Уже имеется оформленный журнал учета счетов-фактур за %1'"),
					ПредставлениеПериода(НачалоКвартала(НалоговыйПериод), КонецКвартала(НалоговыйПериод), "ФП = Истина"));
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Документ, , , Отказ);
				Прервать;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Дата			= НачалоДня(ОбщегоНазначенияБП.ПолучитьРабочуюДату());
	НалоговыйПериод	= НачалоКвартала(Дата); 
	Ответственный	= Пользователи.ТекущийПользователь();

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Формирует сведения необходимые для сохранения и передачи файла (файлов) электронного
// представления документа.
//
// Возвращаемое значение:
//  ТаблицаЗначений - сведения электронного представления документа, включающие в себя
//                    имя файла(файлов), текст(тексты) и кодировку представления.
//
Функция ЭлектронноеПредставление()
	
	ПроизвольнаяСтрока = Новый ОписаниеТипов("Строка");
	
	СведенияЭлектронногоПредставления = Новый ТаблицаЗначений;
	СведенияЭлектронногоПредставления.Колонки.Добавить("ИмяФайла", ПроизвольнаяСтрока);
	СведенияЭлектронногоПредставления.Колонки.Добавить("ТекстФайла", ПроизвольнаяСтрока);
	СведенияЭлектронногоПредставления.Колонки.Добавить("КодировкаТекста", ПроизвольнаяСтрока);
	
	ОсновныеСведения = ОсновныеСведенияЭлектронногоПредставления();
	
	СтруктураВыгрузки = ИзвлечьСтруктуруXML();
	ЗаполнитьДанными(СтруктураВыгрузки, ОсновныеСведения);
	
	Текст = ВыгрузитьДеревоВXML(СтруктураВыгрузки, ОсновныеСведения);
	
	СтрокаСведенийЭлектронногоПредставления = СведенияЭлектронногоПредставления.Добавить();
	СтрокаСведенийЭлектронногоПредставления.ИмяФайла = ОсновныеСведения.ИдФайл + ".xml";
	СтрокаСведенийЭлектронногоПредставления.ТекстФайла = Текст;
	СтрокаСведенийЭлектронногоПредставления.КодировкаТекста = "windows-1251";
	
	Если СведенияЭлектронногоПредставления.Количество() = 0 Тогда
		СведенияЭлектронногоПредставления = Неопределено;
	КонецЕсли;
	Возврат СведенияЭлектронногоПредставления;
	
КонецФункции

Функция ОсновныеСведенияЭлектронногоПредставления()
	
	ОсновныеСведения = Новый Структура;
	
	ОсновныеСведения.Вставить("ДатаФормированияФайла", ТекущаяДата());
	
	ЭтоПБОЮЛ = НЕ РегламентированнаяОтчетностьПереопределяемый.ЭтоЮридическоеЛицо(Организация);
	ОсновныеСведения.Вставить("ЭтоПБОЮЛ", ЭтоПБОЮЛ);
	ОсновныеСведения.Вставить("ЭтоЮЛ", НЕ ЭтоПБОЮЛ);
	ОсновныеСведения.Вставить("ЭтоИП", ЭтоПБОЮЛ);
	
	СведенияОбОрганизации = СведенияОбОрганизации();
	
	Если ЭтоПБОЮЛ Тогда
		ОсновныеСведения.Вставить("ИННФЛ", СведенияОбОрганизации.ИННЮЛ);
		СведенияОЮрФизЛице  = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(Организация, ТекущаяДата());
		СвидетельствоОРегистрации = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОЮрФизЛице, "Свидетельство");
		ОсновныеСведения.Вставить("СвГосРегИП", СвидетельствоОРегистрации);
		
		СведенияОбИП = СведенияОбИП();
		ОсновныеСведения.Вставить("ФамилияИП",  СведенияОбИП.Фамилия);
		ОсновныеСведения.Вставить("ИмяИП",      СведенияОбИП.Имя);
		ОсновныеСведения.Вставить("ОтчествоИП", СведенияОбИП.Отчество);
	Иначе
		ОсновныеСведения.Вставить("НаимОрг", СведенияОбОрганизации.НаимЮЛПол);
		ОсновныеСведения.Вставить("ИННЮЛ", СведенияОбОрганизации.ИННЮЛ);
		ОсновныеСведения.Вставить("КПП", СведенияОбОрганизации.КППЮЛ);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ИФНС) Тогда
		ОсновныеСведения.Вставить("КодНО", ИФНС.Код);
	Иначе
		ОсновныеСведения.Вставить("КодНО", СведенияОбОрганизации.КодНО);
	КонецЕсли;
	
	ДобавитьСведенияОПодписанте(ОсновныеСведения);
	
	ОсновныеСведения.Вставить("Период", ПериодПоСКНП);
	
	ОтчетныйГод = Формат(НалоговыйПериод, "ДФ=yyyy");
	ОсновныеСведения.Вставить("ОтчГод", ОтчетныйГод);
	
	ИдентификаторФайла = ИдентификаторФайлаЭлектронногоПредставления(ОсновныеСведения);
	ОсновныеСведения.Вставить("ИдФайл", ИдентификаторФайла);
	
	Возврат ОсновныеСведения;
	
КонецФункции

Функция СведенияОбОрганизации()
	
	СтрокаСведений = "ИННЮЛ, КППЮЛ, НаимЮЛПол, КодНО, ТелОрганизации, ФИО, ОКВЭД, ОКАТО";
	СведенияОбОрганизации = РегламентированнаяОтчетностьВызовСервера.ПолучитьСведенияОбОрганизации(Организация, ТекущаяДата(), СтрокаСведений);
	
	Возврат СведенияОбОрганизации;
	
КонецФункции

Процедура ДобавитьСведенияОПодписанте(ОсновныеСведения)
	
	Если ЗначениеЗаполнено(ИФНС) И ЗначениеЗаполнено(ИФНС.Представитель) Тогда
		Если ТипЗнч(ИФНС.Представитель) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
			ФИОПодписанта = СведенияОФизЛице(ИФНС.Представитель);
			ОсновныеСведения.Вставить("ПрПодп", "2");
			ОсновныеСведения.Вставить("ПодпФамилия",  ФИОПодписанта.Фамилия);
			ОсновныеСведения.Вставить("ПодпИмя",      ФИОПодписанта.Имя);
			ОсновныеСведения.Вставить("ПодпОтчество", ФИОПодписанта.Отчество);
			ОсновныеСведения.Вставить("НаимДокПодп",  ИФНС.ДокументПредставителя);
			
		ИначеЕсли НЕ ПустаяСтрока(ИФНС.УполномоченноеЛицоПредставителя) Тогда
			ОсновныеСведения.Вставить("ПрПодп", "2");
			СоставляющиеФИО = СоставляющиеФИО(ИФНС.УполномоченноеЛицоПредставителя);
			ОсновныеСведения.Вставить("ПодпФамилия",  СоставляющиеФИО.Фамилия);
			ОсновныеСведения.Вставить("ПодпИмя",      СоставляющиеФИО.Имя);
			ОсновныеСведения.Вставить("ПодпОтчество", СоставляющиеФИО.Отчество);
			ОсновныеСведения.Вставить("НаимДокПодп",  ИФНС.ДокументПредставителя);
			
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ ОсновныеСведения.Свойство("ПрПодп") ИЛИ (ОсновныеСведения.Свойство("ПрПодп") И ОсновныеСведения.ПрПодп <> "2") Тогда
		Если ЗначениеЗаполнено(Подписант) Тогда
			ФИОПодписанта = СведенияОФизЛице(Подписант);
			ОсновныеСведения.Вставить("ПрПодп", "1");
			ОсновныеСведения.Вставить("ПодпФамилия",  ФИОПодписанта.Фамилия);
			ОсновныеСведения.Вставить("ПодпИмя",      ФИОПодписанта.Имя);
			ОсновныеСведения.Вставить("ПодпОтчество", ФИОПодписанта.Отчество);
			
		Иначе
			ФИОПодписанта = СведенияОРуководителе();
			ОсновныеСведения.Вставить("ПрПодп", "1");
			ОсновныеСведения.Вставить("ПодпФамилия",  ФИОПодписанта.Фамилия);
			ОсновныеСведения.Вставить("ПодпИмя",      ФИОПодписанта.Имя);
			ОсновныеСведения.Вставить("ПодпОтчество", ФИОПодписанта.Отчество);
			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Функция СведенияОРуководителе()
	
	Результат = Новый Структура("Фамилия, Имя, Отчество", "", "", "");
	
	РеквизитыОтветственныхЛиц = ОтветственныеЛицаБП.ОтветственныеЛица(Организация, Дата);
	Если ЗначениеЗаполнено(РеквизитыОтветственныхЛиц.Руководитель) Тогда
		Результат.Фамилия 	= СокрЛП(РеквизитыОтветственныхЛиц.РуководительФИО.Фамилия);
		Результат.Имя 		= СокрЛП(РеквизитыОтветственныхЛиц.РуководительФИО.Имя);
		Результат.Отчество	= СокрЛП(РеквизитыОтветственныхЛиц.РуководительФИО.Отчество);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция СведенияОФизЛице(ФизЛицо)
	
	Результат = Новый Структура("Фамилия, Имя, Отчество", "", "", "");
	
	Если ЗначениеЗаполнено(ФизЛицо) Тогда
		ДанныеФЛ = РегистрыСведений.ФИОФизическихЛиц.СрезПоследних(Дата, Новый Структура("ФизическоеЛицо", ФизЛицо));
		Если ДанныеФЛ.Количество() > 0 Тогда
			Результат.Фамилия  = СокрЛП(ДанныеФЛ[0].Фамилия);
			Результат.Имя      = СокрЛП(ДанныеФЛ[0].Имя);
			Результат.Отчество = СокрЛП(ДанныеФЛ[0].Отчество);
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция СоставляющиеФИО(Знач ФИОСтр)
	
	ФИОСтр = СокрЛП(ФИОСтр);
	ФИО = Новый Структура("Фамилия, Имя, Отчество", "", "", "");
	
	ПервыйПробел = Найти(ФИОСтр, " ");
	Если ПервыйПробел = 0 Тогда
		ФИО.Фамилия = ФИОСтр;
		Возврат ФИО;
	КонецЕсли;
	ФИО.Фамилия = СокрЛП(Лев(ФИОСтр, ПервыйПробел - 1));
	ФИОСтр = СокрЛП(Сред(ФИОСтр, ПервыйПробел + 1));
	
	ВторойПробел = Найти(ФИОСтр, " ");
	Если ВторойПробел = 0 Тогда
		ФИО.Имя = ФИОСтр;
		Возврат ФИО;
	КонецЕсли;
	ФИО.Имя = СокрЛП(Лев(ФИОСтр, ВторойПробел - 1));
	
	ФИО.Отчество = СокрЛП(Сред(ФиоСтр, ВторойПробел + 1));
	
	Возврат ФИО;
	
КонецФункции

Функция СведенияОбИП()
	
	Результат = Новый Структура("Фамилия, Имя, Отчество", "", "", "");
	
	ФЛ = Организация.ИндивидуальныйПредприниматель;
	Если ФЛ = Справочники.ФизическиеЛица.ПустаяСсылка() Тогда
		ФИОИП = Организация.НаименованиеПолное;
	Иначе
		ДанныеФЛ = РегистрыСведений.ФИОФизическихЛиц.СрезПоследних(Дата, Новый Структура("ФизическоеЛицо", ФЛ));
		Если ДанныеФЛ.Количество() > 0 Тогда
			Результат.Фамилия = СокрЛП(ДанныеФЛ[0].Фамилия);
			Результат.Имя = СокрЛП(ДанныеФЛ[0].Имя);
			Результат.Отчество = СокрЛП(ДанныеФЛ[0].Отчество);
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ИдентификаторФайлаЭлектронногоПредставления(СведенияОтправки)
	
	Префикс = "1115104";
	Если СведенияОтправки.ЭтоПБОЮЛ Тогда
		ИдентификаторОтправителя = СокрЛП(СведенияОтправки.ИННФЛ);
	Иначе
		ИдентификаторОтправителя = СокрЛП(СведенияОтправки.ИННЮЛ) + СокрЛП(СведенияОтправки.КПП);
	КонецЕсли;
	ИдентификаторПолучателя = СведенияОтправки.КодНО;
	ИдентификационныйНомер1 = Строка(Новый УникальныйИдентификатор);
	ДатаФормированияФайла = Формат(СведенияОтправки.ДатаФормированияФайла, "ДФ=yyyyMMdd");
	ИдентификационныйНомер2 = Строка(Новый УникальныйИдентификатор);
	
	ИдентификаторФайла = Префикс
	                   + "_" + ИдентификаторОтправителя
	                   + "_" + ИдентификаторПолучателя
	                   + "_" + ИдентификационныйНомер1
	                   + "_" + ДатаФормированияФайла
	                   + "_" + ИдентификационныйНомер2;
	
	Возврат ИдентификаторФайла;
	
КонецФункции

Функция ИзвлечьСтруктуруXML()
	
	ДеревоСтруктуры = Новый ДеревоЗначений;
	ДеревоСтруктуры.Колонки.Добавить("Код");
	ДеревоСтруктуры.Колонки.Добавить("ЗначениеПоУмолчанию");
	ДеревоСтруктуры.Колонки.Добавить("Значение");
	ДеревоСтруктуры.Колонки.Добавить("Представление");
	ДеревоСтруктуры.Колонки.Добавить("Тип");
	ДеревоСтруктуры.Колонки.Добавить("Формат");
	ДеревоСтруктуры.Колонки.Добавить("МинРазмерность");
	ДеревоСтруктуры.Колонки.Добавить("МаксРазмерность");
	ДеревоСтруктуры.Колонки.Добавить("Обязательность");
	ДеревоСтруктуры.Колонки.Добавить("Многостраничность");
	ДеревоСтруктуры.Колонки.Добавить("Многострочность");
	ДеревоСтруктуры.Колонки.Добавить("Раздел");
	ДеревоСтруктуры.Колонки.Добавить("Ключ");
	ДеревоСтруктуры.Колонки.Добавить("Условие");
	ДеревоСтруктуры.Колонки.Добавить("Показатели");
	
	Макет = ПолучитьМакет("СхемаВыгрузки501");
	ВысотаТаблицы = Макет.ВысотаТаблицы;
	
	УчтенныеГруппы = Новый Соответствие;
	
	Для Уровень = 0 По Макет.КоличествоУровнейГруппировокСтрок() - 1 Цикл
		Макет.ПоказатьУровеньГруппировокСтрок(Уровень);
		Для НомерСтроки = 2 По ВысотаТаблицы Цикл
			НомСтр = ВысотаТаблицы - НомерСтроки + 2;
			Если Макет.Область(НомСтр, 0, НомСтр, 0).Видимость И УчтенныеГруппы.Получить(НомСтр) = Неопределено Тогда
				
				РодительскийУзел = ДеревоСтруктуры;
				Если Уровень <> 0 Тогда
					Для Инд = 1 По НомСтр - 2 Цикл
						Узел = УчтенныеГруппы.Получить(НомСтр - Инд);
						Если Узел <> Неопределено Тогда
							РодительскийУзел = Узел;
							Прервать;
						КонецЕсли;
					КонецЦикла;
				КонецЕсли;
				
				НовСтр = РодительскийУзел.Строки.Вставить(0);
				НовСтр.Код = СокрЛП(Макет.Область(НомСтр, 1, НомСтр, 1).Текст);
				НовСтр.Раздел = СокрЛП(Макет.Область(НомСтр, 2, НомСтр, 2).Текст);
				НовСтр.Ключ = СокрЛП(Макет.Область(НомСтр, 3, НомСтр, 3).Текст);
				НовСтр.Тип = СокрЛП(Макет.Область(НомСтр, 4, НомСтр, 4).Текст);
				НовСтр.Формат = СокрЛП(Макет.Область(НомСтр, 5, НомСтр, 5).Текст);
				МинРазмерность = СокрЛП(Макет.Область(НомСтр, 6, НомСтр, 6).Текст);
				НовСтр.МинРазмерность = ?(ПустаяСтрока(МинРазмерность), ?(НовСтр.Формат = "N", 99999, 0), Число(МинРазмерность));
				МаксРазмерность = СокрЛП(Макет.Область(НомСтр, 7, НомСтр, 7).Текст);
				НовСтр.МаксРазмерность = ?(ПустаяСтрока(МаксРазмерность), 99999, Число(МаксРазмерность));
				НовСтр.Обязательность = СокрЛП(Макет.Область(НомСтр, 8, НомСтр, 8).Текст);
				НовСтр.Многостраничность = НЕ ПустаяСтрока(Макет.Область(НомСтр, 9, НомСтр, 9).Текст);
				НовСтр.Многострочность = НЕ ПустаяСтрока(Макет.Область(НомСтр, 10, НомСтр, 10).Текст);
				НовСтр.Условие = СокрЛП(Макет.Область(НомСтр, 11, НомСтр, 11).Текст);
				НовСтр.ЗначениеПоУмолчанию = СокрЛП(Макет.Область(НомСтр, 12, НомСтр, 12).Текст);
				НовСтр.Представление = СокрЛП(Макет.Область(НомСтр, 13, НомСтр, 13).Текст);
				
				УчтенныеГруппы.Вставить(НомСтр, НовСтр);
				
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Возврат ДеревоСтруктуры;
	
КонецФункции

Процедура ЗаполнитьДанными(ДеревоВыгрузки, Параметры)
	
	ОбработатьУсловныеЭлементы(Параметры, ДеревоВыгрузки);
	ЗаполнитьПараметры(Параметры, ДеревоВыгрузки);
	ЗаполнитьДаннымиТабличнойЧастиВыставленныеСФ(Параметры, ДеревоВыгрузки);
	ЗаполнитьДаннымиТабличнойЧастиПолученныеСФ(Параметры, ДеревоВыгрузки);
	ОтсечьНезаполненныеНеобязательныеУзлы(ДеревоВыгрузки);
	
КонецПроцедуры

Процедура ОбработатьУсловныеЭлементы(Знач Параметры, Узел)
	
	КоличествоСтрок = Узел.Строки.Количество();
	Для Инд = 1 По КоличествоСтрок Цикл
		ТекСтр = Узел.Строки.Получить(КоличествоСтрок - Инд);
		Если НЕ ПустаяСтрока(ТекСтр.Условие) Тогда
			Если НЕ УсловиеВыполнено(Параметры, ТекСтр.Условие) Тогда
				Узел.Строки.Удалить(ТекСтр);
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		ОбработатьУсловныеЭлементы(Параметры, ТекСтр);
	КонецЦикла;
	
КонецПроцедуры

Функция УсловиеВыполнено(Параметры, Условие)
	
	Попытка
		РезультатВычисленияВыражения = Вычислить(СтрЗаменить(Условие, "&", "Параметры."));
		Возврат НЕ (РезультатВычисленияВыражения = Ложь);
	Исключение
		Возврат Истина;
	КонецПопытки;
	
КонецФункции

Процедура ЗаполнитьПараметры(Параметры, Узел)
	
	Для Каждого Стр из Узел.Строки Цикл
		Если Стр.Тип = "С" ИЛИ Стр.Тип = "C" Тогда // учтем оба варианта: кириллицу и латиницу
			ЗаполнитьПараметры(Параметры, Стр);
		Иначе
			Если ПустаяСтрока(Стр.ЗначениеПоУмолчанию) Тогда
				Если НЕ ПустаяСтрока(Стр.Ключ) Тогда
					ВывестиПоказательВXML(Стр, Параметры[Стр.Ключ]);
				Иначе
					Стр.Значение = "";
				КонецЕсли;
			ИначеЕсли Лев(Стр.ЗначениеПоУмолчанию, 1) = "&" Тогда
				ИмяПараметра = Сред(Стр.ЗначениеПоУмолчанию, 2);
				Если Параметры.Свойство(ИмяПараметра) Тогда
					ВывестиПоказательВXML(Стр, Параметры[ИмяПараметра]);
				КонецЕсли;
			Иначе
				Стр.Значение = Стр.ЗначениеПоУмолчанию;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Функция ВывестиПоказательВXML(Узел, ЗначениеПоказателя)
	
	МинШирина = Узел.МинРазмерность;
	МаксШирина = Узел.МаксРазмерность;
	
	Если Узел.Формат = "T" ИЛИ Узел.Формат = "Т" Тогда // учтем оба варианта: кириллицу и латиницу
		Узел.Значение = ?(МаксШирина < СтрДлина(СокрЛП(ЗначениеПоказателя)), СокрЛП(Лев(СокрЛП(ЗначениеПоказателя), МаксШирина)), СокрЛП(ЗначениеПоказателя));
	ИначеЕсли Узел.Формат = "N" Тогда
		СтрокаФормата = "ЧРД=.;ЧН=0;ЧГ=;";
		Если Узел.МаксРазмерность <> 0 И Узел.МаксРазмерность <> 99999 Тогда
			СтрокаФормата = СтрокаФормата + "ЧЦ=" + Формат(Узел.МаксРазмерность, "ЧГ=") + ";";
		КонецЕсли;
		Если Узел.МинРазмерность <> 99999 Тогда
			СтрокаФормата = СтрокаФормата + "ЧДЦ=" + Формат(Узел.МинРазмерность, "ЧГ=") + ";";
		КонецЕсли;
		Узел.Значение = СокрЛП(Формат(ЗначениеПоказателя, СтрокаФормата));
	ИначеЕсли Узел.Формат = "gYear" Тогда
		Если ТипЗнч(ЗначениеПоказателя) = Тип("Дата") Тогда
			Узел.Значение = СокрЛП(Формат(ЗначениеПоказателя, "ДФ=гггг"));
		Иначе
			Узел.Значение = Прав(СокрЛП(ЗначениеПоказателя), 4);
		КонецЕсли;
	КонецЕсли;
	
КонецФункции

Процедура ЗаполнитьДаннымиТабличнойЧастиВыставленныеСФ(Параметры, ДеревоВыгрузки)
	
	Узел_Документ = ПолучитьПодчиненныйЭлемент(ДеревоВыгрузки, "Документ");
	
	ПрототипУзла_СвЧ1 = ПолучитьПодчиненныйЭлемент(Узел_Документ, "СвЧ1");
	Для Каждого СтрокаТабличнойЧасти Из ВыставленныеСчетаФактуры Цикл
		Узел_СвЧ1 = НовыйУзелИзПрототипа(ПрототипУзла_СвЧ1);
		
		УстановитьЗначениеЭлемента(Узел_СвЧ1, "НомПП", СтрокаТабличнойЧасти.Ном);
		УстановитьЗначениеЭлемента(Узел_СвЧ1, "ДатаВыстСчФ", Формат(СтрокаТабличнойЧасти.ДатаПередачиПолучения, "ДФ=dd.MM.yyyy"));
		УстановитьЗначениеЭлемента(Узел_СвЧ1, "КодСпосВыст", СтрокаТабличнойЧасти.КодСпособаВыставления);
		
		УстановитьЗначениеЭлемента(Узел_СвЧ1, "НомерСчФ", СтрокаТабличнойЧасти.НомерСчетаФактуры);
		УстановитьЗначениеЭлемента(Узел_СвЧ1, "ДатаСчФ", Формат(СтрокаТабличнойЧасти.ДатаСчетаФактуры, "ДФ=dd.MM.yyyy"));
		
		УстановитьЗначениеЭлемента(Узел_СвЧ1, "НомерКСчФ", СтрокаТабличнойЧасти.НомерКорректировочногоСчетаФактуры);
		УстановитьЗначениеЭлемента(Узел_СвЧ1, "ДатаКСчФ", Формат(СтрокаТабличнойЧасти.ДатаКорректировочногоСчетаФактуры, "ДФ=dd.MM.yyyy"));
		
		УстановитьЗначениеЭлемента(Узел_СвЧ1, "НомИспрСчФ", СтрокаТабличнойЧасти.НомерИсправления);
		УстановитьЗначениеЭлемента(Узел_СвЧ1, "ДатаИспрСчФ", Формат(СтрокаТабличнойЧасти.ДатаИсправления, "ДФ=dd.MM.yyyy"));
		
		УстановитьЗначениеЭлемента(Узел_СвЧ1, "НаимПок", СтрокаТабличнойЧасти.КонтрагентНаименование);
		РеквизитыПокупателя = РасщепленнаяСтрока(СтрокаТабличнойЧасти.КонтрагентИННКПП, "/");
		ИНН = СокрЛП(РеквизитыПокупателя[0]);
		КПП = СокрЛП(РеквизитыПокупателя[1]);
		Если СтрДлина(ИНН) = 12 Тогда
			УстановитьЗначениеЭлемента(Узел_СвЧ1, "ИННФЛ", ИНН);
		ИНаче
			УстановитьЗначениеЭлемента(Узел_СвЧ1, "ИННЮЛ", ИНН);
			УстановитьЗначениеЭлемента(Узел_СвЧ1, "КПП", КПП);
		КонецЕсли;
		
		РеквизитыВалюты = РасщепленнаяСтрока(СтрокаТабличнойЧасти.Валюта, ",");
		КодОКВ = СокрЛП(РеквизитыВалюты[1]);
		УстановитьЗначениеЭлемента(Узел_СвЧ1, "КодОКВ", КодОКВ);
		
		УстановитьЗначениеЭлемента(Узел_СвЧ1, "СтТовУчНалВсего", СтрокаТабличнойЧасти.СуммаДокумента);
		
		КодыОпераций = РазложитьСтрокуВМассивПодстрок(СтрокаТабличнойЧасти.КодВидаОперации, ";");
		ПрототипУзла_КодВидОпер = ПолучитьПодчиненныйЭлемент(Узел_СвЧ1, "КодВидОпер");
		УдалятьПрототипУзла_КодВидОпер = Ложь;
		Для Каждого КодОперации Из КодыОпераций Цикл
			Если НЕ ПустаяСтрока(КодыОпераций) Тогда
				Узел_КодВидОпер = НовыйУзелИзПрототипа(ПрототипУзла_КодВидОпер);
				ВывестиПоказательВXML(Узел_КодВидОпер, СокрЛП(КодОперации));
				УдалятьПрототипУзла_КодВидОпер = Истина;
			КонецЕсли;
		КонецЦикла;
		Если УдалятьПрототипУзла_КодВидОпер Тогда
			УдалитьУзел(ПрототипУзла_КодВидОпер);
		КонецЕсли;
		
		Узел_ВтчСумНДС = ПолучитьПодчиненныйЭлемент(Узел_СвЧ1, "ВтчСумНДС");
		УстановитьЗначениеЭлемента(Узел_ВтчСумНДС, "СумНДС", ПредставлениеСуммыНДС(СтрокаТабличнойЧасти.СуммаНДС));
		
		Узел_РазнСтоимКСчФ = ПолучитьПодчиненныйЭлемент(Узел_СвЧ1, "РазнСтоимКСчФ");
		УстановитьЗначениеЭлемента(Узел_РазнСтоимКСчФ, "Ум", СтрокаТабличнойЧасти.СуммаДокументаРазницаУменьшение);
		УстановитьЗначениеЭлемента(Узел_РазнСтоимКСчФ, "Увел", СтрокаТабличнойЧасти.СуммаДокументаРазницаУвеличение);
		
		Узел_РазнНДСКСчФ = ПолучитьПодчиненныйЭлемент(Узел_СвЧ1, "РазнНДСКСчФ");
		Если СтрокаТабличнойЧасти.СуммаНДСРазницаУменьшение <> 0 Тогда
			Узел_РазнНДСКСчФ_Ум = ПолучитьПодчиненныйЭлемент(Узел_РазнНДСКСчФ, "Ум");
			УстановитьЗначениеЭлемента(Узел_РазнНДСКСчФ_Ум, "СумНДС", ПредставлениеСуммыНДС(СтрокаТабличнойЧасти.СуммаНДСРазницаУменьшение));
		КонецЕсли;
		Если СтрокаТабличнойЧасти.СуммаНДСРазницаУвеличение <> 0 Тогда
			Узел_РазнНДСКСчФ_Увел = ПолучитьПодчиненныйЭлемент(Узел_РазнНДСКСчФ, "Увел");
			УстановитьЗначениеЭлемента(Узел_РазнНДСКСчФ_Увел, "СумНДС", ПредставлениеСуммыНДС(СтрокаТабличнойЧасти.СуммаНДСРазницаУвеличение));
		КонецЕсли;
		
	КонецЦикла;
	УдалитьУзел(ПрототипУзла_СвЧ1);
	
КонецПроцедуры

Процедура ЗаполнитьДаннымиТабличнойЧастиПолученныеСФ(Параметры, ДеревоВыгрузки)
	
	Узел_Документ = ПолучитьПодчиненныйЭлемент(ДеревоВыгрузки, "Документ");
	
	ПрототипУзла_СвЧ2 = ПолучитьПодчиненныйЭлемент(Узел_Документ, "СвЧ2");
	Для Каждого СтрокаТабличнойЧасти Из ПолученныеСчетаФактуры Цикл
		Узел_СвЧ2 = НовыйУзелИзПрототипа(ПрототипУзла_СвЧ2);
		
		УстановитьЗначениеЭлемента(Узел_СвЧ2, "НомПП", СтрокаТабличнойЧасти.Ном);
		УстановитьЗначениеЭлемента(Узел_СвЧ2, "ДатаПолСчФ", Формат(СтрокаТабличнойЧасти.ДатаПередачиПолучения, "ДФ=dd.MM.yyyy"));
		УстановитьЗначениеЭлемента(Узел_СвЧ2, "КодСпосВыст", СтрокаТабличнойЧасти.КодСпособаВыставления);
		
		УстановитьЗначениеЭлемента(Узел_СвЧ2, "НомерСчФ", СтрокаТабличнойЧасти.НомерСчетаФактуры);
		УстановитьЗначениеЭлемента(Узел_СвЧ2, "ДатаСчФ", Формат(СтрокаТабличнойЧасти.ДатаСчетаФактуры, "ДФ=dd.MM.yyyy"));
		
		УстановитьЗначениеЭлемента(Узел_СвЧ2, "НомерКСчФ", СтрокаТабличнойЧасти.НомерКорректировочногоСчетаФактуры);
		УстановитьЗначениеЭлемента(Узел_СвЧ2, "ДатаКСчФ", Формат(СтрокаТабличнойЧасти.ДатаКорректировочногоСчетаФактуры, "ДФ=dd.MM.yyyy"));
		
		УстановитьЗначениеЭлемента(Узел_СвЧ2, "НомИспрСчФ", СтрокаТабличнойЧасти.НомерИсправления);
		УстановитьЗначениеЭлемента(Узел_СвЧ2, "ДатаИспрСчФ", Формат(СтрокаТабличнойЧасти.ДатаИсправления, "ДФ=dd.MM.yyyy"));
		
		УстановитьЗначениеЭлемента(Узел_СвЧ2, "НаимПрод", СтрокаТабличнойЧасти.КонтрагентНаименование);
		РеквизитыПокупателя = РасщепленнаяСтрока(СтрокаТабличнойЧасти.КонтрагентИННКПП, "/");
		ИНН = СокрЛП(РеквизитыПокупателя[0]);
		КПП = СокрЛП(РеквизитыПокупателя[1]);
		Если СтрДлина(ИНН) = 12 Тогда
			УстановитьЗначениеЭлемента(Узел_СвЧ2, "ИННФЛ", ИНН);
		ИНаче
			УстановитьЗначениеЭлемента(Узел_СвЧ2, "ИННЮЛ", ИНН);
			УстановитьЗначениеЭлемента(Узел_СвЧ2, "КПП", КПП);
		КонецЕсли;
		
		РеквизитыВалюты = РасщепленнаяСтрока(СтрокаТабличнойЧасти.Валюта, ",");
		КодОКВ = СокрЛП(РеквизитыВалюты[1]);
		УстановитьЗначениеЭлемента(Узел_СвЧ2, "КодОКВ", КодОКВ);
		
		УстановитьЗначениеЭлемента(Узел_СвЧ2, "СтТовУчНалВсего", СтрокаТабличнойЧасти.СуммаДокумента);
		
		КодыОпераций = РазложитьСтрокуВМассивПодстрок(СтрокаТабличнойЧасти.КодВидаОперации, ";");
		ПрототипУзла_КодВидОпер = ПолучитьПодчиненныйЭлемент(Узел_СвЧ2, "КодВидОпер");
		УдалятьПрототипУзла_КодВидОпер = Ложь;
		Для Каждого КодОперации Из КодыОпераций Цикл
			Если НЕ ПустаяСтрока(КодыОпераций) Тогда
				Узел_КодВидОпер = НовыйУзелИзПрототипа(ПрототипУзла_КодВидОпер);
				ВывестиПоказательВXML(Узел_КодВидОпер, СокрЛП(КодОперации));
				УдалятьПрототипУзла_КодВидОпер = Истина;
			КонецЕсли;
		КонецЦикла;
		Если УдалятьПрототипУзла_КодВидОпер Тогда
			УдалитьУзел(ПрототипУзла_КодВидОпер);
		КонецЕсли;
		
		Узел_ВтчСумНДС = ПолучитьПодчиненныйЭлемент(Узел_СвЧ2, "ВтчСумНДС");
		УстановитьЗначениеЭлемента(Узел_ВтчСумНДС, "СумНДС", ПредставлениеСуммыНДС(СтрокаТабличнойЧасти.СуммаНДС));
		
		Узел_РазнСтоимКСчФ = ПолучитьПодчиненныйЭлемент(Узел_СвЧ2, "РазнСтоимКСчФ");
		УстановитьЗначениеЭлемента(Узел_РазнСтоимКСчФ, "Ум", СтрокаТабличнойЧасти.СуммаДокументаРазницаУменьшение);
		УстановитьЗначениеЭлемента(Узел_РазнСтоимКСчФ, "Увел", СтрокаТабличнойЧасти.СуммаДокументаРазницаУвеличение);
		
		Узел_РазнНДСКСчФ = ПолучитьПодчиненныйЭлемент(Узел_СвЧ2, "РазнНДСКСчФ");
		Если СтрокаТабличнойЧасти.СуммаНДСРазницаУменьшение <> 0 Тогда
			Узел_РазнНДСКСчФ_Ум = ПолучитьПодчиненныйЭлемент(Узел_РазнНДСКСчФ, "Ум");
			УстановитьЗначениеЭлемента(Узел_РазнНДСКСчФ_Ум, "СумНДС", ПредставлениеСуммыНДС(СтрокаТабличнойЧасти.СуммаНДСРазницаУменьшение));
		КонецЕсли;
		Если СтрокаТабличнойЧасти.СуммаНДСРазницаУвеличение <> 0 Тогда
			Узел_РазнНДСКСчФ_Увел = ПолучитьПодчиненныйЭлемент(Узел_РазнНДСКСчФ, "Увел");
			УстановитьЗначениеЭлемента(Узел_РазнНДСКСчФ_Увел, "СумНДС", ПредставлениеСуммыНДС(СтрокаТабличнойЧасти.СуммаНДСРазницаУвеличение));
		КонецЕсли;
		
	КонецЦикла;
	УдалитьУзел(ПрототипУзла_СвЧ2);
	
КонецПроцедуры

Функция ПредставлениеСуммыНДС(ЗначениеСуммыНДС)
	
	Возврат Формат(ЗначениеСуммыНДС, "ЧДЦ=2; ЧРД=.; ЧН=; ЧГ=0");
	
КонецФункции

Функция РасщепленнаяСтрока(СтрокаЗначения, Разделитель)
	
	Результат = Новый Массив;
	
	ПозицияРазделителя = Найти(СтрокаЗначения, Разделитель);
	Если ПозицияРазделителя = 0 Тогда
		Результат.Добавить(СтрокаЗначения);
		Результат.Добавить("");
	Иначе
		Результат.Добавить(Лев(СтрокаЗначения, ПозицияРазделителя - 1));
		Результат.Добавить(Сред(СтрокаЗначения, ПозицияРазделителя + 1));
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьПодчиненныйЭлемент(Узел, КодЭлемента)
	
	Для Каждого Стр Из Узел.Строки Цикл
		Если Стр.Код = КодЭлемента Тогда
			Возврат Стр;
		КонецЕсли;
	КонецЦикла;
	
КонецФункции

Функция НовыйУзелИзПрототипа(ПрототипУзла);
	
	РодительУзла = ПрототипУзла.Родитель;
	
	ПозицияИсходногоУзла = РодительУзла.Строки.Индекс(ПрототипУзла);
	НовыйУзел = РодительУзла.Строки.Вставить(ПозицияИсходногоУзла);
	ЗаполнитьЗначенияСвойств(НовыйУзел, ПрототипУзла, , "Родитель, Строки");
	Для Каждого Стр из ПрототипУзла.Строки Цикл
		СкопироватьУзел(НовыйУзел, Стр);
	КонецЦикла;
	
	Возврат НовыйУзел;
	
КонецФункции

Функция СкопироватьУзел(Родитель, Узел)
	
	НовыйУзел = Родитель.Строки.Добавить();
	ЗаполнитьЗначенияСвойств(НовыйУзел, Узел, , "Родитель, Строки");
	Для Каждого Стр из Узел.Строки Цикл
		СкопироватьУзел(НовыйУзел, Стр);
	КонецЦикла;
	Возврат НовыйУзел;
	
КонецФункции

Процедура УстановитьЗначениеЭлемента(УзелРодитель, ИмяЭлемента, ЗначениеЭлемента)
	
	ПодчиненныйЭлемент = ПолучитьПодчиненныйЭлемент(УзелРодитель, ИмяЭлемента);
	ВывестиПоказательВXML(ПодчиненныйЭлемент, ЗначениеЭлемента);
	
КонецПроцедуры

Процедура УдалитьУзел(Узел)
	
	РодительУзла = ?(Узел.Родитель = Неопределено, Узел.Владелец(), Узел.Родитель);
	РодительУзла.Строки.Удалить(Узел);
	
КонецПроцедуры

Процедура ОтсечьНезаполненныеНеобязательныеУзлы(Узел)
	
	КоличествоСтрок = Узел.Строки.Количество();
	Для Инд = 1 По КоличествоСтрок Цикл
		Стр = Узел.Строки.Получить(КоличествоСтрок - Инд);
		ОтсечьНезаполненныеНеобязательныеУзлы(Стр);
	КонецЦикла;
	
	Если ТипЗнч(Узел) <> Тип("ДеревоЗначений") Тогда
		Если (Найти(Узел.Обязательность, "Н") <> 0 ИЛИ Найти(Узел.Обязательность, "H") <> 0) И УзелПуст(Узел) Тогда // учтем оба варианта: кириллицу и латиницу
			УдалитьУзел(Узел);
		ИначеЕсли (Найти(Узел.Обязательность, "М") <> 0 ИЛИ Найти(Узел.Обязательность, "M") <> 0) // учтем оба варианта: кириллицу и латиницу
		И УзелПуст(Узел)
		И ?(Найти(Узел.Обязательность, "О") <> 0 ИЛИ Найти(Узел.Обязательность, "O") <> 0, ИмеютсяАналогичныеСоседниеУзлы(Узел), Истина) Тогда
			УдалитьУзел(Узел);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Функция УзелПуст(Узел)
	
	ПустойУзел = ?(Узел.Формат = "N", Узел.Значение = "0" ИЛИ (НЕ ЗначениеЗаполнено(Узел.Значение)), НЕ ЗначениеЗаполнено(Узел.Значение));
	Для Каждого Стр из Узел.Строки Цикл
		Если НЕ УзелПуст(Стр) Тогда
			Возврат Ложь;
		КонецЕсли;
	КонецЦикла;
	Возврат ПустойУзел;
	
КонецФункции

Функция ИмеютсяАналогичныеСоседниеУзлы(Стр)
	
	Возврат (Стр.Родитель.Строки.НайтиСтроки(Новый Структура("Ключ", Стр.Ключ), Ложь).Количество() > 1);
	
КонецФункции

Функция ВыгрузитьДеревоВXML(ДеревоВыгрузки, Параметры)
	
	ПотокXML = СоздатьНовыйПотокXML(); // создаем новый поток для записи
	ЗаписатьУзелДереваВXML(ДеревоВыгрузки, ПотокXML, Параметры); // пишем дерево в поток
	ТекстДляЗаписи = ПотокXML.Закрыть(); // получаем текст XML
	ТекстДляЗаписи = "<?xml version=""1.0"" encoding=""windows-1251""?>" + Сред(ТекстДляЗаписи, Найти(ТекстДляЗаписи, Символы.ПС));
	Возврат ТекстДляЗаписи;
	
КонецФункции

Функция СоздатьНовыйПотокXML()
	
	ПотокXML = Новый ЗаписьXML();
	ПотокXML.УстановитьСтроку("UTF-8");
	ПотокXML.ЗаписатьОбъявлениеXML();
	ПотокXML.Отступ = Истина;
	Возврат ПотокXML;
	
КонецФункции

Функция ЗаписатьУзелДереваВXML(СтрокаДерева, ПотокXML, Параметры)
	
	Если ТипЗнч(СтрокаДерева) = Тип("ДеревоЗначений") Тогда
		ПотокXML.ЗаписатьНачалоЭлемента("Файл");
		ПотокXML.ЗаписатьАтрибут("xmlns:xsi", "http://www.w3.org/2001/XMLSchema-instance");
		Для Каждого Стр Из СтрокаДерева.Строки Цикл
			ЗаписатьУзелДереваВXML(Стр, ПотокXML, Параметры);
		КонецЦикла;
		ПотокXML.ЗаписатьКонецЭлемента();
	Иначе
		Если СтрокаДерева.Тип = "А" ИЛИ СтрокаДерева.Тип = "A" Тогда 
			ПотокXML.ЗаписатьАтрибут(СтрокаДерева.Код, Строка(СтрокаДерева.Значение));
		Иначе
			ПотокXML.ЗаписатьНачалоЭлемента(СтрокаДерева.Код);
			Для Каждого Лист из СтрокаДерева.Строки Цикл
				ЗаписатьУзелДереваВXML(Лист, ПотокXML, Параметры);
			КонецЦикла;
			ПотокXML.ЗаписатьТекст(Строка(СтрокаДерева.Значение));
			ПотокXML.ЗаписатьКонецЭлемента();
		КонецЕсли;
	КонецЕсли;

КонецФункции

Функция РазложитьСтрокуВМассивПодстрок(Знач Стр, Разделитель = ",")
	
	МассивСтрок = Новый Массив();
	Если Разделитель = " " Тогда
		Стр = СокрЛП(Стр);
		Пока 1=1 Цикл
			Поз = Найти(Стр,Разделитель);
			Если Поз=0 Тогда
				МассивСтрок.Добавить(Стр);
				Возврат МассивСтрок;
			КонецЕсли;
			МассивСтрок.Добавить(Лев(Стр,Поз-1));
			Стр = СокрЛ(Сред(Стр,Поз));
		КонецЦикла;
	Иначе
		ДлинаРазделителя = СтрДлина(Разделитель);
		Пока 1=1 Цикл
			Поз = Найти(Стр,Разделитель);
			Если Поз=0 Тогда
				МассивСтрок.Добавить(Стр);
				Возврат МассивСтрок;
			КонецЕсли;
			МассивСтрок.Добавить(Лев(Стр,Поз-1));
			Стр = Сред(Стр,Поз+ДлинаРазделителя);
		КонецЦикла;
	КонецЕсли;
	
КонецФункции 

#КонецЕсли