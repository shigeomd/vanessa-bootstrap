
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда 
		ВидУведомления = Объект.ВидУведомления;
		Организация = Объект.Организация;
	Иначе
		Если Параметры.Свойство("Организация") Тогда
			Организация = Параметры.Организация;
		КонецЕсли;
		Если Параметры.Свойство("ВидУведомления") Тогда
			ВидУведомления = Параметры.ВидУведомления;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ПолучитьИмяФормы(Вид)
	
	Возврат Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПолноеИмяФормыПоВиду(Вид);
	
КонецФункции

&НаКлиенте
Процедура ПолучитьОрганизациюИОткрыть(Вид, ИмяФормыСообщения)
	Если ЗначениеЗаполнено(ИмяФормыСообщения) Тогда
		Если ЗначениеЗаполнено(Организация) Тогда
			ПараметрыОткрытия = Новый Структура("Организация", Организация);
			Форма = ПолучитьФорму(ИмяФормыСообщения, ПараметрыОткрытия, ЭтаФорма.ВладелецФормы);
			Если Форма <> Неопределено Тогда 
				Форма.Открыть();
			КонецЕсли;
		Иначе
			ПараметрыОткрытия = Новый Структура("Тип", Вид);
			ФормаОрганизации = ПолучитьФорму("Документ.УведомлениеОСпецрежимахНалогообложения.Форма.ФормаВыбораУведомления", ПараметрыОткрытия, ЭтаФорма.ВладелецФормы);
			ФормаОрганизации.РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
			ФормаОрганизации.Открыть();
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Отказ = Истина;
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ПараметрыОткрытияФормы = Новый Структура("Ключ", Объект.Ссылка);
		
		Имя = ПолучитьИмяФормы(ВидУведомления);
		Если Имя = Неопределено Тогда 
			Возврат;
		КонецЕсли;
		
		Форма = ПолучитьФорму(Имя, ПараметрыОткрытияФормы, , Объект);
		Форма.ЗакрыватьПриЗакрытииВладельца = Ложь;
		Форма.Открыть();
		
	ИначеЕсли ЗначениеЗаполнено(ВидУведомления) Тогда
		
		ИмяФормыСообщения = ПолучитьИмяФормы(ВидУведомления);
		ПолучитьОрганизациюИОткрыть(ВидУведомления, ИмяФормыСообщения);
		
	Иначе
		
		ФормаВыбор = ПолучитьФорму("Документ.УведомлениеОСпецрежимахНалогообложения.Форма.ФормаВыбораВидаСообщения", Новый Структура("Организация", Организация));
		ОписаниеОповещения = Новый ОписаниеОповещения("ПриОткрытииЗавершение", ЭтотОбъект);
		ФормаВыбор.ОписаниеОповещенияОЗакрытии = ОписаниеОповещения;
		ФормаВыбор.РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
		ФормаВыбор.Открыть();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытииЗавершение(РезультатВыбора, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(РезультатВыбора) = Тип("Структура")
		И РезультатВыбора.Свойство("РезультатВыбора")
		И ЗначениеЗаполнено(РезультатВыбора.РезультатВыбора) Тогда
		
		ИмяФормыСообщения = ПолучитьИмяФормы(РезультатВыбора.РезультатВыбора);
		ПолучитьОрганизациюИОткрыть(РезультатВыбора.РезультатВыбора, ИмяФормыСообщения);
	КонецЕсли;
	
КонецПроцедуры

