&НаСервере
Функция  ПолучитьСтруктуруРеквизитовДляОбработки(ДокументСсылка, Реквизит)
	
	СтруктураРекизитов = Новый Структура("ВидСчетаФактуры, ПоследнееИсправление, Основание");
	
	РеквизитыСчетаФактуры =  ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументСсылка, "ВидСчетаФактуры, ДокументОснование, ДоговорКонтрагента");
	
	ВидСчетаФактуры = РеквизитыСчетаФактуры.ВидСчетаФактуры;
	ДокументОснование = РеквизитыСчетаФактуры.ДокументОснование;
	ВидДоговораКонтрагента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(РеквизитыСчетаФактуры.ДоговорКонтрагента, "ВидДоговора");

	ПоследнееИсправление = УчетНДСПереопределяемый.ПолучитьПоследнееИсправлениеСчетаФактурыПолученного(ДокументСсылка);
	
	Основание = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПоследнееИсправление, "ДокументОснование");
	
	КорректироватьСчетФактуру = Ложь;
	Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.АвансовыйОтчет") 
		ИЛИ ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ОтчетКомитентуОПродажах")
						И ВидДоговораКонтрагента = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомитентом")Тогда
		
		КорректироватьСчетФактуру = Истина;
		
	КонецЕсли;

	ПорядокИсправления = Новый ФиксированноеСоответствие(
		Документы.СчетФактураПолученный.ПолучитьСоответствиеВидовСчетаФактурыПорядкуИсправления());
	
	СтруктураРекизитов.Вставить("ВидСчетаФактуры", ВидСчетаФактуры);
	СтруктураРекизитов.Вставить("ПоследнееИсправление", ПоследнееИсправление);
	СтруктураРекизитов.Вставить("Основание", Основание);
	СтруктураРекизитов.Вставить("ПорядокИсправления", ПорядокИсправления);
	СтруктураРекизитов.Вставить("КорректироватьСчетФактуру", КорректироватьСчетФактуру);
	
	Возврат СтруктураРекизитов;
	
КонецФункции

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	РеквизитыДляОбработки 	= ПолучитьСтруктуруРеквизитовДляОбработки(ПараметрКоманды, "ВидСчетаФактуры");
	ПорядокИсправления 		= РеквизитыДляОбработки.ПорядокИсправления;
	КорректироватьСчетФактуру	= РеквизитыДляОбработки.КорректироватьСчетФактуру;
	ОснованиеКорректировки 		= ?(КорректироватьСчетФактуру, РеквизитыДляОбработки.ПоследнееИсправление, РеквизитыДляОбработки.Основание);
	
	Если НЕ ПорядокИсправления[РеквизитыДляОбработки.ВидСчетаФактуры] Тогда
		
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Для счета-фактуры с видом ""%1"" возможность корректировки не предусмотрена'"), РеквизитыДляОбработки.ВидСчетаФактуры);
		ВызватьИсключение ТекстСообщения;
			
	Иначе
		
		ПараметрыЗаполнения = Новый Структура("ДокументОснование, Видоперации",ОснованиеКорректировки, 
			ПредопределенноеЗначение("Перечисление.ВидыОперацийИсправленияПоступленияРеализации.СогласованноеИзменение"));
		ПараметрыФормы      = Новый Структура("ЗначенияЗаполнения", ПараметрыЗаполнения);
		
		ОткрытьФорму("Документ.КорректировкаПоступления.ФормаОбъекта", ПараметрыФормы, ПараметрыВыполненияКоманды.Источник, ПараметрыВыполненияКоманды.Уникальность, ПараметрыВыполненияКоманды.Окно);
		
	КонецЕсли;
	
КонецПроцедуры
