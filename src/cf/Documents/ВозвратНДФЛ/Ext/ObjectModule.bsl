
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Подсистема "Управление доступом".

// Процедура ЗаполнитьНаборыЗначенийДоступа по свойствам объекта заполняет наборы значений доступа
// в таблице с полями:
//    НомерНабора     - Число                                     (необязательно, если набор один),
//    ВидДоступа      - ПланВидовХарактеристикСсылка.ВидыДоступа, (обязательно),
//    ЗначениеДоступа - Неопределено, СправочникСсылка или др.    (обязательно),
//    Чтение          - Булево (необязательно, если набор для всех прав) устанавливается для одной строки набора,
//    Добавление      - Булево (необязательно, если набор для всех прав) устанавливается для одной строки набора,
//    Изменение       - Булево (необязательно, если набор для всех прав) устанавливается для одной строки набора,
//    Удаление        - Булево (необязательно, если набор для всех прав) устанавливается для одной строки набора,
//
//  Вызывается из процедуры УправлениеДоступомСлужебный.ЗаписатьНаборыЗначенийДоступа(),
// если объект зарегистрирован в "ПодпискаНаСобытие.ЗаписатьНаборыЗначенийДоступа" и
// из таких же процедур объектов, у которых наборы значений доступа зависят от наборов этого
// объекта (в этом случае объект зарегистрирован в "ПодпискаНаСобытие.ЗаписатьЗависимыеНаборыЗначенийДоступа").
//
// Параметры:
//  Таблица      - ТабличнаяЧасть,
//                 РегистрСведенийНаборЗаписей.НаборыЗначенийДоступа,
//                 ТаблицаЗначений, возвращаемая УправлениеДоступом.ТаблицаНаборыЗначенийДоступа().
//
Процедура ЗаполнитьНаборыЗначенийДоступа(Таблица) Экспорт
	
	ЗарплатаКадры.ЗаполнитьНаборыПоОрганизацииИФизическимЛицам(ЭтотОбъект, Таблица, "Организация", "Сотрудник");
	
КонецПроцедуры

// Подсистема "Управление доступом".

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.Сотрудники") Тогда
		ЗарплатаКадры.ЗаполнитьПоОснованиюСотрудником(ЭтотОбъект, ДанныеЗаполнения, , Истина);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	ДанныеДляПроведения = ПолучитьДанныеДляПроведения();
	
	УчетНДФЛ.СформироватьУдержанныйНалогПоВременнойТаблице(Движения, Отказ, Организация, КонецМесяца(Месяц), ДанныеДляПроведения.МенеджерВременныхТаблиц);
	УчетНачисленнойЗарплаты.ЗарегистрироватьВозвратНДФЛ(Движения, Отказ, Организация, Месяц, ДанныеДляПроведения.Удержания, Перечисления.ХарактерВыплатыЗарплаты.Зарплата);
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Для Каждого СтрокаТаблицы Из СуммыВозврата Цикл
		Если НЕ ЗначениеЗаполнено(СтрокаТаблицы.ПоСтавке09) 
			И НЕ ЗначениеЗаполнено(СтрокаТаблицы.ПоСтавке13)
			И НЕ ЗначениеЗаполнено(СтрокаТаблицы.ПоСтавке35) Тогда
			СообщениеОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='В строке %1 таблицы должен быть заполнен хотя бы один налог к возврату.'"), СтрокаТаблицы.НомерСтроки);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СообщениеОбОшибке,,,,Отказ);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьДанныеДляПроведения()
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВозвратНДФЛСуммыВозврата.Ссылка.Сотрудник КАК ФизическоеЛицо,
	|	ВозвратНДФЛСуммыВозврата.ОбособленноеПодразделение КАК Подразделение,
	|	СУММА(ВозвратНДФЛСуммыВозврата.ПоСтавке13) КАК ПоСтавке13,
	|	СУММА(ВозвратНДФЛСуммыВозврата.ПоСтавке09) КАК ПоСтавке09,
	|	СУММА(ВозвратНДФЛСуммыВозврата.ПоСтавке35) КАК ПоСтавке35,
	|	ВозвратНДФЛСуммыВозврата.НалоговыйПериод,
	|	ВЫБОР
	|		КОГДА ВозвратНДФЛСуммыВозврата.НалоговыйПериод < ГОД(ВозвратНДФЛСуммыВозврата.Ссылка.Месяц)
	|			ТОГДА ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(ВозвратНДФЛСуммыВозврата.Ссылка.Месяц, ГОД), МЕСЯЦ, (ВозвратНДФЛСуммыВозврата.НалоговыйПериод - ГОД(ВозвратНДФЛСуммыВозврата.Ссылка.Месяц)) * 12 + 11)
	|		ИНАЧЕ ВозвратНДФЛСуммыВозврата.Ссылка.Месяц
	|	КОНЕЦ КАК МесяцНалоговогоПериода
	|ПОМЕСТИТЬ ВТСтрокиДокумента
	|ИЗ
	|	Документ.ВозвратНДФЛ.СуммыВозврата КАК ВозвратНДФЛСуммыВозврата
	|ГДЕ
	|	ВозвратНДФЛСуммыВозврата.Ссылка = &Ссылка
	|	И ВозвратНДФЛСуммыВозврата.ПоСтавке13 + ВозвратНДФЛСуммыВозврата.ПоСтавке09 + ВозвратНДФЛСуммыВозврата.ПоСтавке35 > 0
	|
	|СГРУППИРОВАТЬ ПО
	|	ВозвратНДФЛСуммыВозврата.НалоговыйПериод,
	|	ВозвратНДФЛСуммыВозврата.Ссылка.Сотрудник,
	|	ВозвратНДФЛСуммыВозврата.ОбособленноеПодразделение,
	|	ВЫБОР
	|		КОГДА ВозвратНДФЛСуммыВозврата.НалоговыйПериод < ГОД(ВозвратНДФЛСуммыВозврата.Ссылка.Месяц)
	|			ТОГДА ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(ВозвратНДФЛСуммыВозврата.Ссылка.Месяц, ГОД), МЕСЯЦ, (ВозвратНДФЛСуммыВозврата.НалоговыйПериод - ГОД(ВозвратНДФЛСуммыВозврата.Ссылка.Месяц)) * 12 + 11)
	|		ИНАЧЕ ВозвратНДФЛСуммыВозврата.Ссылка.Месяц
	|	КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтрокиДокумента.ФизическоеЛицо,
	|	0 КАК Сумма,
	|	СУММА(СтрокиДокумента.ПоСтавке13 + СтрокиДокумента.ПоСтавке35) КАК СуммаКорректировкиВыплаты,
	|	СтрокиДокумента.Подразделение КАК Подразделение
	|ИЗ
	|	ВТСтрокиДокумента КАК СтрокиДокумента
	|
	|СГРУППИРОВАТЬ ПО
	|	СтрокиДокумента.ФизическоеЛицо,
	|	СтрокиДокумента.Подразделение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтрокиДокумента.ФизическоеЛицо,
	|	СтрокиДокумента.ПоСтавке13 КАК Сумма,
	|	СтрокиДокумента.Подразделение,
	|	ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка13) КАК СтавкаНалогообложенияРезидента,
	|	СтрокиДокумента.НалоговыйПериод,
	|	НАЧАЛОПЕРИОДА(СтрокиДокумента.МесяцНалоговогоПериода, МЕСЯЦ) КАК Период,
	|	СтрокиДокумента.МесяцНалоговогоПериода,
	|	ЗНАЧЕНИЕ(Справочник.ВидыДоходовНДФЛ.ПустаяСсылка) КАК КодДохода
	|ПОМЕСТИТЬ ВТНалогКЗачетуВозврату
	|ИЗ
	|	ВТСтрокиДокумента КАК СтрокиДокумента
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СтрокиДокумента.ФизическоеЛицо,
	|	СтрокиДокумента.ПоСтавке09,
	|	СтрокиДокумента.Подразделение,
	|	ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка09),
	|	СтрокиДокумента.НалоговыйПериод,
	|	НАЧАЛОПЕРИОДА(СтрокиДокумента.МесяцНалоговогоПериода, МЕСЯЦ),
	|	СтрокиДокумента.МесяцНалоговогоПериода,
	|	ЗНАЧЕНИЕ(Справочник.ВидыДоходовНДФЛ.Код1010)
	|ИЗ
	|	ВТСтрокиДокумента КАК СтрокиДокумента
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СтрокиДокумента.ФизическоеЛицо,
	|	СтрокиДокумента.ПоСтавке35,
	|	СтрокиДокумента.Подразделение,
	|	ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка35),
	|	СтрокиДокумента.НалоговыйПериод,
	|	НАЧАЛОПЕРИОДА(СтрокиДокумента.МесяцНалоговогоПериода, МЕСЯЦ),
	|	СтрокиДокумента.МесяцНалоговогоПериода,
	|	ЗНАЧЕНИЕ(Справочник.ВидыДоходовНДФЛ.ПустаяСсылка)
	|ИЗ
	|	ВТСтрокиДокумента КАК СтрокиДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Сотрудники.ФизическоеЛицо КАК ФизическоеЛицо
	|ПОМЕСТИТЬ ВТФизическиеЛица
	|ИЗ
	|	ВТСтрокиДокумента КАК Сотрудники
	|ГДЕ
	|	Сотрудники.ФизическоеЛицо <> ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка) КАК ФизическоеЛицо,
	|	ЗНАЧЕНИЕ(Справочник.Сотрудники.ПустаяСсылка) КАК Сотрудник,
	|	0 КАК СуммаДохода,
	|	0 КАК СуммаВычета,
	|	ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка) КАК Подразделение
	|ПОМЕСТИТЬ ВТДанныеДляРасчетаНДФЛ
	|ГДЕ
	|	ЛОЖЬ";
	Результаты = Запрос.ВыполнитьПакет();
	
	Удержания = Результаты[1].Выгрузить();
	
	УчетНДФЛ.СоздатьВТСтавкаНДФЛПоСтавкеРезидента(Запрос.МенеджерВременныхТаблиц,"ВТНалогКЗачетуВозврату");
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НалогКВозврату.ФизическоеЛицо,
	|	НалогКВозврату.СтавкаНалогообложенияРезидента,
	|	СтавкаНДФЛПоСтавкеРезидента.СтавкаНДФЛ КАК Ставка,
	|	НалогКВозврату.МесяцНалоговогоПериода,
	|	НалогКВозврату.Подразделение,
	|	-НалогКВозврату.Сумма КАК Сумма,
	|	НалогКВозврату.КодДохода
	|ПОМЕСТИТЬ ВТНалогУдержанный
	|ИЗ
	|	ВТНалогКЗачетуВозврату КАК НалогКВозврату
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтавкаНДФЛПоСтавкеРезидента КАК СтавкаНДФЛПоСтавкеРезидента
	|		ПО НалогКВозврату.ФизическоеЛицо = СтавкаНДФЛПоСтавкеРезидента.ФизическоеЛицо
	|			И НалогКВозврату.Период = СтавкаНДФЛПоСтавкеРезидента.Период
	|			И НалогКВозврату.СтавкаНалогообложенияРезидента = СтавкаНДФЛПоСтавкеРезидента.СтавкаНалогообложенияРезидента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТНалогКЗачетуВозврату
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТСтавкаНДФЛПоСтавкеРезидента";
	Запрос.Выполнить();
	
	ДанныеДляПроведения = Новый Структура("Удержания, МенеджерВременныхТаблиц", Удержания, Запрос.МенеджерВременныхТаблиц);
						
	Возврат ДанныеДляПроведения;

КонецФункции

#КонецОбласти

#КонецЕсли