#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Перем мЭтоНовый;

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	мЭтоНовый = ЭтоНовый();
	
	Период = СокрЛП(Формат(ДатаОкончания, "ДФ=yyyyMMdd") + Формат('39991231' - ДатаНачала, "ДФ=yyyyMMdd"));
	ПредставлениеПериода = ПредставлениеПериода(ДатаНачала, КонецДня(ДатаОкончания), "ФП = Истина");
	
	ТекстЗапроса = "ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|   ВыгрузкаРегламентированныхОтчетов.Ссылка
	|ИЗ
	|	Документ.ВыгрузкаРегламентированныхОтчетов.Основная КАК ВыгрузкаРегламентированныхОтчетов
	|ГДЕ
	|	ВыгрузкаРегламентированныхОтчетов.Основание.Ссылка = &Ссылка";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Ссылка", ЭтотОбъект.Ссылка);
	ДокументыВыгрузки = Запрос.Выполнить().Выгрузить();
	
	Если ДокументыВыгрузки.Количество() > 0 Тогда
		
		НачатьТранзакцию();
		
		Для Каждого ДокВыгрузки Из ДокументыВыгрузки Цикл
			
			ДокВыгрузки = ДокВыгрузки.Ссылка.ПолучитьОбъект();
			
			ДокВыгрузки.Заблокировать();
			
			Если ЭтотОбъект.ПометкаУдаления Тогда
				
				Колво = ДокВыгрузки.Основная.Количество();
				
				Если Колво > 1 Тогда
					
					Для Ном = 1 По Колво Цикл
						
						Если ДокВыгрузки.Основная.Получить(Колво - Ном).Основание.Ссылка = ЭтотОбъект.Ссылка Тогда
							ДокВыгрузки.Основная.Удалить(Колво - Ном);
						КонецЕсли;
						
					КонецЦикла;
					
					ДокВыгрузки.Записать(РежимЗаписиДокумента.Запись);
					
				Иначе
					
					ДокВыгрузки.УстановитьПометкуУдаления(Истина);
					
				КонецЕсли;
				
			Иначе	
				
				ДокВыгрузки.УстановитьПометкуУдаления(Ложь);
				
				ДокВыгрузки.Записать(РежимЗаписиДокумента.Проведение);
				
			КонецЕсли;
									
			ДокВыгрузки.Разблокировать();
			
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если мЭтоНовый Тогда
		РегламентированнаяОтчетность.ОбработатьСобытие1СОтчетности(НСтр("ru = 'Регламентированный отчет. Создание'"), ЭтотОбъект.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

#КонецЕсли