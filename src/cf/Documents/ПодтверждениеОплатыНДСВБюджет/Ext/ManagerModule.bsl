#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// ПОДГОТОВКА ПАРАМЕТРОВ ПРОВЕДЕНИЯ ДОКУМЕНТА
	
Функция ПодготовитьПараметрыПроведения(ДокументСсылка, Отказ) Экспорт
	
	ПараметрыПроведения = Новый Структура;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст = ТекстЗапросаРеквизитыДокумента();
	Результат    = Запрос.Выполнить();

	Реквизиты = ОбщегоНазначенияБПВызовСервера.ПолучитьСтруктуруИзРезультатаЗапроса(Результат);
 
	Если НЕ УчетнаяПолитика.Существует(Реквизиты.Организация, Реквизиты.Период, Истина, ДокументСсылка) Тогда
		Отказ = Истина;
		Возврат ПараметрыПроведения;
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Период", Реквизиты.Период);
	Запрос.УстановитьПараметр("Организация", Реквизиты.Организация);
	
	ПараметрыПроведения.Вставить("ТаблицаРеквизиты", Результат.Выгрузить());
	
	НомераТаблиц = Новый Структура;
	
	Запрос.Текст = ТекстЗапросаОплатаПодтверждена(НомераТаблиц)
		+ ТекстЗапросаПоФормированиюРегламентнойОперации(НомераТаблиц);;
	
	Результат = Запрос.ВыполнитьПакет();
		
	ПараметрыПроведения.Вставить("ТаблицаОплатаПодтверждена", Результат[0].Выгрузить());
	
	Для каждого НомерТаблицы Из НомераТаблиц Цикл
		ПараметрыПроведения.Вставить(НомерТаблицы.Ключ, Результат[НомерТаблицы.Значение].Выгрузить());
	КонецЦикла;
	
	Возврат ПараметрыПроведения;

КонецФункции

Функция ТекстЗапросаРеквизитыДокумента()

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Организация,
	|	Реквизиты.Ссылка КАК Регистратор
	|ИЗ
	|	Документ.ПодтверждениеОплатыНДСВБюджет КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка";
	
	Возврат ТекстЗапроса ;
	
КонецФункции

Функция ТекстЗапросаОплатаПодтверждена(НомераТаблиц)
	
	НомераТаблиц.Вставить("ТаблицаОплатаПодтверждена", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ПодтверждениеОплатыНДСВБюджетСостав.ЗаявлениеОВвозе,
	|	ПодтверждениеОплатыНДСВБюджетСостав.ДатаДокументаОплаты КАК ДатаДокументаОплаты,
	|	ПодтверждениеОплатыНДСВБюджетСостав.НомерДокументаОплаты КАК НомерДокументаОплаты
	|ИЗ
	|	Документ.ПодтверждениеОплатыНДСВБюджет.Состав КАК ПодтверждениеОплатыНДСВБюджетСостав
	|ГДЕ
	|	ПодтверждениеОплатыНДСВБюджетСостав.Ссылка = &Ссылка";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ТекстЗапросаПоФормированиюРегламентнойОперации(НомераТаблиц)

	НомераТаблиц.Вставить("ДанныеРегламентнойОперации", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	НАЧАЛОПЕРИОДА(Реквизиты.Дата, КВАРТАЛ) КАК Период,
	|	Реквизиты.Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Перечисление.РегламентныеОперации.ПодтверждениеОплатыНДСВБюджет) КАК РегламентнаяОперация
	|ИЗ
	|	Документ.ПодтверждениеОплатыНДСВБюджет КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБНОВЛЕНИЯ

Процедура ЗарегистрироватьДатуНомерДокументаПодтверждающегоОплатуНДСВБюджет() Экспорт

	// С целью поддержки изменений в правилах ведения книги покупок (по постановлению № 735)
	// в регистр "НДС записи книги покупок" запишем дату и номер документа, подтверждающего оплату НДС
	// для этого перепроведем документы "Подтверждение оплаты НДС в бюджет"
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПодтверждениеОплатыНДСВБюджет.Ссылка КАК ПодтверждениеОплаты
	|ПОМЕСТИТЬ ПодтвержденияОплат
	|ИЗ
	|	Документ.ПодтверждениеОплатыНДСВБюджет КАК ПодтверждениеОплатыНДСВБюджет
	|ГДЕ
	|	ПодтверждениеОплатыНДСВБюджет.Проведен
	|	И НЕ ПодтверждениеОплатыНДСВБюджет.ПометкаУдаления
	|	И НЕ ПодтверждениеОплатыНДСВБюджет.РучнаяКорректировка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НДСЗаписиКнигиПокупок.Регистратор
	|ПОМЕСТИТЬ ЗарегистрированныеОплаты
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПокупок КАК НДСЗаписиКнигиПокупок
	|ГДЕ
	|	НДСЗаписиКнигиПокупок.Регистратор В
	|			(ВЫБРАТЬ
	|				ПодтвержденияОплат.ПодтверждениеОплаты
	|			ИЗ
	|				ПодтвержденияОплат)
	|	И НДСЗаписиКнигиПокупок.ВидЦенности = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ТаможенныйСоюз)
	|	И НДСЗаписиКнигиПокупок.ДатаДокументаОплаты <> ДАТАВРЕМЯ(1, 1, 1)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПодтвержденияОплат.ПодтверждениеОплаты
	|ИЗ
	|	ПодтвержденияОплат КАК ПодтвержденияОплат
	|ГДЕ
	|	НЕ ПодтвержденияОплат.ПодтверждениеОплаты В
	|				(ВЫБРАТЬ
	|					ЗарегистрированныеОплаты.Регистратор
	|				ИЗ
	|					ЗарегистрированныеОплаты)";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		ДокументОбъект = Выборка.ПодтверждениеОплаты.ПолучитьОбъект();
		
		Попытка
			ДокументОбъект.Записать();
		Исключение
			ТекстСообщения	= НСтр("ru = 'Не удалось перепровести документ'");
			ЗаписьЖурналаРегистрации(ТекстСообщения, УровеньЖурналаРегистрации.Ошибка, , , ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));			
		КонецПопытки;
		
	КонецЦикла;

КонецПроцедуры

#КонецЕсли