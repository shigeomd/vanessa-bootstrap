#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

// Заполняет счета учета номенклатуры в табличной части документа
//
Процедура ЗаполнитьСчетаУчетаВТабличнойЧасти(Объект, ИмяТабличнойЧасти) Экспорт

	ТабличнаяЧасть = Объект[ИмяТабличнойЧасти];
	
	ДанныеОбъекта = Новый Структура("Дата, Организация, Склад");
	ЗаполнитьЗначенияСвойств(ДанныеОбъекта, Объект);
	
	СоответствиеСчетовУчета = БухгалтерскийУчетПереопределяемый.ПолучитьСчетаУчетаСпискаНоменклатуры(
		ДанныеОбъекта.Организация, ОбщегоНазначения.ВыгрузитьКолонку(ТабличнаяЧасть, "Номенклатура", Истина), ДанныеОбъекта.Склад, ДанныеОбъекта.Дата);
	
	Для каждого СтрокаТабличнойЧасти Из ТабличнаяЧасть Цикл
		СчетаУчета = СоответствиеСчетовУчета.Получить(СтрокаТабличнойЧасти.Номенклатура);
		ЗаполнитьСчетаУчетаВСтрокеТабличнойЧасти(ДанныеОбъекта, СтрокаТабличнойЧасти, ИмяТабличнойЧасти, СчетаУчета);
	КонецЦикла;

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Заполняет счета учета номенклатуры в строке табличной части документа
//
// Параметры:
//  ДанныеОбъекта         - структура данных объекта, используемых при заполнении счетов учета (вид операции,
//                          вид договора контрагента, признак комиссионной торговли и т.п.)
//  СтрокаТабличнойЧасти  - строка табличной части документа
//  ИмяТабличнойЧасти     - имя табличной части документа
//  СведенияОНоменклатуре - структура сведений о номенклатуре, либо стуктура счетов учета
//
Процедура ЗаполнитьСчетаУчетаВСтрокеТабличнойЧасти(ДанныеОбъекта, СтрокаТабличнойЧасти, ИмяТабличнойЧасти, СведенияОНоменклатуре) Экспорт

	Если СведенияОНоменклатуре = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если СведенияОНоменклатуре.Свойство("СчетаУчета") Тогда
		// СведенияОНоменклатуре - структура сведений номенклатуры
		СчетаУчета = СведенияОНоменклатуре.СчетаУчета;
	ИначеЕсли СведенияОНоменклатуре.Свойство("СчетУчета") Тогда
		// СведенияОНоменклатуре - структура счетов учета номенклатуры
		СчетаУчета = СведенияОНоменклатуре;
	Иначе
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СчетаУчета.СчетУчета) Тогда
		СтрокаТабличнойЧасти.СчетУчета = СчетаУчета.СчетУчета;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СчетаУчета.СчетПередачи) Тогда
		СтрокаТабличнойЧасти.СчетПередачи = СчетаУчета.СчетПередачи;
	КонецЕсли;

КонецПроцедуры

// ПОДГОТОВКА ПАРАМЕТРОВ ПРОВЕДЕНИЯ ДОКУМЕНТА

Функция ПодготовитьПараметрыПроведения(ДокументСсылка, Отказ) Экспорт

	ПараметрыПроведения = Новый Структура;
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Организация КАК Организация
	|ИЗ
	|	Документ.ВозвратМатериаловИзЭксплуатации КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка";
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	Если НЕ УчетнаяПолитика.Существует(Выборка.Организация, Выборка.Период, Истина, ДокументСсылка) Тогда
		Отказ = Истина;
		Возврат ПараметрыПроведения;
	КонецЕсли;
	
	// Определяем необходимость движений по регистрам учета УСН:
	ПрименяетсяУСНДоходыМинусРасходы = УчетнаяПолитика.ПрименяетсяУСНДоходыМинусРасходы(Выборка.Организация, Выборка.Период);
	
	Запрос.УстановитьПараметр("СинонимСпецодежда",    НСтр("ru = 'Спецодежда'"));
	Запрос.УстановитьПараметр("СинонимСпецоснастка",  НСтр("ru = 'Спецоснастка'"));
	Запрос.УстановитьПараметр("УчестьРасходыУСН",     ПрименяетсяУСНДоходыМинусРасходы);
	
	НомераТаблиц = Новый Структура;

	Запрос.Текст = ТекстЗапросаВозвратСпецодеждыСпецоснастки(НомераТаблиц)
		+ ТекстЗапросаНДС(НомераТаблиц)
		+ ТекстЗапросаПоступлениеРасходовУСН(НомераТаблиц);

	Результат = Запрос.ВыполнитьПакет();
	
	Для Каждого НомерТаблицы Из НомераТаблиц Цикл
		ПараметрыПроведения.Вставить(НомерТаблицы.Ключ, Результат[НомерТаблицы.Значение].Выгрузить());
	КонецЦикла;
	
	Возврат ПараметрыПроведения;
	
КонецФункции

Функция ТекстЗапросаВозвратСпецодеждыСпецоснастки(НомераТаблиц)
	
	НомераТаблиц.Вставить("ВозвратСпецодеждыСпецоснасткиРеквизиты", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ВозвратСпецодеждыСпецоснасткиТаблица", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Организация,
	|	Реквизиты.ПодразделениеОрганизации КАК Подразделение
	|ИЗ
	|	Документ.ВозвратМатериаловИзЭксплуатации КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	""Спецодежда"" КАК ИмяСписка,
	|	&СинонимСпецодежда КАК СинонимСписка,
	|	МИНИМУМ(ТаблицаСпецодежда.НомерСтроки) КАК НомерСтроки,
	|	ТаблицаСпецодежда.Номенклатура,
	|	ТаблицаСпецодежда.ПартияМатериаловВЭксплуатации,
	|	ТаблицаСпецодежда.ФизЛицо,
	|	Реквизиты.ПодразделениеОрганизации КАК Подразделение,
	|	ТаблицаСпецодежда.СчетУчета,
	|	ТаблицаСпецодежда.СчетПередачи,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.СпецодеждаВЭксплуатацииВспомогательный) КАК СчетМЦ,
	|	ТаблицаСпецодежда.СчетУчета КАК СчетРасходов,
	|	ТаблицаСпецодежда.Номенклатура КАК СубконтоРасходов1,
	|	Реквизиты.Склад КАК СубконтоРасходов2,
	|	"""" КАК СубконтоРасходов3,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура) КАК ВидСубконтоРасходов1,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Склады) КАК ВидСубконтоРасходов2,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ПустаяСсылка) КАК ВидСубконтоРасходов3,
	|	СУММА(ТаблицаСпецодежда.Количество) КАК Количество
	|ИЗ
	|	Документ.ВозвратМатериаловИзЭксплуатации.Спецодежда КАК ТаблицаСпецодежда
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВозвратМатериаловИзЭксплуатации КАК Реквизиты
	|		ПО ТаблицаСпецодежда.Ссылка = Реквизиты.Ссылка
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаСпецодежда.Номенклатура,
	|	ТаблицаСпецодежда.ПартияМатериаловВЭксплуатации,
	|	ТаблицаСпецодежда.ФизЛицо,
	|	Реквизиты.ПодразделениеОрганизации,
	|	ТаблицаСпецодежда.СчетУчета,
	|	ТаблицаСпецодежда.СчетПередачи,
	|	Реквизиты.Склад,
	|	ТаблицаСпецодежда.СчетУчета,
	|	ТаблицаСпецодежда.Номенклатура
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""Спецоснастка"",
	|	&СинонимСпецоснастка,
	|	МИНИМУМ(ТаблицаСпецоснастка.НомерСтроки),
	|	ТаблицаСпецоснастка.Номенклатура,
	|	ТаблицаСпецоснастка.ПартияМатериаловВЭксплуатации,
	|	ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка),
	|	Реквизиты.ПодразделениеОрганизации,
	|	ТаблицаСпецоснастка.СчетУчета,
	|	ТаблицаСпецоснастка.СчетПередачи,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.СпецоснасткаВЭксплуатацииВспомогательный),
	|	ТаблицаСпецоснастка.СчетУчета,
	|	ТаблицаСпецоснастка.Номенклатура,
	|	Реквизиты.Склад,
	|	"""",
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура),
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Склады),
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ПустаяСсылка),
	|	СУММА(ТаблицаСпецоснастка.Количество)
	|ИЗ
	|	Документ.ВозвратМатериаловИзЭксплуатации.Спецоснастка КАК ТаблицаСпецоснастка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВозвратМатериаловИзЭксплуатации КАК Реквизиты
	|		ПО ТаблицаСпецоснастка.Ссылка = Реквизиты.Ссылка
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаСпецоснастка.Номенклатура,
	|	ТаблицаСпецоснастка.ПартияМатериаловВЭксплуатации,
	|	Реквизиты.ПодразделениеОрганизации,
	|	ТаблицаСпецоснастка.СчетУчета,
	|	ТаблицаСпецоснастка.СчетПередачи,
	|	Реквизиты.Склад,
	|	ТаблицаСпецоснастка.СчетУчета,
	|	ТаблицаСпецоснастка.Номенклатура
	|
	|УПОРЯДОЧИТЬ ПО
	|	ИмяСписка,
	|	НомерСтроки";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ТекстЗапросаНДС(НомераТаблиц)
	
	НомераТаблиц.Вставить("РеквизитыНДС",              НомераТаблиц.Количество());
	НомераТаблиц.Вставить("СпецодеждаСпецоснасткаНДС", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.Склад КАК Склад,
	|	Реквизиты.ПодразделениеОрганизации КАК Подразделение
	|ИЗ
	|	Документ.ВозвратМатериаловИзЭксплуатации КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	1 КАК НомерТабЧасти,
	|	ТаблицаСпецодежда.НомерСтроки КАК НомерСтроки,
	|	ТаблицаСпецодежда.Номенклатура,
	|	ТаблицаСпецодежда.СчетУчета,
	|	ТаблицаСпецодежда.Количество КАК Количество
	|ИЗ
	|	Документ.ВозвратМатериаловИзЭксплуатации.Спецодежда КАК ТаблицаСпецодежда
	|ГДЕ
	|	ТаблицаСпецодежда.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	2,
	|	ТаблицаСпецоснастка.НомерСтроки,
	|	ТаблицаСпецоснастка.Номенклатура,
	|	ТаблицаСпецоснастка.СчетУчета,
	|	ТаблицаСпецоснастка.Количество
	|ИЗ
	|	Документ.ВозвратМатериаловИзЭксплуатации.Спецоснастка КАК ТаблицаСпецоснастка
	|ГДЕ
	|	ТаблицаСпецоснастка.Ссылка = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерТабЧасти,
	|	НомерСтроки";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ТекстЗапросаПоступлениеРасходовУСН(НомераТаблиц)

	НомераТаблиц.Вставить("ПоступлениеРасходовУСНРеквизиты",       НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Организация,
	|	НЕОПРЕДЕЛЕНО КАК ДоговорКонтрагента,
	|	НЕОПРЕДЕЛЕНО КАК Валюта,
	|	ЛОЖЬ КАК УчетАгентскогоНДС,
	|	ИСТИНА КАК ЭтоВозврат
	|ИЗ
	|	Документ.ВозвратМатериаловИзЭксплуатации КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Процедура ДобавитьКолонкуСодержание(ТаблицаЗначений) Экспорт

	Если ТаблицаЗначений.Колонки.Найти("Содержание") = Неопределено Тогда
		ТаблицаЗначений.Колонки.Добавить("Содержание", ОбщегоНазначения.ОписаниеТипаСтрока(150));
	КонецЕсли;

	Для каждого СтрокаТаблицы Из ТаблицаЗначений Цикл
		СтрокаТаблицы.Содержание = "Возврат " + БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьНазваниеОбъекта(СтрокаТаблицы.СчетПередачи) + " из эксплуатации";
	КонецЦикла;

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// Реестр документов
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "Реестр";
	КомандаПечати.Представление = НСтр("ru = 'Реестр документов'");
	КомандаПечати.ЗаголовокФормы= НСтр("ru = 'Реестр документов ""Возврат материалов из эксплуатации""'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечатиРеестраДокументов";
	КомандаПечати.СписокФорм    = "ФормаСписка";
	КомандаПечати.Порядок       = 100;
	
КонецПроцедуры

Функция ПолучитьДополнительныеРеквизитыДляРеестра() Экспорт
	
	Результат = Новый Структура("Информация", "Склад");
	
	Возврат Результат;
	
КонецФункции

#КонецЕсли