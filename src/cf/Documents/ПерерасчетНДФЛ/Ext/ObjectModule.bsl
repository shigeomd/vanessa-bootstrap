#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Подсистема "Управление доступом".

// Процедура ЗаполнитьНаборыЗначенийДоступа по свойствам объекта заполняет наборы значений доступа
// в таблице с полями:
//    НомерНабора     - Число                                     (необязательно, если набор один),
//    ВидДоступа      - ПланВидовХарактеристикСсылка.ВидыДоступа, (обязательно),
//    ЗначениеДоступа - Неопределено, СправочникСсылка или др.    (обязательно),
//    Чтение          - Булево (необязательно, если набор для всех прав) устанавливается для одной строки набора,
//    Добавление      - Булево (необязательно, если набор для всех прав) устанавливается для одной строки набора,
//    Изменение       - Булево (необязательно, если набор для всех прав) устанавливается для одной строки набора,
//    Удаление        - Булево (необязательно, если набор для всех прав) устанавливается для одной строки набора,
//
//  Вызывается из процедуры УправлениеДоступомСлужебный.ЗаписатьНаборыЗначенийДоступа(),
// если объект зарегистрирован в "ПодпискаНаСобытие.ЗаписатьНаборыЗначенийДоступа" и
// из таких же процедур объектов, у которых наборы значений доступа зависят от наборов этого
// объекта (в этом случае объект зарегистрирован в "ПодпискаНаСобытие.ЗаписатьЗависимыеНаборыЗначенийДоступа").
//
// Параметры:
//  Таблица      - ТабличнаяЧасть,
//                 РегистрСведенийНаборЗаписей.НаборыЗначенийДоступа,
//                 ТаблицаЗначений, возвращаемая УправлениеДоступом.ТаблицаНаборыЗначенийДоступа().
//
Процедура ЗаполнитьНаборыЗначенийДоступа(Таблица) Экспорт
	
	ЗарплатаКадры.ЗаполнитьНаборыПоОрганизацииИФизическимЛицам(ЭтотОбъект, Таблица, "Организация", "Сотрудники.Сотрудник");
	
КонецПроцедуры

// Подсистема "Управление доступом".

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	ДанныеДляПроведения = ПолучитьДанныеДляПроведения();
	
	УчетНДФЛ.СформироватьНалогиВычеты(Движения, Отказ, Организация, КонецМесяца(МесяцПерерасчета), ДанныеДляПроведения.НалогиВычеты);
	УчетНачисленнойЗарплаты.ЗарегистрироватьНДФЛ(Движения, Отказ, Организация, МесяцПерерасчета, ДанныеДляПроведения.РезультатыРасчетаНДФЛ, ДанныеДляПроведения.МенеджерВременныхТаблиц, Перечисления.ХарактерВыплатыЗарплаты.Зарплата);
	
	ДанныеДляПроведения = ОтражениеЗарплатыВУчете.НоваяСтруктураРезультатыРасчетаЗарплаты();
	ДанныеДляПроведения.НачисленияУдержания = Движения.НачисленияУдержанияПоСотрудникам.Выгрузить();
	СтрокаСписокТаблиц = "НачисленныйНДФЛ";
	ОтражениеЗарплатыВБухучете.СформироватьДвиженияПоДокументу(Движения, Отказ, Организация, НачалоМесяца(МесяцПерерасчета), ДанныеДляПроведения, СтрокаСписокТаблиц);

КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Для Каждого СтрокаСотрудника Из Сотрудники Цикл
		
		СуммаВычетовНаДетейИИмущественных = ПримененныеВычетыНаДетейИИмущественные.Выгрузить(
			Новый Структура("ИдентификаторСтрокиНДФЛ", СтрокаСотрудника.ИдентификаторСтрокиНДФЛ), "РазмерВычета").Итог("РазмерВычета");
		
		Если НЕ ЗначениеЗаполнено(СтрокаСотрудника.НалогПоСтавке09 + СтрокаСотрудника.НалогПоСтавке13 
			+ СтрокаСотрудника.ЗачтеноАвансовыхПлатежейПоСтавке09 
			+ СтрокаСотрудника.ЗачтеноАвансовыхПлатежейПоСтавке13 
			+ СтрокаСотрудника.ЗачтеноАвансовыхПлатежейПоСтавке35 
			+ СтрокаСотрудника.НалогПоСтавке35 
			+ СтрокаСотрудника.НалогПоСтавке13КЗачетуВозврату
			+ СтрокаСотрудника.НалогПоСтавке35КЗачетуВозврату
			+ СтрокаСотрудника.ПримененныйВычетЛичный
			+ СтрокаСотрудника.ПримененныйВычетЛичныйКЗачетуВозврату
			+ СуммаВычетовНаДетейИИмущественных) Тогда
			
			СообщениеОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='В строке №%1 не указаны суммы перерасчета налога.'"), СтрокаСотрудника.НомерСтроки);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СообщениеОбОшибке,,,,Отказ);
			
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьДанныеДляПроведения()
	
	СписокФизическихЛиц = Неопределено;
	Если ДополнительныеСвойства.Свойство("ФизическиеЛица")
		И ДополнительныеСвойства.ФизическиеЛица.Количество() > 0 Тогда
		
		СписокФизическихЛиц = ДополнительныеСвойства.ФизическиеЛица;
		ОтборПоФизическимЛицам = Истина;
		
	Иначе
		ОтборПоФизическимЛицам = Ложь;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("МесяцНалоговогоПериода", Мин(Дата(НалоговыйПериод, 12, 1), НачалоМесяца(МесяцПерерасчета)));
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПерерасчетНДФЛСотрудники.Ссылка,
	|	ПерерасчетНДФЛСотрудники.НомерСтроки,
	|	ПерерасчетНДФЛСотрудники.ОбособленноеПодразделение КАК Подразделение,
	|	ПерерасчетНДФЛСотрудники.Сотрудник КАК ФизическоеЛицо,
	|	&МесяцНалоговогоПериода КАК МесяцНалоговогоПериода,
	|	ПерерасчетНДФЛСотрудники.НалогПоСтавке09,
	|	ПерерасчетНДФЛСотрудники.НалогПоСтавке13,
	|	ПерерасчетНДФЛСотрудники.НалогПоСтавке35,
	|	ПерерасчетНДФЛСотрудники.НалогПоСтавке13КЗачетуВозврату,
	|	ПерерасчетНДФЛСотрудники.НалогПоСтавке35КЗачетуВозврату,
	|	ПерерасчетНДФЛСотрудники.НалогПоСтавке13 КАК Налог,
	|	ПерерасчетНДФЛСотрудники.НалогПоСтавке13КЗачетуВозврату КАК НалогКЗачетуВозврату,
	|	ПерерасчетНДФЛСотрудники.ПримененныйВычетЛичный,
	|	ПерерасчетНДФЛСотрудники.ПримененныйВычетЛичныйКодВычета,
	|	ПерерасчетНДФЛСотрудники.ПримененныйВычетЛичныйКЗачетуВозврату,
	|	ПерерасчетНДФЛСотрудники.ПримененныйВычетЛичныйКЗачетуВозвратуКодВычета,
	|	ПерерасчетНДФЛСотрудники.ИдентификаторСтрокиНДФЛ,
	|	ПерерасчетНДФЛСотрудники.ЗачтеноАвансовыхПлатежейПоСтавке09,
	|	ПерерасчетНДФЛСотрудники.ЗачтеноАвансовыхПлатежейПоСтавке13,
	|	ПерерасчетНДФЛСотрудники.ЗачтеноАвансовыхПлатежейПоСтавке13 КАК ЗачтеноАвансовыхПлатежей,
	|	ПерерасчетНДФЛСотрудники.ЗачтеноАвансовыхПлатежейПоСтавке35
	|ПОМЕСТИТЬ ВТСтрокиДокумента
	|ИЗ
	|	Документ.ПерерасчетНДФЛ.Сотрудники КАК ПерерасчетНДФЛСотрудники
	|ГДЕ
	|	ПерерасчетНДФЛСотрудники.Ссылка = &Ссылка
	|	И ПерерасчетНДФЛСотрудники.Сотрудник В(&ФизическиеЛица)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтрокиДокумента.Подразделение,
	|	СтрокиДокумента.ФизическоеЛицо,
	|	СтрокиДокумента.МесяцНалоговогоПериода,
	|	СтрокиДокумента.НалогПоСтавке09,
	|	СтрокиДокумента.НалогПоСтавке35,
	|	СтрокиДокумента.НалогПоСтавке35КЗачетуВозврату,
	|	СтрокиДокумента.ЗачтеноАвансовыхПлатежейПоСтавке09,
	|	СтрокиДокумента.ЗачтеноАвансовыхПлатежейПоСтавке35
	|ИЗ
	|	ВТСтрокиДокумента КАК СтрокиДокумента
	|ГДЕ
	|	(СтрокиДокумента.НалогПоСтавке35 <> 0
	|			ИЛИ СтрокиДокумента.НалогПоСтавке35КЗачетуВозврату <> 0
	|			ИЛИ СтрокиДокумента.НалогПоСтавке09 <> 0)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Сотрудники.ФизическоеЛицо КАК ФизическоеЛицо
	|ПОМЕСТИТЬ ВТФизическиеЛица
	|ИЗ
	|	ВТСтрокиДокумента КАК Сотрудники
	|ГДЕ
	|	Сотрудники.ФизическоеЛицо <> ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка) КАК ФизическоеЛицо,
	|	ЗНАЧЕНИЕ(Справочник.Сотрудники.ПустаяСсылка) КАК Сотрудник,
	|	0 КАК СуммаДохода,
	|	0 КАК СуммаВычета,
	|	ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка) КАК Подразделение
	|ПОМЕСТИТЬ ВТДанныеДляРасчетаНДФЛ
	|ГДЕ
	|	ЛОЖЬ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(ДанныеДокумента.Сумма) КАК Сумма,
	|	СУММА(ДанныеДокумента.СуммаКорректировкиВыплаты) КАК СуммаКорректировкиВыплаты,
	|	ДанныеДокумента.Подразделение,
	|	ДанныеДокумента.Подразделение КАК ПодразделениеСотрудника,
	|	ТекущиеКадровыеДанныеСотрудников.Сотрудник,
	|	ДанныеДокумента.ФизическоеЛицо
	|ИЗ
	|	(ВЫБРАТЬ
	|		СтрокиДокумента.НалогПоСтавке13 - СтрокиДокумента.ЗачтеноАвансовыхПлатежейПоСтавке13 КАК Сумма,
	|		СтрокиДокумента.НалогПоСтавке13КЗачетуВозврату КАК СуммаКорректировкиВыплаты,
	|		СтрокиДокумента.Подразделение КАК Подразделение,
	|		СтрокиДокумента.ФизическоеЛицо КАК ФизическоеЛицо
	|	ИЗ
	|		ВТСтрокиДокумента КАК СтрокиДокумента
	|	ГДЕ
	|		(СтрокиДокумента.НалогПоСтавке13 <> 0
	|				ИЛИ СтрокиДокумента.НалогПоСтавке13КЗачетуВозврату <> 0)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СтрокиДокумента.НалогПоСтавке09 - СтрокиДокумента.ЗачтеноАвансовыхПлатежейПоСтавке09,
	|		0,
	|		СтрокиДокумента.Подразделение,
	|		СтрокиДокумента.ФизическоеЛицо
	|	ИЗ
	|		ВТСтрокиДокумента КАК СтрокиДокумента
	|	ГДЕ
	|		СтрокиДокумента.НалогПоСтавке09 <> 0
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СтрокиДокумента.НалогПоСтавке35 - СтрокиДокумента.ЗачтеноАвансовыхПлатежейПоСтавке35,
	|		СтрокиДокумента.НалогПоСтавке35КЗачетуВозврату,
	|		СтрокиДокумента.Подразделение,
	|		СтрокиДокумента.ФизическоеЛицо
	|	ИЗ
	|		ВТСтрокиДокумента КАК СтрокиДокумента
	|	ГДЕ
	|		(СтрокиДокумента.НалогПоСтавке35 <> 0
	|				ИЛИ СтрокиДокумента.НалогПоСтавке35КЗачетуВозврату <> 0)) КАК ДанныеДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТекущиеКадровыеДанныеСотрудников КАК ТекущиеКадровыеДанныеСотрудников
	|		ПО (ТекущиеКадровыеДанныеСотрудников.ФизическоеЛицо = ДанныеДокумента.ФизическоеЛицо)
	|			И (ТекущиеКадровыеДанныеСотрудников.ОсновноеРабочееМестоВОрганизации)
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеДокумента.Подразделение,
	|	ДанныеДокумента.ФизическоеЛицо,
	|	ТекущиеКадровыеДанныеСотрудников.Сотрудник,
	|	ДанныеДокумента.Подразделение";
	
	Если ОтборПоФизическимЛицам Тогда
		Запрос.УстановитьПараметр("ФизическиеЛица", СписокФизическихЛиц);
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "И ПерерасчетНДФЛСотрудники.Сотрудник В(&ФизическиеЛица)", "");
	КонецЕсли;
	
	Результаты = Запрос.ВыполнитьПакет();
	
	ОписаниеТаблиц = Новый Структура("ИмяТаблицыСНалогами,ИмяТаблицыСВычетами", "ВТСтрокиДокумента", "Документ.ПерерасчетНДФЛ.ПримененныеВычетыНаДетейИИмущественные");
	НалогиВычеты = УчетНДФЛ.ДанныеДокументаОНалогеПоОсновнойСтавкеИВычетах(Ссылка, ОписаниеТаблиц, Запрос.МенеджерВременныхТаблиц);
	
	Если Не Результаты[1].Пустой() Тогда
		НалогиВычеты.Колонки.Добавить("НалогПоСтавке09", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(13, 0)));
		НалогиВычеты.Колонки.Добавить("НалогПоСтавке35", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(13, 0)));
		НалогиВычеты.Колонки.Добавить("ЗачтеноАвансовыхПлатежейПоСтавке09", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(13, 0)));
		НалогиВычеты.Колонки.Добавить("ЗачтеноАвансовыхПлатежейПоСтавке35", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(13, 0)));
		НалогиВычеты.Колонки.Добавить("НалогПоСтавке35КЗачетуВозврату", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(13, 0)));
		Выборка = Результаты[1].Выбрать();
		Пока Выборка.Следующий() Цикл
			ЗаполнитьЗначенияСвойств(НалогиВычеты, Выборка);
		КонецЦикла;
	КонецЕсли;
	
	РезультатыРасчетаНДФЛ = Результаты[4].Выгрузить();
	
	ДанныеДляПроведения = Новый Структура("НалогиВычеты, РезультатыРасчетаНДФЛ, МенеджерВременныхТаблиц", НалогиВычеты, РезультатыРасчетаНДФЛ, Запрос.МенеджерВременныхТаблиц);
	
	Возврат ДанныеДляПроведения;

КонецФункции

#КонецОбласти

#КонецЕсли
