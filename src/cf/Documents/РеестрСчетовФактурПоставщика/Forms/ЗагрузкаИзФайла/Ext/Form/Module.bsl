&НаКлиенте
Перем ПараметрыОбработчикаОжидания;

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если ОбщегоНазначенияКлиентСервер.ЭтоВебКлиент() Тогда
		Элементы.ГруппаЗагрузкаФайлаСтраницы.ТекущаяСтраница = Элементы.ГруппаЗагрузкаФайлаВебКлиент;
	КонецЕсли;
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаРеквизиты;
	
	ЕстьПравоВывод = ПравоДоступа("Вывод", Метаданные);
	Если НЕ ЕстьПравоВывод Тогда
		ТолькоПросмотр = Истина;
		Элементы.ФормаЗагрузитьРеестр.Доступность = Ложь;
		Элементы.ВыбратьФайлДляСверкиВебКлиент.Доступность = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ОбщегоНазначенияКлиентСервер.ЭтоВебКлиент() Тогда
		ПодключитьОбработчикОжидания("Подключаемый_ПодключитьРасширениеРаботыСФайлами", 0.1, Истина);
	Иначе
		ЕстьРасширениеРаботыСФайлами = Истина;
		Элементы.ГруппаЗагрузкаФайлаСтраницы.ТекущаяСтраница = Элементы.ГруппаЗагрузкаФайла;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если НЕ ЕстьРасширениеРаботыСФайлами Тогда
		НепроверяемыеРеквизиты = Новый Массив;
		НепроверяемыеРеквизиты.Добавить("ПутьКФайлу");
		ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, НепроверяемыеРеквизиты);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии()
	
	Если ЗначениеЗаполнено(ИдентификаторЗадания) Тогда
		ОтменитьВыполнениеЗадания();
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЗагруженныйРеестр) Тогда
		Если Параметры.РежимСверки Тогда
			ПараметрыОповещения = Новый Структура("Организация,Контрагент,НалоговыйПериод", 
				Организация, Контрагент, НалоговыйПериод);
		Иначе
			ПараметрыОповещения = Новый Структура("Ссылка",	ЗагруженныйРеестр);
		КонецЕсли;
		Оповестить("Запись_РеестрСчетовФактурПоставщика", ПараметрыОповещения, ВладелецФормы);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
 
#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПутьКФайлуНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ДиалогВыбора = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	
	ДиалогВыбора.Фильтр = НСтр("ru='Документ XML (*.xml)|*.xml'");
	ДиалогВыбора.Заголовок = НСтр("ru = 'Выберите файл, полученный от поставщика'");
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыборПутиКФайлуЗавершение", ЭтотОбъект);
	ДиалогВыбора.Показать(ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПутьКФайлуПриИзменении(Элемент)
	
	ОбработатьИзменениеПутиКФайлу();
	
КонецПроцедуры

#КонецОбласти
 
#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыбратьФайлДляЗагрузкиВебКлиент(Команда)
	
	ФайлПрочитан = Ложь;
	ОписаниеОповещения = Новый ОписаниеОповещения("ПомещениеФайлаВебКлиентЗавершение", ЭтотОбъект);
	НачатьПомещениеФайла(ОписаниеОповещения, , "*.xml", Истина, УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьРеестр(Команда)
	
	Если НЕ ФайлПрочитан Тогда
		ТекстСообщения = НСтр("ru='Необходимо выбрать файл для загрузки.'");
		Если ЕстьРасширениеРаботыСФайлами Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "ПутьКФайлу");
		Иначе
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	ОчиститьСообщения();
	
	ОтключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеЗадания");
	
	РезультатВыполнения = ЗагрузитьРеестрНаСервере();
	Если НЕ РезультатВыполнения.ЗаданиеВыполнено Тогда
		ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
		ПодключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеЗадания", 1, Истина);
	ИначеЕсли ЗначениеЗаполнено(ЗагруженныйРеестр) Тогда
		ОповеститьОбИзменении(ЗагруженныйРеестр);
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
 
#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура Подключаемый_ПодключитьРасширениеРаботыСФайлами() Экспорт
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПодключитьРасширениеРаботыСФайламиЗавершение", ЭтотОбъект);
	ОбщегоНазначенияКлиент.ПоказатьВопросОбУстановкеРасширенияРаботыСФайлами(ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодключитьРасширениеРаботыСФайламиЗавершение(РасширениеПодключено, ДопПараметры) Экспорт
	
	ЕстьРасширениеРаботыСФайлами = РасширениеПодключено;
	Элементы.ГруппаЗагрузкаФайлаСтраницы.ТекущаяСтраница = ?(ЕстьРасширениеРаботыСФайлами, 
		Элементы.ГруппаЗагрузкаФайла,
		Элементы.ГруппаЗагрузкаФайлаВебКлиент);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборПутиКФайлуЗавершение(ВыбранныеФайлы, ДопПараметры) Экспорт
	
	Если ВыбранныеФайлы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПутьКФайлу = ВыбранныеФайлы[0];
	ОбработатьИзменениеПутиКФайлу();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьИзменениеПутиКФайлу()

	ФайлПрочитан = Ложь;
	АдресХранилищаДанныхКонтрагента = Неопределено;
	Если НЕ ЗначениеЗаполнено(ПутьКФайлу) Тогда
		Возврат;
	КонецЕсли;
	
	ВыбранныйФайл = Новый Файл(ПутьКФайлу);
	
	Если НРег(ВыбранныйФайл.Расширение) <> ".xml" Тогда
		ТекстСообщения = НСтр("ru='Необходимо выбрать файл с расширением ""xml"".'");
		ПоказатьПредупреждение(, ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	СтруктураПараметров = Новый Структура("Файл", ВыбранныйФайл);
	ОписаниеОповещения = Новый ОписаниеОповещения("ИзменениеПутиКФайлуПроверкаСуществованияЗавершение", 
		ЭтотОбъект, СтруктураПараметров);
	ВыбранныйФайл.НачатьПроверкуСуществования(ОписаниеОповещения);
	
КонецПроцедуры 

&НаКлиенте
Процедура ИзменениеПутиКФайлуПроверкаСуществованияЗавершение(Существует, ДопПараметры) Экспорт
	
	Если НЕ Существует Тогда
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Файл не существует: ""%1""'"),
			ПутьКФайлу);
		ПоказатьПредупреждение(, ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ИзменениеПутиКФайлуПроверкаЭтоФайлЗавершение", ЭтотОбъект);
	ДопПараметры.Файл.НачатьПроверкуЭтоФайл(ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменениеПутиКФайлуПроверкаЭтоФайлЗавершение(ЭтоФайл, ДопПараметры) Экспорт
	
	Если НЕ ЭтоФайл Тогда
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Указан каталог, а не файл: ""%1""'"),
			ПутьКФайлу);
		ПоказатьПредупреждение(, ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ИзменениеПутиКФайлуЗавершение", ЭтотОбъект);
	ПомещаемыеФайлы    = Новый Массив;
	ОписаниеФайла      = Новый ОписаниеПередаваемогоФайла(ПутьКФайлу);
	ПомещаемыеФайлы.Добавить(ОписаниеФайла);
	НачатьПомещениеФайлов(ОписаниеОповещения, ПомещаемыеФайлы, , Ложь, УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменениеПутиКФайлуЗавершение(ПомещенныеФайлы, ДопПараметры) Экспорт
	
	Если ПомещенныеФайлы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	АдресХранилищаДанныхКонтрагента = ПомещенныеФайлы[0].Хранение;
	РазобратьШапкуРеестра();
	
КонецПроцедуры

&НаКлиенте
Процедура ПомещениеФайлаВебКлиентЗавершение(РезультатПомещения, Адрес, ВыбранноеИмяФайла, ДопПараметры) Экспорт
	
	Если НЕ РезультатПомещения Тогда
		Возврат;
	КонецЕсли;
	
	Если НРег(Прав(ВыбранноеИмяФайла, 4)) <> ".xml" Тогда
		ТекстСообщения = НСтр("ru='Необходимо выбрать файл с расширением ""xml"".'");
		ПоказатьПредупреждение(, ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	АдресХранилищаДанныхКонтрагента = Адрес;
	РазобратьШапкуРеестра();
	
КонецПроцедуры

&НаКлиенте
Процедура РазобратьШапкуРеестра()

	РеквизитыРеестра = РеквизитыШапкиРеестра(АдресХранилищаДанныхКонтрагента);
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, РеквизитыРеестра);
	
	Если РеквизитыРеестра.Свойство("ОписаниеОшибки") Тогда
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаРеквизиты;
		ПоказатьПредупреждение(, РеквизитыРеестра.ОписаниеОшибки);
	Иначе
		
		ФайлПрочитан = Истина;
		
		Если ЗначениеЗаполнено(Контрагент)
			И ЗначениеЗаполнено(Организация) Тогда
			Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаРеквизиты;
		Иначе
			Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаОшибка;
			Элементы.Контрагент.ТолькоПросмотр = ЗначениеЗаполнено(Контрагент);
			ПараметрыВыбора = Новый Массив;
			ПараметрИНН = Новый ПараметрВыбора("Отбор.ИНН", ИННПоставщика);
			ПараметрыВыбора.Добавить(ПараметрИНН);
			Если ЗначениеЗаполнено(КПППоставщика) Тогда
				ПараметрКПП = Новый ПараметрВыбора("Отбор.КПП", КПППоставщика);
				ПараметрыВыбора.Добавить(ПараметрКПП);
			КонецЕсли;
			Элементы.КонтрагентОшибка.ПараметрыВыбора = Новый ФиксированныйМассив(ПараметрыВыбора);

			Элементы.Организация.ТолькоПросмотр = ЗначениеЗаполнено(Организация);
			ПараметрыВыбора = Новый Массив;
			ПараметрИНН = Новый ПараметрВыбора("Отбор.ИНН", ИННПокупателя);
			ПараметрыВыбора.Добавить(ПараметрИНН);
			Если ЗначениеЗаполнено(КПППокупателя) Тогда
				ПараметрКПП = Новый ПараметрВыбора("Отбор.КПП", КПППокупателя);
				ПараметрыВыбора.Добавить(ПараметрКПП);
			КонецЕсли;
			Элементы.ОрганизацияОшибка.ПараметрыВыбора = Новый ФиксированныйМассив(ПараметрыВыбора);
		КонецЕсли;
	
	КонецЕсли;
	
КонецПроцедуры 

&НаСервереБезКонтекста
Функция РеквизитыШапкиРеестра(АдресРеестра)
	
	ДвоичныеДанныеКонтрагента = ПолучитьИзВременногоХранилища(АдресРеестра);
	РеквизитыРеестра = СверкаДанныхУчетаНДС.РеквизитыРеестраСчетовФактур(ДвоичныеДанныеКонтрагента);
	
	РеквизитыРеестра.Вставить("Организация");
	РеквизитыРеестра.Вставить("НадписьОрганизация");
	РеквизитыРеестра.Вставить("Контрагент");
	РеквизитыРеестра.Вставить("НадписьКонтрагент");
	РеквизитыРеестра.Вставить("ПредставлениеНалоговогоПериода");
	
	Если РеквизитыРеестра.Свойство("ОписаниеОшибки") Тогда
		Возврат РеквизитыРеестра;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(РеквизитыРеестра.НалоговыйПериод) Тогда
		РеквизитыРеестра.ПредставлениеНалоговогоПериода = БухгалтерскиеОтчетыКлиентСервер.ПолучитьПредставлениеПериода(
			НачалоКвартала(РеквизитыРеестра.НалоговыйПериод), 
			КонецКвартала(РеквизитыРеестра.НалоговыйПериод),
			Истина);
	Иначе
		РеквизитыРеестра.ПредставлениеНалоговогоПериода = НСтр("ru='Не указан'");
	КонецЕсли;
	
	// Определяем контрагента
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Контрагенты.Ссылка,
	|	Контрагенты.КПП
	|ИЗ
	|	Справочник.Контрагенты КАК Контрагенты
	|ГДЕ
	|	Контрагенты.ИНН = &ИННПоставщика";
	Запрос.УстановитьПараметр("ИННПоставщика", РеквизитыРеестра.ИННПоставщика);
	Результат = Запрос.Выполнить();
	Если НЕ Результат.Пустой() Тогда
		ТаблицаИНН = Результат.Выгрузить();
		// Поиск по ИНН и КПП
		Если СтрДлина(РеквизитыРеестра.ИННПоставщика) = 10 
			И ЗначениеЗаполнено(РеквизитыРеестра.КПППоставщика) Тогда
		    ТаблицаКПП = ТаблицаИНН.Скопировать(Новый Структура("КПП", РеквизитыРеестра.КПППоставщика));
			Если ТаблицаКПП.Количество() > 1 Тогда
				РеквизитыРеестра.НадписьКонтрагент = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru='Найдено несколько контрагентов с ИНН %1 и КПП %2'"),
					РеквизитыРеестра.ИННПоставщика,
					РеквизитыРеестра.КПППоставщика);
			ИначеЕсли ТаблицаКПП.Количество() = 1 Тогда
				РеквизитыРеестра.Контрагент = ТаблицаКПП[0].Ссылка;
			КонецЕсли;
		КонецЕсли;
		// Поиск по ИНН
		Если НЕ ЗначениеЗаполнено(РеквизитыРеестра.Контрагент) Тогда
			Если ТаблицаИНН.Количество() > 1 
				И НЕ ЗначениеЗаполнено(РеквизитыРеестра.ПредставлениеКонтрагента) Тогда
				РеквизитыРеестра.НадписьКонтрагент = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru='Найдено несколько контрагентов с ИНН %1'"),
					РеквизитыРеестра.ИННПоставщика);
			ИначеЕсли ТаблицаИНН.Количество() = 1 Тогда
				РеквизитыРеестра.Контрагент = ТаблицаИНН[0].Ссылка;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(РеквизитыРеестра.Контрагент) Тогда
		Если ЗначениеЗаполнено(РеквизитыРеестра.КПППоставщика) Тогда
			РеквизитыРеестра.НадписьКонтрагент = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='Не найден контрагент с ИНН %1 и КПП %2'"),
				РеквизитыРеестра.ИННПоставщика,
				РеквизитыРеестра.КПППоставщика);
		Иначе
			РеквизитыРеестра.НадписьКонтрагент = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='Не найден контрагент с ИНН %1'"),
				РеквизитыРеестра.ИННПоставщика);
		КонецЕсли;
	КонецЕсли;

	// Определяем организацию
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Организации.Ссылка,
	|	Организации.КПП
	|ИЗ
	|	Справочник.Организации КАК Организации
	|ГДЕ
	|	Организации.ИНН = &ИННПокупателя";
	Запрос.УстановитьПараметр("ИННПокупателя", РеквизитыРеестра.ИННПокупателя);
	Результат = Запрос.Выполнить();
	Если НЕ Результат.Пустой() Тогда
		ТаблицаИНН = Результат.Выгрузить();
		// Поиск по ИНН и КПП
		Если СтрДлина(РеквизитыРеестра.ИННПокупателя) = 10 
			И ЗначениеЗаполнено(РеквизитыРеестра.КПППокупателя) Тогда
		    ТаблицаКПП = ТаблицаИНН.Скопировать(Новый Структура("КПП", РеквизитыРеестра.КПППокупателя));
			Если ТаблицаКПП.Количество() > 1 Тогда
				РеквизитыРеестра.НадписьОрганизация = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru='Найдено несколько организаций с ИНН %1 и КПП %2'"),
					РеквизитыРеестра.ИННПокупателя,
					РеквизитыРеестра.КПППокупателя);
			ИначеЕсли ТаблицаКПП.Количество() = 1 Тогда
				РеквизитыРеестра.Организация = ТаблицаКПП[0].Ссылка;
			КонецЕсли;
		КонецЕсли;
		// Поиск по ИНН
		Если НЕ ЗначениеЗаполнено(РеквизитыРеестра.Организация) Тогда
			Если ТаблицаИНН.Количество() > 1 
				И НЕ ЗначениеЗаполнено(РеквизитыРеестра.ПредставлениеОрганизации) Тогда
				РеквизитыРеестра.НадписьОрганизация = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru='Найдено несколько организаций с ИНН %1'"),
					РеквизитыРеестра.ИННПокупателя);
			ИначеЕсли ТаблицаИНН.Количество() = 1 Тогда
				РеквизитыРеестра.Организация = ТаблицаИНН[0].Ссылка;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(РеквизитыРеестра.Организация) Тогда
		Если ЗначениеЗаполнено(РеквизитыРеестра.КПППокупателя) Тогда
			РеквизитыРеестра.НадписьОрганизация = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='Не найдена организация с ИНН %1 и КПП %2'"),
				РеквизитыРеестра.ИННПокупателя,
				РеквизитыРеестра.КПППокупателя);
		Иначе
			РеквизитыРеестра.НадписьОрганизация = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='Не найдена организация с ИНН %1'"),
				РеквизитыРеестра.ИННПокупателя);
		КонецЕсли;
	КонецЕсли;
			
	Возврат РеквизитыРеестра;

КонецФункции

&НаСервере
Функция ЗагрузитьРеестрНаСервере()

	Если НЕ ПроверитьЗаполнение() Тогда 
		Возврат Новый Структура("ЗаданиеВыполнено", Истина);
	КонецЕсли;
	
	ДлительныеОперации.ОтменитьВыполнениеЗадания(ИдентификаторЗадания);
	
	ИдентификаторЗадания = Неопределено;
	
	ПараметрыРеестра = ПодготовитьПараметрыРеестра();
	
	РезультатВыполнения = ДлительныеОперации.ЗапуститьВыполнениеВФоне(
		УникальныйИдентификатор, 
		"СверкаДанныхУчетаНДС.ЗагрузитьРеестрСчетовФактурПоставщика", 
		ПараметрыРеестра,
		Заголовок);
	
	ИдентификаторЗадания = РезультатВыполнения.ИдентификаторЗадания;
	АдресХранилища       = РезультатВыполнения.АдресХранилища;
	
	Если РезультатВыполнения.ЗаданиеВыполнено Тогда
		ОбработатьРезультатЗагрузки();
	КонецЕсли;
	
	Возврат РезультатВыполнения;

КонецФункции 

&НаСервере
Функция ПодготовитьПараметрыРеестра()
	
	ПараметрыРеестра = Новый Структура;
	ПараметрыРеестра.Вставить("Организация", Организация);
	ПараметрыРеестра.Вставить("Контрагент",  Контрагент);
	ДвоичныеДанныеКонтрагента = ПолучитьИзВременногоХранилища(АдресХранилищаДанныхКонтрагента);
	ПараметрыРеестра.Вставить("ДвоичныеДанныеКонтрагента", ДвоичныеДанныеКонтрагента);
	Возврат ПараметрыРеестра;
	
КонецФункции

&НаКлиенте
Процедура Подключаемый_ПроверитьВыполнениеЗадания()

	Попытка
		Если ЗаданиеВыполнено(ИдентификаторЗадания) Тогда
			ОбработатьРезультатЗагрузки();
			Если ЗначениеЗаполнено(ЗагруженныйРеестр) Тогда
				ОповеститьОбИзменении(ЗагруженныйРеестр);
				Закрыть();
			КонецЕсли;
		Иначе
			ДлительныеОперацииКлиент.ОбновитьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
			ПодключитьОбработчикОжидания(
				"Подключаемый_ПроверитьВыполнениеЗадания",
				ПараметрыОбработчикаОжидания.ТекущийИнтервал,
				Истина);
		КонецЕсли;
	Исключение
		ВызватьИсключение;
	КонецПопытки;

КонецПроцедуры

&НаСервере
Процедура ОтменитьВыполнениеЗадания()
	
	ДлительныеОперации.ОтменитьВыполнениеЗадания(ИдентификаторЗадания);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗаданиеВыполнено(ИдентификаторЗадания)
	
	Возврат ДлительныеОперации.ЗаданиеВыполнено(ИдентификаторЗадания);
	
КонецФункции

&НаСервере
Процедура ОбработатьРезультатЗагрузки()

	РезультатВыполнения = ПолучитьИзВременногоХранилища(АдресХранилища);
	
	Если РезультатВыполнения.Свойство("ОписанияОшибок") Тогда
		Для каждого ТекстОшибки Из РезультатВыполнения.ОписанияОшибок Цикл
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
		КонецЦикла;
	Иначе
		ЗагруженныйРеестр = РезультатВыполнения.ЗагруженныйРеестр;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти
