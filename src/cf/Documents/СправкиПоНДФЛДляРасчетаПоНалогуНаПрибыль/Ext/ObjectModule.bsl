#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

// Подсистема "Управление доступом"

// Процедура ЗаполнитьНаборыЗначенийДоступа по свойствам объекта заполняет наборы значений доступа
// в таблице с полями:
//    НомерНабора     - Число                                     (необязательно, если набор один),
//    ВидДоступа      - ПланВидовХарактеристикСсылка.ВидыДоступа, (обязательно),
//    ЗначениеДоступа - Неопределено, СправочникСсылка или др.    (обязательно),
//    Чтение          - Булево (необязательно, если набор для всех прав) устанавливается для одной строки набора,
//    Добавление      - Булево (необязательно, если набор для всех прав) устанавливается для одной строки набора,
//    Изменение       - Булево (необязательно, если набор для всех прав) устанавливается для одной строки набора,
//    Удаление        - Булево (необязательно, если набор для всех прав) устанавливается для одной строки набора,
//
//  Вызывается из процедуры УправлениеДоступомСлужебный.ЗаписатьНаборыЗначенийДоступа(),
// если объект зарегистрирован в "ПодпискаНаСобытие.ЗаписатьНаборыЗначенийДоступа" и
// из таких же процедур объектов, у которых наборы значений доступа зависят от наборов этого
// объекта (в этом случае объект зарегистрирован в "ПодпискаНаСобытие.ЗаписатьЗависимыеНаборыЗначенийДоступа").
//
// Параметры:
//  Таблица      - ТабличнаяЧасть,
//                 РегистрСведенийНаборЗаписей.НаборыЗначенийДоступа,
//                 ТаблицаЗначений, возвращаемая УправлениеДоступом.ТаблицаНаборыЗначенийДоступа().
//
Процедура ЗаполнитьНаборыЗначенийДоступа(Таблица) Экспорт
	
	ЗарплатаКадры.ЗаполнитьНаборыПоОрганизацииИФизическимЛицам(ЭтотОбъект, Таблица, "Организация", "ФизическиеЛица.ФизическоеЛицо");
	
КонецПроцедуры

// Подсистема "Управление доступом"

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	НалоговыйПериодДата = Дата(?(НалоговыйПериод <= 0, 1, НалоговыйПериод), 12, 31);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

Функция СформироватьЗапросПоСтрокамДокументаДляПроверки(Сотрудники)
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТаблицаСотрудников", Сотрудники);
	Запрос.УстановитьПараметр("НалоговыйПериод", НалоговыйПериод);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СправкиПоНДФЛДляРасчетаПоНалогуНаПрибыль.НомерСтроки,
	|	СправкиПоНДФЛДляРасчетаПоНалогуНаПрибыль.Сотрудник,
	|	СправкиПоНДФЛДляРасчетаПоНалогуНаПрибыль.ИНН,
	|	СправкиПоНДФЛДляРасчетаПоНалогуНаПрибыль.НомерСправки,
	|	СправкиПоНДФЛДляРасчетаПоНалогуНаПрибыль.Ставка,
	|	СправкиПоНДФЛДляРасчетаПоНалогуНаПрибыль.Фамилия,
	|	СправкиПоНДФЛДляРасчетаПоНалогуНаПрибыль.Имя,
	|	СправкиПоНДФЛДляРасчетаПоНалогуНаПрибыль.Отчество,
	|	СправкиПоНДФЛДляРасчетаПоНалогуНаПрибыль.Адрес,
	|	СправкиПоНДФЛДляРасчетаПоНалогуНаПрибыль.ВидДокумента,
	|	СправкиПоНДФЛДляРасчетаПоНалогуНаПрибыль.СерияДокумента,
	|	СправкиПоНДФЛДляРасчетаПоНалогуНаПрибыль.НомерДокумента,
	|	СправкиПоНДФЛДляРасчетаПоНалогуНаПрибыль.Гражданство,
	|	СправкиПоНДФЛДляРасчетаПоНалогуНаПрибыль.ДатаРождения,
	|	СправкиПоНДФЛДляРасчетаПоНалогуНаПрибыль.СтатусНалогоплательщика,
	|	СправкиПоНДФЛДляРасчетаПоНалогуНаПрибыль.АдресЗарубежом,
	|	СправкиПоНДФЛДляРасчетаПоНалогуНаПрибыль.ОбщаяСуммаДоходаПоСтавке13,
	|	СправкиПоНДФЛДляРасчетаПоНалогуНаПрибыль.ОблагаемаяСуммаДоходаПоСтавке13,
	|	СправкиПоНДФЛДляРасчетаПоНалогуНаПрибыль.ИсчисленоПоСтавке13,
	|	СправкиПоНДФЛДляРасчетаПоНалогуНаПрибыль.УдержаноПоСтавке13,
	|	СправкиПоНДФЛДляРасчетаПоНалогуНаПрибыль.ЗадолженностьПоСтавке13,
	|	СправкиПоНДФЛДляРасчетаПоНалогуНаПрибыль.ИзлишнеУдержаноПоСтавке13,
	|	СправкиПоНДФЛДляРасчетаПоНалогуНаПрибыль.ПеречисленоПоСтавке13,
	|	СправкиПоНДФЛДляРасчетаПоНалогуНаПрибыль.ОбщаяСуммаДоходаПоСтавке30,
	|	СправкиПоНДФЛДляРасчетаПоНалогуНаПрибыль.ОблагаемаяСуммаДоходаПоСтавке30,
	|	СправкиПоНДФЛДляРасчетаПоНалогуНаПрибыль.ИсчисленоПоСтавке30,
	|	СправкиПоНДФЛДляРасчетаПоНалогуНаПрибыль.УдержаноПоСтавке30,
	|	СправкиПоНДФЛДляРасчетаПоНалогуНаПрибыль.ЗадолженностьПоСтавке30,
	|	СправкиПоНДФЛДляРасчетаПоНалогуНаПрибыль.ИзлишнеУдержаноПоСтавке30,
	|	СправкиПоНДФЛДляРасчетаПоНалогуНаПрибыль.ПеречисленоПоСтавке30,
	|	СправкиПоНДФЛДляРасчетаПоНалогуНаПрибыль.ОбщаяСуммаДоходаПоСтавке9,
	|	СправкиПоНДФЛДляРасчетаПоНалогуНаПрибыль.ОблагаемаяСуммаДоходаПоСтавке9,
	|	СправкиПоНДФЛДляРасчетаПоНалогуНаПрибыль.ИсчисленоПоСтавке9,
	|	СправкиПоНДФЛДляРасчетаПоНалогуНаПрибыль.УдержаноПоСтавке9,
	|	СправкиПоНДФЛДляРасчетаПоНалогуНаПрибыль.ЗадолженностьПоСтавке9,
	|	СправкиПоНДФЛДляРасчетаПоНалогуНаПрибыль.ИзлишнеУдержаноПоСтавке9,
	|	СправкиПоНДФЛДляРасчетаПоНалогуНаПрибыль.ПеречисленоПоСтавке9,
	|	СправкиПоНДФЛДляРасчетаПоНалогуНаПрибыль.ОбщаяСуммаДоходаПоСтавке15,
	|	СправкиПоНДФЛДляРасчетаПоНалогуНаПрибыль.ОблагаемаяСуммаДоходаПоСтавке15,
	|	СправкиПоНДФЛДляРасчетаПоНалогуНаПрибыль.ИсчисленоПоСтавке15,
	|	СправкиПоНДФЛДляРасчетаПоНалогуНаПрибыль.УдержаноПоСтавке15,
	|	СправкиПоНДФЛДляРасчетаПоНалогуНаПрибыль.ЗадолженностьПоСтавке15,
	|	СправкиПоНДФЛДляРасчетаПоНалогуНаПрибыль.ИзлишнеУдержаноПоСтавке15,
	|	СправкиПоНДФЛДляРасчетаПоНалогуНаПрибыль.ПеречисленоПоСтавке15,
	|	СправкиПоНДФЛДляРасчетаПоНалогуНаПрибыль.ОбщаяСуммаДоходаПоСтавке35,
	|	СправкиПоНДФЛДляРасчетаПоНалогуНаПрибыль.ОблагаемаяСуммаДоходаПоСтавке35,
	|	СправкиПоНДФЛДляРасчетаПоНалогуНаПрибыль.ИсчисленоПоСтавке35,
	|	СправкиПоНДФЛДляРасчетаПоНалогуНаПрибыль.УдержаноПоСтавке35,
	|	СправкиПоНДФЛДляРасчетаПоНалогуНаПрибыль.ЗадолженностьПоСтавке35,
	|	СправкиПоНДФЛДляРасчетаПоНалогуНаПрибыль.ИзлишнеУдержаноПоСтавке35,
	|	СправкиПоНДФЛДляРасчетаПоНалогуНаПрибыль.ПеречисленоПоСтавке35
	|ПОМЕСТИТЬ ВТСотрудники
	|ИЗ
	|	&ТаблицаСотрудников КАК СправкиПоНДФЛДляРасчетаПоНалогуНаПрибыль
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СотрудникиДублиНомеров.НомерСтроки
	|ПОМЕСТИТЬ ВТДублиНомеровСправок
	|ИЗ
	|	ВТСотрудники КАК Сотрудники
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСотрудники КАК СотрудникиДублиНомеров
	|		ПО Сотрудники.НомерСтроки < СотрудникиДублиНомеров.НомерСтроки
	|			И Сотрудники.НомерСправки = СотрудникиДублиНомеров.НомерСправки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СотрудникиДублиДанных.НомерСтроки
	|ПОМЕСТИТЬ ВТДублиДанныхСправок
	|ИЗ
	|	ВТСотрудники КАК Сотрудники
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСотрудники КАК СотрудникиДублиДанных
	|		ПО Сотрудники.НомерСтроки < СотрудникиДублиДанных.НомерСтроки
	|			И Сотрудники.Сотрудник = СотрудникиДублиДанных.Сотрудник
	|			И Сотрудники.Ставка = СотрудникиДублиДанных.Ставка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ДублиНомеровСправок.НомерСтроки ЕСТЬ NULL 
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ЕстьПовторяющиесяНомераСправок,
	|	ВЫБОР
	|		КОГДА ДублиДанныхСправок.НомерСтроки ЕСТЬ NULL 
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ЕстьПовторяющиесяДанныеСправок,
	|	Сотрудники.НомерСтроки КАК НомерСтроки,
	|	&НалоговыйПериод КАК НалоговыйПериод,
	|	Сотрудники.Сотрудник,
	|	Сотрудники.ИНН,
	|	Сотрудники.НомерСправки,
	|	Сотрудники.Ставка,
	|	Сотрудники.Фамилия,
	|	Сотрудники.Имя,
	|	Сотрудники.Отчество,
	|	Сотрудники.Адрес,
	|	Сотрудники.ВидДокумента,
	|	Сотрудники.СерияДокумента,
	|	Сотрудники.НомерДокумента,
	|	Сотрудники.Гражданство,
	|	Сотрудники.ДатаРождения,
	|	Сотрудники.СтатусНалогоплательщика,
	|	Сотрудники.АдресЗарубежом,
	|	Сотрудники.ОбщаяСуммаДоходаПоСтавке13,
	|	Сотрудники.ОблагаемаяСуммаДоходаПоСтавке13,
	|	Сотрудники.ИсчисленоПоСтавке13,
	|	Сотрудники.УдержаноПоСтавке13,
	|	Сотрудники.ЗадолженностьПоСтавке13,
	|	Сотрудники.ИзлишнеУдержаноПоСтавке13,
	|	Сотрудники.ПеречисленоПоСтавке13,
	|	Сотрудники.ОбщаяСуммаДоходаПоСтавке30,
	|	Сотрудники.ОблагаемаяСуммаДоходаПоСтавке30,
	|	Сотрудники.ИсчисленоПоСтавке30,
	|	Сотрудники.УдержаноПоСтавке30,
	|	Сотрудники.ЗадолженностьПоСтавке30,
	|	Сотрудники.ИзлишнеУдержаноПоСтавке30,
	|	Сотрудники.ПеречисленоПоСтавке30,
	|	Сотрудники.ОбщаяСуммаДоходаПоСтавке9,
	|	Сотрудники.ОблагаемаяСуммаДоходаПоСтавке9,
	|	Сотрудники.ИсчисленоПоСтавке9,
	|	Сотрудники.УдержаноПоСтавке9,
	|	Сотрудники.ЗадолженностьПоСтавке9,
	|	Сотрудники.ИзлишнеУдержаноПоСтавке9,
	|	Сотрудники.ПеречисленоПоСтавке9,
	|	Сотрудники.ОбщаяСуммаДоходаПоСтавке15,
	|	Сотрудники.ОблагаемаяСуммаДоходаПоСтавке15,
	|	Сотрудники.ИсчисленоПоСтавке15,
	|	Сотрудники.УдержаноПоСтавке15,
	|	Сотрудники.ЗадолженностьПоСтавке15,
	|	Сотрудники.ИзлишнеУдержаноПоСтавке15,
	|	Сотрудники.ПеречисленоПоСтавке15,
	|	Сотрудники.ОбщаяСуммаДоходаПоСтавке35,
	|	Сотрудники.ОблагаемаяСуммаДоходаПоСтавке35,
	|	Сотрудники.ИсчисленоПоСтавке35,
	|	Сотрудники.УдержаноПоСтавке35,
	|	Сотрудники.ЗадолженностьПоСтавке35,
	|	Сотрудники.ИзлишнеУдержаноПоСтавке35,
	|	Сотрудники.ПеречисленоПоСтавке35,
	|	ФизическиеЛица.Наименование КАК СотрудникНаименование,
	|	ПРЕДСТАВЛЕНИЕССЫЛКИ(Сотрудники.Ставка)
	|ИЗ
	|	ВТСотрудники КАК Сотрудники
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ФизическиеЛица КАК ФизическиеЛица
	|		ПО Сотрудники.Сотрудник = ФизическиеЛица.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДублиНомеровСправок КАК ДублиНомеровСправок
	|		ПО Сотрудники.НомерСтроки = ДублиНомеровСправок.НомерСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДублиДанныхСправок КАК ДублиДанныхСправок
	|		ПО Сотрудники.НомерСтроки = ДублиДанныхСправок.НомерСтроки
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	Возврат Запрос.Выполнить();				   
	
КонецФункции

Процедура ПроверитьДанныеДокумента(Отказ) Экспорт
		
	СоответствиеСтавокДоходов = УчетНДФЛ.СоответствиеДоходовСтавкам();
		
	ВыборкаСотрудниковДляПроверки = СформироватьЗапросПоСтрокамДокументаДляПроверки(Сотрудники.Выгрузить()).Выбрать();
	Если ЗначениеЗаполнено(Организация) Тогда
		СтруктураДанныхНА = УчетНДФЛ.СправкиНДФЛДанныеНалоговогоАгента(Организация, НалоговыйПериод, Справочники.РегистрацииВНалоговомОргане.ПустаяСсылка(), Дата, "", Справочники.Должности.ПустаяСсылка(), Неопределено);	
		УчетНДФЛ.СправкиНДФЛПроверитьДанныеНалоговогоАгента(
			ЭтотОбъект, 
			СтруктураДанныхНА, 
			Перечисления.ПорядокФормированияСправкиОДоходахФизическогоЛица.Сводно,
			Отказ);
	КонецЕсли;	
	
	Пока ВыборкаСотрудниковДляПроверки.СледующийПоЗначениюПоля("НомерСтроки") Цикл
			
		Документы.СправкаНДФЛ.ПроверитьДанныеФизическогоЛица(
			Ссылка,
			ВыборкаСотрудниковДляПроверки,
			Дата,
			"Сотрудники",
			ВыборкаСотрудниковДляПроверки.НомерСтроки,
			Отказ);
		
		Если ВыборкаСотрудниковДляПроверки.ЕстьПовторяющиесяНомераСправок Тогда
			ПутьКДанным = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Сотрудники", ВыборкаСотрудниковДляПроверки.НомерСтроки, "НомерСправки");
			
			ТекстСообщения = НСтр("ru = 'Номер справки был введен в документе ранее'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Ссылка,  ПутьКДанным, ,Отказ); 	
		КонецЕсли;
		
		Если ВыборкаСотрудниковДляПроверки.ЕстьПовторяющиесяДанныеСправок Тогда
			ПутьКДанным = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Сотрудники", ВыборкаСотрудниковДляПроверки.НомерСтроки, "Сотрудник");
			
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Нстр("ru = 'Данные по ставке %1 были введены в документе ранее'"), ВыборкаСотрудниковДляПроверки.СтавкаПредставление);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Ссылка, ПутьКДанным, ,Отказ);   
		КонецЕсли;
		
		СтруктураОтбора = Новый Структура("НомерСправки", ВыборкаСотрудниковДляПроверки.НомерСправки);
	
		Доходы = СведенияОДоходах.Выгрузить(СтруктураОтбора);
		Вычеты = СведенияОВычетах.Выгрузить(СтруктураОтбора);

		НачалоСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Нстр("ru = 'Получатель дохода %1, справка № %2:'"), ВыборкаСотрудниковДляПроверки.СотрудникНаименование, ВыборкаСотрудниковДляПроверки.НомерСправки) + " ";
		
		ПутьКДаннымСотрудника = "Сотрудники[" + Формат(ВыборкаСотрудниковДляПроверки.НомерСтроки - 1, "ЧН=0; ЧГ=") + "]";
		
		УчетНДФЛ.СправкиНДФЛПроверитьДанныеОДоходахНалогахВычетах(
			Ссылка,
			Дата,
			ВыборкаСотрудниковДляПроверки,
			ПутьКДаннымСотрудника,
			Доходы,
			Вычеты,
			СоответствиеСтавокДоходов,
			НачалоСообщения,
			Отказ,
			Истина);
			
	КонецЦикла;		
	
КонецПроцедуры

#КонецЕсли