#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Подсистема "Управление доступом".

// Процедура ЗаполнитьНаборыЗначенийДоступа по свойствам объекта заполняет наборы значений доступа
// в таблице с полями:
//    НомерНабора     - Число                                     (необязательно, если набор один),
//    ВидДоступа      - ПланВидовХарактеристикСсылка.ВидыДоступа, (обязательно),
//    ЗначениеДоступа - Неопределено, СправочникСсылка или др.    (обязательно),
//    Чтение          - Булево (необязательно, если набор для всех прав) устанавливается для одной строки набора,
//    Добавление      - Булево (необязательно, если набор для всех прав) устанавливается для одной строки набора,
//    Изменение       - Булево (необязательно, если набор для всех прав) устанавливается для одной строки набора,
//    Удаление        - Булево (необязательно, если набор для всех прав) устанавливается для одной строки набора,
//
//  Вызывается из процедуры УправлениеДоступомСлужебный.ЗаписатьНаборыЗначенийДоступа(),
// если объект зарегистрирован в "ПодпискаНаСобытие.ЗаписатьНаборыЗначенийДоступа" и
// из таких же процедур объектов, у которых наборы значений доступа зависят от наборов этого
// объекта (в этом случае объект зарегистрирован в "ПодпискаНаСобытие.ЗаписатьЗависимыеНаборыЗначенийДоступа").
//
// Параметры:
//  Таблица      - ТабличнаяЧасть,
//                 РегистрСведенийНаборЗаписей.НаборыЗначенийДоступа,
//                 ТаблицаЗначений, возвращаемая УправлениеДоступом.ТаблицаНаборыЗначенийДоступа().
//
Процедура ЗаполнитьНаборыЗначенийДоступа(Таблица) Экспорт
	
	ЗарплатаКадры.ЗаполнитьНаборыПоОрганизацииИФизическимЛицам(ЭтотОбъект, Таблица, "Организация", "Сотрудник");
	
КонецПроцедуры

// Подсистема "Управление доступом".

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.Сотрудники") Тогда
		ЗарплатаКадры.ЗаполнитьПоОснованиюСотрудником(ЭтотОбъект, ДанныеЗаполнения, , Истина);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	ДанныеДляПроведения = ПолучитьДанныеДляПроведения();
	
	УчетНДФЛ.СформироватьДоходыНДФЛПоКодамДоходовИзТаблицыЗначений(Движения, Отказ, Организация, ДатаОперации, ДанныеДляПроведения.Доходы);
	УчетНДФЛ.СформироватьПредоставленныеИмущественныеВычетыПоВременнойТаблице(Движения, Отказ, Организация, ДатаОперации, ДанныеДляПроведения.МенеджерВременныхТаблиц);
	УчетНДФЛ.СформироватьСтандартныеВычетыПоВременнойТаблице(Движения, Отказ, Организация, ДатаОперации, ДанныеДляПроведения.МенеджерВременныхТаблиц);
	УчетНДФЛ.СформироватьИсчисленныйНалогПоВременнойТаблице(Движения, Отказ, Организация, ДатаОперации, ДанныеДляПроведения.МенеджерВременныхТаблиц);
	УчетНДФЛ.СформироватьУдержанныйНалогПоВременнойТаблице(Движения, Отказ, Организация, ДатаОперации, ДанныеДляПроведения.МенеджерВременныхТаблиц);
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если СведенияОДоходах.Количество() = 0
		И НДФЛИсчисленныйПоСтавке13.Количество() = 0
		И НДФЛУдержанный.Количество() = 0
		И ПредоставленныеВычеты.Количество() = 0 Тогда
		
		СообщениеОбОшибке = НСтр("ru='Не заполнены табличные части, должна быть заполнена хотя бы одна табличная часть.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СообщениеОбОшибке,,,,Отказ);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьДанныеДляПроведения()
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Вычеты.Ссылка.Сотрудник КАК ФизическоеЛицо,
	|	Вычеты.МесяцНалоговогоПериода,
	|	Вычеты.ОбособленноеПодразделение КАК Подразделение,
	|	Вычеты.КодВычета,
	|	Вычеты.Сумма
	|ПОМЕСТИТЬ ВТДанныеОбИмущественныхВычетах
	|ИЗ
	|	Документ.ОперацияНалоговогоУчетаПоНДФЛ.ПредоставленныеВычеты КАК Вычеты
	|ГДЕ
	|	Вычеты.Ссылка = &Ссылка
	|	И Вычеты.КодВычета В (ЗНАЧЕНИЕ(Справочник.ВидыВычетовНДФЛ.Код311), ЗНАЧЕНИЕ(Справочник.ВидыВычетовНДФЛ.Код312), ЗНАЧЕНИЕ(Справочник.ВидыВычетовНДФЛ.Код318))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Вычеты.Ссылка.Сотрудник КАК ФизическоеЛицо,
	|	Вычеты.МесяцНалоговогоПериода,
	|	Вычеты.ОбособленноеПодразделение КАК Подразделение,
	|	Вычеты.КодВычета,
	|	Вычеты.Сумма,
	|	Вычеты.ВключатьВДекларациюПоНалогуНаПрибыль
	|ПОМЕСТИТЬ ВТДанныеОСтандартныхВычетах
	|ИЗ
	|	Документ.ОперацияНалоговогоУчетаПоНДФЛ.ПредоставленныеВычеты КАК Вычеты
	|ГДЕ
	|	Вычеты.Ссылка = &Ссылка
	|	И НЕ Вычеты.КодВычета В (ЗНАЧЕНИЕ(Справочник.ВидыВычетовНДФЛ.Код311), ЗНАЧЕНИЕ(Справочник.ВидыВычетовНДФЛ.Код312), ЗНАЧЕНИЕ(Справочник.ВидыВычетовНДФЛ.Код318))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах.Ссылка.Сотрудник КАК ФизическоеЛицо,
	|	ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах.ДатаПолученияДохода,
	|	ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах.ОбособленноеПодразделение КАК Подразделение,
	|	ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах.КодДохода,
	|	ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах.СуммаДохода,
	|	ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах.КодВычета,
	|	ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах.СуммаВычета,
	|	ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах.СуммаНалогаИсчисленная,
	|	ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах.ВключатьВДекларациюПоНалогуНаПрибыль,
	|	ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах.ВключатьВДекларациюПоНалогуНаПрибыль КАК ВключатьВДекларациюПоНалогуНаПрибыль1
	|ПОМЕСТИТЬ ВТСведенияОДоходахПоНДФЛ
	|ИЗ
	|	Документ.ОперацияНалоговогоУчетаПоНДФЛ.СведенияОДоходах КАК ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах
	|ГДЕ
	|	ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОперацияНалоговогоУчетаПоНДФЛНДФЛИсчисленныйПоСтавке13.Ссылка.Сотрудник КАК ФизическоеЛицо,
	|	ОперацияНалоговогоУчетаПоНДФЛНДФЛИсчисленныйПоСтавке13.МесяцНалоговогоПериода,
	|	ОперацияНалоговогоУчетаПоНДФЛНДФЛИсчисленныйПоСтавке13.ОбособленноеПодразделение КАК Подразделение,
	|	ОперацияНалоговогоУчетаПоНДФЛНДФЛИсчисленныйПоСтавке13.Сумма,
	|	ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка13) КАК СтавкаНалогообложенияРезидента,
	|	ЗНАЧЕНИЕ(Справочник.ВидыДоходовНДФЛ.ПустаяСсылка) КАК КодДохода,
	|	ОперацияНалоговогоУчетаПоНДФЛНДФЛИсчисленныйПоСтавке13.ВключатьВДекларациюПоНалогуНаПрибыль КАК ВключатьВДекларациюПоНалогуНаПрибыль
	|ПОМЕСТИТЬ ВТНДФЛИсчисленный
	|ИЗ
	|	Документ.ОперацияНалоговогоУчетаПоНДФЛ.НДФЛИсчисленныйПоСтавке13 КАК ОперацияНалоговогоУчетаПоНДФЛНДФЛИсчисленныйПоСтавке13
	|ГДЕ
	|	ОперацияНалоговогоУчетаПоНДФЛНДФЛИсчисленныйПоСтавке13.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах.ФизическоеЛицо,
	|	НАЧАЛОПЕРИОДА(ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах.ДатаПолученияДохода, МЕСЯЦ),
	|	ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах.Подразделение,
	|	ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах.СуммаНалогаИсчисленная,
	|	ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах.КодДохода.СтавкаНалогообложенияРезидента,
	|	ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах.КодДохода,
	|	ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах.ВключатьВДекларациюПоНалогуНаПрибыль
	|ИЗ
	|	ВТСведенияОДоходахПоНДФЛ КАК ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах
	|ГДЕ
	|	ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах.КодДохода.СтавкаНалогообложенияРезидента <> ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка13)
	|	И ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах.СуммаНалогаИсчисленная <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный.МесяцНалоговогоПериода,
	|	ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный.Ссылка.Сотрудник КАК ФизическоеЛицо,
	|	ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный.ОбособленноеПодразделение КАК Подразделение,
	|	ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный.СтавкаНалогообложения КАК СтавкаНалогообложенияРезидента,
	|	ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный.Ставка,
	|	ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный.Сумма,
	|	ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный.КодДохода,
	|	ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный.ВключатьВДекларациюПоНалогуНаПрибыль
	|ПОМЕСТИТЬ ВТНалогУдержанный
	|ИЗ
	|	Документ.ОперацияНалоговогоУчетаПоНДФЛ.НДФЛУдержанный КАК ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный
	|ГДЕ
	|	ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СведенияОДоходахПоНДФЛ.ФизическоеЛицо,
	|	СведенияОДоходахПоНДФЛ.ДатаПолученияДохода,
	|	НАЧАЛОПЕРИОДА(СведенияОДоходахПоНДФЛ.ДатаПолученияДохода, МЕСЯЦ) КАК МесяцНалоговогоПериода,
	|	СведенияОДоходахПоНДФЛ.Подразделение,
	|	СведенияОДоходахПоНДФЛ.КодДохода,
	|	СведенияОДоходахПоНДФЛ.СуммаДохода,
	|	СведенияОДоходахПоНДФЛ.КодВычета,
	|	СведенияОДоходахПоНДФЛ.СуммаВычета,
	|	СведенияОДоходахПоНДФЛ.ВключатьВДекларациюПоНалогуНаПрибыль
	|ИЗ
	|	ВТСведенияОДоходахПоНДФЛ КАК СведенияОДоходахПоНДФЛ
	|ГДЕ
	|	(СведенияОДоходахПоНДФЛ.СуммаДохода <> 0
	|			ИЛИ СведенияОДоходахПоНДФЛ.СуммаВычета <> 0)";
	Результаты = Запрос.ВыполнитьПакет();
	
	Доходы = Результаты[5].Выгрузить();
	
	ДанныеДляПроведения = Новый Структура("Доходы, МенеджерВременныхТаблиц", Доходы, Запрос.МенеджерВременныхТаблиц);
						
	Возврат ДанныеДляПроведения;

КонецФункции

#КонецОбласти

#КонецЕсли
