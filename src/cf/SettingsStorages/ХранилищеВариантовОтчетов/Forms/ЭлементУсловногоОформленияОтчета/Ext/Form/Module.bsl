#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Не Параметры.Свойство("КомпоновщикНастроек", КомпоновщикНастроек) Тогда
		ВызватьИсключение НСтр("ru = 'Не передан служебный параметр ""КомпоновщикНастроек"".'");
	КонецЕсли;
	Если Не Параметры.Свойство("НастройкиОтчета", НастройкиОтчета) Тогда
		ВызватьИсключение НСтр("ru = 'Не передан служебный параметр ""НастройкиОтчета"".'");
	КонецЕсли;
	Если Не Параметры.Свойство("ИдентификаторТекущегоУзлаКД", ИдентификаторТекущегоУзлаКД) Тогда
		ВызватьИсключение НСтр("ru = 'Не передан служебный параметр ""ИдентификаторТекущегоУзлаКД"".'");
	КонецЕсли;
	Если Не Параметры.Свойство("ИдентификаторКД", ИдентификаторКД) Тогда
		ВызватьИсключение НСтр("ru = 'Не передан служебный параметр ""ИдентификаторКД"".'");
	КонецЕсли;
	
	Источник = Новый ИсточникДоступныхНастроекКомпоновкиДанных(НастройкиОтчета.АдресСхемы);
	КомпоновщикНастроек.Инициализировать(Источник);
	
	УзелКД = УзелКД(ЭтотОбъект);
	Если УзелКД = Неопределено Тогда
		ВызватьИсключение НСтр("ru = 'Не найден узел отчета.'");
	КонецЕсли;
	ЭлементКД = УзелКД.ПолучитьОбъектПоИдентификатору(ИдентификаторКД);
	Если ЭлементКД = Неопределено Тогда
		ВызватьИсключение НСтр("ru = 'Не найден элемент условного оформления.'");
	КонецЕсли;
	
	КомпоновщикНастроек.Настройки.УсловноеОформление.Элементы.Очистить();
	РедактируемыйЭлементКД = КомпоновщикНастроек.Настройки.УсловноеОформление.Элементы.Добавить();
	ЗаполнитьЗначенияСвойств(РедактируемыйЭлементКД, ЭлементКД);
	
	ЗакрыватьПриВыборе = Ложь;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура Выбрать(Команда)
	ВыбратьИЗакрыть();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВыбратьИЗакрыть()
	ЭлементКД = КомпоновщикНастроек.Настройки.УсловноеОформление.Элементы[0];
	ОповеститьОВыборе(ЭлементКД);
	Закрыть(ЭлементКД);
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция УзелКД(ЭтотОбъект)
	Если ЭтотОбъект.ИдентификаторТекущегоУзлаКД = Неопределено Тогда
		Возврат ЭтотОбъект.КомпоновщикНастроек.ПользовательскиеНастройки;
	Иначе
		ТекущийУзелКД = ЭтотОбъект.КомпоновщикНастроек.Настройки.ПолучитьОбъектПоИдентификатору(ЭтотОбъект.ИдентификаторТекущегоУзлаКД);
		Возврат ТекущийУзелКД.УсловноеОформление;
	КонецЕсли;
КонецФункции

#КонецОбласти