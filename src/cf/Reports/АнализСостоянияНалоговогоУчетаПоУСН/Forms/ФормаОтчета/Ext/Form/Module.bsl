&НаКлиенте
Перем ПараметрыОбработчикаОжидания; 

&НаКлиенте
Перем ПараметрыОбработчикаОжиданияАктуализации Экспорт;

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ДанныеРасшифровки = ПоместитьВоВременноеХранилище(ДанныеРасшифровки, УникальныйИдентификатор);
	
	УстановитьНачальныеНастройки();
	
	УправлениеФормой(ЭтаФорма);
	
	ОбщегоНазначенияБПВызовСервера.ЗаполнитьСписокОрганизаций(Элементы.ПолеОрганизация, СоответствиеОрганизаций);
		
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	БухгалтерскиеОтчетыКлиент.ПриОткрытии(ЭтаФорма, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	Если ТекущийМакет <> "МакетКарты" И НЕ ЗавершитьРаботуСОтчетом Тогда
		
		Отказ = Истина;
		Оповещение = Новый ОписаниеОповещения("ВопросПередЗакрытиемЗавершение", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, НСтр("ru = 'Завершить работу с отчетом?'"), РежимДиалогаВопрос.ДаНет);
		
	КонецЕсли;
	
	БухгалтерскиеОтчетыКлиент.ПередЗакрытием(ЭтаФорма, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии()
	
	Если ЗначениеЗаполнено(ИдентификаторЗадания) Тогда
		ОтменитьВыполнениеЗадания();
	КонецЕсли;
	
	БухгалтерскийУчетКлиентПереопределяемый.ОтменитьЗаданияАктуализации(ЭтотОбъект, Отчет.Организация);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("Структура") Тогда		
		СохранитьНастройкуМакета(ВыбранноеЗначение);		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриСохраненииПользовательскихНастроекНаСервере(Настройки)
	
	БухгалтерскиеОтчетыВызовСервера.ПриСохраненииПользовательскихНастроекНаСервере(ЭтаФорма, Настройки, Истина);
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеПользовательскихНастроекНаСервере(Настройки)
	
	БухгалтерскиеОтчетыВызовСервера.ПриЗагрузкеПользовательскихНастроекНаСервере(ЭтаФорма, Настройки);
	
	ОбновитьТекстЗаголовка(ЭтаФорма);
	
	УправлениеФормой(ЭтаФорма);
		
КонецПроцедуры


&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	БухгалтерскийУчетКлиентПереопределяемый.ОбработкаОповещенияАктуализации(ЭтотОбъект, Отчет.Организация, Отчет.КонецПериода, ИмяСобытия, Параметр, Источник);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура НачалоПериодаПриИзменении(Элемент)
	ОбновитьТекстЗаголовка(ЭтаФорма);
	Если Не ЗначениеЗаполнено(ИдентификаторЗадания) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеАктуальность");
	КонецЕсли;
	
	БухгалтерскийУчетКлиентПереопределяемый.СкрытьПанельАктуализацииАвтоматически(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура КонецПериодаПриИзменении(Элемент)
	ОбновитьТекстЗаголовка(ЭтаФорма);
	Если Не ЗначениеЗаполнено(ИдентификаторЗадания) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеАктуальность");
	КонецЕсли;
	
	БухгалтерскийУчетКлиентПереопределяемый.СкрытьПанельАктуализацииАвтоматически(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ПолеОрганизацияПриИзменении(Элемент)
	
	ОбщегоНазначенияБПКлиент.ПолеОрганизацияПриИзменении(Элемент, ПолеОрганизация,
		Отчет.Организация, Отчет.ВключатьОбособленныеПодразделения);
	
	ОбновитьТекстЗаголовка(ЭтаФорма);
	
	БухгалтерскийУчетКлиентПереопределяемый.СкрытьПанельАктуализацииАвтоматически(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолеОрганизацияОткрытие(Элемент, СтандартнаяОбработка)
	
	ОбщегоНазначенияБПКлиент.ПолеОрганизацияОткрытие(Элемент, СтандартнаяОбработка,
		ПолеОрганизация, СоответствиеОрганизаций);
		
КонецПроцедуры
	
&НаКлиенте
Процедура ПолеОрганизацияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ОбщегоНазначенияБПКлиент.ПолеОрганизацияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка, 
		СоответствиеОрганизаций, Отчет.Организация, Отчет.ВключатьОбособленныеПодразделения);
	
КонецПроцедуры

/////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ПОЛЯ ТАБЛИЧНОГО ДОКУМЕНТА

&НаКлиенте
Процедура РезультатПриАктивизацииОбласти(Элемент)
	
	Если ТипЗнч(Результат.ВыделенныеОбласти) = Тип("ВыделенныеОбластиТабличногоДокумента") Тогда
		ИнтервалОжидания = ?(ПолучитьСкоростьКлиентскогоСоединения() = СкоростьКлиентскогоСоединения.Низкая, 1, 0.2);
		ПодключитьОбработчикОжидания("Подключаемый_РезультатПриАктивизацииОбластиПодключаемый", ИнтервалОжидания, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РезультатСхемыВыбор(Элемент)
	
	Если Элемент.ТекущийЭлемент = Неопределено ИЛИ НЕ РасшифровкиДоступны Тогда		
		Возврат;		
	КонецЕсли;
	
	ОтключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеЗадания");

	ИмяЭлемента = Элемент.ТекущийЭлемент.Имя;
	
	Если Найти(ИмяЭлемента, "Расшифровка") = 0 Тогда	
		Возврат;		
	КонецЕсли;
	
	РезультатВыполнения = РасшифроватьСхемуНалоговойБазыНаСервере(ИмяЭлемента);
	Если Не РезультатВыполнения.ЗаданиеВыполнено Тогда		
		ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
		ПодключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеЗадания", 1, Истина);
		Элементы.ГруппаРезультатСхема.ТекущаяСтраница = Элементы.СтраницаИндикаторВыполнения;
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.ИндикаторВыполнения, "ФормированиеОтчета");
	Иначе
		ОбновитьСтраницы();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РезультатОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ИмяФормыВызова       = Неопределено;
	
	Если НЕ РасшифровкиДоступны Тогда
		 Возврат;		 
	КонецЕсли;
	 
	ЗначениеРасшифровки = Неопределено;
	
	ОбработкаРасшифровкиНаСервере(Расшифровка, ИмяФормыВызова, ЗначениеРасшифровки);
	
	Если НЕ ИмяФормыВызова = Неопределено Тогда		
		ПараметрыФормы = Новый Структура("Ключ", ЗначениеРасшифровки); 
		ОткрытьФорму(ИмяФормыВызова, ПараметрыФормы);		
	КонецЕсли;
	
	ОбновитьСтраницы();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ВыбратьПериод(Команда)
	
	ПараметрыВыбора = Новый Структура("НачалоПериода,КонецПериода", Отчет.НачалоПериода, Отчет.КонецПериода);
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыбратьПериодЗавершение", ЭтотОбъект);
	ОткрытьФорму("ОбщаяФорма.ВыборСтандартногоПериода", ПараметрыВыбора, Элементы.ВыбратьПериод, , , , ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПериодЗавершение(РезультатВыбора, ДопПараметры) Экспорт
	
	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ЗаполнитьЗначенияСвойств(Отчет, РезультатВыбора, "НачалоПериода,КонецПериода");
	
	ОбновитьТекстЗаголовка(ЭтаФорма); 
	Если Не ЗначениеЗаполнено(ИдентификаторЗадания) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеАктуальность");
	КонецЕсли;
	
	БухгалтерскийУчетКлиентПереопределяемый.СкрытьПанельАктуализацииАвтоматически(ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура КомандаНазад(Команда)
	
	ОбработатьПереходНазад(Ложь);
	ОбновитьСтраницы();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаСтруктураНалоговойБазы(Команда)
	
	ОбработатьПереходНазад(Истина);
	ОбновитьСтраницы();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуНастройкиСхемы(Команда)
	
	ПараметрыДляФормы = Новый Структура("ИмяМакета, Настройка", ТекущийМакет, ПолучитьНастройкуМакета(ТекущийМакет));
	
	ОткрытьФорму("Отчет.АнализСостоянияНалоговогоУчетаПоУСН.Форма.ФормаВарианта", ПараметрыДляФормы, ЭтаФорма); 
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьОтчет(Команда)
	
	ОчиститьСообщения();
	
	ОтключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеЗадания");
	
	РезультатВыполнения = СформироватьОтчетНаСервере();
	Если Не РезультатВыполнения.ЗаданиеВыполнено Тогда		
		ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
		ПодключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеЗадания", 1, Истина);
		
		Элементы.ГруппаРезультатСхема.ТекущаяСтраница = Элементы.СтраницаИндикаторВыполнения;
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.ИндикаторВыполнения, "ФормированиеОтчета");
	Иначе
				
		БухгалтерскийУчетКлиентПереопределяемый.ПодключитьПроверкуАктуальности(ЭтотОбъект);
		ОбновитьСтраницы();	
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура Подключаемый_ПроверитьАктуальность()
	
	БухгалтерскийУчетКлиентПереопределяемый.ПроверитьАктуальность(ЭтотОбъект, Отчет.Организация, Отчет.КонецПериода);

КонецПроцедуры


&НаКлиенте
Процедура Подключаемый_ОбработатьРезультатПроверкиАктуальности()

	БухгалтерскийУчетКлиентПереопределяемый.ПроверитьВыполнениеПроверкиАктуальностиОтчета(ЭтотОбъект);	

КонецПроцедуры


&НаКлиенте
Процедура Актуализировать(Команда)
	
	БухгалтерскийУчетКлиентПереопределяемый.Актуализировать(ЭтотОбъект, Отчет.Организация, Отчет.КонецПериода);
	
КонецПроцедуры


&НаКлиенте
Процедура Подключаемый_ПроверитьВыполнениеАктуализации()
	
	БухгалтерскийУчетКлиентПереопределяемый.ПроверитьВыполнениеАктуализацииОтчета(ЭтотОбъект, Отчет.Организация, Отчет.КонецПериода);

КонецПроцедуры


&НаКлиенте
Процедура ОтменитьАктуализацию(Команда)
	
	БухгалтерскийУчетКлиентПереопределяемый.ОтменитьАктуализацию(ЭтотОбъект, Отчет.Организация, Отчет.КонецПериода);
	
КонецПроцедуры


&НаКлиенте
Процедура СкрытьНажатие(Элемент)
	
	БухгалтерскийУчетКлиентПереопределяемый.СкрытьПанельАктуализации(ЭтотОбъект);
	
КонецПроцедуры


&НаКлиенте
Процедура Подключаемый_ПроверитьЗавершениеАктуализации()
	
	БухгалтерскийУчетКлиентПереопределяемый.ПроверитьЗавершениеАктуализации(ЭтотОбъект, Отчет.Организация, Отчет.КонецПериода);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере
Процедура ДополнитьПараметрыОтчетаДляРасшифровкиНаСервере(ПараметрыОтчета, ИмяЭлемента)
	
	Если Найти(ИмяЭлемента, "Расшифровка") = 0 Тогда	
		Возврат;		
	КонецЕсли;
	
	ИмяРасшифровки = СтрЗаменить(ИмяЭлемента, "Расшифровка", "");
	
	ОтключитьТаблицы = Ложь;
	Расшифровка 	 = Неопределено;
	ВыделятьНДСУСН 	 = Отчеты.АнализСостоянияНалоговогоУчетаПоУСН.ВыделятьНДСУСН(ПараметрыОтчета);
	
	Если Найти(ИмяРасшифровки, "МакетРасшифровкиПоНоменклатуре") > 0 Тогда
		ИмяМакета = ?(ВыделятьНДСУСН, "МакетРасшифровкиПоНоменклатуре", "МакетРасшифровкиПоНоменклатуре2009");
	ИначеЕсли Найти(ИмяРасшифровки, "МакетРасшифровкиПоРБП") > 0 Тогда
		ИмяМакета = ?(ВыделятьНДСУСН, "МакетРасшифровкиПоРБП", "МакетРасшифровкиПоРБП2009");
	ИначеЕсли Найти(ИмяРасшифровки, "МакетРасшифровкиПоТЗР") > 0 Тогда
		ИмяМакета = ?(ВыделятьНДСУСН, "МакетРасшифровкиПоТЗР", "МакетРасшифровкиПоТЗР2009");
	ИначеЕсли Найти(ИмяРасшифровки, "МакетРасшифровкиПоНДС") > 0 Тогда
		ИмяМакета = ?(ВыделятьНДСУСН, "МакетРасшифровкиПоНДС", "МакетРасшифровкиПоНДС2009");
	ИначеЕсли СтрДлина(ИмяРасшифровки) > 1 Тогда
		ИмяМакета = ИмяРасшифровки;
	ИначеЕсли СтрДлина(ИмяРасшифровки) = 1 Тогда
		ИмяМакета        = "МакетРасшифровкиДоходов";
		ОтключитьТаблицы = Истина;
		Расшифровка      = Число(ИмяРасшифровки);
	Иначе 	
		Возврат;
	КонецЕсли;
	                    
	ПутьМакета   = ТекущийМакет + "/" + ПутьМакета;
	ТекущийМакет = ИмяМакета;
	
	ПараметрыОтчета.Вставить("Расшифровка"     , Расшифровка);
	ПараметрыОтчета.Вставить("ОтключитьТаблицы", ОтключитьТаблицы);
    ПараметрыОтчета.Вставить("ИмяМакета"       , ИмяМакета);
	
КонецПроцедуры

&НаСервере
Функция ПодготовитьПараметрыОтчетаНаСервере()
	
	ПараметрыОтчета = Новый Структура;
	ПараметрыОтчета.Вставить("Организация"                      , Отчет.Организация);
	ПараметрыОтчета.Вставить("НачалоПериода"                    , Отчет.НачалоПериода);
	ПараметрыОтчета.Вставить("КонецПериода"                     , Отчет.КонецПериода);
	ПараметрыОтчета.Вставить("ДатаИзмененияУПпоНДС"             , Отчет.ДатаИзмененияУПпоНДС);
	ПараметрыОтчета.Вставить("ВключатьОбособленныеПодразделения", Отчет.ВключатьОбособленныеПодразделения);
	ПараметрыОтчета.Вставить("УникальныйИдентификатор"          , УникальныйИдентификатор);
	ПараметрыОтчета.Вставить("КоллекцияНастроек"                , Отчет.КоллекцияНастроек);
	ПараметрыОтчета.Вставить("ИмяМакета"                        , ТекущийМакет);
	
	Возврат ПараметрыОтчета;
	
КонецФункции

&НаКлиенте
Процедура Подключаемый_ПроверитьВыполнениеЗадания()
	
	Попытка
		Если ЗаданиеВыполнено(ИдентификаторЗадания) Тогда 
			ЗагрузитьПодготовленныеДанныеНаСервере();
			Элементы.ГруппаРезультатСхема.ТекущаяСтраница = Элементы.СтраницаРезультатСхема;
			ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.ИндикаторВыполнения, "НеИспользовать");
			ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеИспользовать");
			
			ОбновитьСтраницы();
			
					
			БухгалтерскийУчетКлиентПереопределяемый.ПодключитьПроверкуАктуальности(ЭтотОбъект);
		Иначе
			ДлительныеОперацииКлиент.ОбновитьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
			ПодключитьОбработчикОжидания(
				"Подключаемый_ПроверитьВыполнениеЗадания", 
				ПараметрыОбработчикаОжидания.ТекущийИнтервал, 
				Истина);
		КонецЕсли;
	Исключение
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеИспользовать");
		Элементы.ГруппаРезультатСхема.ТекущаяСтраница = Элементы.СтраницаРезультатСхема;
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.ИндикаторВыполнения, "НеИспользовать");
		
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗаданиеВыполнено(ИдентификаторЗадания)
	
	Возврат ДлительныеОперации.ЗаданиеВыполнено(ИдентификаторЗадания);
	
КонецФункции

&НаСервере
Процедура ОтменитьВыполнениеЗадания()
	
	ДлительныеОперации.ОтменитьВыполнениеЗадания(ИдентификаторЗадания);
	
КонецПроцедуры

&НаСервере
Функция СформироватьОтчетНаСервере() Экспорт
	
	Отчет.ДатаИзмененияУПпоНДС = Отчеты.АнализСостоянияНалоговогоУчетаПоУСН.ДатаИзмененияУчетнойПолитикиПоНДС(
		Отчет.Организация, Отчет.КонецПериода);
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат Новый Структура("ЗаданиеВыполнено", Истина);
	КонецЕсли;
	
	ДлительныеОперации.ОтменитьВыполнениеЗадания(ИдентификаторЗадания);
	
	ИдентификаторЗадания = Неопределено;
	
	ПараметрыОтчета = ПодготовитьПараметрыОтчетаНаСервере();
	
	// очистим КЭШ отчетов 
	КэшОтчетов = Новый Структура;
	
	Если ОбщегоНазначения.ИнформационнаяБазаФайловая() Тогда
		АдресХранилища = ПоместитьВоВременноеХранилище(Неопределено, УникальныйИдентификатор);
		Отчеты.АнализСостоянияНалоговогоУчетаПоУСН.ЗаполнитьСхемуНалоговойБазы(ПараметрыОтчета, АдресХранилища);
		РезультатВыполнения = Новый Структура("ЗаданиеВыполнено", Истина);
	Иначе
		РезультатВыполнения = ДлительныеОперации.ЗапуститьВыполнениеВФоне(
			УникальныйИдентификатор, 
			"Отчеты.АнализСостоянияНалоговогоУчетаПоУСН.ЗаполнитьСхемуНалоговойБазы", 
			ПараметрыОтчета, 
			БухгалтерскиеОтчетыКлиентСервер.ПолучитьНаименованиеЗаданияВыполненияОтчета(ЭтаФорма));
			
		АдресХранилища       = РезультатВыполнения.АдресХранилища;
		ИдентификаторЗадания = РезультатВыполнения.ИдентификаторЗадания;		
	КонецЕсли;
	
	Если РезультатВыполнения.ЗаданиеВыполнено Тогда
		ЗагрузитьПодготовленныеДанныеНаСервере();
	КонецЕсли;
	
	Возврат РезультатВыполнения;
	
КонецФункции

&НаСервере
Функция РасшифроватьСхемуНалоговойБазыНаСервере(ИмяЭлемента)
	
	Результат.Очистить();
	
	ДлительныеОперации.ОтменитьВыполнениеЗадания(ИдентификаторЗадания);
	
	ИдентификаторЗадания = Неопределено;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеИспользовать");
	
	Если ЗначениеЗаполнено(КэшОтчетовАдресХранилища) Тогда
		КэшОтчетов = ПолучитьИзВременногоХранилища(КэшОтчетовАдресХранилища);
	Иначе
		КэшОтчетов = Новый Структура;
	КонецЕсли;
	
	ПараметрыОтчета = ПодготовитьПараметрыОтчетаНаСервере();
	ПараметрыОтчета.Вставить("ИмяЭлемента", ИмяЭлемента);
	ПараметрыОтчета.Вставить("КэшОтчетов" , КэшОтчетов);
	
	ДополнитьПараметрыОтчетаДляРасшифровкиНаСервере(ПараметрыОтчета, ИмяЭлемента);
	
	Если ОбщегоНазначения.ИнформационнаяБазаФайловая() Тогда
		АдресХранилища = ПоместитьВоВременноеХранилище(Неопределено, УникальныйИдентификатор);
		Отчеты.АнализСостоянияНалоговогоУчетаПоУСН.СформироватьРасшифровку(ПараметрыОтчета, АдресХранилища);
		РезультатВыполнения = Новый Структура("ЗаданиеВыполнено", Истина);
	Иначе
		РезультатВыполнения = ДлительныеОперации.ЗапуститьВыполнениеВФоне(УникальныйИдентификатор, 
																			"Отчеты.АнализСостоянияНалоговогоУчетаПоУСН.СформироватьРасшифровку", 
																			ПараметрыОтчета, БухгалтерскиеОтчетыКлиентСервер.ПолучитьНаименованиеЗаданияВыполненияОтчета(ЭтаФорма));
		АдресХранилища       = РезультатВыполнения.АдресХранилища;
		ИдентификаторЗадания = РезультатВыполнения.ИдентификаторЗадания;		
	КонецЕсли;
	
	Если РезультатВыполнения.ЗаданиеВыполнено Тогда
		ЗагрузитьПодготовленныеДанныеНаСервере();
	КонецЕсли;
	
	Возврат РезультатВыполнения;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьТекстЗаголовка(Форма)
	
	Отчет = Форма.Отчет;
	
	ЗаголовокОтчета = "Анализ состояния налогового учета по УСН" + БухгалтерскиеОтчетыКлиентСервер.ПолучитьПредставлениеПериода(Отчет.НачалоПериода, Отчет.КонецПериода);
	
	Если ЗначениеЗаполнено(Отчет.Организация) И Форма.ИспользуетсяНесколькоОрганизаций Тогда
		
		ЗаголовокОтчета = ЗаголовокОтчета + " " + БухгалтерскиеОтчетыВызовСервераПовтИсп.ПолучитьТекстОрганизация(Отчет.Организация, Отчет.ВключатьОбособленныеПодразделения);
		
	КонецЕсли;
	
	Форма.Заголовок = ЗаголовокОтчета;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	
	Форма.Период = ВыборПериодаКлиентСервер.ПолучитьПредставлениеПериодаОтчета(
		Форма.ВидПериода, Форма.Отчет.НачалоПериода, Форма.Отчет.КонецПериода);
		
КонецПроцедуры

&НаСервере
Процедура ОбработатьПереходНазад(НаГлавную = Ложь)
	
	ТочкаРазделителя = Найти(ПутьМакета, "/");
	
	Если ТочкаРазделителя = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ИмяМакета = Лев(ПутьМакета, ТочкаРазделителя - 1);
	
	Если ИмяМакета <> "МакетКарты" Тогда
		СхемаКомпоновкиДанных = Отчеты.АнализСостоянияНалоговогоУчетаПоУСН.ПолучитьМакет(ИмяМакета);
		
		ПараметрыОтчета = Новый Структура("ИмяМакета, КоллекцияНастроек", ИмяМакета, Отчет.КоллекцияНастроек);
		Настройки = Отчеты.АнализСостоянияНалоговогоУчетаПоУСН.ПолучитьНастройкуМакета(ПараметрыОтчета);
		Отчет.КомпоновщикНастроек.ЗагрузитьНастройки(Настройки);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(КэшОтчетовАдресХранилища) Тогда
		КэшОтчетов = ПолучитьИзВременногоХранилища(КэшОтчетовАдресХранилища);
	Иначе
		КэшОтчетов = Новый Структура;
	КонецЕсли;
	
	КэшРезультата = Неопределено;
	КэшОтчетов.Свойство(ИмяМакета, КэшРезультата);
	
	КэшОтчетовАдресХранилища = ПоместитьВоВременноеХранилище(КэшОтчетов, Новый УникальныйИдентификатор);
	
	Если НаГлавную или КэшРезультата = Неопределено Тогда
		
		ПутьМакета   = "";
		ТекущийМакет = "МакетКарты";
				
	Иначе
		
		// Загрузим результат, если есть сохраненный
		ЭтаФорма.Результат.Очистить();
		ЭтаФорма.Результат.Вывести(КэшРезультата.Результат);
		ЭтаФорма.Результат.ФиксацияСверху = КэшРезультата.ФиксацияСверху;
		
		Если Найти(ИмяМакета, "МакетРасшифровкиПоНоменклатуре") Тогда
			
			ДанныеРасшифровки = ПоместитьВоВременноеХранилище(ПолучитьИзВременногоХранилища(КэшРезультата.ДанныеРасшифровки), ДанныеРасшифровки);
			
		КонецЕсли;	
		
		ПутьМакета   = СтрЗаменить(ПутьМакета, ИмяМакета + "/", "");
		ТекущийМакет = ИмяМакета;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаРасшифровкиНаСервере(Расшифровка, ИмяФормыВызова, ЗначениеРасшифровки = Неопределено)
	
	Если НЕ ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	ОбъектОтчет    = ЭтаФорма.РеквизитФормыВЗначение("Отчет");                
	ВыделятьНДСУСН = Истина;
	
	Если ЗначениеЗаполнено(КэшОтчетовАдресХранилища) Тогда
		КэшОтчетов = ПолучитьИзВременногоХранилища(КэшОтчетовАдресХранилища);
	Иначе
		КэшОтчетов = Новый Структура;
	КонецЕсли;
	
	ДанныеРасшифровкиИзХранилища = ПолучитьИзВременногоХранилища(ДанныеРасшифровки);
	ЭлементРасшифровкиДанных     = ДанныеРасшифровкиИзХранилища.ДанныеРасшифровки.Элементы[Расшифровка];
	ЭлементРасшифровкиДанныхПоля = ЭлементРасшифровкиДанных.ПолучитьПоля();
	
	Если Найти(ТекущийМакет, "МакетРасшифровкиПоНоменклатуре") Тогда 
		
		КэшОтчета	= КэшОтчетов[ТекущийМакет];
		КэшОтчета.Вставить("ДанныеРасшифровки",     ПоместитьВоВременноеХранилище(ДанныеРасшифровкиИзХранилища, Новый УникальныйИдентификатор));
		КэшОтчетов.Вставить(ТекущийМакет, КэшОтчета);
		
		ПолеРасшифровки = "ЭлементРасхода";
		СтрокаЗначенияРасшифровки = ЭлементРасшифровкиДанныхПоля.Найти(ПолеРасшифровки);
		ЗначениеРасшифровки       = ?(СтрокаЗначенияРасшифровки = Неопределено, Неопределено, СтрокаЗначенияРасшифровки.Значение);
		
		Если ЗначениеРасшифровки = Неопределено Тогда
			
			МассивРасшифровок = БухгалтерскиеОтчетыВызовСервера.ПолучитьМассивПолейРасшифровки(Расшифровка, ДанныеРасшифровкиИзХранилища.ДанныеРасшифровки, ,);
			
			Для Каждого Элемент Из МассивРасшифровок Цикл
				
				Если Элемент.Поле = ПолеРасшифровки Тогда
					
					ЗначениеРасшифровки = Элемент.Значение;
					Прервать;
					
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;
		
		ИмяМакета 			   = ?(ВыделятьНДСУСН, "МакетРасшифровкиНоменклатурыПоДокументам", "МакетРасшифровкиНоменклатурыПоДокументам2009");
		ПутьМакета   = ТекущийМакет + "/" + ПутьМакета;
		ТекущийМакет = ИмяМакета;
		
		ЗначениеРасшифровки = Новый Структура("ИмяПараметра, ЗначениеПараметра", "Номенклатура", ЗначениеРасшифровки);
		
		Результат.Очистить();
		
		ДлительныеОперации.ОтменитьВыполнениеЗадания(ИдентификаторЗадания);
		
		ИдентификаторЗадания = Неопределено;
		
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеИспользовать");
		
		ПараметрыОтчета = ПодготовитьПараметрыОтчетаНаСервере();
		ПараметрыОтчета.Вставить("КэшОтчетов" , КэшОтчетов);
		ПараметрыОтчета.Вставить("ОтключитьТаблицы", Ложь);
		ПараметрыОтчета.Вставить("Расшифровка", ЗначениеРасшифровки);
		
		Если ОбщегоНазначения.ИнформационнаяБазаФайловая() Тогда
			АдресХранилища = ПоместитьВоВременноеХранилище(Неопределено, УникальныйИдентификатор);
			Отчеты.АнализСостоянияНалоговогоУчетаПоУСН.СформироватьРасшифровку(ПараметрыОтчета, АдресХранилища);
			РезультатВыполнения = Новый Структура("ЗаданиеВыполнено", Истина);
		Иначе
			РезультатВыполнения = ДлительныеОперации.ЗапуститьВыполнениеВФоне(УникальныйИдентификатор, "Отчеты.АнализСостоянияНалоговогоУчетаПоУСН.СформироватьРасшифровку", 
																				ПараметрыОтчета, БухгалтерскиеОтчетыКлиентСервер.ПолучитьНаименованиеЗаданияВыполненияОтчета(ЭтаФорма));
			
			АдресХранилища       = РезультатВыполнения.АдресХранилища;
			ИдентификаторЗадания = РезультатВыполнения.ИдентификаторЗадания;		
		КонецЕсли;
		
		Если РезультатВыполнения.ЗаданиеВыполнено Тогда
			ЗагрузитьПодготовленныеДанныеНаСервере();
		КонецЕсли;
		
	Иначе	
		МассивРасшифровок = БухгалтерскиеОтчетыВызовСервера.ПолучитьМассивПолейРасшифровки(Расшифровка, ДанныеРасшифровкиИзХранилища.ДанныеРасшифровки, ,);		
		Для Каждого Элемент Из МассивРасшифровок Цикл
			
			Если Найти(Элемент.Поле, "Регистратор") Тогда
				
				ЗначениеРасшифровки = Элемент.Значение;
				Объект 				= ЗначениеРасшифровки.ПолучитьОбъект();
				
				Если НЕ Метаданные.Документы.Найти(Объект.Метаданные().Имя) = Неопределено Тогда
					
					ИмяФормыВызова = "Документ." + Объект.Метаданные().Имя + ".ФормаОбъекта";
					
				КонецЕсли;
				Прервать;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СохранитьНастройкуМакета(Значение)
	
	Если Отчет.КоллекцияНастроек = Неопределено Тогда
		
		Отчет.КоллекцияНастроек = Новый Структура;
		
	КонецЕсли;
	
	Отчет.КоллекцияНастроек.Вставить(Значение.ИмяМакета, Значение.Настройка);
	
КонецПроцедуры

&НаСервере
Функция ПолучитьНастройкуМакета(ИмяМакета)
	
	ПараметрыОтчета = Новый Структура("ИмяМакета, КоллекцияНастроек", ИмяМакета, Отчет.КоллекцияНастроек);
	Настройки = Отчеты.АнализСостоянияНалоговогоУчетаПоУСН.ПолучитьНастройкуМакета(ПараметрыОтчета);
	
	Возврат ПоместитьВоВременноеХранилище(Настройки, УникальныйИдентификатор);
	
КонецФункции 

&НаСервере
Процедура УстановитьНачальныеНастройки()
	
	Отчет.Организация = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновнаяОрганизация");
	
	ВидПериода	= Перечисления.ДоступныеПериодыОтчета.Квартал;
	
	ТекущаяДата	= ОбщегоНазначенияБП.ПолучитьРабочуюДату();
	Отчет.НачалоПериода	= НачалоКвартала(ТекущаяДата);
	Отчет.КонецПериода	= КонецКвартала(ТекущаяДата);
	
	Отчет.КоллекцияНастроек = ?(Отчет.КоллекцияНастроек  = Неопределено, Новый Структура, Отчет.КоллекцияНастроек);
	КэшОтчетов              = Новый Структура;
	
	ТекущийМакет = "МакетКарты";
	
	ИмяМакетаКарты = "МакетКарты";
	НастройкиКлиента = ХранилищеСистемныхНастроек.Загрузить("Общее/НастройкиКлиентскогоПриложения");
	Если ТипЗнч(НастройкиКлиента) = Тип("НастройкиКлиентскогоПриложения") 
		И НастройкиКлиента.ВариантИнтерфейсаКлиентскогоПриложения = ВариантИнтерфейсаКлиентскогоПриложения.Версия8_2 Тогда
		ИмяМакетаКарты = "МакетКарты82";
	КонецЕсли;
	
	ОбъектОтчет		= РеквизитФормыВЗначение("Отчет");
	РезультатСхемы	= ОбъектОтчет.ПолучитьМакет(ИмяМакетаКарты);
	ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеАктуальность");
	
	ЗавершитьРаботуСОтчетом = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСтраницы()
	
	Главная = ТекущийМакет = "МакетКарты";
	
	СтраницаГлавная           = Элементы.ГруппаГлавнаяСтраница;
	СтраницаГлавная.Видимость = Главная;
	Элементы.ГруппаНаборСтраниц.ТекущаяСтраница = ?(Главная, СтраницаГлавная, 
												Элементы.ГруппаСтраницаРасшифровки);
	
КонецПроцедуры	

&НаСервере
Процедура ЗагрузитьПодготовленныеДанныеНаСервере()

	РезультатВыполнения = ПолучитьИзВременногоХранилища(АдресХранилища);
	Если ЗначениеЗаполнено(КэшОтчетовАдресХранилища) Тогда
		КэшОтчетов = ПолучитьИзВременногоХранилища(КэшОтчетовАдресХранилища);
	Иначе
		КэшОтчетов = Новый Структура;
	КонецЕсли;
	
	Если ТипЗнч(РезультатВыполнения) = Тип("Структура") Тогда
		Если РезультатВыполнения.Свойство("РезультатСхема") Тогда
			РезультатСхемы = РезультатВыполнения.РезультатСхема;
			РасшифровкиДоступны = Истина;
		Иначе
			Результат = РезультатВыполнения.Результат;
			Если ТипЗнч(КэшОтчетов) = Тип("Структура") Тогда				
				КэшОтчетов = РезультатВыполнения.КэшОтчетов;
				КэшОтчетовАдресХранилища = ПоместитьВоВременноеХранилище(КэшОтчетов, Новый УникальныйИдентификатор);
			КонецЕсли;
			ДанныеРасшифровки = ПоместитьВоВременноеХранилище(РезультатВыполнения.ДанныеРасшифровки, УникальныйИдентификатор);
		КонецЕсли;
	КонецЕсли;
	
	ИдентификаторЗадания = Неопределено;
	
	Элементы.ГруппаРезультатСхема.ТекущаяСтраница = Элементы.СтраницаРезультатСхема;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросПередЗакрытиемЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ЗавершитьРаботуСОтчетом = Истина;
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ВычислитьСуммуВыделенныхЯчеекТабличногоДокументаВКонтекстеНаСервере()
	
	ПолеСумма = БухгалтерскиеОтчетыВызовСервера.ВычислитьСуммуВыделенныхЯчеекТабличногоДокумента(
		Результат, КэшВыделеннойОбласти);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_РезультатПриАктивизацииОбластиПодключаемый()
	
	НеобходимоВычислятьНаСервере = Ложь;
	БухгалтерскиеОтчетыКлиент.ВычислитьСуммуВыделенныхЯчеекТабличногоДокумента(
		ПолеСумма, Результат, КэшВыделеннойОбласти, НеобходимоВычислятьНаСервере);
	
	Если НеобходимоВычислятьНаСервере Тогда
		ВычислитьСуммуВыделенныхЯчеекТабличногоДокументаВКонтекстеНаСервере();
	КонецЕсли;
	
	ОтключитьОбработчикОжидания("Подключаемый_РезультатПриАктивизацииОбластиПодключаемый");
	
КонецПроцедуры
