#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда


Процедура СформироватьОтчет(ПараметрыОтчета, АдресХранилища) Экспорт
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	Макет = Отчеты.ТоварныйОтчетТОРГ29.ПолучитьМакет("Макет");

	// Выведем заголовок
	СведенияОПокупателе = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(ПараметрыОтчета.Организация, ПараметрыОтчета.КонецПериода);
	МОЛВыбранногоСклада = ОтветственныеЛицаБП.ОтветственноеЛицоНаСкладе(ПараметрыОтчета.Склад, КонецДня(ПараметрыОтчета.КонецПериода));

	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");

	ОбластьМакета.Параметры.ОрганизацияПредставление = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОПокупателе);
	ОбластьМакета.Параметры.ДатаСоставления 		 = ТекущаяДата();
	ОбластьМакета.Параметры.ДатаНачала 				 = ПараметрыОтчета.НачалоПериода;
	ОбластьМакета.Параметры.ДатаКонца 				 = ПараметрыОтчета.КонецПериода;
	ОбластьМакета.Параметры.ОрганизацияПоОКПО 		 = СведенияОПокупателе.КодПоОКПО;
	ОбластьМакета.Параметры.МОЛ 					 = ?(НЕ ЗначениеЗаполнено(МОЛВыбранногоСклада), "", МОЛВыбранногоСклада);
	ОбластьМакета.Параметры.МОЛТабельныйНомер 		 = ?(НЕ ЗначениеЗаполнено(МОЛВыбранногоСклада), "", МОЛВыбранногоСклада.Код);
	ОбластьМакета.Параметры.Номер 					 = ПараметрыОтчета.НомерОтчета;
	ВысотаЗаголовка = ОбластьМакета.ВысотаТаблицы;
	ТабличныйДокумент.Вывести(ОбластьМакета);

	// Выведем шапку
	ОбластьМакета = Макет.ПолучитьОбласть("Шапка");

	ПовторятьПриПечатиСтроки = ТабличныйДокумент.Область(1 + ВысотаЗаголовка, , 2 + ВысотаЗаголовка);

	ТабличныйДокумент.Вывести(ОбластьМакета);

	МассивСчетов = Новый Массив;
	МассивСчетов.Добавить(ПланыСчетов.Хозрасчетный.ТоварыВРозничнойТорговлеВПродажныхЦенахАТТ);
	МассивСчетов.Добавить(ПланыСчетов.Хозрасчетный.ТоварыВРозничнойТорговлеВПродажныхЦенахНТТ);
	МассивСчетов.Добавить(ПланыСчетов.Хозрасчетный.ТараПодТоваромИПорожняя);

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДатаНачала",    НачалоДня(ПараметрыОтчета.НачалоПериода));
	Запрос.УстановитьПараметр("ДатаОкончания", Новый Граница(КонецДня(ПараметрыОтчета.КонецПериода), ВидГраницы.Включая));
	Запрос.УстановитьПараметр("Организация",   ПараметрыОтчета.Организация);
	Запрос.УстановитьПараметр("ВидСубконто",   ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Склады);
	Запрос.УстановитьПараметр("Склад", 		   ПараметрыОтчета.Склад);
	Запрос.УстановитьПараметр("Счета", 		   МассивСчетов);
	Запрос.УстановитьПараметр("СчетТары", 	   ПланыСчетов.Хозрасчетный.ТараПодТоваромИПорожняя);

	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ХозрасчетныйОстаткиИОбороты.Период КАК ДатаДок,
		|	ХозрасчетныйОстаткиИОбороты.Регистратор КАК Док,
		|	ДанныеПервичныхДокументов.НомерРегистратора КАК НомерДок,
		|	СУММА(ХозрасчетныйОстаткиИОбороты.СуммаОборотДт) КАК ПризнакПрихода,
		|	СУММА(ХозрасчетныйОстаткиИОбороты.СуммаОборотКт) КАК ПризнакРасхода,
		|	СУММА(ВЫБОР
		|			КОГДА ХозрасчетныйОстаткиИОбороты.Счет <> &СчетТары
		|				ТОГДА ХозрасчетныйОстаткиИОбороты.СуммаНачальныйОстатокДт - ХозрасчетныйОстаткиИОбороты.СуммаНачальныйОстатокКт
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК НачальныйОстаток,
		|	СУММА(ВЫБОР
		|			КОГДА ХозрасчетныйОстаткиИОбороты.Счет = &СчетТары
		|				ТОГДА ХозрасчетныйОстаткиИОбороты.СуммаНачальныйОстатокДт - ХозрасчетныйОстаткиИОбороты.СуммаНачальныйОстатокКт
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК НачальныйОстатокТары,
		|	СУММА(ВЫБОР
		|			КОГДА ХозрасчетныйОстаткиИОбороты.Счет <> &СчетТары
		|				ТОГДА ХозрасчетныйОстаткиИОбороты.СуммаОборотДт
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК Приход,
		|	СУММА(ВЫБОР
		|			КОГДА ХозрасчетныйОстаткиИОбороты.Счет = &СчетТары
		|				ТОГДА ХозрасчетныйОстаткиИОбороты.СуммаОборотДт
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК ПриходТары,
		|	СУММА(ВЫБОР
		|			КОГДА ХозрасчетныйОстаткиИОбороты.Счет <> &СчетТары
		|				ТОГДА ХозрасчетныйОстаткиИОбороты.СуммаОборотКт
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК Расход,
		|	СУММА(ВЫБОР
		|			КОГДА ХозрасчетныйОстаткиИОбороты.Счет = &СчетТары
		|				ТОГДА ХозрасчетныйОстаткиИОбороты.СуммаОборотКт
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК РасходТары,
		|	СУММА(ВЫБОР
		|			КОГДА ХозрасчетныйОстаткиИОбороты.Счет <> &СчетТары
		|				ТОГДА ХозрасчетныйОстаткиИОбороты.СуммаКонечныйОстатокДт - ХозрасчетныйОстаткиИОбороты.СуммаКонечныйОстатокКт
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК КонечныйОстаток,
		|	СУММА(ВЫБОР
		|			КОГДА ХозрасчетныйОстаткиИОбороты.Счет = &СчетТары
		|				ТОГДА ХозрасчетныйОстаткиИОбороты.СуммаКонечныйОстатокДт - ХозрасчетныйОстаткиИОбороты.СуммаКонечныйОстатокКт
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК КонечныйОстатокТары
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.ОстаткиИОбороты(&ДатаНачала, &ДатаОкончания, Регистратор, , Счет В (&Счета), &ВидСубконто, Организация = &Организация) КАК ХозрасчетныйОстаткиИОбороты
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
		|		ПО (ДанныеПервичныхДокументов.Организация = &Организация)
		|			И ХозрасчетныйОстаткиИОбороты.Регистратор = ДанныеПервичныхДокументов.Документ
		|ГДЕ
		|	ХозрасчетныйОстаткиИОбороты.Субконто1 = &Склад
		|
		|СГРУППИРОВАТЬ ПО
		|	ХозрасчетныйОстаткиИОбороты.Период,
		|	ХозрасчетныйОстаткиИОбороты.Регистратор,
		|	ДанныеПервичныхДокументов.НомерРегистратора
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДатаДок";

	РезультатЗапроса = Запрос.Выполнить();

	ТаблицаОстатков = РезультатЗапроса.Выгрузить();
	Если ТаблицаОстатков.Количество() = 0 Тогда
		НачальныйОстаток 	 = 0;
		КонечныйОстаток 	 = 0;
		НачальныйОстатокТары = 0;
		КонечныйОстатокТары  = 0;
	Иначе
		НачальныйОстаток 	 = ТаблицаОстатков[0].НачальныйОстаток;
		КонечныйОстаток 	 = ТаблицаОстатков[ТаблицаОстатков.Количество() - 1].КонечныйОстаток;
		НачальныйОстатокТары = ТаблицаОстатков[0].НачальныйОстатокТары;
		КонечныйОстатокТары  = ТаблицаОстатков[ТаблицаОстатков.Количество() - 1].КонечныйОстатокТары;
	КонецЕсли;
	
	// Выведем остатки на начало
	ОбластьМакета = Макет.ПолучитьОбласть("ОстатокНачала");
	
	ОбластьМакета.Параметры.ДатаНачала 				= "Остаток на " + Формат(ПараметрыОтчета.НачалоПериода, "ДЛФ=Д");
	ОбластьМакета.Параметры.НачальнаяСтоимостьВсего = ОбщегоНазначенияБПВызовСервера.ФорматСумм(НачальныйОстаток);
	ОбластьМакета.Параметры.НачальнаяСтоимостьТара 	= ОбщегоНазначенияБПВызовСервера.ФорматСумм(НачальныйОстатокТары);
	
	ТабличныйДокумент.Вывести(ОбластьМакета);
	
	// Выведем приходы	
	ОбластьМакета = Макет.ПолучитьОбласть("Приход");
	ТабличныйДокумент.Вывести(ОбластьМакета);
	
	ТаблицаПриходов = ОтобратьСтрокиПоКритериям(РезультатЗапроса, Новый Структура("ПризнакПрихода", 0),
		Новый Структура("ПризнакПрихода", ВидСравнения.НеРавно)).Выгрузить();
	
	ТаблицаПриходов.Сортировать("ДатаДок Возр");
	
	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	
	Для Каждого СтрокаПрихода Из ТаблицаПриходов Цикл
		ОбластьМакета.Параметры.Докум 		   = СтрокаПрихода.Док;
		ОбластьМакета.Параметры.Расшифровка    = СтрокаПрихода.Док;
		ОбластьМакета.Параметры.ДатаДокумента  = СтрокаПрихода.ДатаДок;
		ОбластьМакета.Параметры.НомерДокумента = ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(СтрокаПрихода.Док.Номер, Истина, Ложь);
		ОбластьМакета.Параметры.СуммаТовара    = ОбщегоНазначенияБПВызовСервера.ФорматСумм(СтрокаПрихода.Приход);
		ОбластьМакета.Параметры.СуммаТары 	   = ОбщегоНазначенияБПВызовСервера.ФорматСумм(СтрокаПрихода.ПриходТары);
		ТабличныйДокумент.Вывести(ОбластьМакета);
	КонецЦикла;
	
	Приход 	   = ТаблицаПриходов.Итог("Приход");
	ПриходТары = ТаблицаПриходов.Итог("ПриходТары");
	
	// Выведем итоги по приходам
	ОбластьМакета = Макет.ПолучитьОбласть("ИтогоПриход");
	ОбластьМакета.Параметры.ПриходСтоимостьВсего = ОбщегоНазначенияБПВызовСервера.ФорматСумм(Приход);
	ОбластьМакета.Параметры.ПриходСтоимостьТара  = ОбщегоНазначенияБПВызовСервера.ФорматСумм(ПриходТары);
	ТабличныйДокумент.Вывести(ОбластьМакета);
	
	ОбластьМакета = Макет.ПолучитьОбласть("ВсегоПриход");
	ОбластьМакета.Параметры.ПриходСОстатком 	= ОбщегоНазначенияБПВызовСервера.ФорматСумм(Приход + НачальныйОстаток);
	ОбластьМакета.Параметры.ПриходСОстаткомТара = ОбщегоНазначенияБПВызовСервера.ФорматСумм(ПриходТары + НачальныйОстатокТары);
	ТабличныйДокумент.Вывести(ОбластьМакета);
	
	ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
	
	// Выведем расходы
	ОбластьМакета = Макет.ПолучитьОбласть("Расход");
	ТабличныйДокумент.Вывести(ОбластьМакета);
	
	ТаблицаРасходов = ОтобратьСтрокиПоКритериям(РезультатЗапроса, Новый Структура("ПризнакРасхода", 0),
		Новый Структура("ПризнакРасхода", ВидСравнения.НеРавно)).Выгрузить();
	
	ТаблицаРасходов.Сортировать("ДатаДок Возр");
	
	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	
	Для Каждого СтрокаРасхода Из ТаблицаРасходов Цикл
		ОбластьМакета.Параметры.Докум 		   = СтрокаРасхода.Док;
		ОбластьМакета.Параметры.Расшифровка    = СтрокаРасхода.Док;
		ОбластьМакета.Параметры.ДатаДокумента  = СтрокаРасхода.ДатаДок;
		ОбластьМакета.Параметры.НомерДокумента = ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(СтрокаРасхода.Док.Номер, Истина, Ложь);
		ОбластьМакета.Параметры.СуммаТовара    = ОбщегоНазначенияБПВызовСервера.ФорматСумм(СтрокаРасхода.Расход);
		ОбластьМакета.Параметры.СуммаТары 	   = ОбщегоНазначенияБПВызовСервера.ФорматСумм(СтрокаРасхода.РасходТары);
		ТабличныйДокумент.Вывести(ОбластьМакета);
	КонецЦикла;
	
	Расход	   = ТаблицаРасходов.Итог("Расход");
	РасходТары = ТаблицаРасходов.Итог("РасходТары");
	
	// Выведем итоги по расходам
	ОбластьМакета = Макет.ПолучитьОбласть("ИтогоРасход");
	
	ОбластьМакета.Параметры.РасходСтоимостьВсего = ОбщегоНазначенияБПВызовСервера.ФорматСумм(Расход);
	ОбластьМакета.Параметры.РасходСтоимостьТара  = ОбщегоНазначенияБПВызовСервера.ФорматСумм(РасходТары);
	
	ТабличныйДокумент.Вывести(ОбластьМакета);
	
	ОбластьМакета = Макет.ПолучитьОбласть("ОстатокКонец");
	
	ОбластьМакета.Параметры.ДатаКонца 			   = "Остаток на " + Формат(ПараметрыОтчета.КонецПериода, "ДЛФ=Д");
	ОбластьМакета.Параметры.КонечнаяСтоимостьВсего = ОбщегоНазначенияБПВызовСервера.ФорматСумм(КонечныйОстаток);
	ОбластьМакета.Параметры.КонечнаяСтоимостьТара  = ОбщегоНазначенияБПВызовСервера.ФорматСумм(КонечныйОстатокТары);
	
	ТабличныйДокумент.Вывести(ОбластьМакета);
	
	// Выведем подвал
	ОбластьМакета = Макет.ПолучитьОбласть("Подвал");
	ТабличныйДокумент.Вывести(ОбластьМакета);
	ТабличныйДокумент.ПовторятьПриПечатиСтроки = ПовторятьПриПечатиСтроки;
	
	ПоместитьВоВременноеХранилище(ТабличныйДокумент, АдресХранилища);
	
КонецПроцедуры

Функция ОтобратьСтрокиПоКритериям(Источник, СтруктураКритериев, СтруктураСложныхКритериев = Неопределено)
	
	Перем ВидСравненияСложный;
	
	Если СтруктураСложныхКритериев = Неопределено Тогда
		СтруктураСложныхКритериев = Новый Структура;
	КонецЕсли;
	
	ПостроительЗапроса 				  = Новый ПостроительЗапроса;
	ПостроительЗапроса.ИсточникДанных = Новый ОписаниеИсточникаДанных(Источник);
	
	Для Каждого Критерий Из СтруктураКритериев Цикл
		НовыйОтбор = ПостроительЗапроса.Отбор.Добавить(Критерий.Ключ);
		
		СтруктураСложныхКритериев.Свойство(Критерий.Ключ, ВидСравненияСложный);
		
		Если ВидСравненияСложный = Неопределено Тогда
			НовыйОтбор.Установить(Критерий.Значение);
		Иначе
			НовыйОтбор.Использование = Истина;
			НовыйОтбор.ВидСравнения  = ВидСравненияСложный;
			НовыйОтбор.Значение 	 = Критерий.Значение;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ПостроительЗапроса.Результат;
	
КонецФункции // ОтобратьСтрокиПоКритериям()


#КонецЕсли