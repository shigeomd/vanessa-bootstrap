#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

Функция ПолучитьПараметрыИсполненияОтчета() Экспорт
	
	Возврат Новый Структура("ИспользоватьПередКомпоновкойМакета,
							|ИспользоватьВнешниеНаборыДанных,
							|ИспользоватьПослеВыводаРезультата,
							|ИспользоватьДанныеРасшифровки,
							|ИспользоватьПривилегированныйРежим",
							Истина, Истина, Истина, Истина, Ложь);
							
КонецФункции

Функция ПолучитьВнешниеНаборыДанных(ПараметрыОтчета, МакетКомпоновки) Экспорт
	
	СтруктураВнешнихНаборовДанных = Новый Структура();	
	
	Запрос =  ПолучитьПараметрыЗапроса(ПараметрыОтчета);
	
	ИдентификаторМакета = ПараметрыОтчета.ИдентификаторМакета;
	Если ИдентификаторМакета = "РасшифровкаНачислениеРеализация" Тогда
		ПолучитьНаборДанныхНачислениеРеализация(Запрос, СтруктураВнешнихНаборовДанных);	
	ИначеЕсли ИдентификаторМакета = "РасшифровкаНачислениеИсполнениеОНА" Тогда
		ПолучитьНаборДанныхНачислениеНалоговыйАгент(Запрос, СтруктураВнешнихНаборовДанных);
	ИначеЕсли ИдентификаторМакета = "РасшифровкаНачислениеВосстановлениеСАвансовВыданныхУпрощенныйУчетНДС" Тогда 
		ПолучитьНаборДанныхНачислениеВосстановлениеСАвансовВыданныхУпрощенныйУчетНДС(Запрос, СтруктураВнешнихНаборовДанных);
	ИначеЕсли ИдентификаторМакета = "РасшифровкаНачислениеСМРХозСпособом" Тогда
		ПолучитьНаборДанныхНачислениеСМРХозСпособом(Запрос, СтруктураВнешнихНаборовДанных);
	ИначеЕсли ИдентификаторМакета = "РасшифровкаВычетПриобретенныеЦенности" Тогда
		ПолучитьНаборДанныхВычетПоПриобретеннымЦенностям(Запрос, СтруктураВнешнихНаборовДанных);
	ИначеЕсли ИдентификаторМакета = "РасшифровкаВычетИсполнениеОНА" Тогда
		ПолучитьНаборДанныхВычетНалоговыйАгент(Запрос, СтруктураВнешнихНаборовДанных);
	ИначеЕсли ИдентификаторМакета = "РасшифровкаВычетСАвансовПолученныхУпрощенныйУчетНДС" Тогда
		ПолучитьНаборДанныхВычетСАвансовПолученныхУпрощенныйУчетНДС(Запрос, СтруктураВнешнихНаборовДанных);
	ИначеЕсли ИдентификаторМакета = "РасшифровкаВычетАвансыВыданныеУпрощенныйУчетНДС" Тогда
		ПолучитьНаборДанныхВычетАвансыВыданныеУпрощенныйУчетНДС(Запрос, СтруктураВнешнихНаборовДанных);
	ИначеЕсли ИдентификаторМакета = "РасшифровкаНачислениеРеализацияУпрощенныйУчетНДС" Тогда
		ПолучитьНаборДанныхНачислениеРеализацияУпрощенныйУчетНДС(Запрос, СтруктураВнешнихНаборовДанных);
	ИначеЕсли ИдентификаторМакета = "РасшифровкаНачислениеАвансыПолученныеУпрощенныйУчетНДС" Тогда
		ПолучитьНаборДанныхНачислениеАвансыПолученныеУпрощенныйУчетНДС(Запрос, СтруктураВнешнихНаборовДанных);
	ИначеЕсли ИдентификаторМакета = "РасшифровкаНачислениеДругиеОперацииУпрощенныйУчетНДС" Тогда
		ПолучитьНаборДанныхНачислениеДругиеОперацииУпрощенныйУчетНДС(Запрос, СтруктураВнешнихНаборовДанных);
	ИначеЕсли ИдентификаторМакета = "РасшифровкаВычетПоПриобретеннымЦенностямУпрощенныйУчетНДС" Тогда
		ПолучитьНаборДанныхВычетПоПриобретеннымЦенностямУпрощенныйУчетНДС(Запрос, СтруктураВнешнихНаборовДанных);
	ИначеЕсли ИдентификаторМакета = "РасшифровкаВычетДругиеОперацииУпрощенныйУчетНДС" Тогда
		ПолучитьНаборДанныхВычетДругиеОперацииУпрощенныйУчетНДС(Запрос, СтруктураВнешнихНаборовДанных);
	ИначеЕсли ИдентификаторМакета = "РасшифровкаНачислениеРеализация0" Тогда
		ПолучитьНаборДанныхНачислениеРеализация0(Запрос, СтруктураВнешнихНаборовДанных);
	ИначеЕсли ИдентификаторМакета = "РасшифровкаНачислениеВосстановлениеДругиеОперации" Тогда
		ПолучитьНаборДанныхНачислениеВосстановлениеДругиеОперации(Запрос, СтруктураВнешнихНаборовДанных);
	ИначеЕсли ИдентификаторМакета = "РасшифровкаНачислениеНеОтражаетсяВКнигеПродаж" Тогда
		ПолучитьНаборДанныхНачислениеНеОтражаетсяВКнигеПродаж(Запрос, СтруктураВнешнихНаборовДанных);
	ИначеЕсли ИдентификаторМакета = "РасшифровкаВычетПоПриобретеннымЦенностям0" Тогда
		ПолучитьНаборДанныхВычетПоПриобретеннымЦенностям0(Запрос, СтруктураВнешнихНаборовДанных);
	ИначеЕсли ИдентификаторМакета = "РасшифровкаВычетИсполнениеОНА0" Тогда
		ПолучитьНаборДанныхВычетНалоговыйАгент0(Запрос, СтруктураВнешнихНаборовДанных);
	ИначеЕсли ИдентификаторМакета = "РасшифровкаНачислениеРеализацияСложныйУчетНДС" Тогда
		ПолучитьНаборДанныхНачислениеРеализацияСложныйУчетНДС(Запрос, СтруктураВнешнихНаборовДанных);
	ИначеЕсли ИдентификаторМакета = "ВосстановлениеНДСпоОбъектамНедвижимости" Тогда
		ПолучитьНаборДанныхВосстановлениеНДСпоОбъектамНедвижимости(Запрос, СтруктураВнешнихНаборовДанных);
	ИначеЕсли ИдентификаторМакета = "РасшифровкаСтроки130Декларации" Тогда
		ПолучитьНаборДанныхВычетПоПриобретеннымЦенностямРасшифровка130СтрокиДекларации(Запрос, СтруктураВнешнихНаборовДанных);
	КонецЕсли;
	
	Возврат СтруктураВнешнихНаборовДанных["ВнешнийНаборДанных" + СтрЗаменить(ПараметрыОтчета.ИдентификаторМакета,"Расшифровка", "")];

КонецФункции	

Функция ПолучитьТекстЗаголовка(ПараметрыОтчета, ОрганизацияВНачале = Истина) Экспорт 
		
	ТекстЗаголовок = ПараметрыОтчета.СхемаКомпоновкиДанных.ВариантыНастроек[ПараметрыОтчета.ИдентификаторМакета].Представление;
	
	Возврат ТекстЗаголовок + " " + БухгалтерскиеОтчетыКлиентСервер.ПолучитьПредставлениеПериода(ПараметрыОтчета.НачалоПериода, ПараметрыОтчета.КонецПериода);
	
КонецФункции

Процедура ПослеВыводаРезультата(ПараметрыОтчета, Результат) Экспорт
	
	БухгалтерскиеОтчетыВызовСервера.ОбработкаРезультатаОтчета(ПараметрыОтчета.ИдентификаторОтчета, Результат);

	Если Результат.Области.Найти("Заголовок") <> Неопределено Тогда
		Результат.ФиксацияСверху = Результат.Области.Заголовок.Низ;
	КонецЕсли;
	Результат.ФиксацияСлева = 0;	
КонецПроцедуры

// В процедуре можно доработать компоновщик перед выводом в отчет
// Изменения сохранены не будут
Процедура ПередКомпоновкойМакета(ПараметрыОтчета, Схема, КомпоновщикНастроек) Экспорт
	
	// Получим предопределенные настройки отчета
	ОсновнаяСхемаКомпоновкиДанных = ПолучитьМакет("ОсновнаяСхемаКомпоновкиДанных");
	ПредопределенныеНастройки = ОсновнаяСхемаКомпоновкиДанных.ВариантыНастроек[ПараметрыОтчета.ИдентификаторМакета].Настройки;
	
	// Очищаем структуру отчета
	КомпоновщикНастроек.Настройки.Структура.Очистить();
	КомпоновщикНастроек.Настройки.Выбор.Элементы.Очистить();
	
	// Найдем элемент офрмления Автоотступ
	УсловноеОформлениеАвтоотступа = Неопределено;
	Для каждого ЭлементОформления Из КомпоновщикНастроек.Настройки.УсловноеОформление.Элементы Цикл
		Если ЭлементОформления.Представление = "Уменьшенный автоотступ" Тогда
			УсловноеОформлениеАвтоотступа = ЭлементОформления;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	// Если автоотступ не задан добавляем его
	// если задан очистим элеметы к которым он применяется
	// дальше при добавлениии группировок элементы буду добавлены
	Если УсловноеОформлениеАвтоотступа = Неопределено Тогда
		УсловноеОформлениеАвтоотступа = КомпоновщикНастроек.Настройки.УсловноеОформление.Элементы.Добавить();
		УсловноеОформлениеАвтоотступа.Представление = "Уменьшенный автоотступ";
		УсловноеОформлениеАвтоотступа.Оформление.УстановитьЗначениеПараметра("Автоотступ", 1);
		УсловноеОформлениеАвтоотступа.Использование = Ложь;
		УсловноеОформлениеАвтоотступа.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.БыстрыйДоступ;
	Иначе
		УсловноеОформлениеАвтоотступа.Поля.Элементы.Очистить();
	КонецЕсли;
	
	// В структуру отчета добавляем таблицу
	Таблица = КомпоновщикНастроек.Настройки.Структура.Добавить(Тип("ТаблицаКомпоновкиДанных"));	
	
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметрВывода(Таблица, "ВертикальноеРасположениеОбщихИтогов", РасположениеИтоговКомпоновкиДанных.Конец); 
	
	// В предопределенных настройках указаны ресурсы которые должны быть выведены в отчет
	// В доступных полях можно узнать является ли поле ресурсом, и проверить целесобразность вывода поля
	ДоступныеПоля = КомпоновщикНастроек.Настройки.ДоступныеПоляВыбора;
	
	Для Каждого ВыбранноеПоле Из ПредопределенныеНастройки.Выбор.Элементы Цикл
		
		Если ВыбранноеПоле.Использование И ТипЗнч(ВыбранноеПоле) <> Тип("АвтоВыбранноеПолеКомпоновкиДанных") Тогда
			
			// Ищем выбранное поле среди доступных
			СвойстваПоля = ДоступныеПоля.НайтиПоле(ВыбранноеПоле.Поле);	
			
			// Если поле доступно и это ресурс, добавляем
			// поля не ресурсы задаются в таблице "Дополнительные данные"
			Если СвойстваПоля <> Неопределено И СвойстваПоля.Ресурс Тогда
				
				Колонка = Таблица.Колонки.Добавить();
				Колонка.Имя = ВыбранноеПоле.Поле; 
				БухгалтерскиеОтчетыКлиентСервер.ДобавитьВыбранноеПоле(Колонка.Выбор, ВыбранноеПоле.Поле);
				
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	// Для того чтобы имитировать обращение к объекту КоллекцияЭлементовСтруктурыТаблицыКомпоновкиДанных 
	// как к ГруппировкаТаблицыКомпоновкиДанных поместим коллекцию в структуру 
	Структура =  Новый Структура("Структура",Таблица.Строки);	
	
	// Перебираем таблицу группировок
	Для Каждого ПолеВыбраннойГруппировки Из ПараметрыОтчета.Группировка Цикл 
		Если ПолеВыбраннойГруппировки.Использование Тогда
			Структура = Структура.Структура.Добавить();
			ПолеГруппировки = Структура.ПоляГруппировки.Элементы.Добавить(Тип("ПолеГруппировкиКомпоновкиДанных"));
			ПолеГруппировки.Использование  = Истина;
			ПолеГруппировки.Поле           = Новый ПолеКомпоновкиДанных(ПолеВыбраннойГруппировки.Поле);
			Если ПолеВыбраннойГруппировки.ТипГруппировки = 1 Тогда
				ПолеГруппировки.ТипГруппировки = ТипГруппировкиКомпоновкиДанных.Иерархия;
			ИначеЕсли ПолеВыбраннойГруппировки.ТипГруппировки = 2 Тогда
				ПолеГруппировки.ТипГруппировки = ТипГруппировкиКомпоновкиДанных.ТолькоИерархия;
			Иначе
				ПолеГруппировки.ТипГруппировки = ТипГруппировкиКомпоновкиДанных.Элементы;
			КонецЕсли;
			
			Структура.Выбор.Элементы.Добавить(Тип("АвтоВыбранноеПолеКомпоновкиДанных"));
			Структура.Порядок.Элементы.Добавить(Тип("АвтоЭлементПорядкаКомпоновкиДанных"));
			
			ПолеОформления = УсловноеОформлениеАвтоотступа.Поля.Элементы.Добавить();
			ПолеОформления.Поле = ПолеГруппировки.Поле;
			
		КонецЕсли;
	КонецЦикла;
	
	//Добавляем предопределенные недоступные группировки (всегда в конце)
	ПредопределеннаяГруппировка = ПредопределенныеНастройки.Структура[0].Строки;
	
	Пока ПредопределеннаяГруппировка.Количество() > 0 Цикл
		
		Если ПредопределеннаяГруппировка[0].РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный Тогда
			
			Если ПредопределеннаяГруппировка[0].Состояние = СостояниеЭлементаНастройкиКомпоновкиДанных.Включен Тогда
				Структура = Структура.Структура.Добавить();
				
				//Поля группировок
				Для Каждого ПолеПредопределеннойГруппировки Из ПредопределеннаяГруппировка[0].ПоляГруппировки.Элементы Цикл
					Если Тип(ПолеПредопределеннойГруппировки) = Тип("АвтоПолеГруппировкиКомпоновкиДанных") Тогда
						ПолеГруппировки = Структура.ПоляГруппировки.Элементы.Добавить(Тип("АвтоПолеГруппировкиКомпоновкиДанных"));
					Иначе
						Если БухгалтерскиеОтчетыКлиентСервер.НайтиГруппировку(Таблица.Строки, ПолеПредопределеннойГруппировки.Поле) = Неопределено Тогда
							ПолеГруппировки = Структура.ПоляГруппировки.Элементы.Добавить(Тип("ПолеГруппировкиКомпоновкиДанных"));
							ЗаполнитьЗначенияСвойств(ПолеГруппировки,ПолеПредопределеннойГруппировки);
							ПолеОформления = УсловноеОформлениеАвтоотступа.Поля.Элементы.Добавить();
							ПолеОформления.Поле = ПолеГруппировки.Поле;
						КонецЕсли;
					КонецЕсли;
				КонецЦикла;	
				
				//Отборы
				Для Каждого ОтборПредопределеннойГруппировки Из ПредопределеннаяГруппировка[0].Отбор.Элементы Цикл
					ОтборГруппировки =  Структура.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
					ЗаполнитьЗначенияСвойств(ОтборГруппировки,ОтборПредопределеннойГруппировки);
				КонецЦикла;	
				
				//Условное оформление
				Для Каждого УсловноеОформлениеПредопределеннойГруппировки Из ПредопределеннаяГруппировка[0].УсловноеОформление.Элементы Цикл
					УсловноеОформлениеГруппировки =  Структура.УсловноеОформление.Элементы.Добавить();
					ЗаполнитьЗначенияСвойств(УсловноеОформлениеГруппировки,УсловноеОформлениеПредопределеннойГруппировки);
					
					//Отбор
					Для Каждого ОтборПредопределенногоОформления Из УсловноеОформлениеПредопределеннойГруппировки.Отбор.Элементы Цикл
						ОтборОформления =  УсловноеОформлениеГруппировки.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
						ЗаполнитьЗначенияСвойств(ОтборОформления,ОтборПредопределенногоОформления);
					КонецЦикла;	
					
					//Параметры оформления
					Для ИндексПараметра = 0 По УсловноеОформлениеПредопределеннойГруппировки.Оформление.Элементы.Количество() - 1 Цикл
						ОформлениеПредопределеннаяГруппировка = УсловноеОформлениеПредопределеннойГруппировки.Оформление.Элементы[ИндексПараметра];
						ОформлениеГруппировки =  УсловноеОформлениеГруппировки.Оформление.Элементы[ИндексПараметра];
						ЗаполнитьЗначенияСвойств(ОформлениеГруппировки,ОформлениеПредопределеннаяГруппировка);
					КонецЦикла;	
					
					Для Каждого ПредопределенноеОформляемоеПолеГруппировки Из УсловноеОформлениеПредопределеннойГруппировки.Поля.Элементы Цикл
						ОформляемоеПоле = УсловноеОформлениеГруппировки.Поля.Элементы.Добавить();
						ЗаполнитьЗначенияСвойств(ОформляемоеПоле,ПредопределенноеОформляемоеПолеГруппировки);
					КонецЦикла;	
					
				КонецЦикла;	
				
				//Выбор
				Для Каждого ВыборПредопределеннойГруппировки Из ПредопределеннаяГруппировка[0].Выбор.Элементы Цикл
					Если Тип(ВыборПредопределеннойГруппировки) = Тип("АвтоВыбранноеПолеКомпоновкиДанных") Тогда
						Структура.Выбор.Элементы.Добавить(Тип("АвтоВыбранноеПолеКомпоновкиДанных"));	
					Иначе
						Если БухгалтерскиеОтчетыКлиентСервер.НайтиГруппировку(Таблица.Строки, ВыборПредопределеннойГруппировки.Поле) = Неопределено Тогда
							ВыборГруппировки =  Структура.Выбор.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
							ЗаполнитьЗначенияСвойств(ВыборГруппировки,ВыборПредопределеннойГруппировки);
						КонецЕсли;
					КонецЕсли;
				КонецЦикла;	
				
				//Порядок
				Для Каждого ПорядокПредопределеннойГруппировки Из ПредопределеннаяГруппировка[0].Порядок.Элементы Цикл
					Если Тип(ПорядокПредопределеннойГруппировки) = Тип("АвтоЭлементПорядкаКомпоновкиДанных") Тогда
						Структура.Порядок.Элементы.Добавить(Тип("АвтоЭлементПорядкаКомпоновкиДанных"));	
					Иначе	
						ПорядокГруппировки =  Структура.Порядок.Элементы.Добавить(Тип("ЭлементПорядкаКомпоновкиДанных"));
						ЗаполнитьЗначенияСвойств(ПорядокГруппировки,ПорядокПредопределеннойГруппировки);
					КонецЕсли;
				КонецЦикла;	
				
				//Параметры вывода
				Для ИндексПараметра = 0 По ПредопределеннаяГруппировка[0].ПараметрыВывода.Элементы.Количество() - 1 Цикл
					ПараметрыВыводаПредопределеннаяГруппировка = ПредопределеннаяГруппировка[0].ПараметрыВывода.Элементы[ИндексПараметра];
					ПараметрыВыводаГруппировки =  Структура.ПараметрыВывода.Элементы[ИндексПараметра];
					ЗаполнитьЗначенияСвойств(ПараметрыВыводаГруппировки,ПараметрыВыводаПредопределеннаяГруппировка);
				КонецЦикла;	
				
			КонецЕсли;
		КонецЕсли;
		
		ПредопределеннаяГруппировка = ПредопределеннаяГруппировка[0].Структура;
		
	КонецЦикла;
	
	// Дополнительные данные
	БухгалтерскиеОтчетыВызовСервера.ДобавитьДополнительныеПоля(ПараметрыОтчета, КомпоновщикНастроек);
	
	Если УсловноеОформлениеАвтоотступа.Поля.Элементы.Количество() = 0 Тогда
		УсловноеОформлениеАвтоотступа.Использование = Ложь;
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьНаборПоказателей() Экспорт
	
	НаборПоказателей = Новый Массив;
	Возврат НаборПоказателей;
	
КонецФункции	

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

//	Универсальные наборы

//  Создает запрос, устанавливает параметры запроса 
//
//
// Возвращаемое значение:
//   Запрос   - заготовка запроса, с установленными параметрами
//
Функция ПолучитьПараметрыЗапроса(ПараметрыОтчета) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	НеОтражаютсяВРазделеНачислениеПоРеализации = Новый СписокЗначений;
	НеОтражаютсяВРазделеНачислениеПоРеализации.Добавить(Перечисления.НДСВидНачисления.НДСсАвансов);
	НеОтражаютсяВРазделеНачислениеПоРеализации.Добавить(Перечисления.НДСВидНачисления.НДСВосстановлен);
	НеОтражаютсяВРазделеНачислениеПоРеализации.Добавить(Перечисления.НДСВидНачисления.НДСНачисленКУплате);
	НеОтражаютсяВРазделеНачислениеПоРеализации.Добавить(Перечисления.НДСВидНачисления.РеализацияЕНВД);
	НеОтражаютсяВРазделеНачислениеПоРеализации.Добавить(Перечисления.НДСВидНачисления.Реализация0);
	
	НеОтражаютсяВРазделеНачислениеПоРеализацииСложныйУчет = Новый СписокЗначений;
	НеОтражаютсяВРазделеНачислениеПоРеализацииСложныйУчет.Добавить(Перечисления.НДСВидНачисления.НДСсАвансов);
	НеОтражаютсяВРазделеНачислениеПоРеализацииСложныйУчет.Добавить(Перечисления.НДСВидНачисления.НДСНачисленКУплате);
	НеОтражаютсяВРазделеНачислениеПоРеализацииСложныйУчет.Добавить(Перечисления.НДСВидНачисления.РеализацияЕНВД);
	НеОтражаютсяВРазделеНачислениеПоРеализацииСложныйУчет.Добавить(Перечисления.НДСВидНачисления.Реализация0);
	
	ВидыЦенностейАвансыВсе = Новый СписокЗначений;
	ВидыЦенностейАвансыВсе.Добавить(Перечисления.ВидыЦенностей.АвансыВыданные);
	ВидыЦенностейАвансыВсе.Добавить(Перечисления.ВидыЦенностей.АвансыПолученные);
	ВидыЦенностейАвансыВсе.Добавить(Перечисления.ВидыЦенностей.АвансыПолученные0);
	ВидыЦенностейАвансыВсе.Добавить(Перечисления.ВидыЦенностей.ВозвратАвансовПолученных);
	
	ВидыЦенностейАвансыПолученные = Новый СписокЗначений;
	ВидыЦенностейАвансыПолученные.Добавить(Перечисления.ВидыЦенностей.АвансыПолученные);
	ВидыЦенностейАвансыПолученные.Добавить(Перечисления.ВидыЦенностей.АвансыПолученные0);
	ВидыЦенностейАвансыПолученные.Добавить(Перечисления.ВидыЦенностей.ВозвратАвансовПолученных);
	
	ВидыЦенностейНалоговыйАгент = Новый СписокЗначений;
	ВидыЦенностейНалоговыйАгент.Добавить(Перечисления.ВидыЦенностей.НалоговыйАгентАренда);
	ВидыЦенностейНалоговыйАгент.Добавить(Перечисления.ВидыЦенностей.НалоговыйАгентИностранцы);
	ВидыЦенностейНалоговыйАгент.Добавить(Перечисления.ВидыЦенностей.НалоговыйАгентРеализацияИмущества);
	
	СписокВидовСубконто = Новый СписокЗначений;
	СписокВидовСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты);
	СписокВидовСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Договоры);
	
	СписокВидовСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ДокументыРасчетовСКонтрагентами);
	СчетВыручкиЕНВД = ПланыСчетов.Хозрасчетный.ВыручкаЕНВД;
	
	ВидыСубконтоДокументыДоговоры = Новый Массив;
	ВидыСубконтоДокументыДоговоры.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ДокументыРасчетовСКонтрагентами);
	ВидыСубконтоДокументыДоговоры.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Договоры);
		
	ВедетсяУчетОбособленныхПодразделений = ПолучитьФункциональнуюОпцию("ВестиУчетПоПодразделениям");
									
	Если ВедетсяУчетОбособленныхПодразделений И ПараметрыОтчета.ВключатьОбособленныеПодразделения Тогда
		
		СписокОрганизаций = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьСписокОбособленныхПодразделений(ПараметрыОтчета.Организация);
		
	Иначе
		
		СписокОрганизаций = Новый СписокЗначений;
		СписокОрганизаций.Добавить(ПараметрыОтчета.Организация);
		
	КонецЕсли;	
	
	// С 01.10.2014 при реализации без НДС счета-фактуры не выставляются,	
	// кроме случая освобождения по ст.145 и 145.1.
	ПрименяетсяОсвобождениеОтУплатыНДС = УчетнаяПолитика.ПрименяетсяОсвобождениеОтУплатыНДС(
		ПараметрыОтчета.Организация, ПараметрыОтчета.КонецПериода);
	
	//	Установка параметров запроса
	
	//	Необходимо проверить остатки по регистру НДС начисленный после регламентных документов
	Запрос.УстановитьПараметр("НачалоСледующегоПериода", 					КонецДня(ПараметрыОтчета.КонецПериода) + 1);
	Запрос.УстановитьПараметр("НачалоПериода",								НачалоДня(ПараметрыОтчета.НачалоПериода));
	Запрос.УстановитьПараметр("КонецПериодаГраница", 						Новый Граница(КонецДня(ПараметрыОтчета.КонецПериода), ВидГраницы.Включая));
	Запрос.УстановитьПараметр("КонецПериода", 								КонецДня(ПараметрыОтчета.КонецПериода));	
	Запрос.УстановитьПараметр("Организация",								СписокОрганизаций);
	Запрос.УстановитьПараметр("НеОтражаютсяВРазделеНачислениеПоРеализации", НеОтражаютсяВРазделеНачислениеПоРеализации);
	Запрос.УстановитьПараметр("ПустаяСсылкаНаСчетФактуруВыданный", 			Документы.СчетФактураВыданный.ПустаяСсылка());
	Запрос.УстановитьПараметр("НДСсАвансов", 								Перечисления.НДСВидНачисления.НДСсАвансов);
	Запрос.УстановитьПараметр("ВидыСубконто",								СписокВидовСубконто);
	Запрос.УстановитьПараметр("ВидыСубконтоДокументыДоговоры",				ВидыСубконтоДокументыДоговоры);
	Запрос.УстановитьПараметр("ВидыЦенностейНалоговыйАгент",				ВидыЦенностейНалоговыйАгент); 
	Запрос.УстановитьПараметр("ВидыЦенностейАвансыВсе", 					ВидыЦенностейАвансыВсе);
	Запрос.УстановитьПараметр("ВидыЦенностейАвансыПолученные",				ВидыЦенностейАвансыПолученные);
	Запрос.УстановитьПараметр("ВыручкаЕНВД",								СчетВыручкиЕНВД);
	Запрос.УстановитьПараметр("ПустаяНоменклатура", 						Справочники.Номенклатура.ПустаяСсылка());
	Запрос.УстановитьПараметр("НеОтражаютсяВРазделеНачислениеПоРеализацииСложныйУчет", НеОтражаютсяВРазделеНачислениеПоРеализацииСложныйУчет);
	Запрос.УстановитьПараметр("ДатаНачалаДействияПостановления735", 		'2014-10-01'); // Постановление Правительства РФ от 30.07.2014 № 735
	Запрос.УстановитьПараметр("ПрименяетсяОсвобождениеОтУплатыНДС", 		ПрименяетсяОсвобождениеОтУплатыНДС); 

	
	//	Тексты ошибок
	
	Запрос.УстановитьПараметр("ТекстОтраженыВКнигеПродаж", 	  НСтр("ru='Отражены в книге продаж'"));
	Запрос.УстановитьПараметр("ТекстНеОтраженыВКнигеПродаж",  НСтр("ru='Не отражены в книге продаж'"));	
	Запрос.УстановитьПараметр("ТекстОтраженыВКнигеПокупок",   НСтр("ru='Отражены в книге покупок'"));	
	Запрос.УстановитьПараметр("ТекстНеОтраженыВКнигеПокупок", НСтр("ru='Не отражены в книге покупок'"));	
	
	Запрос.УстановитьПараметр("ТекстБезОшибок", НСтр("ru='Без ошибок'"));
				
	Запрос.УстановитьПараметр("ТекстОшибкаРеализацияЕНВДСНДС", 
		НСтр("ru='Ошибка: Начислен НДС при реализации по деятельности, облагаемой ЕНВД'"));
		
	Запрос.УстановитьПараметр("ТекстОшибкаРеализацияБезСФ", 
		НСтр("ru='Ошибка: Не выписан счет-фактура при реализации'"));
	
	Запрос.УстановитьПараметр("ТекстНДСНеУчитывается", 
		НСтр("ru='Не установлен флажок ""Учитывать НДС""'"));
	
	Запрос.УстановитьПараметр("ТекстНДСВключенВСтоимость", 
		НСтр("ru='НДС включен в стоимость'"));
	
	Запрос.УстановитьПараметр("ТекстНачислениеНеОтраженоВКнигеПродаж", 
		НСтр("ru='Начисление не отражено в книге продаж'"));
	
	Запрос.УстановитьПараметр("ТекстНеОтраженВКнигеПродажАвансПоКомиссии",
		НСтр("ru='Счет-фактура на аванс комитента (не отражается книге продаж)'"));
	
	Запрос.УстановитьПараметр("ТекстКорректировкаКнигиПродаж", 
		НСтр("ru='Произведена корректировка документов, уже отраженных в книге продаж'"));
	
	Запрос.УстановитьПараметр("ТекстПолученныйАвансБезСФ",
		НСтр("ru='Не выписан счет-фактура на полученный аванс'"));
		
	Запрос.УстановитьПараметр("ТекстПолученныйАвансПоСтавке0ИлиБезНДС",
		НСтр("ru='Авансы по ставке 0% или без НДС'"));
		
	Запрос.УстановитьПараметр("ТекстСФНаАвансПоНепроведенномуПоручению", 
		НСтр("ru='Ошибка: Счет-фактура на аванс выписан по непроведенному платежному поручению'"));	
		
	Запрос.УстановитьПараметр("ТекстСФНаПолученныйАвансНеПроведен", 
		НСтр("ru='Счет-фактура на полученный аванс выписан, но не проведен'"));
		
	Запрос.УстановитьПараметр("ТекстСФНалоговогоАгентаНеВыписан", 
		НСтр("ru='Счет-фактура налогового агента не выписан'"));
			
	Запрос.УстановитьПараметр("ТекстСФНалоговогоАгентаНеПроведен", 
		НСтр("ru='Счет-фактура налогового агента выписан, но не проведен'"));	
		
	Запрос.УстановитьПараметр("ТекстВыданныйАвансНеОтраженВКнигеПокупок", 
		НСтр("ru='НДС с выданного аванса не отражен в книге покупок'"));	
		
	Запрос.УстановитьПараметр("ТекстОплаченныеЦенностиНеПоступили", 
		НСтр("ru='Оплаченные ценности пока не поступили'"));	
		
	Запрос.УстановитьПараметр("ТекстНДСПолностьюВосстановлен", 
		НСтр("ru='НДС полностью восстановлен'"));	
		
	Запрос.УстановитьПараметр("ТекстНДСВосстановленЧастично", 
		НСтр("ru='НДС восстановлен частично'"));	
				  		
	Запрос.УстановитьПараметр("ТекстОбъектНеПринятКУчету", 
		НСтр("ru='Объект ОС или НМА пока не принят к учету'"));	
		
	Запрос.УстановитьПараметр("ТекстНеПодтвержденаСтавка", 
		НСтр("ru='Не подтверждена ставка 0%'"));
		
	Запрос.УстановитьПараметр("ТекстНеПодтвержденаОплатаВБюджет", 
		НСтр("ru='Не подтверждена оплата в бюджет'"));	
		
	Запрос.УстановитьПараметр("ТекстОтсутствуетСФПолученный", 
		НСтр("ru='Отсутствует счет-фактура полученный'"));	
		
	Запрос.УстановитьПараметр("ТекстСФПолученныйНеПроведен", 
		НСтр("ru='Счет-фактура полученный не проведен'"));	
		
	Запрос.УстановитьПараметр("ТекстВычетНеОтраженВКнигеПокупок", 
		НСтр("ru='Вычет не отражен в книге покупок'"));	
		
	Запрос.УстановитьПараметр("ТекстКорректировкаКнигиПокупок", 
		НСтр("ru='Произведена корректировка документов, уже отраженных в книге покупок'"));
		
	Запрос.УстановитьПараметр("ТекстПоступлениеНеОплаченоПоставщику", 
		НСтр("ru='Поступление по договору с исполнением обязанностей налогового агента не оплачено поставщику'"));	
		
	Запрос.УстановитьПараметр("ТекстНДСНеУплаченВБюджет", 
		НСтр("ru='НДС, начисленный при исполнении обязанностей налогового агента, не уплачен в бюджет'"));	
		
	Запрос.УстановитьПараметр("ТекстПолученныйАвансНеОтраженВКнигеПродаж", 
		НСтр("ru='НДС с полученного аванса не отражен в книге продаж'"));	
		
	Запрос.УстановитьПараметр("ТекстОплаченныеЦенностиНеРеализованы", 
		НСтр("ru='Оплаченные ценности пока не реализованы'"));	
		
	Запрос.УстановитьПараметр("ТекстНДСПринятКВычетуПолностью", 
		НСтр("ru='НДС принят к вычету полностью'"));	
		
	Запрос.УстановитьПараметр("ТекстНДСПринятКВычетуЧастично", 
		НСтр("ru='НДС принят к вычету частично'"));	
		
	Запрос.УстановитьПараметр("ТекстВыданныйАвансБезСФ", 
		НСтр("ru='Отсутствует счет-фактура на выданный аванс'"));	
		
	Запрос.УстановитьПараметр("ТекстСФНаВыданныйАвансНеПроведен", 
		НСтр("ru='Счет-фактура на выданный аванс не проведен'"));	
		
	Запрос.УстановитьПараметр("ТекстНДССписанНаРасходы", 
		НСтр("ru='НДС списан на расходы'"));	
		
	Запрос.УстановитьПараметр("ТекстВычетНеОтраженДоПодтвержденияСтавки0", 
		НСтр("ru='Вычет НДС не может быть отражен до подтверждения ставки 0%'"));
		
	Запрос.УстановитьПараметр("ТекстСноска", НСтр("ru='*'"));
		
	Запрос.УстановитьПараметр("ТекстНетСноски", НСтр("ru=''"));
	
	Запрос.УстановитьПараметр("ТекстОбъектНеПринятКУчетуОбъектСтроительства", 
		НСтр("ru='Объект строительства пока не принят к учету'"));
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Хозрасчетный.Ссылка КАК Счет
	|ИЗ
	|	ПланСчетов.Хозрасчетный КАК Хозрасчетный
	|ГДЕ
	|	Хозрасчетный.Ссылка В ИЕРАРХИИ (ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыПоАвансамПолученным), ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыПоАвансамПолученнымВал), ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыПоАвансамПолученнымУЕ))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Хозрасчетный.Ссылка КАК Счет
	|ИЗ
	|	ПланСчетов.Хозрасчетный КАК Хозрасчетный
	|ГДЕ
	|	Хозрасчетный.Ссылка В ИЕРАРХИИ (ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыПоАвансамВыданным), ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыПоАвансамВыданнымВал), ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыПоАвансамВыданнымУЕ))";
	
	Результат = Запрос.ВыполнитьПакет();
	Запрос.УстановитьПараметр("СписокСчетовАвансовПолученных", Результат[0].Выгрузить().ВыгрузитьКолонку("Счет"));
	Запрос.УстановитьПараметр("СписокСчетовАвансовВыданных",   Результат[1].Выгрузить().ВыгрузитьКолонку("Счет"));
	
	Возврат Запрос;
	
КонецФункции

// Выполняет запрос к базе данных для получения набора данных, необходимого для формирования расшифровки по
//	начисленному НДС по реализации
//
// Параметры
//  Запрос  - Запрос - запрос к базе данных
//  СтруктураВнешнихНаборов - структура внешних источников данных для расшифровки.
//
// Возвращаемое значение:
//   Структура   - структура параметров для формирования карты отчета
//
Процедура ПолучитьНаборДанныхНачислениеРеализация(Запрос, СтруктураВнешнихНаборов) 
	
	//	Документы включенные в книгу продаж (НДС по реализации)
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КнигаПродаж.СтавкаНДС КАК СтавкаНДС,
	|	КнигаПродаж.СуммаБезНДСОборот КАК Сумма,
	|	КнигаПродаж.НДСОборот КАК СуммаНДС,
	|	КнигаПродаж.ВидЦенности,
	|	КнигаПродаж.Событие,
	|	КнигаПродаж.ЗаписьДополнительногоЛиста,
	|	КнигаПродаж.Период,
	|	КнигаПродаж.Регистратор,
	|	КнигаПродаж.НомерСтроки КАК НомерСтроки,
	|	ВЫБОР
	|		КОГДА КнигаПродаж.ИсправленныйСчетФактура = НЕОПРЕДЕЛЕНО
	|			ТОГДА КнигаПродаж.СчетФактура
	|		ИНАЧЕ КнигаПродаж.ИсправленныйСчетФактура
	|	КОНЕЦ КАК СчетФактура,
	|	ВЫБОР
	|		КОГДА КнигаПродаж.СуммаБезНДСОборот < 0
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Сторно
	|ПОМЕСТИТЬ ВТСоставКнигиПродаж
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПродаж.Обороты(
	|			&НачалоПериода,
	|			&КонецПериодаГраница,
	|			Запись,
	|			Организация В (&Организация)
	|				И НЕ ВидЦенности В (&ВидыЦенностейАвансыВсе, &ВидыЦенностейНалоговыйАгент, ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.СМРСобственнымиСилами))
	|				И НЕ Событие В (ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПродажи.ПодтвержденаСтавка0), ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПродажи.НеПодтвержденаСтавка0))
	|				И НЕ ЗаписьДополнительногоЛиста) КАК КнигаПродаж
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КнигаПродаж.СтавкаНДС,
	|	КнигаПродаж.СуммаБезНДСОборот,
	|	КнигаПродаж.НДСОборот,
	|	КнигаПродаж.ВидЦенности,
	|	КнигаПродаж.Событие,
	|	КнигаПродаж.ЗаписьДополнительногоЛиста,
	|	КнигаПродаж.Период,
	|	КнигаПродаж.Регистратор,
	|	КнигаПродаж.НомерСтроки,
	|	ВЫБОР
	|		КОГДА КнигаПродаж.ИсправленныйСчетФактура = НЕОПРЕДЕЛЕНО
	|			ТОГДА КнигаПродаж.СчетФактура
	|		ИНАЧЕ КнигаПродаж.ИсправленныйСчетФактура
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА КнигаПродаж.СуммаБезНДСОборот < 0
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПродаж.Обороты(
	|			&НачалоПериода,
	|			,
	|			Запись,
	|			Организация В (&Организация)
	|				И НЕ ВидЦенности В (&ВидыЦенностейАвансыВсе, &ВидыЦенностейНалоговыйАгент, ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.СМРСобственнымиСилами))
	|				И НЕ Событие В (ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПродажи.ПодтвержденаСтавка0), ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПродажи.НеПодтвержденаСтавка0))
	|				И (ЗаписьДополнительногоЛиста
	|					И (КорректируемыйПериод МЕЖДУ &НачалоПериода И &КонецПериода))) КАК КнигаПродаж
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДСНачисленныйОбороты.СчетФактура КАК СчетФактура,
	|	НДСНачисленныйОбороты.СтавкаНДС
	|ПОМЕСТИТЬ ВТНДСНачисленныйОбороты
	|ИЗ
	|	РегистрНакопления.НДСНачисленный.Обороты(
	|			&НачалоПериода,
	|			&КонецПериодаГраница,
	|			,
	|			Организация В (&Организация)
	|				И НЕ СчетФактура ССЫЛКА Документ.ОтчетОРозничныхПродажах
	|				И СчетФактура В
	|					(ВЫБРАТЬ
	|						КнигаПродаж.СчетФактура КАК СчетФактура
	|					ИЗ
	|						ВТСоставКнигиПродаж КАК КнигаПродаж)) КАК НДСНачисленныйОбороты
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СчетФактураВыданныйДокументыОснования.Ссылка КАК СчетФактура,
	|	СчетФактураВыданныйДокументыОснования.ДокументОснование КАК ДокументОснование
	|ПОМЕСТИТЬ СчетаФактуры
	|ИЗ
	|	Документ.СчетФактураВыданный.ДокументыОснования КАК СчетФактураВыданныйДокументыОснования
	|ГДЕ
	|	СчетФактураВыданныйДокументыОснования.ДокументОснование В
	|			(ВЫБРАТЬ
	|				ВТСоставКнигиПродаж.СчетФактура
	|			ИЗ
	|				ВТСоставКнигиПродаж КАК ВТСоставКнигиПродаж)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТСоставКнигиПродаж.СчетФактура,
	|	ВТСоставКнигиПродаж.СчетФактура
	|ИЗ
	|	ВТСоставКнигиПродаж КАК ВТСоставКнигиПродаж
	|ГДЕ
	|	ВТСоставКнигиПродаж.СчетФактура ССЫЛКА Документ.СчетФактураВыданный
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СчетФактураПолученныйДокументыОснования.Ссылка,
	|	СчетФактураПолученныйДокументыОснования.ДокументОснование
	|ИЗ
	|	Документ.СчетФактураПолученный.ДокументыОснования КАК СчетФактураПолученныйДокументыОснования
	|ГДЕ
	|	СчетФактураПолученныйДокументыОснования.ДокументОснование В
	|			(ВЫБРАТЬ
	|				ВТСоставКнигиПродаж.СчетФактура
	|			ИЗ
	|				ВТСоставКнигиПродаж КАК ВТСоставКнигиПродаж)
	|	И НЕ СчетФактураПолученныйДокументыОснования.ДокументОснование ССЫЛКА Документ.ОтчетКомитентуОПродажах
	|	И НЕ СчетФактураПолученныйДокументыОснования.ДокументОснование ССЫЛКА Документ.ОтчетКомиссионераОПродажах
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДСНачисленныйОбороты.СчетФактура КАК ДокументРегистратор,
	|	НДСНачисленныйОбороты.СтавкаНДС,
	|	2 КАК РангОшибки
	|ПОМЕСТИТЬ ВТОперацииСОшибками
	|ИЗ
	|	ВТСоставКнигиПродаж КАК НДСНачисленныйОбороты
	|		ЛЕВОЕ СОЕДИНЕНИЕ СчетаФактуры КАК СчетаФактуры
	|		ПО НДСНачисленныйОбороты.СчетФактура = СчетаФактуры.ДокументОснование
	|ГДЕ
	|	СчетаФактуры.СчетФактура ЕСТЬ NULL 
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НДСНачисленный.СчетФактура,
	|	НДСНачисленный.СтавкаНДС,
	|	1
	|ИЗ
	|	РегистрНакопления.НДСНачисленный КАК НДСНачисленный
	|		ЛЕВОЕ СОЕДИНЕНИЕ СчетаФактуры КАК СчетаФактуры
	|		ПО НДСНачисленный.СчетФактура = СчетаФактуры.ДокументОснование
	|ГДЕ
	|	НДСНачисленный.СтавкаНДС <> ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
	|	И НДСНачисленный.СчетДоходовБУ = &ВыручкаЕНВД
	|	И НДСНачисленный.СчетФактура В
	|			(ВЫБРАТЬ
	|				КнигаПродаж.СчетФактура КАК СчетФактура
	|			ИЗ
	|				ВТСоставКнигиПродаж КАК КнигаПродаж)
	|	И НДСНачисленный.Организация В(&Организация)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументРегистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТОперацииСОшибками.ДокументРегистратор,
	|	СУММА(ВЫБОР
	|			КОГДА ВТОперацииСОшибками.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК КоличествоСтрокБезНДС,
	|	КОЛИЧЕСТВО(*) КАК ВсегоСтрок
	|ПОМЕСТИТЬ ВТ_ОперацииПоСтавкеБезНДС
	|ИЗ
	|	ВТОперацииСОшибками КАК ВТОперацииСОшибками
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТОперацииСОшибками.ДокументРегистратор
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ВТОперацииСОшибками.ДокументРегистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТСоставКнигиПродаж.СчетФактура КАК СчетФактура,
	|	ВТСоставКнигиПродаж.Сторно КАК Сторно,
	|	ВТСоставКнигиПродаж.СтавкаНДС,
	|	ВТОперацииСОшибками.ДокументРегистратор,
	|	МИНИМУМ(ВТСоставКнигиПродаж.Сумма) КАК Сумма,
	|	МИНИМУМ(ВТСоставКнигиПродаж.СуммаНДС) КАК СуммаНДС,
	|	МИНИМУМ(ВЫБОР
	|			КОГДА ВТОперацииСОшибками.РангОшибки <> 2
	|				ТОГДА ВТОперацииСОшибками.РангОшибки
	|			КОГДА ВТСоставКнигиПродаж.Период < &ДатаНачалаДействияПостановления735
	|				ТОГДА ВТОперацииСОшибками.РангОшибки
	|			КОГДА &ПрименяетсяОсвобождениеОтУплатыНДС
	|				ТОГДА ВТОперацииСОшибками.РангОшибки
	|			КОГДА ВТ_ОперацииПоСтавкеБезНДС.ВсегоСтрок = ВТ_ОперацииПоСтавкеБезНДС.КоличествоСтрокБезНДС
	|				ТОГДА NULL
	|			ИНАЧЕ ВТОперацииСОшибками.РангОшибки
	|		КОНЕЦ) КАК РангОшибки,
	|	ВТСоставКнигиПродаж.Событие,
	|	ВТСоставКнигиПродаж.ВидЦенности,
	|	ВТСоставКнигиПродаж.ЗаписьДополнительногоЛиста,
	|	ВТСоставКнигиПродаж.НомерСтроки
	|ПОМЕСТИТЬ ВТПроведеноРанжированиеОшибок
	|ИЗ
	|	ВТСоставКнигиПродаж КАК ВТСоставКнигиПродаж
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОперацииСОшибками КАК ВТОперацииСОшибками
	|		ПО ВТСоставКнигиПродаж.СчетФактура = ВТОперацииСОшибками.ДокументРегистратор
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ОперацииПоСтавкеБезНДС КАК ВТ_ОперацииПоСтавкеБезНДС
	|		ПО ВТСоставКнигиПродаж.СчетФактура = ВТ_ОперацииПоСтавкеБезНДС.ДокументРегистратор
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТСоставКнигиПродаж.СчетФактура,
	|	ВТСоставКнигиПродаж.Сторно,
	|	ВТСоставКнигиПродаж.СтавкаНДС,
	|	ВТОперацииСОшибками.ДокументРегистратор,
	|	ВТСоставКнигиПродаж.Событие,
	|	ВТСоставКнигиПродаж.ВидЦенности,
	|	ВТСоставКнигиПродаж.ЗаписьДополнительногоЛиста,
	|	ВТСоставКнигиПродаж.НомерСтроки
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СчетаФактуры.ДокументОснование КАК ДокументОснование,
	|	СчетаФактуры.СчетФактура,
	|	ВЫБОР
	|		КОГДА СчетаФактуры.СчетФактура.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.НаРеализацию)
	|				ИЛИ СчетаФактуры.СчетФактура.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.Корректировочный)
	|			ТОГДА NULL
	|		ИНАЧЕ СчетаФактуры.СчетФактура.СтавкаНДС
	|	КОНЕЦ КАК СтавкаНДССчетаФактуры
	|ПОМЕСТИТЬ ВТСчетФактураВыданныйДокументыОснования
	|ИЗ
	|	СчетаФактуры КАК СчетаФактуры
	|ГДЕ
	|	ВЫБОР
	|			КОГДА СчетаФактуры.ДокументОснование ССЫЛКА Документ.ОтчетКомитентуОПродажах
	|				ТОГДА СчетаФактуры.СчетФактура.Продавец = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТПроведеноРанжированиеОшибок.СтавкаНДС,
	|	ВТПроведеноРанжированиеОшибок.СчетФактура КАК ДокументРегистратор,
	|	ВТПроведеноРанжированиеОшибок.Сумма,
	|	ВТПроведеноРанжированиеОшибок.СуммаНДС,
	|	1 КАК КоличествоДокументов,
	|	ВЫБОР
	|		КОГДА ВТПроведеноРанжированиеОшибок.РангОшибки ЕСТЬ NULL 
	|			ТОГДА &ТекстБезОшибок
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ВТПроведеноРанжированиеОшибок.РангОшибки = 1
	|					ТОГДА &ТекстОшибкаРеализацияЕНВДСНДС
	|				КОГДА ВТПроведеноРанжированиеОшибок.РангОшибки = 2
	|					ТОГДА &ТекстОшибкаРеализацияБезСФ
	|			КОНЕЦ
	|	КОНЕЦ КАК ПризнакНаличияОшибок,
	|	ВЫБОР
	|		КОГДА ВТПроведеноРанжированиеОшибок.РангОшибки = 0
	|			ТОГДА &ТекстСноска
	|		ИНАЧЕ &ТекстНетСноски
	|	КОНЕЦ КАК СимволСноски,
	|	&ТекстОтраженыВКнигеПродаж КАК ПризнакОтраженияНДС,
	|	ВЫБОР
	|		КОГДА ВТПроведеноРанжированиеОшибок.РангОшибки ЕСТЬ NULL 
	|			ТОГДА 0
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК КоличествоОшибок,
	|	ВТПроведеноРанжированиеОшибок.Событие,
	|	ВТПроведеноРанжированиеОшибок.ВидЦенности,
	|	ВТПроведеноРанжированиеОшибок.НомерСтроки,
	|	ВТПроведеноРанжированиеОшибок.ЗаписьДополнительногоЛиста,
	|	СчетФактураВыданныйДокументыОснования.СчетФактура КАК СчетФактура
	|ИЗ
	|	ВТПроведеноРанжированиеОшибок КАК ВТПроведеноРанжированиеОшибок
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСчетФактураВыданныйДокументыОснования КАК СчетФактураВыданныйДокументыОснования
	|		ПО (ВТПроведеноРанжированиеОшибок.СчетФактура = СчетФактураВыданныйДокументыОснования.ДокументОснование
	|				ИЛИ ВТПроведеноРанжированиеОшибок.СчетФактура = СчетФактураВыданныйДокументыОснования.СчетФактура
	|					И ВТПроведеноРанжированиеОшибок.СтавкаНДС = ЕСТЬNULL(СчетФактураВыданныйДокументыОснования.СтавкаНДССчетаФактуры, ВТПроведеноРанжированиеОшибок.СтавкаНДС))";
	
	НачисленияПоРеализацииВключенные = Запрос.Выполнить();
	
	//	Документы не включенные в книгу продаж
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НДСНачисленныйОстатки.СчетФактура КАК СчетФактура,
	|	НДСНачисленныйОстатки.СтавкаНДС,
	|	НДСНачисленныйОстатки.СуммаБезНДСОстаток,
	|	НДСНачисленныйОстатки.НДСОстаток,
	|	НДСНачисленныйОстатки.ВидЦенности
	|ПОМЕСТИТЬ ВТНДСНачисленныйОстатки
	|ИЗ
	|	РегистрНакопления.НДСНачисленный.Остатки(
	|			&КонецПериодаГраница,
	|			Организация В (&Организация)
	|				И (НЕ ВидНачисления В (&НеОтражаютсяВРазделеНачислениеПоРеализации)
	|					И НЕ ВидЦенности В (&ВидыЦенностейНалоговыйАгент)
	|					И НЕ ВидЦенности = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.СМРСобственнымиСилами))) КАК НДСНачисленныйОстатки
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДСНачисленныйОстатки.СчетФактура КАК ДокументРегистратор,
	|	НДСНачисленныйОстатки.СтавкаНДС КАК СтавкаНДС,
	|	СУММА(НДСНачисленныйОстатки.СуммаБезНДСОстаток) КАК Сумма,
	|	СУММА(НДСНачисленныйОстатки.НДСОстаток) КАК СуммаНДС,
	|	СчетФактураВыданныйДокументыОснования.Ссылка КАК СчетФактура,
	|	1 КАК КоличествоДокументов,
	|	ВЫБОР
	|		КОГДА СУММА(НДСНачисленныйОстатки.НДСОстаток) > 0
	|			ТОГДА &ТекстНачислениеНеОтраженоВКнигеПродаж
	|		ИНАЧЕ &ТекстКорректировкаКнигиПродаж
	|	КОНЕЦ КАК ПризнакНаличияОшибок,
	|	&ТекстНеОтраженыВКнигеПродаж КАК ПризнакОтраженияНДС,
	|	НДСНачисленныйОстатки.ВидЦенности
	|ИЗ
	|	ВТНДСНачисленныйОстатки КАК НДСНачисленныйОстатки
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураВыданный.ДокументыОснования КАК СчетФактураВыданныйДокументыОснования
	|		ПО (НДСНачисленныйОстатки.СчетФактура = СчетФактураВыданныйДокументыОснования.ДокументОснование
	|				ИЛИ НДСНачисленныйОстатки.СчетФактура = СчетФактураВыданныйДокументыОснования.Ссылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	НДСНачисленныйОстатки.СчетФактура,
	|	НДСНачисленныйОстатки.СтавкаНДС,
	|	СчетФактураВыданныйДокументыОснования.Ссылка,
	|	НДСНачисленныйОстатки.ВидЦенности";
	
	НачисленияПоРеализацииНеВключенныеНДСНеОтражен = Запрос.Выполнить();
				
	ВнешнийНаборДанныхНачислениеРеализация = Новый Структура;
	ВнешнийНаборДанныхНачислениеРеализация.Вставить("НачисленияПоРеализацииВключенные",	
													НачисленияПоРеализацииВключенные);
	ВнешнийНаборДанныхНачислениеРеализация.Вставить("НачисленияПоРеализацииНеВключенныеНДСНеОтражен",	
													НачисленияПоРеализацииНеВключенныеНДСНеОтражен);
	
	СтруктураВнешнихНаборов.Вставить("ВнешнийНаборДанныхНачислениеРеализация", ВнешнийНаборДанныхНачислениеРеализация);
		
КонецПроцедуры // ПолучитьНаборДанныхНачислениеРеализация()

// Выполняет запрос к базе данных для получения набора данных, необходимого для формирования расшифровки по
//	начисленному НДС при исполнении обязанностей налогового агента
//
// Параметры
//  Запрос  - Запрос - запрос к базе данных
//  СтруктураВнешнихНаборов - структура внешних источников данных для расшифровки.
//
// Возвращаемое значение:
//   Структура   - структура параметров для формирования карты отчета
//
Процедура ПолучитьНаборДанныхНачислениеНалоговыйАгент(Запрос, СтруктураВнешнихНаборов) 
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НДСЗаписиКнигиПродажОбороты.СчетФактура КАК СчетФактура,
	|	НДСЗаписиКнигиПродажОбороты.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	НДСЗаписиКнигиПродажОбороты.Покупатель КАК Покупатель,
	|	НДСЗаписиКнигиПродажОбороты.СуммаБезНДСОборот КАК СуммаБезНДСОборот,
	|	НДСЗаписиКнигиПродажОбороты.НДСОборот КАК НДСОборот,
	|	НДСЗаписиКнигиПродажОбороты.Событие,
	|	НДСЗаписиКнигиПродажОбороты.ВидЦенности,
	|	НДСЗаписиКнигиПродажОбороты.ЗаписьДополнительногоЛиста,
	|	НДСЗаписиКнигиПродажОбороты.НомерСтроки
	|ПОМЕСТИТЬ ВТНДСЗаписиКнигиПродажОбороты
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПродаж.Обороты(
	|			&НачалоПериода,
	|			&КонецПериодаГраница,
	|			Запись,
	|			Организация В (&Организация)
	|				И ВидЦенности В (&ВидыЦенностейНалоговыйАгент)
	|				И НЕ ЗаписьДополнительногоЛиста) КАК НДСЗаписиКнигиПродажОбороты
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НДСЗаписиКнигиПродажОбороты.СчетФактура,
	|	НДСЗаписиКнигиПродажОбороты.ДоговорКонтрагента,
	|	НДСЗаписиКнигиПродажОбороты.Покупатель,
	|	НДСЗаписиКнигиПродажОбороты.СуммаБезНДСОборот,
	|	НДСЗаписиКнигиПродажОбороты.НДСОборот,
	|	НДСЗаписиКнигиПродажОбороты.Событие,
	|	НДСЗаписиКнигиПродажОбороты.ВидЦенности,
	|	НДСЗаписиКнигиПродажОбороты.ЗаписьДополнительногоЛиста,
	|	НДСЗаписиКнигиПродажОбороты.НомерСтроки
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПродаж.Обороты(
	|			&НачалоПериода,
	|			,
	|			Запись,
	|			Организация В (&Организация)
	|				И ВидЦенности В (&ВидыЦенностейНалоговыйАгент)
	|				И (ЗаписьДополнительногоЛиста
	|					И (КорректируемыйПериод МЕЖДУ &НачалоПериода И &КонецПериода))) КАК НДСЗаписиКнигиПродажОбороты
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактура,
	|	ДоговорКонтрагента,
	|	Покупатель
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Хозрасчетный.Ссылка КАК СчетСсылка
	|ПОМЕСТИТЬ ВТСчета
	|ИЗ
	|	ПланСчетов.Хозрасчетный КАК Хозрасчетный
	|ГДЕ
	|	НЕ Хозрасчетный.ВидыСубконто.ВидСубконто = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты)
	|	И НЕ Хозрасчетный.ВидыСубконто.ВидСубконто = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ПрочиеДоходыИРасходы)
	|	И НЕ Хозрасчетный.ЗапретитьИспользоватьВПроводках
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетСсылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДоговорыКонтрагентов.Ссылка КАК Договор
	|ПОМЕСТИТЬ Договоры
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|ГДЕ
	|	ДоговорыКонтрагентов.Организация В(&Организация)
	|	И ДоговорыКонтрагентов.УчетАгентскогоНДС = ИСТИНА
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Договор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ХозрасчетныйСубконто.Регистратор КАК Регистратор,
	|	ХозрасчетныйСубконто.НомерСтроки КАК НомерСтроки
	|ПОМЕСТИТЬ НомераПроводок
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Субконто КАК ХозрасчетныйСубконто
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Договоры КАК Договоры
	|		ПО (ХозрасчетныйСубконто.Вид = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Договоры))
	|			И ХозрасчетныйСубконто.Значение = Договоры.Договор
	|			И (ХозрасчетныйСубконто.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияБухгалтерии.Дебет))
	|			И (ХозрасчетныйСубконто.Период МЕЖДУ &НачалоПериода И &КонецПериода)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор,
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ХозрасчетныйСубконто.Регистратор КАК Регистратор,
	|	ХозрасчетныйСубконто.НомерСтроки КАК НомерСтроки,
	|	ХозрасчетныйСубконто.Вид,
	|	ХозрасчетныйСубконто.Значение
	|ПОМЕСТИТЬ ЗначенияСубконто
	|ИЗ
	|	НомераПроводок КАК НомераПроводок
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.Субконто КАК ХозрасчетныйСубконто
	|		ПО НомераПроводок.Регистратор = ХозрасчетныйСубконто.Регистратор
	|			И НомераПроводок.НомерСтроки = ХозрасчетныйСубконто.НомерСтроки
	|			И (ХозрасчетныйСубконто.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияБухгалтерии.Дебет))
	|			И (ХозрасчетныйСубконто.Вид В (ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Договоры), ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты), ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ДокументыРасчетовСКонтрагентами)))
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор,
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Хозрасчетный.Регистратор КАК Регистратор,
	|	Контрагенты.Значение КАК СубконтоДт1,
	|	Договоры.Значение КАК СубконтоДт2,
	|	ДокументыРасчетов.Значение КАК СубконтоДт3,
	|	Хозрасчетный.Сумма КАК СуммаОборот,
	|	ВЫРАЗИТЬ(Хозрасчетный.Сумма * 0.18 КАК ЧИСЛО(15, 2)) КАК СуммаНДСОборот
	|ПОМЕСТИТЬ ВТХозрасчетныйОборотыДтКт
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный КАК Хозрасчетный
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ЗначенияСубконто КАК Договоры
	|		ПО Хозрасчетный.Регистратор = Договоры.Регистратор
	|			И Хозрасчетный.НомерСтроки = Договоры.НомерСтроки
	|			И (Договоры.Вид = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Договоры))
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ЗначенияСубконто КАК Контрагенты
	|		ПО Хозрасчетный.Регистратор = Контрагенты.Регистратор
	|			И Хозрасчетный.НомерСтроки = Контрагенты.НомерСтроки
	|			И (Контрагенты.Вид = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты))
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ЗначенияСубконто КАК ДокументыРасчетов
	|		ПО Хозрасчетный.Регистратор = ДокументыРасчетов.Регистратор
	|			И Хозрасчетный.НомерСтроки = ДокументыРасчетов.НомерСтроки
	|			И (ДокументыРасчетов.Вид = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ДокументыРасчетовСКонтрагентами))
	|ГДЕ
	|	Хозрасчетный.СчетКт В
	|			(ВЫБРАТЬ
	|				ВТСчета.СчетСсылка
	|			ИЗ
	|				ВТСчета КАК ВТСчета)
	|	И НЕ Хозрасчетный.СчетДт = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НДСНалоговогоАгента)
	|	И Хозрасчетный.Организация В(&Организация)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор,
	|	СубконтоДт1,
	|	СубконтоДт2
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СчетФактураВыданный.Ссылка КАК Ссылка,
	|	СчетФактураВыданный.Ссылка.Проведен КАК Проведен,
	|	СчетФактураВыданный.Ссылка.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	СчетФактураВыданный.Ссылка.ДокументОснование КАК ДокументОснование
	|ПОМЕСТИТЬ ВТСчетФактураВыданный
	|ИЗ
	|	Документ.СчетФактураВыданный.ДокументыОснования КАК СчетФактураВыданный
	|ГДЕ
	|	СчетФактураВыданный.Ссылка.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.НалоговыйАгент)
	|	И СчетФактураВыданный.Ссылка.Организация В(&Организация)
	|	И СчетФактураВыданный.Ссылка.ПометкаУдаления = ЛОЖЬ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДоговорКонтрагента,
	|	ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ХозрасчетныйОборотыДтКт.Регистратор КАК ДокументОплаты,
	|	ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС18_118) КАК СтавкаНДС,
	|	СУММА(ЕСТЬNULL(ХозрасчетныйОборотыДтКт.СуммаОборот, ЕСТЬNULL(НДСЗаписиКнигиПродажОбороты.СуммаБезНДСОборот, 0))) КАК Сумма,
	|	СУММА(ЕСТЬNULL(ХозрасчетныйОборотыДтКт.СуммаНДСОборот, ЕСТЬNULL(НДСЗаписиКнигиПродажОбороты.НДСОборот, 0))) КАК СуммаНДС,
	|	ЕСТЬNULL(СчетФактураВыданный.Ссылка, НЕОПРЕДЕЛЕНО) КАК СчетФактура,
	|	ВЫБОР
	|		КОГДА НДСЗаписиКнигиПродажОбороты.СуммаБезНДСОборот ЕСТЬ NULL 
	|			ТОГДА ВЫБОР
	|					КОГДА СчетФактураВыданный.Ссылка ЕСТЬ NULL 
	|						ТОГДА &ТекстСФНалоговогоАгентаНеВыписан
	|					ИНАЧЕ ВЫБОР
	|							КОГДА СчетФактураВыданный.Проведен
	|								ТОГДА &ТекстНачислениеНеОтраженоВКнигеПродаж
	|							ИНАЧЕ &ТекстСФНалоговогоАгентаНеПроведен
	|						КОНЕЦ
	|				КОНЕЦ
	|		ИНАЧЕ &ТекстБезОшибок
	|	КОНЕЦ КАК ПризнакНаличияОшибок,
	|	ВЫБОР
	|		КОГДА НДСЗаписиКнигиПродажОбороты.СуммаБезНДСОборот ЕСТЬ NULL 
	|			ТОГДА &ТекстНеОтраженыВКнигеПродаж
	|		ИНАЧЕ &ТекстОтраженыВКнигеПродаж
	|	КОНЕЦ КАК ПризнакОтраженияНДС,
	|	СУММА(1) КАК КоличествоДокументов,
	|	ХозрасчетныйОборотыДтКт.СубконтоДт3 КАК ДокументРегистратор,
	|	СУММА(ВЫБОР
	|			КОГДА НДСЗаписиКнигиПродажОбороты.СуммаБезНДСОборот ЕСТЬ NULL 
	|				ТОГДА 0
	|			ИНАЧЕ НДСЗаписиКнигиПродажОбороты.НДСОборот
	|		КОНЕЦ) КАК СуммаВключенныхВБазу,
	|	СУММА(ВЫБОР
	|			КОГДА НДСЗаписиКнигиПродажОбороты.СуммаБезНДСОборот ЕСТЬ NULL 
	|				ТОГДА ХозрасчетныйОборотыДтКт.СуммаНДСОборот
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК СуммаНеВключенныхВБазу,
	|	ХозрасчетныйОборотыДтКт.СубконтоДт1 КАК Контрагент,
	|	НДСЗаписиКнигиПродажОбороты.ВидЦенности,
	|	НДСЗаписиКнигиПродажОбороты.Событие,
	|	ХозрасчетныйОборотыДтКт.СубконтоДт2 КАК ДоговорКонтрагента,
	|	НДСЗаписиКнигиПродажОбороты.ЗаписьДополнительногоЛиста,
	|	НДСЗаписиКнигиПродажОбороты.НомерСтроки
	|ИЗ
	|	ВТХозрасчетныйОборотыДтКт КАК ХозрасчетныйОборотыДтКт
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСчетФактураВыданный КАК СчетФактураВыданный
	|		ПО ХозрасчетныйОборотыДтКт.Регистратор = СчетФактураВыданный.ДокументОснование
	|			И ХозрасчетныйОборотыДтКт.СубконтоДт2 = СчетФактураВыданный.ДоговорКонтрагента
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНДСЗаписиКнигиПродажОбороты КАК НДСЗаписиКнигиПродажОбороты
	|		ПО ХозрасчетныйОборотыДтКт.Регистратор = НДСЗаписиКнигиПродажОбороты.СчетФактура
	|			И ХозрасчетныйОборотыДтКт.СубконтоДт2 = НДСЗаписиКнигиПродажОбороты.ДоговорКонтрагента
	|			И ХозрасчетныйОборотыДтКт.СубконтоДт1 = НДСЗаписиКнигиПродажОбороты.Покупатель
	|
	|СГРУППИРОВАТЬ ПО
	|	ХозрасчетныйОборотыДтКт.Регистратор,
	|	ЕСТЬNULL(СчетФактураВыданный.Ссылка, НЕОПРЕДЕЛЕНО),
	|	ХозрасчетныйОборотыДтКт.СубконтоДт3,
	|	НДСЗаписиКнигиПродажОбороты.СуммаБезНДСОборот,
	|	НДСЗаписиКнигиПродажОбороты.НДСОборот,
	|	ХозрасчетныйОборотыДтКт.СубконтоДт1,
	|	ХозрасчетныйОборотыДтКт.СубконтоДт2,
	|	НДСЗаписиКнигиПродажОбороты.ВидЦенности,
	|	НДСЗаписиКнигиПродажОбороты.Событие,
	|	НДСЗаписиКнигиПродажОбороты.ЗаписьДополнительногоЛиста,
	|	НДСЗаписиКнигиПродажОбороты.НомерСтроки,
	|	ВЫБОР
	|		КОГДА НДСЗаписиКнигиПродажОбороты.СуммаБезНДСОборот ЕСТЬ NULL 
	|			ТОГДА ВЫБОР
	|					КОГДА СчетФактураВыданный.Ссылка ЕСТЬ NULL 
	|						ТОГДА &ТекстСФНалоговогоАгентаНеВыписан
	|					ИНАЧЕ ВЫБОР
	|							КОГДА СчетФактураВыданный.Проведен
	|								ТОГДА &ТекстНачислениеНеОтраженоВКнигеПродаж
	|							ИНАЧЕ &ТекстСФНалоговогоАгентаНеПроведен
	|						КОНЕЦ
	|				КОНЕЦ
	|		ИНАЧЕ &ТекстБезОшибок
	|	КОНЕЦ";
	
	НачисленияИсполнениеОНА = Запрос.Выполнить();
	ВнешнийНаборДанныхНачислениеИсполнениеОНА = Новый Структура;
	ВнешнийНаборДанныхНачислениеИсполнениеОНА.Вставить("НачисленияИсполнениеОНА", НачисленияИсполнениеОНА);
	                          
	СтруктураВнешнихНаборов.Вставить("ВнешнийНаборДанныхНачислениеИсполнениеОНА", 
									 ВнешнийНаборДанныхНачислениеИсполнениеОНА);
	
	
КонецПроцедуры // ПолучитьНаборДанныхНачислениеИсполнениеОНА()

// Выполняет запрос к базе данных для получения набора данных, необходимого для формирования расшифровки по
//	начисленному НДС при выполнении строительно-монтажных работ хозяйственным способом
//
// Параметры
//  Запрос  - Запрос - запрос к базе данных
//  СтруктураВнешнихНаборов - структура внешних источников данных для расшифровки.
//
// Возвращаемое значение:
//   Структура   - структура параметров для формирования карты отчета
//
Процедура ПолучитьНаборДанныхНачислениеСМРХозСпособом(Запрос, СтруктураВнешнихНаборов) 
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НДСНачисленныйОстатки.СчетФактура КАК СчетФактура,
	|	НДСНачисленныйОстатки.СтавкаНДС,
	|	НДСНачисленныйОстатки.СуммаБезНДСОстаток,
	|	НДСНачисленныйОстатки.НДСОстаток,
	|	НДСНачисленныйОстатки.ВидЦенности
	|ПОМЕСТИТЬ НДСНачисленныйПоСМР
	|ИЗ
	|	РегистрНакопления.НДСНачисленный.Остатки(
	|			&НачалоСледующегоПериода,
	|			Организация В (&Организация)
	|				И ВидНачисления = ЗНАЧЕНИЕ(Перечисление.НДСВидНачисления.НДСНачисленКУплате)
	|				И ВидЦенности = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.СМРСобственнымиСилами)) КАК НДСНачисленныйОстатки
	|ГДЕ
	|	НЕ(НДСНачисленныйОстатки.СуммаБезНДСОстаток = 0
	|				И НДСНачисленныйОстатки.НДСОстаток = 0)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДСЗаписиКнигиПродажОбороты.СчетФактура КАК СчетФактура,
	|	НДСЗаписиКнигиПродажОбороты.СтавкаНДС,
	|	НДСЗаписиКнигиПродажОбороты.СуммаБезНДСОборот,
	|	НДСЗаписиКнигиПродажОбороты.НДСОборот,
	|	НДСЗаписиКнигиПродажОбороты.Событие,
	|	НДСЗаписиКнигиПродажОбороты.ВидЦенности,
	|	НДСЗаписиКнигиПродажОбороты.ЗаписьДополнительногоЛиста,
	|	НДСЗаписиКнигиПродажОбороты.НомерСтроки
	|ПОМЕСТИТЬ ЗаписиКнигиПродажВЧастиСМР
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПродаж.Обороты(
	|			&НачалоПериода,
	|			&КонецПериодаГраница,
	|			Запись,
	|			Организация В (&Организация)
	|				И ВидЦенности = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.СМРСобственнымиСилами)
	|				И НЕ ЗаписьДополнительногоЛиста) КАК НДСЗаписиКнигиПродажОбороты
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НДСЗаписиКнигиПродажОбороты.СчетФактура,
	|	НДСЗаписиКнигиПродажОбороты.СтавкаНДС,
	|	НДСЗаписиКнигиПродажОбороты.СуммаБезНДСОборот,
	|	НДСЗаписиКнигиПродажОбороты.НДСОборот,
	|	НДСЗаписиКнигиПродажОбороты.Событие,
	|	НДСЗаписиКнигиПродажОбороты.ВидЦенности,
	|	НДСЗаписиКнигиПродажОбороты.ЗаписьДополнительногоЛиста,
	|	НДСЗаписиКнигиПродажОбороты.НомерСтроки
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПродаж.Обороты(
	|			&НачалоПериода,
	|			,
	|			Запись,
	|			Организация В (&Организация)
	|				И ВидЦенности = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.СМРСобственнымиСилами)
	|				И (ЗаписьДополнительногоЛиста
	|					И (КорректируемыйПериод МЕЖДУ &НачалоПериода И &КонецПериода))) КАК НДСЗаписиКнигиПродажОбороты
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДСНачисленныйОстатки.СчетФактура КАК ДокументРегистратор,
	|	СчетаФактурыВыданные.Ссылка КАК СчетФактура,
	|	НДСНачисленныйОстатки.СтавкаНДС,
	|	НДСНачисленныйОстатки.СуммаБезНДСОстаток КАК Сумма,
	|	НДСНачисленныйОстатки.НДСОстаток КАК СуммаНДС,
	|	ВЫБОР
	|		КОГДА НДСНачисленныйОстатки.НДСОстаток > 0
	|			ТОГДА &ТекстНеОтраженыВКнигеПродаж
	|		ИНАЧЕ &ТекстКорректировкаКнигиПродаж
	|	КОНЕЦ КАК ПризнакОтраженияНДС,
	|	1 КАК КоличествоДокументов,
	|	НДСНачисленныйОстатки.НДСОстаток КАК НеВключенныйНДС,
	|	0 КАК ВключенныйНДС,
	|	НДСНачисленныйОстатки.ВидЦенности,
	|	NULL КАК Событие,
	|	ЛОЖЬ КАК ЗаписьДополнительногоЛиста,
	|	1 КАК НомерСтроки
	|ИЗ
	|	НДСНачисленныйПоСМР КАК НДСНачисленныйОстатки
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураВыданный.ДокументыОснования КАК СчетаФактурыВыданные
	|		ПО (СчетаФактурыВыданные.ДокументОснование = НДСНачисленныйОстатки.СчетФактура)
	|ГДЕ
	|	НЕ(НДСНачисленныйОстатки.СуммаБезНДСОстаток = 0
	|				И НДСНачисленныйОстатки.НДСОстаток = 0)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НДСЗаписиКнигиПродажОбороты.СчетФактура,
	|	СчетаФактурыВыданные.Ссылка,
	|	НДСЗаписиКнигиПродажОбороты.СтавкаНДС,
	|	НДСЗаписиКнигиПродажОбороты.СуммаБезНДСОборот,
	|	НДСЗаписиКнигиПродажОбороты.НДСОборот,
	|	&ТекстОтраженыВКнигеПродаж,
	|	1,
	|	0,
	|	НДСЗаписиКнигиПродажОбороты.НДСОборот,
	|	НДСЗаписиКнигиПродажОбороты.ВидЦенности,
	|	НДСЗаписиКнигиПродажОбороты.Событие,
	|	НДСЗаписиКнигиПродажОбороты.ЗаписьДополнительногоЛиста,
	|	НДСЗаписиКнигиПродажОбороты.НомерСтроки
	|ИЗ
	|	ЗаписиКнигиПродажВЧастиСМР КАК НДСЗаписиКнигиПродажОбороты
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураВыданный.ДокументыОснования КАК СчетаФактурыВыданные
	|		ПО (СчетаФактурыВыданные.ДокументОснование = НДСЗаписиКнигиПродажОбороты.СчетФактура)";
	
	НачислениеСМРХозСпособом = Запрос.Выполнить();
	
	ВнешнийНаборДанныхНачислениеСМРХозСпособом = Новый Структура;
	ВнешнийНаборДанныхНачислениеСМРХозСпособом.Вставить("НачислениеСМРХозСпособом",	НачислениеСМРХозСпособом);
	
	СтруктураВнешнихНаборов.Вставить("ВнешнийНаборДанныхНачислениеСМРХозСпособом", 
									 ВнешнийНаборДанныхНачислениеСМРХозСпособом);
	
	
КонецПроцедуры // ПолучитьНаборДанныхНачисление()

// Выполняет запрос к базе данных для получения набора данных, необходимого для формирования расшифровки по
//	вычету НДС по приобретенным ценностям
//
// Параметры
//  Запрос  - Запрос - запрос к базе данных
//  СтруктураВнешнихНаборов - структура внешних источников данных для расшифровки.
//
// Возвращаемое значение:
//   Структура   - структура параметров для формирования карты отчета
//
Процедура ПолучитьНаборДанныхВычетПоПриобретеннымЦенностям(Запрос, СтруктураВнешнихНаборов) 
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА СоставКнигиПокупок.ИсправленныйСчетФактура = НЕОПРЕДЕЛЕНО
	|			ТОГДА СоставКнигиПокупок.СчетФактура
	|		ИНАЧЕ СоставКнигиПокупок.ИсправленныйСчетФактура
	|	КОНЕЦ КАК ДокументРегистратор,
	|	СоставКнигиПокупок.СтавкаНДС,
	|	СУММА(СоставКнигиПокупок.СуммаБезНДСОборот) КАК СуммаБезНДСОборот,
	|	СУММА(СоставКнигиПокупок.НДСОборот) КАК НДСОборот,
	|	СоставКнигиПокупок.ЗаписьДополнительногоЛиста,
	|	СоставКнигиПокупок.НомерСтроки,
	|	СоставКнигиПокупок.Событие,
	|	СоставКнигиПокупок.ВидЦенности
	|ПОМЕСТИТЬ ВТСоставКнигиПокупок
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПокупок.Обороты(
	|			&НачалоПериода,
	|			&КонецПериодаГраница,
	|			Запись,
	|			Организация В (&Организация)
	|				И НЕ ВидЦенности В (&ВидыЦенностейАвансыВсе, &ВидыЦенностейНалоговыйАгент)
	|				И Событие <> ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПокупки.ПредъявленНДСКВычету0)
	|				И НЕ ЗаписьДополнительногоЛиста) КАК СоставКнигиПокупок
	|
	|СГРУППИРОВАТЬ ПО
	|	ВЫБОР
	|		КОГДА СоставКнигиПокупок.ИсправленныйСчетФактура = НЕОПРЕДЕЛЕНО
	|			ТОГДА СоставКнигиПокупок.СчетФактура
	|		ИНАЧЕ СоставКнигиПокупок.ИсправленныйСчетФактура
	|	КОНЕЦ,
	|	СоставКнигиПокупок.ЗаписьДополнительногоЛиста,
	|	СоставКнигиПокупок.НомерСтроки,
	|	СоставКнигиПокупок.СтавкаНДС,
	|	СоставКнигиПокупок.Событие,
	|	СоставКнигиПокупок.ВидЦенности
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА СоставКнигиПокупок.ИсправленныйСчетФактура = НЕОПРЕДЕЛЕНО
	|			ТОГДА СоставКнигиПокупок.СчетФактура
	|		ИНАЧЕ СоставКнигиПокупок.ИсправленныйСчетФактура
	|	КОНЕЦ,
	|	СоставКнигиПокупок.СтавкаНДС,
	|	СУММА(СоставКнигиПокупок.СуммаБезНДСОборот),
	|	СУММА(СоставКнигиПокупок.НДСОборот),
	|	СоставКнигиПокупок.ЗаписьДополнительногоЛиста,
	|	СоставКнигиПокупок.НомерСтроки,
	|	СоставКнигиПокупок.Событие,
	|	СоставКнигиПокупок.ВидЦенности
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПокупок.Обороты(
	|			&НачалоПериода,
	|			,
	|			Запись,
	|			Организация В (&Организация)
	|				И НЕ ВидЦенности В (&ВидыЦенностейАвансыВсе, &ВидыЦенностейНалоговыйАгент)
	|				И Событие <> ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПокупки.ПредъявленНДСКВычету0)
	|				И (ЗаписьДополнительногоЛиста
	|					И (КорректируемыйПериод МЕЖДУ &НачалоПериода И &КонецПериода))) КАК СоставКнигиПокупок
	|
	|СГРУППИРОВАТЬ ПО
	|	СоставКнигиПокупок.СтавкаНДС,
	|	СоставКнигиПокупок.ЗаписьДополнительногоЛиста,
	|	СоставКнигиПокупок.НомерСтроки,
	|	ВЫБОР
	|		КОГДА СоставКнигиПокупок.ИсправленныйСчетФактура = НЕОПРЕДЕЛЕНО
	|			ТОГДА СоставКнигиПокупок.СчетФактура
	|		ИНАЧЕ СоставКнигиПокупок.ИсправленныйСчетФактура
	|	КОНЕЦ,
	|	СоставКнигиПокупок.Событие,
	|	СоставКнигиПокупок.ВидЦенности
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументРегистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СчетаФактурыДокументы.СчетФактура КАК СчетФактура,
	|	СчетаФактурыДокументы.ССылка КАК ССылка,
	|	СчетаФактурыДокументы.ДокументОснование КАК ДокументОснование,
	|	СчетаФактурыДокументы.СчетФактураПроведен КАК СчетФактураПроведен,
	|	СчетаФактурыДокументы.СчетФактураДата КАК СчетФактураДата,
	|	СчетаФактурыДокументы.НомерВходящегоСчетаФактуры,
	|	СчетаФактурыДокументы.ДатаВходящегоСчетаФактуры
	|ПОМЕСТИТЬ ВременнаяТаблицаСчетФактураПолученный
	|ИЗ
	|	(ВЫБРАТЬ
	|		СчетФактураПолученный.Ссылка КАК СчетФактура,
	|		СчетФактураПолученный.Ссылка КАК ССылка,
	|		СчетФактураПолученный.Ссылка КАК ДокументОснование,
	|		СчетФактураПолученный.Проведен КАК СчетФактураПроведен,
	|		СчетФактураПолученный.Дата КАК СчетФактураДата,
	|		СчетФактураПолученный.НомерВходящегоДокумента КАК НомерВходящегоСчетаФактуры,
	|		СчетФактураПолученный.ДатаВходящегоДокумента КАК ДатаВходящегоСчетаФактуры
	|	ИЗ
	|		Документ.СчетФактураПолученный КАК СчетФактураПолученный
	|	ГДЕ
	|		СчетФактураПолученный.Ссылка.Проведен
	|		И НЕ СчетФактураПолученный.Ссылка.ПометкаУдаления
	|		И СчетФактураПолученный.Ссылка.Организация В(&Организация)
	|		И СчетФактураПолученный.Ссылка В
	|				(ВЫБРАТЬ
	|					ВТСоставКнигиПокупок.ДокументРегистратор
	|				ИЗ
	|					ВТСоставКнигиПокупок)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СчетФактураПолученный.ДокументОснование,
	|		СчетФактураПолученный.Ссылка,
	|		СчетФактураПолученный.ДокументОснование,
	|		СчетФактураПолученный.Ссылка.Проведен,
	|		СчетФактураПолученный.Ссылка.Дата,
	|		СчетФактураПолученный.Ссылка.НомерВходящегоДокумента,
	|		СчетФактураПолученный.Ссылка.ДатаВходящегоДокумента
	|	ИЗ
	|		Документ.СчетФактураПолученный.ДокументыОснования КАК СчетФактураПолученный
	|	ГДЕ
	|		СчетФактураПолученный.Ссылка.Проведен
	|		И НЕ СчетФактураПолученный.Ссылка.ПометкаУдаления
	|		И СчетФактураПолученный.Ссылка.Организация В(&Организация)
	|		И СчетФактураПолученный.ДокументОснование В
	|				(ВЫБРАТЬ
	|					ВТСоставКнигиПокупок.ДокументРегистратор
	|				ИЗ
	|					ВТСоставКнигиПокупок)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВозвратТоваровОтПокупателя.Ссылка,
	|		ВозвратТоваровОтПокупателя.Ссылка,
	|		ВозвратТоваровОтПокупателя.Ссылка,
	|		ВозвратТоваровОтПокупателя.Проведен,
	|		ВозвратТоваровОтПокупателя.Дата,
	|		ВозвратТоваровОтПокупателя.НомерРасходногоКассовогоОрдера,
	|		ВозвратТоваровОтПокупателя.ДатаРасходногоКассовогоОрдера
	|	ИЗ
	|		Документ.ВозвратТоваровОтПокупателя КАК ВозвратТоваровОтПокупателя
	|	ГДЕ
	|		ВозвратТоваровОтПокупателя.Сделка ССЫЛКА Документ.ОтчетОРозничныхПродажах
	|		И ВозвратТоваровОтПокупателя.НомерРасходногоКассовогоОрдера <> """"
	|		И ВозвратТоваровОтПокупателя.ДатаРасходногоКассовогоОрдера <> ДАТАВРЕМЯ(1, 1, 1)
	|		И ВозвратТоваровОтПокупателя.Проведен
	|		И НЕ ВозвратТоваровОтПокупателя.ПометкаУдаления
	|		И ВозвратТоваровОтПокупателя.Организация В(&Организация)
	|		И ВозвратТоваровОтПокупателя.Ссылка В
	|				(ВЫБРАТЬ
	|					ВТСоставКнигиПокупок.ДокументРегистратор
	|				ИЗ
	|					ВТСоставКнигиПокупок)) КАК СчетаФактурыДокументы
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СчетФактураВыданный.Ссылка КАК СчетФактура,
	|	СчетФактураВыданный.Ссылка КАК Ссылка,
	|	СчетФактураВыданный.Ссылка КАК ДокументОснование,
	|	СчетФактураВыданный.ДокументОснование КАК СчетФактураДокументОснование,
	|	СчетФактураВыданный.Проведен КАК СчетФактураПроведен,
	|	СчетФактураВыданный.Дата КАК ДатаСчетаФактуры,
	|	СчетФактураВыданный.Номер КАК НомерСчетаФактуры
	|ПОМЕСТИТЬ ВременнаяТаблицаСчетФактураВыданный
	|ИЗ
	|	Документ.СчетФактураВыданный КАК СчетФактураВыданный
	|ГДЕ
	|	СчетФактураВыданный.Ссылка В
	|			(ВЫБРАТЬ
	|				ВТСоставКнигиПокупок.ДокументРегистратор
	|			ИЗ
	|				ВТСоставКнигиПокупок)
	|	И (СчетФактураВыданный.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.НаРеализацию)
	|			ИЛИ СчетФактураВыданный.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.Корректировочный))
	|	И СчетФактураВыданный.ДокументОснование ССЫЛКА Документ.КорректировкаРеализации
	|	И СчетФактураВыданный.Проведен
	|	И НЕ СчетФактураВыданный.ПометкаУдаления
	|	И СчетФактураВыданный.Организация В(&Организация)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СчетФактураВыданный.ДокументОснование,
	|	СчетФактураВыданный.Ссылка,
	|	СчетФактураВыданный.ДокументОснование,
	|	СчетФактураВыданный.Ссылка.ДокументОснование,
	|	СчетФактураВыданный.Ссылка.Проведен,
	|	СчетФактураВыданный.Ссылка.Дата,
	|	СчетФактураВыданный.Ссылка.Номер
	|ИЗ
	|	Документ.СчетФактураВыданный.ДокументыОснования КАК СчетФактураВыданный
	|ГДЕ
	|	СчетФактураВыданный.ДокументОснование В
	|			(ВЫБРАТЬ
	|				ВТСоставКнигиПокупок.ДокументРегистратор
	|			ИЗ
	|				ВТСоставКнигиПокупок)
	|	И (СчетФактураВыданный.Ссылка.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.НаРеализацию)
	|			ИЛИ СчетФактураВыданный.Ссылка.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.Корректировочный))
	|	И СчетФактураВыданный.ДокументОснование ССЫЛКА Документ.КорректировкаРеализации
	|	И СчетФактураВыданный.Ссылка.Проведен
	|	И НЕ СчетФактураВыданный.Ссылка.ПометкаУдаления
	|	И СчетФактураВыданный.Ссылка.Организация В(&Организация)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТСоставКнигиПокупок.ДокументРегистратор,
	|	ВТСоставКнигиПокупок.СтавкаНДС,
	|	ВТСоставКнигиПокупок.СуммаБезНДСОборот КАК Сумма,
	|	ВТСоставКнигиПокупок.НДСОборот КАК СуммаНДС,
	|	&ТекстБезОшибок КАК ПризнакНаличияОшибок,
	|	&ТекстНетСноски КАК СимволСноски,
	|	ВЫБОР
	|		КОГДА ВТСоставКнигиПокупок.ДокументРегистратор ССЫЛКА Документ.СчетФактураВыданный
	|				ИЛИ ВТСоставКнигиПокупок.ДокументРегистратор ССЫЛКА Документ.ГТДИмпорт
	|				ИЛИ ВТСоставКнигиПокупок.ДокументРегистратор ССЫЛКА Документ.ОтчетОРозничныхПродажах
	|			ТОГДА ВТСоставКнигиПокупок.ДокументРегистратор
	|		КОГДА ВТСоставКнигиПокупок.ДокументРегистратор ССЫЛКА Документ.ОтчетКомиссионераОПродажах
	|			ТОГДА СчетФактураПолученныйДокументыОснования.ССылка
	|		ИНАЧЕ ЕСТЬNULL(СчетФактураПолученныйДокументыОснования.ССылка, СчетФактураВыданныйДокументыОснования.Ссылка)
	|	КОНЕЦ КАК СчетФактура,
	|	&ТекстОтраженыВКнигеПокупок КАК ПризнакОтраженияНДС,
	|	1 КАК КоличествоДокументов,
	|	ВЫБОР
	|		КОГДА ВТСоставКнигиПокупок.ДокументРегистратор ССЫЛКА Документ.СчетФактураВыданный
	|				ИЛИ ВТСоставКнигиПокупок.ДокументРегистратор ССЫЛКА Документ.ГТДИмпорт
	|				ИЛИ ВТСоставКнигиПокупок.ДокументРегистратор ССЫЛКА Документ.ОтчетОРозничныхПродажах
	|			ТОГДА ДанныеПервичныхДокументов.НомерРегистратора
	|		КОГДА ВТСоставКнигиПокупок.ДокументРегистратор ССЫЛКА Документ.ОтчетКомиссионераОПродажах
	|			ТОГДА СчетФактураПолученныйДокументыОснования.НомерВходящегоСчетаФактуры
	|		ИНАЧЕ ЕСТЬNULL(СчетФактураПолученныйДокументыОснования.НомерВходящегоСчетаФактуры, СчетФактураВыданныйДокументыОснования.НомерСчетаФактуры)
	|	КОНЕЦ КАК НомерВходящегоДокумента,
	|	ВЫБОР
	|		КОГДА ВТСоставКнигиПокупок.ДокументРегистратор ССЫЛКА Документ.СчетФактураВыданный
	|				ИЛИ ВТСоставКнигиПокупок.ДокументРегистратор ССЫЛКА Документ.ГТДИмпорт
	|				ИЛИ ВТСоставКнигиПокупок.ДокументРегистратор ССЫЛКА Документ.ОтчетОРозничныхПродажах
	|			ТОГДА ДанныеПервичныхДокументов.ДатаРегистратора
	|		КОГДА ВТСоставКнигиПокупок.ДокументРегистратор ССЫЛКА Документ.ОтчетКомиссионераОПродажах
	|			ТОГДА СчетФактураПолученныйДокументыОснования.ДатаВходящегоСчетаФактуры
	|		ИНАЧЕ ЕСТЬNULL(СчетФактураПолученныйДокументыОснования.ДатаВходящегоСчетаФактуры, СчетФактураВыданныйДокументыОснования.ДатаСчетаФактуры)
	|	КОНЕЦ КАК ДатаВходящегоДокумента,
	|	0 КАК КоличествоОшибок,
	|	ВТСоставКнигиПокупок.ЗаписьДополнительногоЛиста,
	|	ВТСоставКнигиПокупок.НомерСтроки,
	|	ВТСоставКнигиПокупок.Событие,
	|	ВТСоставКнигиПокупок.ВидЦенности
	|ИЗ
	|	ВТСоставКнигиПокупок КАК ВТСоставКнигиПокупок
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаСчетФактураПолученный КАК СчетФактураПолученныйДокументыОснования
	|		ПО ВТСоставКнигиПокупок.ДокументРегистратор = СчетФактураПолученныйДокументыОснования.ДокументОснование
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаСчетФактураВыданный КАК СчетФактураВыданныйДокументыОснования
	|		ПО ВТСоставКнигиПокупок.ДокументРегистратор = СчетФактураВыданныйДокументыОснования.ДокументОснование
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
	|		ПО (ДанныеПервичныхДокументов.Организация В (&Организация))
	|			И ВТСоставКнигиПокупок.ДокументРегистратор = ДанныеПервичныхДокументов.Документ";

	ВычетПоПриобретеннымЦенностямВключенные = Запрос.Выполнить();
	
	Запрос.Текст = 
	"УНИЧТОЖИТЬ ВременнаяТаблицаСчетФактураПолученный
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВременнаяТаблицаСчетФактураВыданный
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА НДСПредъявленныйОстатки.ИсправленныйСчетФактура = НЕОПРЕДЕЛЕНО
	|			ТОГДА НДСПредъявленныйОстатки.СчетФактура
	|		ИНАЧЕ НДСПредъявленныйОстатки.ИсправленныйСчетФактура
	|	КОНЕЦ КАК ДокументОснование,
	|	НДСПредъявленныйОстатки.СтавкаНДС КАК СтавкаНДС,
	|	НДСПредъявленныйОстатки.ВидЦенности КАК ВидЦенности,
	|	НДСПредъявленныйОстатки.СчетУчетаНДС КАК СчетУчетаНДС,
	|	НДСПредъявленныйОстатки.СуммаБезНДСОстаток,
	|	НДСПредъявленныйОстатки.НДСОстаток,
	|	НДСПредъявленныйОстатки.СчетФактура
	|ПОМЕСТИТЬ НДСПредъявленныйОстаткиПоПриобретеннымЦенностям
	|ИЗ
	|	РегистрНакопления.НДСПредъявленный.Остатки(
	|			&НачалоСледующегоПериода,
	|			Организация В (&Организация)
	|				И НЕ ВидЦенности В (&ВидыЦенностейАвансыВсе)) КАК НДСПредъявленныйОстатки
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументОснование,
	|	ВидЦенности,
	|	СтавкаНДС,
	|	СчетУчетаНДС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	КнигаПокупок.СчетФактура КАК ЗаявлениеОВвозе
	|ПОМЕСТИТЬ КнигаПокупокВЧастиЗаявленийОВвозе
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПокупок КАК КнигаПокупок
	|ГДЕ
	|	КнигаПокупок.Организация В(&Организация)
	|	И КнигаПокупок.Период <= &КонецПериода
	|	И КнигаПокупок.ВидЦенности = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ТаможенныйСоюз)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	КнигаПокупок.СчетФактура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДСпоОСиНМАОстатки.СчетФактура КАК СчетФактура,
	|	НДСпоОСиНМАОстатки.СтавкаНДС КАК СтавкаНДС,
	|	НДСпоОСиНМАОстатки.ВидЦенности КАК ВидЦенности,
	|	НДСпоОСиНМАОстатки.СчетУчетаНДС КАК СчетУчетаНДС,
	|	НДСпоОСиНМАОстатки.СуммаБезНДСОстаток,
	|	НДСпоОСиНМАОстатки.НДСОстаток
	|ПОМЕСТИТЬ НДСпоОСиНМАОстаткиПоПриобретеннымЦенностям
	|ИЗ
	|	РегистрНакопления.НДСпоОСиНМА.Остатки(
	|			&НачалоСледующегоПериода,
	|			Организация В (&Организация)
	|				И НДСВключенВСтоимость = ЛОЖЬ
	|				И НЕ ВидЦенности В (&ВидыЦенностейАвансыВсе)) КАК НДСпоОСиНМАОстатки
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактура,
	|	ВидЦенности,
	|	СтавкаНДС,
	|	СчетУчетаНДС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДСПредъявленныйРеализация0Остатки.СчетФактура КАК СчетФактура,
	|	НДСПредъявленныйРеализация0Остатки.СтавкаНДС КАК СтавкаНДС,
	|	НДСПредъявленныйРеализация0Остатки.ВидЦенности КАК ВидЦенности,
	|	НДСПредъявленныйРеализация0Остатки.СчетУчетаНДС КАК СчетУчетаНДС,
	|	НДСПредъявленныйРеализация0Остатки.СуммаБезНДСОстаток,
	|	НДСПредъявленныйРеализация0Остатки.НДСОстаток
	|ПОМЕСТИТЬ НДСПредъявленныйРеализация0ОстаткиПоПриобретеннымЦенностям
	|ИЗ
	|	РегистрНакопления.НДСПредъявленныйРеализация0.Остатки(
	|			&НачалоСледующегоПериода,
	|			Организация В (&Организация)
	|				И НЕ ВидЦенности В (&ВидыЦенностейАвансыВсе)) КАК НДСПредъявленныйРеализация0Остатки
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактура,
	|	ВидЦенности,
	|	СтавкаНДС,
	|	СчетУчетаНДС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СчетаФактурыДокументы.СчетФактура КАК СчетФактура,
	|	СчетаФактурыДокументы.ССылка КАК ССылка,
	|	СчетаФактурыДокументы.ДокументОснование КАК ДокументОснование,
	|	СчетаФактурыДокументы.СчетФактураПроведен КАК СчетФактураПроведен,
	|	СчетаФактурыДокументы.СчетФактураДата КАК СчетФактураДата,
	|	СчетаФактурыДокументы.НомерВходящегоСчетаФактуры,
	|	СчетаФактурыДокументы.ДатаВходящегоСчетаФактуры
	|ПОМЕСТИТЬ ВременнаяТаблицаСчетФактураПолученный
	|ИЗ
	|	(ВЫБРАТЬ
	|		СчетФактураПолученный.Ссылка КАК СчетФактура,
	|		СчетФактураПолученный.Ссылка КАК ССылка,
	|		СчетФактураПолученный.Ссылка КАК ДокументОснование,
	|		СчетФактураПолученный.Проведен КАК СчетФактураПроведен,
	|		СчетФактураПолученный.Дата КАК СчетФактураДата,
	|		СчетФактураПолученный.НомерВходящегоДокумента КАК НомерВходящегоСчетаФактуры,
	|		СчетФактураПолученный.ДатаВходящегоДокумента КАК ДатаВходящегоСчетаФактуры
	|	ИЗ
	|		Документ.СчетФактураПолученный КАК СчетФактураПолученный
	|	ГДЕ
	|		СчетФактураПолученный.Ссылка.Проведен
	|		И НЕ СчетФактураПолученный.Ссылка.ПометкаУдаления
	|		И СчетФактураПолученный.Ссылка.Организация В(&Организация)
	|		И СчетФактураПолученный.Ссылка В
	|				(ВЫБРАТЬ
	|					НДСПредъявленныйОстаткиПоПриобретеннымЦенностям.ДокументОснование
	|				ИЗ
	|					НДСПредъявленныйОстаткиПоПриобретеннымЦенностям)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СчетФактураПолученный.ДокументОснование,
	|		СчетФактураПолученный.Ссылка,
	|		СчетФактураПолученный.ДокументОснование,
	|		СчетФактураПолученный.Ссылка.Проведен,
	|		СчетФактураПолученный.Ссылка.Дата,
	|		СчетФактураПолученный.Ссылка.НомерВходящегоДокумента,
	|		СчетФактураПолученный.Ссылка.ДатаВходящегоДокумента
	|	ИЗ
	|		Документ.СчетФактураПолученный.ДокументыОснования КАК СчетФактураПолученный
	|	ГДЕ
	|		СчетФактураПолученный.Ссылка.Проведен
	|		И НЕ СчетФактураПолученный.Ссылка.ПометкаУдаления
	|		И СчетФактураПолученный.Ссылка.Организация В(&Организация)
	|		И СчетФактураПолученный.ДокументОснование В
	|				(ВЫБРАТЬ
	|					НДСПредъявленныйОстаткиПоПриобретеннымЦенностям.ДокументОснование
	|				ИЗ
	|					НДСПредъявленныйОстаткиПоПриобретеннымЦенностям)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВозвратТоваровОтПокупателя.Ссылка,
	|		ВозвратТоваровОтПокупателя.Ссылка,
	|		ВозвратТоваровОтПокупателя.Ссылка,
	|		ВозвратТоваровОтПокупателя.Проведен,
	|		ВозвратТоваровОтПокупателя.Дата,
	|		ВозвратТоваровОтПокупателя.НомерРасходногоКассовогоОрдера,
	|		ВозвратТоваровОтПокупателя.ДатаРасходногоКассовогоОрдера
	|	ИЗ
	|		Документ.ВозвратТоваровОтПокупателя КАК ВозвратТоваровОтПокупателя
	|	ГДЕ
	|		ВозвратТоваровОтПокупателя.Сделка ССЫЛКА Документ.ОтчетОРозничныхПродажах
	|		И ВозвратТоваровОтПокупателя.НомерРасходногоКассовогоОрдера <> """"
	|		И ВозвратТоваровОтПокупателя.ДатаРасходногоКассовогоОрдера <> ДАТАВРЕМЯ(1, 1, 1)
	|		И ВозвратТоваровОтПокупателя.Проведен
	|		И НЕ ВозвратТоваровОтПокупателя.ПометкаУдаления
	|		И ВозвратТоваровОтПокупателя.Организация В(&Организация)
	|		И ВозвратТоваровОтПокупателя.Ссылка В
	|				(ВЫБРАТЬ
	|					НДСПредъявленныйОстаткиПоПриобретеннымЦенностям.ДокументОснование
	|				ИЗ
	|					НДСПредъявленныйОстаткиПоПриобретеннымЦенностям)) КАК СчетаФактурыДокументы
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СчетФактураВыданный.Ссылка КАК СчетФактура,
	|	СчетФактураВыданный.Ссылка КАК Ссылка,
	|	СчетФактураВыданный.Ссылка КАК ДокументОснование,
	|	СчетФактураВыданный.ДокументОснование КАК СчетФактураДокументОснование,
	|	СчетФактураВыданный.Проведен КАК СчетФактураПроведен,
	|	СчетФактураВыданный.Дата КАК ДатаСчетаФактуры,
	|	СчетФактураВыданный.Номер КАК НомерСчетаФактуры
	|ПОМЕСТИТЬ ВременнаяТаблицаСчетФактураВыданный
	|ИЗ
	|	Документ.СчетФактураВыданный КАК СчетФактураВыданный
	|ГДЕ
	|	СчетФактураВыданный.Ссылка В
	|			(ВЫБРАТЬ
	|				НДСПредъявленныйОстаткиПоПриобретеннымЦенностям.ДокументОснование
	|			ИЗ
	|				НДСПредъявленныйОстаткиПоПриобретеннымЦенностям)
	|	И (СчетФактураВыданный.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.НаРеализацию)
	|			ИЛИ СчетФактураВыданный.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.Корректировочный))
	|	И СчетФактураВыданный.ДокументОснование ССЫЛКА Документ.КорректировкаРеализации
	|	И СчетФактураВыданный.Проведен
	|	И НЕ СчетФактураВыданный.ПометкаУдаления
	|	И СчетФактураВыданный.Организация В(&Организация)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СчетФактураВыданный.ДокументОснование,
	|	СчетФактураВыданный.Ссылка,
	|	СчетФактураВыданный.ДокументОснование,
	|	СчетФактураВыданный.Ссылка.ДокументОснование,
	|	СчетФактураВыданный.Ссылка.Проведен,
	|	СчетФактураВыданный.Ссылка.Дата,
	|	СчетФактураВыданный.Ссылка.Номер
	|ИЗ
	|	Документ.СчетФактураВыданный.ДокументыОснования КАК СчетФактураВыданный
	|ГДЕ
	|	СчетФактураВыданный.ДокументОснование В
	|			(ВЫБРАТЬ
	|				НДСПредъявленныйОстаткиПоПриобретеннымЦенностям.ДокументОснование
	|			ИЗ
	|				НДСПредъявленныйОстаткиПоПриобретеннымЦенностям)
	|	И (СчетФактураВыданный.Ссылка.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.НаРеализацию)
	|			ИЛИ СчетФактураВыданный.Ссылка.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.Корректировочный))
	|	И СчетФактураВыданный.ДокументОснование ССЫЛКА Документ.КорректировкаРеализации
	|	И СчетФактураВыданный.Ссылка.Проведен
	|	И НЕ СчетФактураВыданный.Ссылка.ПометкаУдаления
	|	И СчетФактураВыданный.Ссылка.Организация В(&Организация)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДСПредъявленныйОстатки.ДокументОснование КАК ДокументРегистратор,
	|	НДСПредъявленныйОстатки.СтавкаНДС,
	|	СУММА(ЕСТЬNULL(НДСПредъявленныйОстатки.СуммаБезНДСОстаток, 0) - ЕСТЬNULL(НДСПредъявленныйРеализация0Остатки.СуммаБезНДСОстаток, 0)) КАК Сумма,
	|	СУММА(ЕСТЬNULL(НДСПредъявленныйОстатки.НДСОстаток, 0) - ЕСТЬNULL(НДСПредъявленныйРеализация0Остатки.НДСОстаток, 0)) КАК СуммаНДС,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(НДСПредъявленныйОстатки.СуммаБезНДСОстаток, 0) + ЕСТЬNULL(НДСПредъявленныйОстатки.НДСОстаток, 0) - ЕСТЬNULL(НДСпоОСиНМАОстатки.СуммаБезНДСОстаток, 0) - ЕСТЬNULL(НДСпоОСиНМАОстатки.НДСОстаток, 0) = 0
	|				И НДСПредъявленныйОстатки.ВидЦенности = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ОС)
	|			ТОГДА &ТекстОбъектНеПринятКУчету
	|		КОГДА ЕСТЬNULL(НДСПредъявленныйОстатки.СуммаБезНДСОстаток, 0) + ЕСТЬNULL(НДСПредъявленныйОстатки.НДСОстаток, 0) - ЕСТЬNULL(НДСпоОСиНМАОстатки.СуммаБезНДСОстаток, 0) - ЕСТЬNULL(НДСпоОСиНМАОстатки.НДСОстаток, 0) = 0
	|				И НДСПредъявленныйОстатки.ВидЦенности = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.СМРСобственнымиСилами)
	|			ТОГДА &ТекстОбъектНеПринятКУчетуОбъектСтроительства
	|		КОГДА ЕСТЬNULL(НДСПредъявленныйОстатки.СуммаБезНДСОстаток, 0) + ЕСТЬNULL(НДСПредъявленныйОстатки.НДСОстаток, 0) - ЕСТЬNULL(НДСПредъявленныйРеализация0Остатки.СуммаБезНДСОстаток, 0) - ЕСТЬNULL(НДСПредъявленныйРеализация0Остатки.НДСОстаток, 0) = 0
	|			ТОГДА &ТекстНеПодтвержденаСтавка
	|		ИНАЧЕ ВЫБОР
	|				КОГДА НДСПредъявленныйОстатки.ДокументОснование ССЫЛКА Документ.ЗаявлениеОВвозеТоваров
	|					ТОГДА ВЫБОР
	|							КОГДА КнигаПокупокВЧастиЗаявленийОВвозе.ЗаявлениеОВвозе ЕСТЬ NULL 
	|								ТОГДА &ТекстНеПодтвержденаОплатаВБюджет
	|							ИНАЧЕ &ТекстВычетНеОтраженВКнигеПокупок
	|						КОНЕЦ
	|				КОГДА ВЫБОР
	|							КОГДА НДСПредъявленныйОстатки.ДокументОснование ССЫЛКА Документ.СчетФактураВыданный
	|									ИЛИ НДСПредъявленныйОстатки.ДокументОснование ССЫЛКА Документ.ГТДИмпорт
	|									ИЛИ НДСПредъявленныйОстатки.ДокументОснование ССЫЛКА Документ.ОтчетОРозничныхПродажах
	|								ТОГДА НДСПредъявленныйОстатки.ДокументОснование
	|							КОГДА НДСПредъявленныйОстатки.ДокументОснование ССЫЛКА Документ.ОтчетКомиссионераОПродажах
	|								ТОГДА СчетФактураПолученныйДокументыОснования.ССылка
	|							ИНАЧЕ ЕСТЬNULL(СчетФактураПолученныйДокументыОснования.ССылка, СчетФактураВыданныйДокументыОснования.Ссылка)
	|						КОНЕЦ ЕСТЬ NULL 
	|						ИЛИ ВЫБОР
	|							КОГДА НДСПредъявленныйОстатки.ДокументОснование ССЫЛКА Документ.СчетФактураВыданный
	|									ИЛИ НДСПредъявленныйОстатки.ДокументОснование ССЫЛКА Документ.ГТДИмпорт
	|									ИЛИ НДСПредъявленныйОстатки.ДокументОснование ССЫЛКА Документ.ОтчетОРозничныхПродажах
	|								ТОГДА ДанныеПервичныхДокументов.ДатаРегистратора
	|							КОГДА НДСПредъявленныйОстатки.ДокументОснование ССЫЛКА Документ.ОтчетКомиссионераОПродажах
	|								ТОГДА СчетФактураПолученныйДокументыОснования.ДатаВходящегоСчетаФактуры
	|							ИНАЧЕ ЕСТЬNULL(СчетФактураПолученныйДокументыОснования.ДатаВходящегоСчетаФактуры, СчетФактураВыданныйДокументыОснования.ДатаСчетаФактуры)
	|						КОНЕЦ > &КонецПериода
	|					ТОГДА &ТекстОтсутствуетСФПолученный
	|				КОГДА НЕ ВЫБОР
	|							КОГДА НДСПредъявленныйОстатки.ДокументОснование ССЫЛКА Документ.СчетФактураВыданный
	|									ИЛИ НДСПредъявленныйОстатки.ДокументОснование ССЫЛКА Документ.ГТДИмпорт
	|									ИЛИ НДСПредъявленныйОстатки.ДокументОснование ССЫЛКА Документ.ОтчетОРозничныхПродажах
	|								ТОГДА НДСПредъявленныйОстатки.ДокументОснование.Проведен
	|							КОГДА НДСПредъявленныйОстатки.ДокументОснование ССЫЛКА Документ.ОтчетКомиссионераОПродажах
	|								ТОГДА СчетФактураПолученныйДокументыОснования.СчетФактураПроведен
	|							ИНАЧЕ ЕСТЬNULL(СчетФактураПолученныйДокументыОснования.СчетФактураПроведен, СчетФактураВыданныйДокументыОснования.СчетФактураПроведен)
	|						КОНЕЦ
	|					ТОГДА &ТекстСФПолученныйНеПроведен
	|				ИНАЧЕ ВЫБОР
	|						КОГДА ЕСТЬNULL(НДСПредъявленныйОстатки.НДСОстаток, 0) >= 0
	|							ТОГДА &ТекстВычетНеОтраженВКнигеПокупок
	|						ИНАЧЕ &ТекстКорректировкаКнигиПокупок
	|					КОНЕЦ
	|			КОНЕЦ
	|	КОНЕЦ КАК ПризнакНаличияОшибок,
	|	&ТекстНеОтраженыВКнигеПокупок КАК ПризнакОтраженияНДС,
	|	1 КАК КоличествоДокументов,
	|	ВЫБОР
	|		КОГДА НДСПредъявленныйОстатки.ДокументОснование ССЫЛКА Документ.СчетФактураВыданный
	|				ИЛИ НДСПредъявленныйОстатки.ДокументОснование ССЫЛКА Документ.ГТДИмпорт
	|				ИЛИ НДСПредъявленныйОстатки.ДокументОснование ССЫЛКА Документ.ОтчетОРозничныхПродажах
	|			ТОГДА НДСПредъявленныйОстатки.ДокументОснование
	|		КОГДА НДСПредъявленныйОстатки.ДокументОснование ССЫЛКА Документ.ОтчетКомиссионераОПродажах
	|			ТОГДА СчетФактураПолученныйДокументыОснования.ССылка
	|		ИНАЧЕ ЕСТЬNULL(СчетФактураПолученныйДокументыОснования.ССылка, СчетФактураВыданныйДокументыОснования.Ссылка)
	|	КОНЕЦ КАК СчетФактура,
	|	ВЫБОР
	|		КОГДА НДСПредъявленныйОстатки.ДокументОснование ССЫЛКА Документ.СчетФактураВыданный
	|				ИЛИ НДСПредъявленныйОстатки.ДокументОснование ССЫЛКА Документ.ГТДИмпорт
	|				ИЛИ НДСПредъявленныйОстатки.ДокументОснование ССЫЛКА Документ.ОтчетОРозничныхПродажах
	|			ТОГДА ДанныеПервичныхДокументов.НомерРегистратора
	|		КОГДА НДСПредъявленныйОстатки.ДокументОснование ССЫЛКА Документ.ОтчетКомиссионераОПродажах
	|			ТОГДА СчетФактураПолученныйДокументыОснования.НомерВходящегоСчетаФактуры
	|		ИНАЧЕ ЕСТЬNULL(СчетФактураПолученныйДокументыОснования.НомерВходящегоСчетаФактуры, СчетФактураВыданныйДокументыОснования.НомерСчетаФактуры)
	|	КОНЕЦ КАК НомерВходящегоДокумента,
	|	ВЫБОР
	|		КОГДА НДСПредъявленныйОстатки.ДокументОснование ССЫЛКА Документ.СчетФактураВыданный
	|				ИЛИ НДСПредъявленныйОстатки.ДокументОснование ССЫЛКА Документ.ГТДИмпорт
	|				ИЛИ НДСПредъявленныйОстатки.ДокументОснование ССЫЛКА Документ.ОтчетОРозничныхПродажах
	|			ТОГДА ДанныеПервичныхДокументов.ДатаРегистратора
	|		КОГДА НДСПредъявленныйОстатки.ДокументОснование ССЫЛКА Документ.ОтчетКомиссионераОПродажах
	|			ТОГДА СчетФактураПолученныйДокументыОснования.ДатаВходящегоСчетаФактуры
	|		ИНАЧЕ ЕСТЬNULL(СчетФактураПолученныйДокументыОснования.ДатаВходящегоСчетаФактуры, СчетФактураВыданныйДокументыОснования.ДатаСчетаФактуры)
	|	КОНЕЦ КАК ДатаВходящегоДокумента
	|ИЗ
	|	НДСПредъявленныйОстаткиПоПриобретеннымЦенностям КАК НДСПредъявленныйОстатки
	|		ЛЕВОЕ СОЕДИНЕНИЕ НДСпоОСиНМАОстаткиПоПриобретеннымЦенностям КАК НДСпоОСиНМАОстатки
	|		ПО НДСПредъявленныйОстатки.СчетФактура = НДСпоОСиНМАОстатки.СчетФактура
	|			И НДСПредъявленныйОстатки.ВидЦенности = НДСпоОСиНМАОстатки.ВидЦенности
	|			И НДСПредъявленныйОстатки.СтавкаНДС = НДСпоОСиНМАОстатки.СтавкаНДС
	|			И НДСПредъявленныйОстатки.СчетУчетаНДС = НДСпоОСиНМАОстатки.СчетУчетаНДС
	|			И (НДСпоОСиНМАОстатки.СуммаБезНДСОстаток + НДСпоОСиНМАОстатки.НДСОстаток > 0)
	|		ЛЕВОЕ СОЕДИНЕНИЕ НДСПредъявленныйРеализация0ОстаткиПоПриобретеннымЦенностям КАК НДСПредъявленныйРеализация0Остатки
	|		ПО НДСПредъявленныйОстатки.СчетФактура = НДСПредъявленныйРеализация0Остатки.СчетФактура
	|			И НДСПредъявленныйОстатки.ВидЦенности = НДСПредъявленныйРеализация0Остатки.ВидЦенности
	|			И НДСПредъявленныйОстатки.СтавкаНДС = НДСПредъявленныйРеализация0Остатки.СтавкаНДС
	|			И НДСПредъявленныйОстатки.СчетУчетаНДС = НДСПредъявленныйРеализация0Остатки.СчетУчетаНДС
	|			И (НДСПредъявленныйРеализация0Остатки.СуммаБезНДСОстаток + НДСПредъявленныйРеализация0Остатки.НДСОстаток > 0)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаСчетФактураПолученный КАК СчетФактураПолученныйДокументыОснования
	|		ПО (НДСПредъявленныйОстатки.ДокументОснование = СчетФактураПолученныйДокументыОснования.ДокументОснование
	|				ИЛИ НДСПредъявленныйОстатки.ДокументОснование = СчетФактураПолученныйДокументыОснования.ССылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаСчетФактураВыданный КАК СчетФактураВыданныйДокументыОснования
	|		ПО НДСПредъявленныйОстатки.ДокументОснование = СчетФактураВыданныйДокументыОснования.ДокументОснование
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
	|		ПО (ДанныеПервичныхДокументов.Организация В (&Организация))
	|			И НДСПредъявленныйОстатки.ДокументОснование = ДанныеПервичныхДокументов.Документ
	|		ЛЕВОЕ СОЕДИНЕНИЕ КнигаПокупокВЧастиЗаявленийОВвозе КАК КнигаПокупокВЧастиЗаявленийОВвозе
	|		ПО НДСПредъявленныйОстатки.СчетФактура = КнигаПокупокВЧастиЗаявленийОВвозе.ЗаявлениеОВвозе
	|ГДЕ
	|	(НДСПредъявленныйОстатки.СуммаБезНДСОстаток > 0
	|			ИЛИ НДСПредъявленныйОстатки.НДСОстаток > 0)
	|	И ЕСТЬNULL(НДСПредъявленныйОстатки.СуммаБезНДСОстаток, 0) + ЕСТЬNULL(НДСПредъявленныйОстатки.НДСОстаток, 0) - ЕСТЬNULL(НДСПредъявленныйРеализация0Остатки.СуммаБезНДСОстаток, 0) - ЕСТЬNULL(НДСПредъявленныйРеализация0Остатки.НДСОстаток, 0) > 0
	|
	|СГРУППИРОВАТЬ ПО
	|	НДСПредъявленныйОстатки.ДокументОснование,
	|	НДСПредъявленныйОстатки.СтавкаНДС,
	|	ВЫБОР
	|		КОГДА НДСПредъявленныйОстатки.ДокументОснование ССЫЛКА Документ.СчетФактураВыданный
	|				ИЛИ НДСПредъявленныйОстатки.ДокументОснование ССЫЛКА Документ.ГТДИмпорт
	|				ИЛИ НДСПредъявленныйОстатки.ДокументОснование ССЫЛКА Документ.ОтчетОРозничныхПродажах
	|			ТОГДА НДСПредъявленныйОстатки.ДокументОснование
	|		КОГДА НДСПредъявленныйОстатки.ДокументОснование ССЫЛКА Документ.ОтчетКомиссионераОПродажах
	|			ТОГДА СчетФактураПолученныйДокументыОснования.ССылка
	|		ИНАЧЕ ЕСТЬNULL(СчетФактураПолученныйДокументыОснования.ССылка, СчетФактураВыданныйДокументыОснования.Ссылка)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА НДСПредъявленныйОстатки.ДокументОснование ССЫЛКА Документ.СчетФактураВыданный
	|				ИЛИ НДСПредъявленныйОстатки.ДокументОснование ССЫЛКА Документ.ГТДИмпорт
	|				ИЛИ НДСПредъявленныйОстатки.ДокументОснование ССЫЛКА Документ.ОтчетОРозничныхПродажах
	|			ТОГДА ДанныеПервичныхДокументов.НомерРегистратора
	|		КОГДА НДСПредъявленныйОстатки.ДокументОснование ССЫЛКА Документ.ОтчетКомиссионераОПродажах
	|			ТОГДА СчетФактураПолученныйДокументыОснования.НомерВходящегоСчетаФактуры
	|		ИНАЧЕ ЕСТЬNULL(СчетФактураПолученныйДокументыОснования.НомерВходящегоСчетаФактуры, СчетФактураВыданныйДокументыОснования.НомерСчетаФактуры)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА НДСПредъявленныйОстатки.ДокументОснование ССЫЛКА Документ.СчетФактураВыданный
	|				ИЛИ НДСПредъявленныйОстатки.ДокументОснование ССЫЛКА Документ.ГТДИмпорт
	|				ИЛИ НДСПредъявленныйОстатки.ДокументОснование ССЫЛКА Документ.ОтчетОРозничныхПродажах
	|			ТОГДА ДанныеПервичныхДокументов.ДатаРегистратора
	|		КОГДА НДСПредъявленныйОстатки.ДокументОснование ССЫЛКА Документ.ОтчетКомиссионераОПродажах
	|			ТОГДА СчетФактураПолученныйДокументыОснования.ДатаВходящегоСчетаФактуры
	|		ИНАЧЕ ЕСТЬNULL(СчетФактураПолученныйДокументыОснования.ДатаВходящегоСчетаФактуры, СчетФактураВыданныйДокументыОснования.ДатаСчетаФактуры)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(НДСПредъявленныйОстатки.СуммаБезНДСОстаток, 0) + ЕСТЬNULL(НДСПредъявленныйОстатки.НДСОстаток, 0) - ЕСТЬNULL(НДСпоОСиНМАОстатки.СуммаБезНДСОстаток, 0) - ЕСТЬNULL(НДСпоОСиНМАОстатки.НДСОстаток, 0) = 0
	|				И НДСПредъявленныйОстатки.ВидЦенности = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ОС)
	|			ТОГДА &ТекстОбъектНеПринятКУчету
	|		КОГДА ЕСТЬNULL(НДСПредъявленныйОстатки.СуммаБезНДСОстаток, 0) + ЕСТЬNULL(НДСПредъявленныйОстатки.НДСОстаток, 0) - ЕСТЬNULL(НДСпоОСиНМАОстатки.СуммаБезНДСОстаток, 0) - ЕСТЬNULL(НДСпоОСиНМАОстатки.НДСОстаток, 0) = 0
	|				И НДСПредъявленныйОстатки.ВидЦенности = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.СМРСобственнымиСилами)
	|			ТОГДА &ТекстОбъектНеПринятКУчетуОбъектСтроительства
	|		КОГДА ЕСТЬNULL(НДСПредъявленныйОстатки.СуммаБезНДСОстаток, 0) + ЕСТЬNULL(НДСПредъявленныйОстатки.НДСОстаток, 0) - ЕСТЬNULL(НДСПредъявленныйРеализация0Остатки.СуммаБезНДСОстаток, 0) - ЕСТЬNULL(НДСПредъявленныйРеализация0Остатки.НДСОстаток, 0) = 0
	|			ТОГДА &ТекстНеПодтвержденаСтавка
	|		ИНАЧЕ ВЫБОР
	|				КОГДА НДСПредъявленныйОстатки.ДокументОснование ССЫЛКА Документ.ЗаявлениеОВвозеТоваров
	|					ТОГДА ВЫБОР
	|							КОГДА КнигаПокупокВЧастиЗаявленийОВвозе.ЗаявлениеОВвозе ЕСТЬ NULL 
	|								ТОГДА &ТекстНеПодтвержденаОплатаВБюджет
	|							ИНАЧЕ &ТекстВычетНеОтраженВКнигеПокупок
	|						КОНЕЦ
	|				КОГДА ВЫБОР
	|							КОГДА НДСПредъявленныйОстатки.ДокументОснование ССЫЛКА Документ.СчетФактураВыданный
	|									ИЛИ НДСПредъявленныйОстатки.ДокументОснование ССЫЛКА Документ.ГТДИмпорт
	|									ИЛИ НДСПредъявленныйОстатки.ДокументОснование ССЫЛКА Документ.ОтчетОРозничныхПродажах
	|								ТОГДА НДСПредъявленныйОстатки.ДокументОснование
	|							КОГДА НДСПредъявленныйОстатки.ДокументОснование ССЫЛКА Документ.ОтчетКомиссионераОПродажах
	|								ТОГДА СчетФактураПолученныйДокументыОснования.ССылка
	|							ИНАЧЕ ЕСТЬNULL(СчетФактураПолученныйДокументыОснования.ССылка, СчетФактураВыданныйДокументыОснования.Ссылка)
	|						КОНЕЦ ЕСТЬ NULL 
	|						ИЛИ ВЫБОР
	|							КОГДА НДСПредъявленныйОстатки.ДокументОснование ССЫЛКА Документ.СчетФактураВыданный
	|									ИЛИ НДСПредъявленныйОстатки.ДокументОснование ССЫЛКА Документ.ГТДИмпорт
	|									ИЛИ НДСПредъявленныйОстатки.ДокументОснование ССЫЛКА Документ.ОтчетОРозничныхПродажах
	|								ТОГДА ДанныеПервичныхДокументов.ДатаРегистратора
	|							КОГДА НДСПредъявленныйОстатки.ДокументОснование ССЫЛКА Документ.ОтчетКомиссионераОПродажах
	|								ТОГДА СчетФактураПолученныйДокументыОснования.ДатаВходящегоСчетаФактуры
	|							ИНАЧЕ ЕСТЬNULL(СчетФактураПолученныйДокументыОснования.ДатаВходящегоСчетаФактуры, СчетФактураВыданныйДокументыОснования.ДатаСчетаФактуры)
	|						КОНЕЦ > &КонецПериода
	|					ТОГДА &ТекстОтсутствуетСФПолученный
	|				КОГДА НЕ ВЫБОР
	|							КОГДА НДСПредъявленныйОстатки.ДокументОснование ССЫЛКА Документ.СчетФактураВыданный
	|									ИЛИ НДСПредъявленныйОстатки.ДокументОснование ССЫЛКА Документ.ГТДИмпорт
	|									ИЛИ НДСПредъявленныйОстатки.ДокументОснование ССЫЛКА Документ.ОтчетОРозничныхПродажах
	|								ТОГДА НДСПредъявленныйОстатки.ДокументОснование.Проведен
	|							КОГДА НДСПредъявленныйОстатки.ДокументОснование ССЫЛКА Документ.ОтчетКомиссионераОПродажах
	|								ТОГДА СчетФактураПолученныйДокументыОснования.СчетФактураПроведен
	|							ИНАЧЕ ЕСТЬNULL(СчетФактураПолученныйДокументыОснования.СчетФактураПроведен, СчетФактураВыданныйДокументыОснования.СчетФактураПроведен)
	|						КОНЕЦ
	|					ТОГДА &ТекстСФПолученныйНеПроведен
	|				ИНАЧЕ ВЫБОР
	|						КОГДА ЕСТЬNULL(НДСПредъявленныйОстатки.НДСОстаток, 0) >= 0
	|							ТОГДА &ТекстВычетНеОтраженВКнигеПокупок
	|						ИНАЧЕ &ТекстКорректировкаКнигиПокупок
	|					КОНЕЦ
	|			КОНЕЦ
	|	КОНЕЦ";
				
	ВычетПоПриобретеннымЦенностямНеВключенныеРегламенты = Запрос.Выполнить();

	Запрос.Текст =
	"ВЫБРАТЬ
	|	НДСВключенныйВСтоимость.СчетФактура КАК СчетФактура,
	|	НДСВключенныйВСтоимость.СуммаБезНДС КАК СуммаБезНДСОборот,
	|	НДСВключенныйВСтоимость.НДС КАК НДСОборот,
	|	НДСВключенныйВСтоимость.СтавкаНДС,
	|	НДСВключенныйВСтоимость.Событие,
	|	НДСВключенныйВСтоимость.ВидЦенности
	|ПОМЕСТИТЬ ВТНДСВключенныйВСтоимость
	|ИЗ
	|	РегистрНакопления.НДСПредъявленный КАК НДСВключенныйВСтоимость
	|ГДЕ
	|	НДСВключенныйВСтоимость.Организация В(&Организация)
	|	И НДСВключенныйВСтоимость.Период <= &КонецПериода
	|	И НДСВключенныйВСтоимость.Событие = ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПокупки.НДСВключенВСтоимость)
	|	И НДСВключенныйВСтоимость.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	АвансовыйОтчет.Ссылка КАК ДокументРегистратор,
	|	ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС18_118) КАК СтавкаНДС,
	|	ВЫРАЗИТЬ(АвансовыйОтчет.СуммаДокумента * 18 / 118 КАК ЧИСЛО(15, 2)) КАК СуммаНДС,
	|	ВЫРАЗИТЬ(АвансовыйОтчет.СуммаДокумента - АвансовыйОтчет.СуммаДокумента * 18 / 118 КАК ЧИСЛО(15, 2)) КАК Сумма,
	|	1 КАК КоличествоДокументов,
	|	&ТекстНДСВключенВСтоимость КАК ПризнакНаличияОшибок,
	|	&ТекстНеОтраженыВКнигеПокупок КАК ПризнакОтраженияНДС
	|ПОМЕСТИТЬ ВТДокументы
	|ИЗ
	|	Документ.АвансовыйОтчет КАК АвансовыйОтчет
	|ГДЕ
	|	АвансовыйОтчет.Ссылка В
	|			(ВЫБРАТЬ
	|				НДСВключенныйВСтоимость.СчетФактура
	|			ИЗ
	|				ВТНДСВключенныйВСтоимость КАК НДСВключенныйВСтоимость)
	|	И АвансовыйОтчет.Проведен
	|	И АвансовыйОтчет.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|	И АвансовыйОтчет.Организация В(&Организация)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПоступлениеДопРасходов.Ссылка,
	|	ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС18_118),
	|	ВЫРАЗИТЬ(ПоступлениеДопРасходов.СуммаДокумента * 18 / 118 КАК ЧИСЛО(15, 2)),
	|	ВЫРАЗИТЬ(ПоступлениеДопРасходов.СуммаДокумента - ПоступлениеДопРасходов.СуммаДокумента * 18 / 118 КАК ЧИСЛО(15, 2)),
	|	1,
	|	&ТекстНДСВключенВСтоимость,
	|	&ТекстНеОтраженыВКнигеПокупок
	|ИЗ
	|	Документ.ПоступлениеДопРасходов КАК ПоступлениеДопРасходов
	|ГДЕ
	|	ПоступлениеДопРасходов.Ссылка В
	|			(ВЫБРАТЬ
	|				НДСВключенныйВСтоимость.СчетФактура
	|			ИЗ
	|				ВТНДСВключенныйВСтоимость КАК НДСВключенныйВСтоимость)
	|	И ПоступлениеДопРасходов.Проведен
	|	И ПоступлениеДопРасходов.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|	И ПоступлениеДопРасходов.Организация В(&Организация)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПоступлениеНМА.Ссылка,
	|	ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС18_118),
	|	ВЫРАЗИТЬ(ПоступлениеНМА.СуммаДокумента * 18 / 118 КАК ЧИСЛО(15, 2)),
	|	ВЫРАЗИТЬ(ПоступлениеНМА.СуммаДокумента - ПоступлениеНМА.СуммаДокумента * 18 / 118 КАК ЧИСЛО(15, 2)),
	|	1,
	|	&ТекстНДСВключенВСтоимость,
	|	&ТекстНеОтраженыВКнигеПокупок
	|ИЗ
	|	Документ.ПоступлениеНМА КАК ПоступлениеНМА
	|ГДЕ
	|	ПоступлениеНМА.Ссылка В
	|			(ВЫБРАТЬ
	|				НДСВключенныйВСтоимость.СчетФактура
	|			ИЗ
	|				ВТНДСВключенныйВСтоимость КАК НДСВключенныйВСтоимость)
	|	И ПоступлениеНМА.Проведен
	|	И ПоступлениеНМА.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|	И ПоступлениеНМА.Организация В(&Организация)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПоступлениеТоваровУслуг.Ссылка,
	|	ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС18_118),
	|	ВЫРАЗИТЬ(ПоступлениеТоваровУслуг.СуммаДокумента * 18 / 118 КАК ЧИСЛО(15, 2)),
	|	ВЫРАЗИТЬ(ПоступлениеТоваровУслуг.СуммаДокумента - ПоступлениеТоваровУслуг.СуммаДокумента * 18 / 118 КАК ЧИСЛО(15, 2)),
	|	1,
	|	&ТекстНДСВключенВСтоимость,
	|	&ТекстНеОтраженыВКнигеПокупок
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг КАК ПоступлениеТоваровУслуг
	|ГДЕ
	|	ПоступлениеТоваровУслуг.Ссылка В
	|			(ВЫБРАТЬ
	|				НДСВключенныйВСтоимость.СчетФактура
	|			ИЗ
	|				ВТНДСВключенныйВСтоимость КАК НДСВключенныйВСтоимость)
	|	И ПоступлениеТоваровУслуг.Проведен
	|	И ПоступлениеТоваровУслуг.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|	И ПоступлениеТоваровУслуг.Организация В(&Организация)
	|	И ПоступлениеТоваровУслуг.ВидОперации <> ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеТоваровУслуг.ВПереработку)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПоступлениеИзПереработки.Ссылка,
	|	ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС18_118),
	|	ВЫРАЗИТЬ(ПоступлениеИзПереработки.СуммаДокумента * 18 / 118 КАК ЧИСЛО(15, 2)),
	|	ВЫРАЗИТЬ(ПоступлениеИзПереработки.СуммаДокумента - ПоступлениеИзПереработки.СуммаДокумента * 18 / 118 КАК ЧИСЛО(15, 2)),
	|	1,
	|	&ТекстНДСВключенВСтоимость,
	|	&ТекстНеОтраженыВКнигеПокупок
	|ИЗ
	|	Документ.ПоступлениеИзПереработки КАК ПоступлениеИзПереработки
	|ГДЕ
	|	ПоступлениеИзПереработки.Ссылка В
	|			(ВЫБРАТЬ
	|				НДСВключенныйВСтоимость.СчетФактура
	|			ИЗ
	|				ВТНДСВключенныйВСтоимость КАК НДСВключенныйВСтоимость)
	|	И ПоступлениеИзПереработки.Проведен
	|	И ПоступлениеИзПереработки.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|	И ПоступлениеИзПереработки.Организация В(&Организация)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументРегистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТДокументы.ДокументРегистратор КАК ДокументРегистратор,
	|	ЕСТЬNULL(ВТНДСВключенныйВСтоимость.СтавкаНДС, ВТДокументы.СтавкаНДС) КАК СтавкаНДС,
	|	ЕСТЬNULL(ВТНДСВключенныйВСтоимость.НДСОборот, ВТДокументы.СуммаНДС) КАК СуммаНДС,
	|	ЕСТЬNULL(ВТНДСВключенныйВСтоимость.СуммаБезНДСОборот, ВТДокументы.Сумма) КАК Сумма,
	|	ВТДокументы.КоличествоДокументов КАК КоличествоДокументов,
	|	ВТДокументы.ПризнакНаличияОшибок,
	|	ВТДокументы.ПризнакОтраженияНДС КАК ПризнакОтраженияНДС,
	|	СчетФактураПолученный.Ссылка КАК СчетФактура,
	|	СчетФактураПолученный.Ссылка.ДатаВходящегоДокумента КАК ДатаВходящегоДокумента,
	|	СчетФактураПолученный.Ссылка.НомерВходящегоДокумента КАК НомерВходящегоДокумента,
	|	ВТНДСВключенныйВСтоимость.Событие,
	|	ВТНДСВключенныйВСтоимость.ВидЦенности
	|ИЗ
	|	ВТДокументы КАК ВТДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНДСВключенныйВСтоимость КАК ВТНДСВключенныйВСтоимость
	|		ПО ВТДокументы.ДокументРегистратор = ВТНДСВключенныйВСтоимость.СчетФактура
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураПолученный.ДокументыОснования КАК СчетФактураПолученный
	|		ПО (СчетФактураПолученный.ДокументОснование = ВТДокументы.ДокументРегистратор)
	|			И (НЕ(СчетФактураПолученный.Ссылка.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыПолученного.НаАванс)
	|					ИЛИ СчетФактураПолученный.Ссылка.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыПолученного.НаАвансКомитента)))";
	
	ВычетПоПриобретеннымЦенностямНеВключенныеПервичка = Запрос.Выполнить();
					 
	ВнешнийНаборДанныхВычетПриобретенныеЦенности = Новый Структура;
	
	ВнешнийНаборДанныхВычетПриобретенныеЦенности.Вставить("ВычетПоПриобретеннымЦенностямВключенные", 
		ВычетПоПриобретеннымЦенностямВключенные);
	ВнешнийНаборДанныхВычетПриобретенныеЦенности.Вставить("ВычетПоПриобретеннымЦенностямНеВключенныеРегламенты", 
		ВычетПоПриобретеннымЦенностямНеВключенныеРегламенты);
	ВнешнийНаборДанныхВычетПриобретенныеЦенности.Вставить("ВычетПоПриобретеннымЦенностямНеВключенныеПервичка", 
		ВычетПоПриобретеннымЦенностямНеВключенныеПервичка);
	
	СтруктураВнешнихНаборов.Вставить("ВнешнийНаборДанныхВычетПриобретенныеЦенности", 
									 ВнешнийНаборДанныхВычетПриобретенныеЦенности);
	
КонецПроцедуры // ПолучитьНаборДанныхВычетПоПриобретеннымЦенностям()


// Выполняет запрос к базе данных для получения набора данных, необходимого для формирования расшифровки по
//	вычету НДС по приобретенным ценностям
//
// Параметры
//  Запрос  - Запрос - запрос к базе данных
//  СтруктураВнешнихНаборов - структура внешних источников данных для расшифровки.
//
// Возвращаемое значение:
//   Структура   - структура параметров для формирования карты отчета
//
Процедура ПолучитьНаборДанныхВычетПоПриобретеннымЦенностямРасшифровка130СтрокиДекларации(Запрос, СтруктураВнешнихНаборов) 
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА СоставКнигиПокупок.ИсправленныйСчетФактура = НЕОПРЕДЕЛЕНО
	|			ТОГДА СоставКнигиПокупок.СчетФактура
	|		ИНАЧЕ СоставКнигиПокупок.ИсправленныйСчетФактура
	|	КОНЕЦ КАК ДокументРегистратор,
	|	СоставКнигиПокупок.СтавкаНДС,
	|	СУММА(СоставКнигиПокупок.СуммаБезНДСОборот) КАК СуммаБезНДСОборот,
	|	СУММА(СоставКнигиПокупок.НДСОборот) КАК НДСОборот,
	|	СоставКнигиПокупок.ЗаписьДополнительногоЛиста,
	|	СоставКнигиПокупок.НомерСтроки,
	|	СоставКнигиПокупок.Событие,
	|	СоставКнигиПокупок.ВидЦенности
	|ПОМЕСТИТЬ ВТСоставКнигиПокупок
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПокупок.Обороты(
	|			&НачалоПериода,
	|			&КонецПериодаГраница,
	|			Запись,
	|			Организация В (&Организация)
	|				И Событие <> ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПокупки.ПредъявленНДСКВычету0)
	|				И НЕ ЗаписьДополнительногоЛиста) КАК СоставКнигиПокупок
	|
	|СГРУППИРОВАТЬ ПО
	|	ВЫБОР
	|		КОГДА СоставКнигиПокупок.ИсправленныйСчетФактура = НЕОПРЕДЕЛЕНО
	|			ТОГДА СоставКнигиПокупок.СчетФактура
	|		ИНАЧЕ СоставКнигиПокупок.ИсправленныйСчетФактура
	|	КОНЕЦ,
	|	СоставКнигиПокупок.ЗаписьДополнительногоЛиста,
	|	СоставКнигиПокупок.НомерСтроки,
	|	СоставКнигиПокупок.СтавкаНДС,
	|	СоставКнигиПокупок.Событие,
	|	СоставКнигиПокупок.ВидЦенности
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА СоставКнигиПокупок.ИсправленныйСчетФактура = НЕОПРЕДЕЛЕНО
	|			ТОГДА СоставКнигиПокупок.СчетФактура
	|		ИНАЧЕ СоставКнигиПокупок.ИсправленныйСчетФактура
	|	КОНЕЦ,
	|	СоставКнигиПокупок.СтавкаНДС,
	|	СУММА(СоставКнигиПокупок.СуммаБезНДСОборот),
	|	СУММА(СоставКнигиПокупок.НДСОборот),
	|	СоставКнигиПокупок.ЗаписьДополнительногоЛиста,
	|	СоставКнигиПокупок.НомерСтроки,
	|	СоставКнигиПокупок.Событие,
	|	СоставКнигиПокупок.ВидЦенности
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПокупок.Обороты(
	|			&НачалоПериода,
	|			,
	|			Запись,
	|			Организация В (&Организация)
	|				И Событие <> ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПокупки.ПредъявленНДСКВычету0)
	|				И (ЗаписьДополнительногоЛиста
	|					И (КорректируемыйПериод МЕЖДУ &НачалоПериода И &КонецПериода))) КАК СоставКнигиПокупок
	|
	|СГРУППИРОВАТЬ ПО
	|	СоставКнигиПокупок.СтавкаНДС,
	|	СоставКнигиПокупок.ЗаписьДополнительногоЛиста,
	|	СоставКнигиПокупок.НомерСтроки,
	|	ВЫБОР
	|		КОГДА СоставКнигиПокупок.ИсправленныйСчетФактура = НЕОПРЕДЕЛЕНО
	|			ТОГДА СоставКнигиПокупок.СчетФактура
	|		ИНАЧЕ СоставКнигиПокупок.ИсправленныйСчетФактура
	|	КОНЕЦ,
	|	СоставКнигиПокупок.Событие,
	|	СоставКнигиПокупок.ВидЦенности
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументРегистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СчетаФактурыДокументы.СчетФактура КАК СчетФактура,
	|	СчетаФактурыДокументы.ССылка КАК ССылка,
	|	СчетаФактурыДокументы.ДокументОснование КАК ДокументОснование,
	|	СчетаФактурыДокументы.СчетФактураПроведен КАК СчетФактураПроведен,
	|	СчетаФактурыДокументы.СчетФактураДата КАК СчетФактураДата,
	|	СчетаФактурыДокументы.НомерВходящегоСчетаФактуры,
	|	СчетаФактурыДокументы.ДатаВходящегоСчетаФактуры
	|ПОМЕСТИТЬ ВременнаяТаблицаСчетФактураПолученный
	|ИЗ
	|	(ВЫБРАТЬ
	|		СчетФактураПолученный.Ссылка КАК СчетФактура,
	|		СчетФактураПолученный.Ссылка КАК ССылка,
	|		СчетФактураПолученный.Ссылка КАК ДокументОснование,
	|		СчетФактураПолученный.Проведен КАК СчетФактураПроведен,
	|		СчетФактураПолученный.Дата КАК СчетФактураДата,
	|		СчетФактураПолученный.НомерВходящегоДокумента КАК НомерВходящегоСчетаФактуры,
	|		СчетФактураПолученный.ДатаВходящегоДокумента КАК ДатаВходящегоСчетаФактуры
	|	ИЗ
	|		Документ.СчетФактураПолученный КАК СчетФактураПолученный
	|	ГДЕ
	|		СчетФактураПолученный.Ссылка.Проведен
	|		И НЕ СчетФактураПолученный.Ссылка.ПометкаУдаления
	|		И СчетФактураПолученный.Ссылка.Организация В(&Организация)
	|		И СчетФактураПолученный.Ссылка В
	|				(ВЫБРАТЬ
	|					ВТСоставКнигиПокупок.ДокументРегистратор
	|				ИЗ
	|					ВТСоставКнигиПокупок)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СчетФактураПолученный.ДокументОснование,
	|		СчетФактураПолученный.Ссылка,
	|		СчетФактураПолученный.ДокументОснование,
	|		СчетФактураПолученный.Ссылка.Проведен,
	|		СчетФактураПолученный.Ссылка.Дата,
	|		СчетФактураПолученный.Ссылка.НомерВходящегоДокумента,
	|		СчетФактураПолученный.Ссылка.ДатаВходящегоДокумента
	|	ИЗ
	|		Документ.СчетФактураПолученный.ДокументыОснования КАК СчетФактураПолученный
	|	ГДЕ
	|		СчетФактураПолученный.Ссылка.Проведен
	|		И НЕ СчетФактураПолученный.Ссылка.ПометкаУдаления
	|		И СчетФактураПолученный.Ссылка.Организация В(&Организация)
	|		И СчетФактураПолученный.ДокументОснование В
	|				(ВЫБРАТЬ
	|					ВТСоставКнигиПокупок.ДокументРегистратор
	|				ИЗ
	|					ВТСоставКнигиПокупок)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВозвратТоваровОтПокупателя.Ссылка,
	|		ВозвратТоваровОтПокупателя.Ссылка,
	|		ВозвратТоваровОтПокупателя.Ссылка,
	|		ВозвратТоваровОтПокупателя.Проведен,
	|		ВозвратТоваровОтПокупателя.Дата,
	|		ВозвратТоваровОтПокупателя.НомерРасходногоКассовогоОрдера,
	|		ВозвратТоваровОтПокупателя.ДатаРасходногоКассовогоОрдера
	|	ИЗ
	|		Документ.ВозвратТоваровОтПокупателя КАК ВозвратТоваровОтПокупателя
	|	ГДЕ
	|		ВозвратТоваровОтПокупателя.Сделка ССЫЛКА Документ.ОтчетОРозничныхПродажах
	|		И ВозвратТоваровОтПокупателя.НомерРасходногоКассовогоОрдера <> """"
	|		И ВозвратТоваровОтПокупателя.ДатаРасходногоКассовогоОрдера <> ДАТАВРЕМЯ(1, 1, 1)
	|		И ВозвратТоваровОтПокупателя.Проведен
	|		И НЕ ВозвратТоваровОтПокупателя.ПометкаУдаления
	|		И ВозвратТоваровОтПокупателя.Организация В(&Организация)
	|		И ВозвратТоваровОтПокупателя.Ссылка В
	|				(ВЫБРАТЬ
	|					ВТСоставКнигиПокупок.ДокументРегистратор
	|				ИЗ
	|					ВТСоставКнигиПокупок)) КАК СчетаФактурыДокументы
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СчетФактураВыданный.Ссылка КАК СчетФактура,
	|	СчетФактураВыданный.Ссылка КАК Ссылка,
	|	СчетФактураВыданный.Ссылка КАК ДокументОснование,
	|	СчетФактураВыданный.ДокументОснование КАК СчетФактураДокументОснование,
	|	СчетФактураВыданный.Проведен КАК СчетФактураПроведен,
	|	СчетФактураВыданный.Дата КАК ДатаСчетаФактуры,
	|	СчетФактураВыданный.Номер КАК НомерСчетаФактуры
	|ПОМЕСТИТЬ ВременнаяТаблицаСчетФактураВыданный
	|ИЗ
	|	Документ.СчетФактураВыданный КАК СчетФактураВыданный
	|ГДЕ
	|	СчетФактураВыданный.Ссылка В
	|			(ВЫБРАТЬ
	|				ВТСоставКнигиПокупок.ДокументРегистратор
	|			ИЗ
	|				ВТСоставКнигиПокупок)
	|	И СчетФактураВыданный.Проведен
	|	И НЕ СчетФактураВыданный.ПометкаУдаления
	|	И СчетФактураВыданный.Организация В(&Организация)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СчетФактураВыданный.ДокументОснование,
	|	СчетФактураВыданный.Ссылка,
	|	СчетФактураВыданный.ДокументОснование,
	|	СчетФактураВыданный.Ссылка.ДокументОснование,
	|	СчетФактураВыданный.Ссылка.Проведен,
	|	СчетФактураВыданный.Ссылка.Дата,
	|	СчетФактураВыданный.Ссылка.Номер
	|ИЗ
	|	Документ.СчетФактураВыданный.ДокументыОснования КАК СчетФактураВыданный
	|ГДЕ
	|	СчетФактураВыданный.ДокументОснование В
	|			(ВЫБРАТЬ
	|				ВТСоставКнигиПокупок.ДокументРегистратор
	|			ИЗ
	|				ВТСоставКнигиПокупок)
	|	И СчетФактураВыданный.Ссылка.Проведен
	|	И НЕ СчетФактураВыданный.Ссылка.ПометкаУдаления
	|	И СчетФактураВыданный.Ссылка.Организация В(&Организация)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТСоставКнигиПокупок.ДокументРегистратор,
	|	ВТСоставКнигиПокупок.СтавкаНДС,
	|	ВТСоставКнигиПокупок.СуммаБезНДСОборот КАК Сумма,
	|	ВТСоставКнигиПокупок.НДСОборот КАК СуммаНДС,
	|	&ТекстБезОшибок КАК ПризнакНаличияОшибок,
	|	&ТекстНетСноски КАК СимволСноски,
	|	ВЫБОР
	|		КОГДА ВТСоставКнигиПокупок.ДокументРегистратор ССЫЛКА Документ.СчетФактураВыданный
	|				ИЛИ ВТСоставКнигиПокупок.ДокументРегистратор ССЫЛКА Документ.ГТДИмпорт
	|				ИЛИ ВТСоставКнигиПокупок.ДокументРегистратор ССЫЛКА Документ.ОтчетОРозничныхПродажах
	|			ТОГДА ВТСоставКнигиПокупок.ДокументРегистратор
	|		КОГДА ВТСоставКнигиПокупок.ДокументРегистратор ССЫЛКА Документ.ОтчетКомиссионераОПродажах
	|			ТОГДА СчетФактураПолученныйДокументыОснования.ССылка
	|		ИНАЧЕ ЕСТЬNULL(СчетФактураПолученныйДокументыОснования.ССылка, СчетФактураВыданныйДокументыОснования.Ссылка)
	|	КОНЕЦ КАК СчетФактура,
	|	&ТекстОтраженыВКнигеПокупок КАК ПризнакОтраженияНДС,
	|	1 КАК КоличествоДокументов,
	|	ВЫБОР
	|		КОГДА ВТСоставКнигиПокупок.ДокументРегистратор ССЫЛКА Документ.СчетФактураВыданный
	|				ИЛИ ВТСоставКнигиПокупок.ДокументРегистратор ССЫЛКА Документ.ГТДИмпорт
	|				ИЛИ ВТСоставКнигиПокупок.ДокументРегистратор ССЫЛКА Документ.ОтчетОРозничныхПродажах
	|			ТОГДА ДанныеПервичныхДокументов.НомерРегистратора
	|		КОГДА ВТСоставКнигиПокупок.ДокументРегистратор ССЫЛКА Документ.ОтчетКомиссионераОПродажах
	|			ТОГДА СчетФактураПолученныйДокументыОснования.НомерВходящегоСчетаФактуры
	|		ИНАЧЕ ЕСТЬNULL(СчетФактураПолученныйДокументыОснования.НомерВходящегоСчетаФактуры, СчетФактураВыданныйДокументыОснования.НомерСчетаФактуры)
	|	КОНЕЦ КАК НомерВходящегоДокумента,
	|	ВЫБОР
	|		КОГДА ВТСоставКнигиПокупок.ДокументРегистратор ССЫЛКА Документ.СчетФактураВыданный
	|				ИЛИ ВТСоставКнигиПокупок.ДокументРегистратор ССЫЛКА Документ.ГТДИмпорт
	|				ИЛИ ВТСоставКнигиПокупок.ДокументРегистратор ССЫЛКА Документ.ОтчетОРозничныхПродажах
	|			ТОГДА ДанныеПервичныхДокументов.ДатаРегистратора
	|		КОГДА ВТСоставКнигиПокупок.ДокументРегистратор ССЫЛКА Документ.ОтчетКомиссионераОПродажах
	|			ТОГДА СчетФактураПолученныйДокументыОснования.ДатаВходящегоСчетаФактуры
	|		ИНАЧЕ ЕСТЬNULL(СчетФактураПолученныйДокументыОснования.ДатаВходящегоСчетаФактуры, СчетФактураВыданныйДокументыОснования.ДатаСчетаФактуры)
	|	КОНЕЦ КАК ДатаВходящегоДокумента,
	|	0 КАК КоличествоОшибок,
	|	ВТСоставКнигиПокупок.ЗаписьДополнительногоЛиста,
	|	ВТСоставКнигиПокупок.НомерСтроки,
	|	ВТСоставКнигиПокупок.Событие,
	|	ВТСоставКнигиПокупок.ВидЦенности
	|ИЗ
	|	ВТСоставКнигиПокупок КАК ВТСоставКнигиПокупок
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаСчетФактураПолученный КАК СчетФактураПолученныйДокументыОснования
	|		ПО ВТСоставКнигиПокупок.ДокументРегистратор = СчетФактураПолученныйДокументыОснования.ДокументОснование
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаСчетФактураВыданный КАК СчетФактураВыданныйДокументыОснования
	|		ПО ВТСоставКнигиПокупок.ДокументРегистратор = СчетФактураВыданныйДокументыОснования.ДокументОснование
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
	|		ПО (ДанныеПервичныхДокументов.Организация В (&Организация))
	|			И ВТСоставКнигиПокупок.ДокументРегистратор = ДанныеПервичныхДокументов.Документ";

	ВычетПоПриобретеннымЦенностямРасшифровка130СтрокиДекларации = Запрос.Выполнить();
	
	ВнешнийНаборДанныхВычетПриобретенныеЦенности = Новый Структура;
	ВнешнийНаборДанныхВычетПриобретенныеЦенности.Вставить("ВычетПоПриобретеннымЦенностямРасшифровка130СтрокиДекларации", 
		ВычетПоПриобретеннымЦенностямРасшифровка130СтрокиДекларации);
	
	СтруктураВнешнихНаборов.Вставить("ВнешнийНаборДанныхСтроки130Декларации", 
									 ВнешнийНаборДанныхВычетПриобретенныеЦенности);
	
КонецПроцедуры

// Выполняет запрос к базе данных для получения набора данных, необходимого для формирования расшифровки по
//	вычету НДС при исполнении обязанностей налогового агента
//
// Параметры
//  Запрос  - Запрос - запрос к базе данных
//  СтруктураВнешнихНаборов - структура внешних источников данных для расшифровки.
//
// Возвращаемое значение:
//   Структура   - структура параметров для формирования карты отчета
//
Процедура ПолучитьНаборДанныхВычетНалоговыйАгент(Запрос, СтруктураВнешнихНаборов) 
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НДСПредъявленный.СчетФактура КАК СчетФактура,
	|	НДСПредъявленный.СтавкаНДС КАК СтавкаНДС,
	|	НДСПредъявленный.СуммаБезНДСОстаток,
	|	НДСПредъявленный.НДСОстаток,
	|	НДСПредъявленный.ВидЦенности КАК ВидЦенности,
	|	НДСПредъявленный.СчетУчетаНДС КАК СчетУчетаНДС,
	|	НДСПредъявленный.Поставщик,
	|	НДСПредъявленный.ДоговорКонтрагента
	|ПОМЕСТИТЬ НДСПредъявленный
	|ИЗ
	|	РегистрНакопления.НДСПредъявленный.Остатки(
	|			&НачалоСледующегоПериода,
	|			Организация В (&Организация)
	|				И ВидЦенности В (&ВидыЦенностейНалоговыйАгент)
	|				И ДоговорКонтрагента <> ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)) КАК НДСПредъявленный
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактура,
	|	ВидЦенности,
	|	СчетУчетаНДС,
	|	СтавкаНДС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДСпоОСиНМА.СчетФактура КАК СчетФактура,
	|	НДСпоОСиНМА.ВидЦенности КАК ВидЦенности,
	|	НДСпоОСиНМА.СтавкаНДС КАК СтавкаНДС,
	|	НДСпоОСиНМА.СчетУчетаНДС КАК СчетУчетаНДС,
	|	НДСпоОСиНМА.СуммаБезНДСОстаток,
	|	НДСпоОСиНМА.НДСОстаток
	|ПОМЕСТИТЬ НДСпоОСиНМА
	|ИЗ
	|	РегистрНакопления.НДСпоОСиНМА.Остатки(
	|			&НачалоСледующегоПериода,
	|			Организация В (&Организация)
	|				И НДСВключенВСтоимость = ЛОЖЬ
	|				И ВидЦенности В (&ВидыЦенностейНалоговыйАгент)
	|				И НеВлияетНаВычет = ЛОЖЬ
	|				И СчетФактура В
	|					(ВЫБРАТЬ
	|						НДСПредъявленный.СчетФактура
	|					ИЗ
	|						НДСПредъявленный КАК НДСПредъявленный)) КАК НДСпоОСиНМА
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактура,
	|	ВидЦенности,
	|	СтавкаНДС,
	|	СчетУчетаНДС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДСПредъявленныйРеализация0.СчетФактура КАК СчетФактура,
	|	НДСПредъявленныйРеализация0.ВидЦенности КАК ВидЦенности,
	|	НДСПредъявленныйРеализация0.СтавкаНДС КАК СтавкаНДС,
	|	НДСПредъявленныйРеализация0.СчетУчетаНДС КАК СчетУчетаНДС,
	|	НДСПредъявленныйРеализация0.СуммаБезНДСОстаток,
	|	НДСПредъявленныйРеализация0.НДСОстаток
	|ПОМЕСТИТЬ НДСПредъявленныйРеализация0
	|ИЗ
	|	РегистрНакопления.НДСПредъявленныйРеализация0.Остатки(
	|			&НачалоСледующегоПериода,
	|			Организация В (&Организация)
	|				И ВидЦенности В (&ВидыЦенностейНалоговыйАгент)
	|				И СчетФактура В
	|					(ВЫБРАТЬ
	|						НДСПредъявленный.СчетФактура
	|					ИЗ
	|						НДСПредъявленный КАК НДСПредъявленный)) КАК НДСПредъявленныйРеализация0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактура,
	|	ВидЦенности,
	|	СтавкаНДС,
	|	СчетУчетаНДС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗадолженностьПоставщику.Субконто1 КАК Субконто1,
	|	ЗадолженностьПоставщику.СуммаОстатокКт
	|ПОМЕСТИТЬ ЗадолженностьПоставщику
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(
	|			&НачалоСледующегоПериода,
	|			,
	|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ДокументыРасчетовСКонтрагентами),
	|			Организация В (&Организация)
	|				И Субконто1 В
	|					(ВЫБРАТЬ
	|						НДСПредъявленный.СчетФактура
	|					ИЗ
	|						НДСПредъявленный КАК НДСПредъявленный)) КАК ЗадолженностьПоставщику
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Субконто1
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОплатыПоставщику.Регистратор КАК Регистратор,
	|	ОплатыПоставщику.Субконто1
	|ПОМЕСТИТЬ ОплатыПоставщику
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(
	|			,
	|			&КонецПериодаГраница,
	|			Регистратор,
	|			,
	|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ДокументыРасчетовСКонтрагентами),
	|			Организация В (&Организация)
	|				И Субконто1 В
	|					(ВЫБРАТЬ
	|						НДСПредъявленный.СчетФактура
	|					ИЗ
	|						НДСПредъявленный КАК НДСПредъявленный),
	|			КорСчет В (ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетныеСчета), ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ВалютныеСчета)),
	|			) КАК ОплатыПоставщику
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗадолженностьВБюджет.Субконто1 КАК Субконто1,
	|	ЗадолженностьВБюджет.СуммаОстатокКт
	|ПОМЕСТИТЬ ЗадолженностьВБюджет
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(&НачалоСледующегоПериода, Счет = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НДСНалоговогоАгента), ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ДокументыРасчетовСКонтрагентами), Организация В (&Организация)) КАК ЗадолженностьВБюджет
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Субконто1
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОплатыВБюджет.Субконто1 КАК Субконто1,
	|	ОплатыВБюджет.Регистратор
	|ПОМЕСТИТЬ ОплатыВБюджет
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(, &КонецПериодаГраница, Регистратор, Счет = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НДСНалоговогоАгента), ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ДокументыРасчетовСКонтрагентами), Организация В (&Организация), КорСчет В (ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетныеСчета), ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ВалютныеСчета)), ) КАК ОплатыВБюджет
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Субконто1
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОплатыПоставщику.Субконто1 КАК Субконто1,
	|	ОплатыПоставщику.Регистратор КАК Регистратор,
	|	ОплатыВБюджет.Регистратор КАК ДокументОплатыВБюджет
	|ПОМЕСТИТЬ ТаблицаСвязиОплат
	|ИЗ
	|	ОплатыПоставщику КАК ОплатыПоставщику
	|		ЛЕВОЕ СОЕДИНЕНИЕ ОплатыВБюджет КАК ОплатыВБюджет
	|		ПО ОплатыПоставщику.Регистратор = ОплатыВБюджет.Субконто1
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Субконто1
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДСПредъявленный.СчетФактура КАК ДокументРегистратор,
	|	НДСПредъявленный.СтавкаНДС КАК СтавкаНДС,
	|	СУММА(ЕСТЬNULL(НДСПредъявленный.СуммаБезНДСОстаток, 0) - ЕСТЬNULL(НДСпоОСиНМА.СуммаБезНДСОстаток, 0) - ЕСТЬNULL(НДСПредъявленныйРеализация0.СуммаБезНДСОстаток, 0)) КАК Сумма,
	|	СУММА(ЕСТЬNULL(НДСПредъявленный.НДСОстаток, 0) - ЕСТЬNULL(НДСПредъявленныйРеализация0.НДСОстаток, 0) - ЕСТЬNULL(НДСпоОСиНМА.НДСОстаток, 0)) КАК СуммаНДС,
	|	ЗадолженностьПоставщику.СуммаОстатокКт,
	|	ТаблицаСвязиОплат.Регистратор КАК ДокументОплатыПоставщику,
	|	ТаблицаСвязиОплат.Субконто1 КАК КонтрагентОплата,
	|	НДСПредъявленный.ДоговорКонтрагента КАК ДоговорОплата,
	|	НДСПредъявленный.СчетФактура КАК ДокументОплата,
	|	ЗадолженностьВБюджет.СуммаОстатокКт КАК СуммаЗадолженностиПередБюджетом,
	|	ОснованияСчетаФактуры.Ссылка КАК СчетФактура,
	|	ТаблицаСвязиОплат.ДокументОплатыВБюджет КАК ДокументОплатыВБюджет,
	|	&ТекстНеОтраженыВКнигеПокупок КАК ПризнакОтраженияНДС,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ЗадолженностьПоставщику.СуммаОстатокКт, 0) > 0
	|			ТОГДА &ТекстПоступлениеНеОплаченоПоставщику
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ОснованияСчетаФактуры.Ссылка ЕСТЬ NULL 
	|					ТОГДА &ТекстСФНалоговогоАгентаНеВыписан
	|				ИНАЧЕ ВЫБОР
	|						КОГДА НЕ ОснованияСчетаФактуры.Ссылка.Проведен
	|							ТОГДА &ТекстСФНалоговогоАгентаНеПроведен
	|						ИНАЧЕ ВЫБОР
	|								КОГДА ЗадолженностьВБюджет.СуммаОстатокКт ЕСТЬ NULL 
	|									ТОГДА &ТекстВычетНеОтраженВКнигеПокупок
	|								ИНАЧЕ &ТекстНДСНеУплаченВБюджет
	|							КОНЕЦ
	|					КОНЕЦ
	|			КОНЕЦ
	|	КОНЕЦ КАК ПризнакНаличияОшибок,
	|	1 КАК КоличествоДокументов
	|ИЗ
	|	НДСПредъявленный КАК НДСПредъявленный
	|		ЛЕВОЕ СОЕДИНЕНИЕ НДСпоОСиНМА КАК НДСпоОСиНМА
	|		ПО НДСПредъявленный.СчетФактура = НДСпоОСиНМА.СчетФактура
	|			И НДСПредъявленный.ВидЦенности = НДСпоОСиНМА.ВидЦенности
	|			И НДСПредъявленный.СтавкаНДС = НДСпоОСиНМА.СтавкаНДС
	|			И НДСПредъявленный.СчетУчетаНДС = НДСпоОСиНМА.СчетУчетаНДС
	|			И (НДСпоОСиНМА.СуммаБезНДСОстаток + НДСпоОСиНМА.НДСОстаток > 0)
	|		ЛЕВОЕ СОЕДИНЕНИЕ НДСПредъявленныйРеализация0 КАК НДСПредъявленныйРеализация0
	|		ПО НДСПредъявленный.СчетФактура = НДСПредъявленныйРеализация0.СчетФактура
	|			И НДСПредъявленный.ВидЦенности = НДСПредъявленныйРеализация0.ВидЦенности
	|			И НДСПредъявленный.СтавкаНДС = НДСПредъявленныйРеализация0.СтавкаНДС
	|			И НДСПредъявленный.СчетУчетаНДС = НДСПредъявленныйРеализация0.СчетУчетаНДС
	|			И (НДСПредъявленныйРеализация0.СуммаБезНДСОстаток + НДСПредъявленныйРеализация0.НДСОстаток > 0)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ЗадолженностьПоставщику КАК ЗадолженностьПоставщику
	|		ПО НДСПредъявленный.СчетФактура = ЗадолженностьПоставщику.Субконто1
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаСвязиОплат КАК ТаблицаСвязиОплат
	|			ЛЕВОЕ СОЕДИНЕНИЕ ЗадолженностьВБюджет КАК ЗадолженностьВБюджет
	|			ПО ТаблицаСвязиОплат.Регистратор = ЗадолженностьВБюджет.Субконто1
	|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураВыданный.ДокументыОснования КАК ОснованияСчетаФактуры
	|			ПО ТаблицаСвязиОплат.Регистратор = ОснованияСчетаФактуры.ДокументОснование
	|		ПО НДСПредъявленный.СчетФактура = ТаблицаСвязиОплат.Субконто1
	|ГДЕ
	|	(НДСПредъявленный.СуммаБезНДСОстаток > 0
	|			ИЛИ НДСПредъявленный.НДСОстаток > 0)
	|	И ОснованияСчетаФактуры.Ссылка.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.НалоговыйАгент)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаСвязиОплат.ДокументОплатыВБюджет,
	|	НДСПредъявленный.СчетФактура,
	|	НДСПредъявленный.СтавкаНДС,
	|	ЗадолженностьПоставщику.СуммаОстатокКт,
	|	ТаблицаСвязиОплат.Регистратор,
	|	ТаблицаСвязиОплат.Субконто1,
	|	НДСПредъявленный.ДоговорКонтрагента,
	|	ЗадолженностьВБюджет.СуммаОстатокКт,
	|	ОснованияСчетаФактуры.Ссылка,
	|	НДСПредъявленный.СчетФактура,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ЗадолженностьПоставщику.СуммаОстатокКт, 0) > 0
	|			ТОГДА &ТекстПоступлениеНеОплаченоПоставщику
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ОснованияСчетаФактуры.Ссылка ЕСТЬ NULL 
	|					ТОГДА &ТекстСФНалоговогоАгентаНеВыписан
	|				ИНАЧЕ ВЫБОР
	|						КОГДА НЕ ОснованияСчетаФактуры.Ссылка.Проведен
	|							ТОГДА &ТекстСФНалоговогоАгентаНеПроведен
	|						ИНАЧЕ ВЫБОР
	|								КОГДА ЗадолженностьВБюджет.СуммаОстатокКт ЕСТЬ NULL 
	|									ТОГДА &ТекстВычетНеОтраженВКнигеПокупок
	|								ИНАЧЕ &ТекстНДСНеУплаченВБюджет
	|							КОНЕЦ
	|					КОНЕЦ
	|			КОНЕЦ
	|	КОНЕЦ
	|
	|ИМЕЮЩИЕ
	|	СУММА(ЕСТЬNULL(НДСПредъявленный.СуммаБезНДСОстаток, 0) - ЕСТЬNULL(НДСпоОСиНМА.СуммаБезНДСОстаток, 0) - ЕСТЬNULL(НДСПредъявленныйРеализация0.СуммаБезНДСОстаток, 0) + ЕСТЬNULL(НДСПредъявленный.НДСОстаток, 0) - ЕСТЬNULL(НДСПредъявленныйРеализация0.НДСОстаток, 0) - ЕСТЬNULL(НДСпоОСиНМА.НДСОстаток, 0)) > 0";
	
	ВычетПриИсполненииОНАНеВключенные = Запрос.Выполнить();
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НДСЗаписиКнигиПокупокОбороты.СчетФактура,
	|	НДСЗаписиКнигиПокупокОбороты.СтавкаНДС,
	|	НДСЗаписиКнигиПокупокОбороты.СуммаБезНДСОборот,
	|	НДСЗаписиКнигиПокупокОбороты.НДСОборот,
	|	НДСЗаписиКнигиПокупокОбороты.ДокументОплаты КАК ДокументОплаты,
	|	НДСЗаписиКнигиПокупокОбороты.Событие,
	|	НДСЗаписиКнигиПокупокОбороты.ВидЦенности,
	|	НДСЗаписиКнигиПокупокОбороты.ЗаписьДополнительногоЛиста,
	|	НДСЗаписиКнигиПокупокОбороты.НомерСтроки КАК НомерСтроки
	|ПОМЕСТИТЬ НДСЗаписиКнигиПокупокОборотыНалоговыйАгентВычеты
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПокупок.Обороты(
	|			&НачалоПериода,
	|			&КонецПериодаГраница,
	|			Запись,
	|			ВидЦенности В (&ВидыЦенностейНалоговыйАгент)
	|				И Организация В (&Организация)
	|				И Событие <> ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПокупки.ПредъявленНДСКВычету0)
	|				И НЕ ЗаписьДополнительногоЛиста) КАК НДСЗаписиКнигиПокупокОбороты
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НДСЗаписиКнигиПокупокОбороты.СчетФактура,
	|	НДСЗаписиКнигиПокупокОбороты.СтавкаНДС,
	|	НДСЗаписиКнигиПокупокОбороты.СуммаБезНДСОборот,
	|	НДСЗаписиКнигиПокупокОбороты.НДСОборот,
	|	НДСЗаписиКнигиПокупокОбороты.ДокументОплаты,
	|	НДСЗаписиКнигиПокупокОбороты.Событие,
	|	НДСЗаписиКнигиПокупокОбороты.ВидЦенности,
	|	НДСЗаписиКнигиПокупокОбороты.ЗаписьДополнительногоЛиста,
	|	НДСЗаписиКнигиПокупокОбороты.НомерСтроки
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПокупок.Обороты(
	|			&НачалоПериода,
	|			,
	|			Запись,
	|			ВидЦенности В (&ВидыЦенностейНалоговыйАгент)
	|				И Организация В (&Организация)
	|				И Событие <> ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПокупки.ПредъявленНДСКВычету0)
	|				И (ЗаписьДополнительногоЛиста
	|					И (КорректируемыйПериод МЕЖДУ &НачалоПериода И &КонецПериода))) КАК НДСЗаписиКнигиПокупокОбороты
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументОплаты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ХозрасчетныйОбороты.Субконто1 КАК Субконто1,
	|	ХозрасчетныйОбороты.Регистратор,
	|	ХозрасчетныйОбороты.СуммаОборот
	|ПОМЕСТИТЬ ХозрасчетныйОборотыОплатаНалоговыйАгентВычет
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(, &КонецПериодаГраница, Регистратор, Счет = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НДСНалоговогоАгента), ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ДокументыРасчетовСКонтрагентами), Организация В (&Организация), КорСчет В (ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетныеСчета), ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ВалютныеСчета)), ) КАК ХозрасчетныйОбороты
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Субконто1
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДСЗаписиКнигиПокупокОбороты.СчетФактура КАК ДокументРегистратор,
	|	НДСЗаписиКнигиПокупокОбороты.СтавкаНДС,
	|	ХозрасчетныйОбороты.СуммаОборот / 18 * 100 КАК Сумма,
	|	ХозрасчетныйОбороты.СуммаОборот КАК СуммаНДС,
	|	НДСЗаписиКнигиПокупокОбороты.ДокументОплаты КАК ДокументОплатыПоставщику,
	|	СчетФактураВыданныйДокументыОснования.Ссылка КАК СчетФактура,
	|	ХозрасчетныйОбороты.Регистратор КАК ДокументОплатыВБюджет,
	|	1 КАК КоличествоДокументов,
	|	&ТекстОтраженыВКнигеПокупок КАК ПризнакОтраженияНДС,
	|	&ТекстБезОшибок КАК ПризнакНаличияОшибок,
	|	НДСЗаписиКнигиПокупокОбороты.ВидЦенности,
	|	НДСЗаписиКнигиПокупокОбороты.Событие,
	|	НДСЗаписиКнигиПокупокОбороты.ЗаписьДополнительногоЛиста,
	|	НДСЗаписиКнигиПокупокОбороты.НомерСтроки
	|ИЗ
	|	НДСЗаписиКнигиПокупокОборотыНалоговыйАгентВычеты КАК НДСЗаписиКнигиПокупокОбороты
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураВыданный.ДокументыОснования КАК СчетФактураВыданныйДокументыОснования
	|		ПО НДСЗаписиКнигиПокупокОбороты.ДокументОплаты = СчетФактураВыданныйДокументыОснования.ДокументОснование
	|		ЛЕВОЕ СОЕДИНЕНИЕ ХозрасчетныйОборотыОплатаНалоговыйАгентВычет КАК ХозрасчетныйОбороты
	|		ПО НДСЗаписиКнигиПокупокОбороты.ДокументОплаты = ХозрасчетныйОбороты.Субконто1";
	
	ВычетПриИсполненииОНАВключенные = Запрос.Выполнить();
	
	ВнешнийНаборДанныхВычетИсполнениеОНА = Новый Структура;
	ВнешнийНаборДанныхВычетИсполнениеОНА.Вставить("ВычетПриИсполненииОНАНеВключенные", ВычетПриИсполненииОНАНеВключенные);
	ВнешнийНаборДанныхВычетИсполнениеОНА.Вставить("ВычетПриИсполненииОНАВключенные",   ВычетПриИсполненииОНАВключенные);
	
	СтруктураВнешнихНаборов.Вставить("ВнешнийНаборДанныхВычетИсполнениеОНА", ВнешнийНаборДанныхВычетИсполнениеОНА);
		
КонецПроцедуры // ПолучитьНаборДанныхВычетИсполнениеОНА()

//	Наборы при упрощенном учете НДС

// Выполняет запрос к базе данных для получения набора данных, необходимого для формирования расшифровки по
//	начисленному НДС при реализации в случае ведения упрощенного учета НДС
//
// Параметры
//  Запрос  - Запрос - запрос к базе данных
//  СтруктураВнешнихНаборов - структура внешних источников данных для расшифровки.
//
// Возвращаемое значение:
//   Структура   - структура параметров для формирования карты отчета
//
Процедура ПолучитьНаборДанныхНачислениеРеализацияУпрощенныйУчетНДС(Запрос, СтруктураВнешнихНаборов) 
				
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КнигаПродаж.СчетФактура КАК СчетФактура,
	|	КнигаПродаж.СтавкаНДС КАК СтавкаНДС,
	|	КнигаПродаж.СуммаБезНДСОборот КАК Сумма,
	|	КнигаПродаж.НДСОборот КАК СуммаНДС,
	|	КнигаПродаж.Событие,
	|	КнигаПродаж.ВидЦенности,
	|	КнигаПродаж.ЗаписьДополнительногоЛиста,
	|	КнигаПродаж.НомерСтроки
	|ПОМЕСТИТЬ ВТСоставКнигиПродаж
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПродаж.Обороты(
	|			&НачалоПериода,
	|			&КонецПериодаГраница,
	|			Запись,
	|			Организация В (&Организация)
	|				И НЕ ВидЦенности В (&ВидыЦенностейАвансыВсе)
	|				И НЕ ЗаписьДополнительногоЛиста) КАК КнигаПродаж
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КнигаПродаж.СчетФактура,
	|	КнигаПродаж.СтавкаНДС,
	|	КнигаПродаж.СуммаБезНДСОборот,
	|	КнигаПродаж.НДСОборот,
	|	КнигаПродаж.Событие,
	|	КнигаПродаж.ВидЦенности,
	|	КнигаПродаж.ЗаписьДополнительногоЛиста,
	|	КнигаПродаж.НомерСтроки
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПродаж.Обороты(
	|			&НачалоПериода,
	|			,
	|			Запись,
	|			Организация В (&Организация)
	|				И НЕ ВидЦенности В (&ВидыЦенностейАвансыВсе)
	|				И (ЗаписьДополнительногоЛиста
	|					И (КорректируемыйПериод МЕЖДУ &НачалоПериода И &КонецПериода))) КАК КнигаПродаж
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КнигаПродаж.СчетФактура КАК ДокументРегистратор,
	|	КнигаПродаж.СтавкаНДС,
	|	2 КАК РангОшибки
	|ПОМЕСТИТЬ ВТОперацииСОшибками
	|ИЗ
	|	ВТСоставКнигиПродаж КАК КнигаПродаж
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураВыданный.ДокументыОснования КАК СчетФактураВыданный
	|		ПО (СчетФактураВыданный.ДокументОснование = КнигаПродаж.СчетФактура)
	|			И (НЕ(СчетФактураВыданный.Ссылка.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.НаАванс)
	|					ИЛИ СчетФактураВыданный.Ссылка.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.НаАвансКомитента)))
	|ГДЕ
	|	СчетФактураВыданный.Ссылка ЕСТЬ NULL 
	|	И НЕ КнигаПродаж.СчетФактура ССЫЛКА Документ.ОтчетОРозничныхПродажах
	|	И НЕ КнигаПродаж.СчетФактура ССЫЛКА Документ.ПриходныйКассовыйОрдер
	|	И НЕ КнигаПродаж.СчетФактура ССЫЛКА Документ.СчетФактураВыданный
	|
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТСоставКнигиПродаж.СчетФактура КАК СчетФактура,
	|	ВТСоставКнигиПродаж.СтавкаНДС,
	|	ВТОперацииСОшибками.ДокументРегистратор,
	|	МИНИМУМ(ВТСоставКнигиПродаж.Сумма) КАК Сумма,
	|	МИНИМУМ(ВТСоставКнигиПродаж.СуммаНДС) КАК СуммаНДС,
	|	МИНИМУМ(ВТОперацииСОшибками.РангОшибки) КАК РангОшибки,
	|	ВТСоставКнигиПродаж.Событие,
	|	ВТСоставКнигиПродаж.ВидЦенности,
	|	ВТСоставКнигиПродаж.ЗаписьДополнительногоЛиста,
	|	ВТСоставКнигиПродаж.НомерСтроки
	|ПОМЕСТИТЬ ВТПроведеноРанжированиеОшибок
	|ИЗ
	|	ВТСоставКнигиПродаж КАК ВТСоставКнигиПродаж
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОперацииСОшибками КАК ВТОперацииСОшибками
	|		ПО ВТСоставКнигиПродаж.СчетФактура = ВТОперацииСОшибками.ДокументРегистратор
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТСоставКнигиПродаж.СчетФактура,
	|	ВТСоставКнигиПродаж.СтавкаНДС,
	|	ВТОперацииСОшибками.ДокументРегистратор,
	|	ВТСоставКнигиПродаж.Событие,
	|	ВТСоставКнигиПродаж.ВидЦенности,
	|	ВТСоставКнигиПродаж.ЗаписьДополнительногоЛиста,
	|	ВТСоставКнигиПродаж.НомерСтроки
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СчетФактураВыданныйДокументыОснования.ДокументОснование КАК ДокументОснование,
	|	СчетФактураВыданныйДокументыОснования.Ссылка,
	|	ВЫБОР
	|		КОГДА СчетФактураВыданныйДокументыОснования.Ссылка.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.НаРеализацию)
	|				ИЛИ СчетФактураВыданныйДокументыОснования.Ссылка.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.Корректировочный)
	|			ТОГДА NULL
	|		ИНАЧЕ СчетФактураВыданныйДокументыОснования.Ссылка.СтавкаНДС
	|	КОНЕЦ КАК СтавкаНДССчетаФактуры
	|ПОМЕСТИТЬ ВТСчетФактураВыданныйДокументыОснования
	|ИЗ
	|	Документ.СчетФактураВыданный.ДокументыОснования КАК СчетФактураВыданныйДокументыОснования
	|ГДЕ
	|	СчетФактураВыданныйДокументыОснования.Ссылка.Организация В(&Организация)
	|	И СчетФактураВыданныйДокументыОснования.Ссылка.ПометкаУдаления = ЛОЖЬ
	|	И НЕ(СчетФактураВыданныйДокументыОснования.Ссылка.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.НаАванс)
	|				ИЛИ СчетФактураВыданныйДокументыОснования.Ссылка.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.НаАвансКомитента))
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СчетФактураВыданныйДокументыОснования.Ссылка КАК СчетФактура,
	|	ВТПроведеноРанжированиеОшибок.СтавкаНДС,
	|	ВТПроведеноРанжированиеОшибок.СчетФактура КАК ДокументРегистратор,
	|	ВТПроведеноРанжированиеОшибок.Сумма,
	|	ВТПроведеноРанжированиеОшибок.СуммаНДС,
	|	1 КАК КоличествоДокументов,
	|	ВЫБОР
	|		КОГДА ВТПроведеноРанжированиеОшибок.РангОшибки ЕСТЬ NULL 
	|			ТОГДА &ТекстБезОшибок
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ВТПроведеноРанжированиеОшибок.РангОшибки = 1
	|					ТОГДА &ТекстОшибкаРеализацияЕНВДСНДС
	|				КОГДА ВТПроведеноРанжированиеОшибок.РангОшибки = 2
	|					ТОГДА &ТекстОшибкаРеализацияБезСФ
	|			КОНЕЦ
	|	КОНЕЦ КАК ПризнакНаличияОшибок,
	|	ВЫБОР
	|		КОГДА ВТПроведеноРанжированиеОшибок.РангОшибки = 0
	|			ТОГДА &ТекстСноска
	|		ИНАЧЕ &ТекстНетСноски
	|	КОНЕЦ КАК СимволСноски,
	|	&ТекстОтраженыВКнигеПродаж КАК ПризнакОтраженияНДС,
	|	ВЫБОР
	|		КОГДА ВТПроведеноРанжированиеОшибок.РангОшибки ЕСТЬ NULL 
	|			ТОГДА 0
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК КоличествоОшибок,
	|	ВТПроведеноРанжированиеОшибок.Событие,
	|	ВТПроведеноРанжированиеОшибок.ВидЦенности,
	|	ВТПроведеноРанжированиеОшибок.ЗаписьДополнительногоЛиста,
	|	ВТПроведеноРанжированиеОшибок.НомерСтроки
	|ИЗ
	|	ВТПроведеноРанжированиеОшибок КАК ВТПроведеноРанжированиеОшибок
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСчетФактураВыданныйДокументыОснования КАК СчетФактураВыданныйДокументыОснования
	|		ПО (ВТПроведеноРанжированиеОшибок.СчетФактура = СчетФактураВыданныйДокументыОснования.ДокументОснование
	|				ИЛИ ВТПроведеноРанжированиеОшибок.СчетФактура = СчетФактураВыданныйДокументыОснования.Ссылка
	|					И ВТПроведеноРанжированиеОшибок.СтавкаНДС = ЕСТЬNULL(СчетФактураВыданныйДокументыОснования.СтавкаНДССчетаФактуры, ВТПроведеноРанжированиеОшибок.СтавкаНДС))";
	
	НачислениеРеализацияУпрощенныйУчетНДСВключенные = Запрос.Выполнить();
		
	ВнешнийНаборДанныхНачислениеРеализацияУпрощенныйУчетНДС = Новый Структура;
	ВнешнийНаборДанныхНачислениеРеализацияУпрощенныйУчетНДС.Вставить("НачислениеРеализацияУпрощенныйУчетНДСВключенные",
																	 НачислениеРеализацияУпрощенныйУчетНДСВключенные);
																		
	СтруктураВнешнихНаборов.Вставить("ВнешнийНаборДанныхНачислениеРеализацияУпрощенныйУчетНДС",
									 ВнешнийНаборДанныхНачислениеРеализацияУпрощенныйУчетНДС);
		
КонецПроцедуры // ПолучитьНаборДанныхНачислениеРеализацияУпрощенныйУчетНДС()

// Выполняет запрос к базе данных для получения набора данных, необходимого для формирования расшифровки по
//	начисленному НДС с полученных авансов в случае ведения упрощенного учета НДС
//
// Параметры
//  Запрос  - Запрос - запрос к базе данных
//  СтруктураВнешнихНаборов - структура внешних источников данных для расшифровки.
//
// Возвращаемое значение:
//   Структура   - структура параметров для формирования карты отчета
//
Процедура ПолучитьНаборДанныхНачислениеАвансыПолученныеУпрощенныйУчетНДС(Запрос, СтруктураВнешнихНаборов) 
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	СоставКнигиПродаж.СчетФактура КАК СчетФактура,
	|	СоставКнигиПродаж.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	СоставКнигиПродаж.СтавкаНДС,
	|	СоставКнигиПродаж.СуммаБезНДСОборот,
	|	СоставКнигиПродаж.НДСОборот,
	|	СоставКнигиПродаж.ВидЦенности,
	|	СоставКнигиПродаж.Событие,
	|	СоставКнигиПродаж.Регистратор,
	|	СоставКнигиПродаж.ЗаписьДополнительногоЛиста,
	|	СоставКнигиПродаж.НомерСтроки КАК НомерСтроки
	|ПОМЕСТИТЬ ВТСоставКнигиПродаж
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПродаж.Обороты(
	|			&НачалоПериода,
	|			&КонецПериодаГраница,
	|			Запись,
	|			Организация В (&Организация)
	|				И ВидЦенности В (&ВидыЦенностейАвансыПолученные)
	|				И НЕ ЗаписьДополнительногоЛиста) КАК СоставКнигиПродаж
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СоставКнигиПродаж.СчетФактура,
	|	СоставКнигиПродаж.ДоговорКонтрагента,
	|	СоставКнигиПродаж.СтавкаНДС,
	|	СоставКнигиПродаж.СуммаБезНДСОборот,
	|	СоставКнигиПродаж.НДСОборот,
	|	СоставКнигиПродаж.ВидЦенности,
	|	СоставКнигиПродаж.Событие,
	|	СоставКнигиПродаж.Регистратор,
	|	СоставКнигиПродаж.ЗаписьДополнительногоЛиста,
	|	СоставКнигиПродаж.НомерСтроки
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПродаж.Обороты(
	|			&НачалоПериода,
	|			,
	|			Запись,
	|			Организация В (&Организация)
	|				И ВидЦенности В (&ВидыЦенностейАвансыПолученные)
	|				И ЗаписьДополнительногоЛиста
	|				И КорректируемыйПериод МЕЖДУ &НачалоПериода И &КонецПериода) КАК СоставКнигиПродаж
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактура,
	|	ДоговорКонтрагента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ХозрасчетныйОстаткиИОбороты.Субконто2 КАК ДоговорКонтрагента,
	|	ХозрасчетныйОстаткиИОбороты.Субконто1 КАК СчетФактура,
	|	ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС18_118) КАК СтавкаНДСПредположительная,
	|	ВЫРАЗИТЬ(ХозрасчетныйОстаткиИОбороты.СуммаКонечныйОстатокКт - ХозрасчетныйОстаткиИОбороты.СуммаКонечныйОстатокКт * 18 / 118 КАК ЧИСЛО(15, 2)) КАК СуммаБезНДС,
	|	ВЫРАЗИТЬ(ХозрасчетныйОстаткиИОбороты.СуммаКонечныйОстатокКт * 18 / 118 КАК ЧИСЛО(15, 2)) КАК СуммаНДС
	|ПОМЕСТИТЬ ВТХозрасчетныйВсеОстаткиАвансыПолученные
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.ОстаткиИОбороты(&НачалоПериода, &КонецПериодаГраница, , , Счет В (&СписокСчетовАвансовПолученных), &ВидыСубконтоДокументыДоговоры, Организация В (&Организация)) КАК ХозрасчетныйОстаткиИОбороты
	|ГДЕ
	|	ХозрасчетныйОстаткиИОбороты.СуммаКонечныйОстатокКт > ХозрасчетныйОстаткиИОбороты.СуммаНачальныйОстатокКт
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ХозрасчетныйОстаткиИОбороты.Субконто1,
	|	ХозрасчетныйОстаткиИОбороты.Субконто2
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасшифровкаПлатежа.Ссылка КАК Ссылка,
	|	РасшифровкаПлатежа.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	ВЫБОР
	|		КОГДА РасшифровкаПлатежа.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС18)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС18_118)
	|		КОГДА РасшифровкаПлатежа.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС10)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС10_110)
	|		КОГДА РасшифровкаПлатежа.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС0)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС0)
	|		КОГДА РасшифровкаПлатежа.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
	|	КОНЕЦ КАК СтавкаНДС,
	|	СУММА(РасшифровкаПлатежа.СуммаНДС) КАК СуммаНДС,
	|	СУММА(РасшифровкаПлатежа.СуммаПлатежа - РасшифровкаПлатежа.СуммаНДС) КАК СуммаБезНДС
	|ПОМЕСТИТЬ СуммыПлатежей
	|ИЗ
	|	Документ.ПоступлениеНаРасчетныйСчет.РасшифровкаПлатежа КАК РасшифровкаПлатежа
	|ГДЕ
	|	РасшифровкаПлатежа.Ссылка В
	|			(ВЫБРАТЬ
	|				ВТХозрасчетныйВсеОстаткиАвансыПолученные.СчетФактура
	|			ИЗ
	|				ВТХозрасчетныйВсеОстаткиАвансыПолученные)
	|
	|СГРУППИРОВАТЬ ПО
	|	РасшифровкаПлатежа.Ссылка,
	|	РасшифровкаПлатежа.ДоговорКонтрагента,
	|	РасшифровкаПлатежа.СтавкаНДС
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РасшифровкаПлатежа.Ссылка,
	|	РасшифровкаПлатежа.ДоговорКонтрагента,
	|	ВЫБОР
	|		КОГДА РасшифровкаПлатежа.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС18)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС18_118)
	|		КОГДА РасшифровкаПлатежа.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС10)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС10_110)
	|		КОГДА РасшифровкаПлатежа.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС0)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС0)
	|		КОГДА РасшифровкаПлатежа.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
	|	КОНЕЦ,
	|	СУММА(РасшифровкаПлатежа.СуммаНДС),
	|	СУММА(РасшифровкаПлатежа.СуммаПлатежа - РасшифровкаПлатежа.СуммаНДС)
	|ИЗ
	|	Документ.ПриходныйКассовыйОрдер.РасшифровкаПлатежа КАК РасшифровкаПлатежа
	|ГДЕ
	|	РасшифровкаПлатежа.Ссылка В
	|			(ВЫБРАТЬ
	|				ВТХозрасчетныйВсеОстаткиАвансыПолученные.СчетФактура
	|			ИЗ
	|				ВТХозрасчетныйВсеОстаткиАвансыПолученные)
	|
	|СГРУППИРОВАТЬ ПО
	|	РасшифровкаПлатежа.Ссылка,
	|	РасшифровкаПлатежа.ДоговорКонтрагента,
	|	РасшифровкаПлатежа.СтавкаНДС
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТАвансыПолученныеВременнаяТаблица.СчетФактура,
	|	ВТАвансыПолученныеВременнаяТаблица.ДоговорКонтрагента,
	|	ЕСТЬNULL(СуммыПлатежей.СтавкаНДС, ВТАвансыПолученныеВременнаяТаблица.СтавкаНДСПредположительная) КАК СтавкаНДС,
	|	ЕСТЬNULL(СуммыПлатежей.СуммаБезНДС, ВТАвансыПолученныеВременнаяТаблица.СуммаБезНДС) КАК СуммаБезНДС,
	|	ЕСТЬNULL(СуммыПлатежей.СуммаНДС, ВТАвансыПолученныеВременнаяТаблица.СуммаНДС) КАК СуммаНДС
	|ПОМЕСТИТЬ ВТАвансыПолученные
	|ИЗ
	|	ВТХозрасчетныйВсеОстаткиАвансыПолученные КАК ВТАвансыПолученныеВременнаяТаблица
	|		ЛЕВОЕ СОЕДИНЕНИЕ СуммыПлатежей КАК СуммыПлатежей
	|		ПО ВТАвансыПолученныеВременнаяТаблица.СчетФактура = СуммыПлатежей.Ссылка
	|			И ВТАвансыПолученныеВременнаяТаблица.ДоговорКонтрагента = СуммыПлатежей.ДоговорКонтрагента
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ВТАвансыПолученныеВременнаяТаблица.СчетФактура,
	|	ВТАвансыПолученныеВременнаяТаблица.ДоговорКонтрагента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	АвансыПолученные.СчетФактура,
	|	АвансыПолученные.ДоговорКонтрагента,
	|	АвансыПолученные.СтавкаНДС,
	|	АвансыПолученные.СуммаБезНДС - ЕСТЬNULL(ВТСоставКнигиПродаж.СуммаБезНДСОборот, 0) КАК СуммаБезНДС,
	|	АвансыПолученные.СуммаНДС - ЕСТЬNULL(ВТСоставКнигиПродаж.НДСОборот, 0) КАК СуммаНДС,
	|	ВТСоставКнигиПродаж.ВидЦенности,
	|	ВТСоставКнигиПродаж.Событие
	|ПОМЕСТИТЬ НевосстановленныеАвансы
	|ИЗ
	|	ВТАвансыПолученные КАК АвансыПолученные
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСоставКнигиПродаж КАК ВТСоставКнигиПродаж
	|		ПО АвансыПолученные.СчетФактура = ВТСоставКнигиПродаж.СчетФактура
	|			И АвансыПолученные.ДоговорКонтрагента = ВТСоставКнигиПродаж.ДоговорКонтрагента
	|			И АвансыПолученные.СтавкаНДС = ВТСоставКнигиПродаж.СтавкаНДС
	|ГДЕ
	|	АвансыПолученные.СуммаНДС - ЕСТЬNULL(ВТСоставКнигиПродаж.НДСОборот, 0) > 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	АвансыПолученные.СчетФактура,
	|	АвансыПолученные.ДоговорКонтрагента,
	|	АвансыПолученные.СтавкаНДС,
	|	АвансыПолученные.СуммаБезНДС - ЕСТЬNULL(ВТСоставКнигиПродаж.СуммаБезНДСОборот, 0),
	|	АвансыПолученные.СуммаНДС - ЕСТЬNULL(ВТСоставКнигиПродаж.НДСОборот, 0),
	|	ВТСоставКнигиПродаж.ВидЦенности,
	|	ВТСоставКнигиПродаж.Событие
	|ИЗ
	|	ВТАвансыПолученные КАК АвансыПолученные
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСоставКнигиПродаж КАК ВТСоставКнигиПродаж
	|		ПО АвансыПолученные.СчетФактура = ВТСоставКнигиПродаж.СчетФактура
	|			И АвансыПолученные.ДоговорКонтрагента = ВТСоставКнигиПродаж.ДоговорКонтрагента
	|			И АвансыПолученные.СтавкаНДС = ВТСоставКнигиПродаж.СтавкаНДС
	|ГДЕ
	|	АвансыПолученные.СуммаНДС = 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	АвансыПолученные.СчетФактура,
	|	АвансыПолученные.ДоговорКонтрагента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТХозрасчетныйВсеОстаткиАвансыПолученные
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ХозрасчетныйОстатки.СчетФактура КАК ДокументРегистратор,
	|	ХозрасчетныйОстатки.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	ХозрасчетныйОстатки.СуммаБезНДС КАК Сумма,
	|	ХозрасчетныйОстатки.СуммаНДС КАК СуммаНДС,
	|	ХозрасчетныйОстатки.СтавкаНДС КАК СтавкаНДС,
	|	СчетФактураВыданныйДокументыОснования.Ссылка КАК СчетФактура,
	|	ВЫБОР
	|		КОГДА СчетФактураВыданныйДокументыОснования.Ссылка.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.НаАвансКомитента)
	|			ТОГДА &ТекстНеОтраженВКнигеПродажАвансПоКомиссии
	|		ИНАЧЕ &ТекстНеОтраженыВКнигеПродаж
	|	КОНЕЦ КАК ПризнакОтраженияНДС,
	|	ВЫБОР
	|		КОГДА ХозрасчетныйОстатки.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС0)
	|			ИЛИ ХозрасчетныйОстатки.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС) ТОГДА
	|			&ТекстПолученныйАвансПоСтавке0ИлиБезНДС
	|		КОГДА СчетФактураВыданныйДокументыОснования.Ссылка ЕСТЬ NULL 
	|			ТОГДА &ТекстПолученныйАвансБезСФ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА СчетФактураВыданныйДокументыОснования.Ссылка.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.НаАвансКомитента)
	|					ТОГДА &ТекстБезОшибок
	|				КОГДА СчетФактураВыданныйДокументыОснования.Ссылка.Проведен
	|					ТОГДА &ТекстНачислениеНеОтраженоВКнигеПродаж
	|				ИНАЧЕ &ТекстСФНаПолученныйАвансНеПроведен
	|			КОНЕЦ
	|	КОНЕЦ КАК ПризнакНаличияОшибок,
	|	0 КАК СуммаВключенные,
	|	ХозрасчетныйОстатки.СуммаНДС КАК СуммаНеВключенные,
	|	1 КАК КоличествоДокументов,
	|	ХозрасчетныйОстатки.ВидЦенности,
	|	ХозрасчетныйОстатки.Событие,
	|	ЛОЖЬ КАК ЗаписьДополнительногоЛиста,
	|	1 КАК НомерСтроки
	|ИЗ
	|	НевосстановленныеАвансы КАК ХозрасчетныйОстатки
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураВыданный.ДокументыОснования КАК СчетФактураВыданныйДокументыОснования
	|		ПО ХозрасчетныйОстатки.СчетФактура = СчетФактураВыданныйДокументыОснования.ДокументОснование
	|			И ХозрасчетныйОстатки.ДоговорКонтрагента = СчетФактураВыданныйДокументыОснования.Ссылка.ДоговорКонтрагента
	|			И (НЕ СчетФактураВыданныйДокументыОснования.Ссылка.ПометкаУдаления)
	|			И (СчетФактураВыданныйДокументыОснования.Ссылка.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.НаАванс)
	|				ИЛИ СчетФактураВыданныйДокументыОснования.Ссылка.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.НаАвансКомитента))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТСоставКнигиПродаж.СчетФактура,
	|	ВТСоставКнигиПродаж.ДоговорКонтрагента,
	|	ВТСоставКнигиПродаж.СуммаБезНДСОборот,
	|	ВТСоставКнигиПродаж.НДСОборот,
	|	ВТСоставКнигиПродаж.СтавкаНДС,
	|	СчетФактураВыданныйДокументыОснования.Ссылка,
	|	&ТекстОтраженыВКнигеПродаж,
	|	&ТекстБезОшибок,
	|	ВТСоставКнигиПродаж.НДСОборот,
	|	0,
	|	1,
	|	ВТСоставКнигиПродаж.ВидЦенности,
	|	ВТСоставКнигиПродаж.Событие,
	|	ВТСоставКнигиПродаж.ЗаписьДополнительногоЛиста,
	|	ВТСоставКнигиПродаж.НомерСтроки
	|ИЗ
	|	ВТСоставКнигиПродаж КАК ВТСоставКнигиПродаж
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураВыданный.ДокументыОснования КАК СчетФактураВыданныйДокументыОснования
	|		ПО ВТСоставКнигиПродаж.СчетФактура = СчетФактураВыданныйДокументыОснования.ДокументОснование
	|			И ВТСоставКнигиПродаж.ДоговорКонтрагента = СчетФактураВыданныйДокументыОснования.Ссылка.ДоговорКонтрагента
	|			И (СчетФактураВыданныйДокументыОснования.Ссылка.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.НаАванс)
	|				ИЛИ СчетФактураВыданныйДокументыОснования.Ссылка.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.НаАвансКомитента))
	|			И ВТСоставКнигиПродаж.Регистратор = СчетФактураВыданныйДокументыОснования.Ссылка
	|ГДЕ
	|	НЕ СчетФактураВыданныйДокументыОснования.Ссылка.ПометкаУдаления";
		
	Запрос.Текст = ТекстЗапроса;
	
	НачислениеАвансыПолученныеУпрощенныйУчетНДС = Запрос.Выполнить();	
	
	ВнешнийНаборДанныхНачислениеАвансыПолученныеУпрощенныйУчетНДС = Новый Структура;
	
	ВнешнийНаборДанныхНачислениеАвансыПолученныеУпрощенныйУчетНДС.Вставить("НачислениеАвансыПолученныеУпрощенныйУчетНДС",
																		  	НачислениеАвансыПолученныеУпрощенныйУчетНДС);
	СтруктураВнешнихНаборов.Вставить("ВнешнийНаборДанныхНачислениеАвансыПолученныеУпрощенныйУчетНДС",
										ВнешнийНаборДанныхНачислениеАвансыПолученныеУпрощенныйУчетНДС);

КонецПроцедуры // ПолучитьНаборДанныхНачислениеАвансыПолученныеУпрощенныйУчетНДС()

// Выполняет запрос к базе данных для получения набора данных, необходимого для формирования расшифровки по
//	начисленному НДС с выданных авансов (по факту поступления) в случае ведения упрощенного учета НДС
//
// Параметры
//  Запрос  - Запрос - запрос к базе данных
//  СтруктураВнешнихНаборов - структура внешних источников данных для расшифровки.
//
// Возвращаемое значение:
//   Структура   - структура параметров для формирования карты отчета
//
Процедура ПолучитьНаборДанныхНачислениеВосстановлениеСАвансовВыданныхУпрощенныйУчетНДС(Запрос, СтруктураВнешнихНаборов) 

	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ХозрасчетныйОстатки.Субконто1 КАК Субконто1
	|ПОМЕСТИТЬ ВТХозрасчетныйОстаткиНДСпоАвансамИПредоплатамВыданным
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(&НачалоСледующегоПериода, Счет = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НДСпоАвансамИПредоплатамВыданным), ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.СФПолученные), Организация В (&Организация)) КАК ХозрасчетныйОстатки
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Субконто1
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ХозрасчетныйОстатки.Субконто1 КАК Субконто1,
	|	ХозрасчетныйОстатки.СуммаОстатокДт
	|ПОМЕСТИТЬ ВТХозрасчетныйОстаткиНДСпоАвансамПоСпискуСчетов
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(&НачалоСледующегоПериода, Счет В (&СписокСчетовАвансовВыданных), ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ДокументыРасчетовСКонтрагентами), Организация В (&Организация)) КАК ХозрасчетныйОстатки
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Субконто1
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДСЗаписиКнигиПокупокОбороты.СчетФактура КАК СчетФактура,
	|	НДСЗаписиКнигиПокупокОбороты.ВидЦенности КАК ВидЦенности,
	|	НДСЗаписиКнигиПокупокОбороты.СтавкаНДС КАК СтавкаНДС,
	|	НДСЗаписиКнигиПокупокОбороты.СуммаБезНДСОборот,
	|	НДСЗаписиКнигиПокупокОбороты.НДСОборот,
	|	НДСЗаписиКнигиПокупокОбороты.Событие,
	|	НДСЗаписиКнигиПокупокОбороты.ЗаписьДополнительногоЛиста,
	|	НДСЗаписиКнигиПокупокОбороты.НомерСтроки
	|ПОМЕСТИТЬ ВТЗаписиКнигиПокупокОборотыСАвансовВыданныхУпрощенныйУчет
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПокупок.Обороты(
	|			,
	|			&КонецПериодаГраница,
	|			Запись,
	|			Организация В (&Организация)
	|				И СчетФактура В
	|					(ВЫБРАТЬ
	|						ХозрасчетныйОстатки.Субконто1 КАК СчетФактура
	|					ИЗ
	|						ВТХозрасчетныйОстаткиНДСпоАвансамИПредоплатамВыданным КАК ХозрасчетныйОстатки)
	|				И ВидЦенности = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.АвансыВыданные)
	|				И НЕ ЗаписьДополнительногоЛиста) КАК НДСЗаписиКнигиПокупокОбороты
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НДСЗаписиКнигиПокупокОбороты.СчетФактура,
	|	НДСЗаписиКнигиПокупокОбороты.ВидЦенности,
	|	НДСЗаписиКнигиПокупокОбороты.СтавкаНДС,
	|	НДСЗаписиКнигиПокупокОбороты.СуммаБезНДСОборот,
	|	НДСЗаписиКнигиПокупокОбороты.НДСОборот,
	|	НДСЗаписиКнигиПокупокОбороты.Событие,
	|	НДСЗаписиКнигиПокупокОбороты.ЗаписьДополнительногоЛиста,
	|	НДСЗаписиКнигиПокупокОбороты.НомерСтроки
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПокупок.Обороты(
	|			,
	|			,
	|			Запись,
	|			Организация В (&Организация)
	|				И СчетФактура В
	|					(ВЫБРАТЬ
	|						ХозрасчетныйОстатки.Субконто1 КАК СчетФактура
	|					ИЗ
	|						ВТХозрасчетныйОстаткиНДСпоАвансамИПредоплатамВыданным КАК ХозрасчетныйОстатки)
	|				И ВидЦенности = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.АвансыВыданные)
	|				И (ЗаписьДополнительногоЛиста
	|					И (КорректируемыйПериод МЕЖДУ &НачалоПериода И &КонецПериода))) КАК НДСЗаписиКнигиПокупокОбороты
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактура,
	|	ВидЦенности,
	|	СтавкаНДС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДСЗаписиКнигиПродажОбороты.Покупатель,
	|	НДСЗаписиКнигиПродажОбороты.ДоговорКонтрагента,
	|	НДСЗаписиКнигиПродажОбороты.СчетФактура КАК СчетФактура,
	|	НДСЗаписиКнигиПродажОбороты.ВидЦенности КАК ВидЦенности,
	|	НДСЗаписиКнигиПродажОбороты.СтавкаНДС КАК СтавкаНДС,
	|	НДСЗаписиКнигиПродажОбороты.СуммаБезНДСОборот,
	|	НДСЗаписиКнигиПродажОбороты.НДСОборот
	|ПОМЕСТИТЬ ВТЗаписиКнигиПродажОборотыСАвансовВыданныхУпрощенныйУчет
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПродаж.Обороты(
	|			,
	|			&КонецПериодаГраница,
	|			,
	|			Организация В (&Организация)
	|				И СчетФактура В
	|					(ВЫБРАТЬ
	|						ХозрасчетныйОстатки.Субконто1 КАК СчетФактура
	|					ИЗ
	|						ВТХозрасчетныйОстаткиНДСпоАвансамИПредоплатамВыданным КАК ХозрасчетныйОстатки)
	|				И ВидЦенности = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.АвансыВыданные)
	|				И НЕ ЗаписьДополнительногоЛиста) КАК НДСЗаписиКнигиПродажОбороты
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НДСЗаписиКнигиПродажОбороты.Покупатель,
	|	НДСЗаписиКнигиПродажОбороты.ДоговорКонтрагента,
	|	НДСЗаписиКнигиПродажОбороты.СчетФактура,
	|	НДСЗаписиКнигиПродажОбороты.ВидЦенности,
	|	НДСЗаписиКнигиПродажОбороты.СтавкаНДС,
	|	НДСЗаписиКнигиПродажОбороты.СуммаБезНДСОборот,
	|	НДСЗаписиКнигиПродажОбороты.НДСОборот
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПродаж.Обороты(
	|			,
	|			,
	|			,
	|			Организация В (&Организация)
	|				И СчетФактура В
	|					(ВЫБРАТЬ
	|						ХозрасчетныйОстатки.Субконто1 КАК СчетФактура
	|					ИЗ
	|						ВТХозрасчетныйОстаткиНДСпоАвансамИПредоплатамВыданным КАК ХозрасчетныйОстатки)
	|				И ВидЦенности = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.АвансыВыданные)
	|				И (ЗаписьДополнительногоЛиста
	|					И (КорректируемыйПериод МЕЖДУ &НачалоПериода И &КонецПериода))) КАК НДСЗаписиКнигиПродажОбороты
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактура,
	|	ВидЦенности,
	|	СтавкаНДС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(НДСЗаписиКнигиПокупокОбороты.СчетФактура, НДСЗаписиКнигиПродажОбороты.СчетФактура) КАК ДокументРегистратор,
	|	СУММА(ЕСТЬNULL(НДСЗаписиКнигиПокупокОбороты.СуммаБезНДСОборот, 0) - ЕСТЬNULL(НДСЗаписиКнигиПродажОбороты.СуммаБезНДСОборот, 0)) КАК Сумма,
	|	СУММА(ЕСТЬNULL(НДСЗаписиКнигиПокупокОбороты.НДСОборот, 0) - ЕСТЬNULL(НДСЗаписиКнигиПродажОбороты.НДСОборот, 0)) КАК СуммаНДС,
	|	ХозрасчетныйОстатки.СуммаОстатокДт,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(НДСЗаписиКнигиПокупокОбороты.СуммаБезНДСОборот, 0) - ЕСТЬNULL(НДСЗаписиКнигиПродажОбороты.СуммаБезНДСОборот, 0) + ЕСТЬNULL(НДСЗаписиКнигиПокупокОбороты.НДСОборот, 0) - ЕСТЬNULL(НДСЗаписиКнигиПродажОбороты.НДСОборот, 0) - ХозрасчетныйОстатки.СуммаОстатокДт = 0
	|			ТОГДА &ТекстОплаченныеЦенностиНеПоступили
	|		ИНАЧЕ &ТекстНачислениеНеОтраженоВКнигеПродаж
	|	КОНЕЦ КАК ПризнакНаличияОшибок,
	|	&ТекстНеОтраженыВКнигеПродаж КАК ПризнакОтраженияНДС,
	|	1 КАК КоличествоДокументов,
	|	ЕСТЬNULL(НДСЗаписиКнигиПродажОбороты.СтавкаНДС, ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС18_118)) КАК СтавкаНДС,
	|	НДСЗаписиКнигиПокупокОбороты.Событие,
	|	НДСЗаписиКнигиПокупокОбороты.ВидЦенности,
	|	НДСЗаписиКнигиПокупокОбороты.ЗаписьДополнительногоЛиста,
	|	НДСЗаписиКнигиПокупокОбороты.НомерСтроки
	|ИЗ
	|	ВТЗаписиКнигиПокупокОборотыСАвансовВыданныхУпрощенныйУчет КАК НДСЗаписиКнигиПокупокОбороты
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТЗаписиКнигиПродажОборотыСАвансовВыданныхУпрощенныйУчет КАК НДСЗаписиКнигиПродажОбороты
	|		ПО НДСЗаписиКнигиПокупокОбороты.СчетФактура = НДСЗаписиКнигиПродажОбороты.СчетФактура
	|			И НДСЗаписиКнигиПокупокОбороты.ВидЦенности = НДСЗаписиКнигиПродажОбороты.ВидЦенности
	|			И НДСЗаписиКнигиПокупокОбороты.СтавкаНДС = НДСЗаписиКнигиПродажОбороты.СтавкаНДС
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТХозрасчетныйОстаткиНДСпоАвансамПоСпискуСчетов КАК ХозрасчетныйОстатки
	|		ПО НДСЗаписиКнигиПокупокОбороты.СчетФактура = ХозрасчетныйОстатки.Субконто1
	|
	|СГРУППИРОВАТЬ ПО
	|	НДСЗаписиКнигиПродажОбороты.Покупатель,
	|	НДСЗаписиКнигиПродажОбороты.ДоговорКонтрагента,
	|	НДСЗаписиКнигиПродажОбороты.ВидЦенности,
	|	НДСЗаписиКнигиПокупокОбороты.СуммаБезНДСОборот,
	|	НДСЗаписиКнигиПокупокОбороты.НДСОборот,
	|	ХозрасчетныйОстатки.СуммаОстатокДт,
	|	НДСЗаписиКнигиПокупокОбороты.Событие,
	|	НДСЗаписиКнигиПокупокОбороты.ВидЦенности,
	|	ЕСТЬNULL(НДСЗаписиКнигиПродажОбороты.СтавкаНДС, ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС18_118)),
	|	НДСЗаписиКнигиПокупокОбороты.ЗаписьДополнительногоЛиста,
	|	НДСЗаписиКнигиПокупокОбороты.НомерСтроки,
	|	ЕСТЬNULL(НДСЗаписиКнигиПокупокОбороты.СчетФактура, НДСЗаписиКнигиПродажОбороты.СчетФактура),
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(НДСЗаписиКнигиПокупокОбороты.СуммаБезНДСОборот, 0) - ЕСТЬNULL(НДСЗаписиКнигиПродажОбороты.СуммаБезНДСОборот, 0) + ЕСТЬNULL(НДСЗаписиКнигиПокупокОбороты.НДСОборот, 0) - ЕСТЬNULL(НДСЗаписиКнигиПродажОбороты.НДСОборот, 0) - ХозрасчетныйОстатки.СуммаОстатокДт = 0
	|			ТОГДА &ТекстОплаченныеЦенностиНеПоступили
	|		ИНАЧЕ &ТекстНачислениеНеОтраженоВКнигеПродаж
	|	КОНЕЦ";  
	
	ВосстановлениеСАвансовВыданныхУпрощенныйУчетНДСНеВключенные = Запрос.Выполнить();
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	НДСЗаписиКнигиПродаж.СчетФактура КАК СчетФактура,
	|	НДСЗаписиКнигиПродаж.СтавкаНДС,
	|	НДСЗаписиКнигиПродаж.СуммаБезНДСОборот,
	|	НДСЗаписиКнигиПродаж.НДСОборот,
	|	НДСЗаписиКнигиПродаж.Событие,
	|	НДСЗаписиКнигиПродаж.ВидЦенности,
	|	НДСЗаписиКнигиПродаж.ЗаписьДополнительногоЛиста,
	|	НДСЗаписиКнигиПродаж.НомерСтроки
	|ПОМЕСТИТЬ ВТНДСЗаписиКнигиПродажОбороты
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПродаж.Обороты(
	|			&НачалоПериода,
	|			&КонецПериодаГраница,
	|			Запись,
	|			Организация В (&Организация)
	|				И НЕ ЗаписьДополнительногоЛиста) КАК НДСЗаписиКнигиПродаж
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НДСЗаписиКнигиПродаж.СчетФактура,
	|	НДСЗаписиКнигиПродаж.СтавкаНДС,
	|	НДСЗаписиКнигиПродаж.СуммаБезНДСОборот,
	|	НДСЗаписиКнигиПродаж.НДСОборот,
	|	НДСЗаписиКнигиПродаж.Событие,
	|	НДСЗаписиКнигиПродаж.ВидЦенности,
	|	НДСЗаписиКнигиПродаж.ЗаписьДополнительногоЛиста,
	|	НДСЗаписиКнигиПродаж.НомерСтроки
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПродаж.Обороты(
	|			&НачалоПериода,
	|			,
	|			Запись,
	|			Организация В (&Организация)
	|				И (ЗаписьДополнительногоЛиста
	|					И (КорректируемыйПериод МЕЖДУ &НачалоПериода И &КонецПериода))) КАК НДСЗаписиКнигиПродаж
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДСЗаписиКнигиПродаж.СчетФактура КАК ДокументРегистратор,
	|	НДСЗаписиКнигиПродаж.СуммаБезНДСОборот КАК Сумма,
	|	НДСЗаписиКнигиПродаж.НДСОборот КАК СуммаНДС,
	|	ХозрасчетныйОстатки.СуммаОстатокДт,
	|	ВЫБОР
	|		КОГДА ХозрасчетныйОстатки.СуммаОстатокДт ЕСТЬ NULL 
	|			ТОГДА &ТекстНДСПолностьюВосстановлен
	|		ИНАЧЕ &ТекстНДСВосстановленЧастично
	|	КОНЕЦ КАК ПризнакНаличияОшибок,
	|	&ТекстОтраженыВКнигеПродаж КАК ПризнакОтраженияНДС,
	|	1 КАК КоличествоДокументов,
	|	НДСЗаписиКнигиПродаж.СтавкаНДС КАК СтавкаНДС,
	|	НДСЗаписиКнигиПродаж.ВидЦенности,
	|	НДСЗаписиКнигиПродаж.Событие,
	|	НДСЗаписиКнигиПродаж.ЗаписьДополнительногоЛиста,
	|	НДСЗаписиКнигиПродаж.НомерСтроки
	|ИЗ
	|	ВТНДСЗаписиКнигиПродажОбороты КАК НДСЗаписиКнигиПродаж
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТХозрасчетныйОстаткиНДСпоАвансамПоСпискуСчетов КАК ХозрасчетныйОстатки
	|		ПО НДСЗаписиКнигиПродаж.СчетФактура = ХозрасчетныйОстатки.Субконто1";	
	
	Запрос.Текст = ТекстЗапроса;
	
	ВосстановлениеСАвансовВыданныхУпрощенныйУчетНДСВключенные = Запрос.Выполнить();
		
	ВнешнийНаборДанныхНачислениеВосстановлениеСАвансовВыданныхУпрощенныйУчетНДС = Новый Структура;
	
	ВнешнийНаборДанныхНачислениеВосстановлениеСАвансовВыданныхУпрощенныйУчетНДС.Вставить("ВосстановлениеСАвансовВыданныхУпрощенныйУчетНДСНеВключенные",
																					     	ВосстановлениеСАвансовВыданныхУпрощенныйУчетНДСНеВключенные);
	ВнешнийНаборДанныхНачислениеВосстановлениеСАвансовВыданныхУпрощенныйУчетНДС.Вставить("ВосстановлениеСАвансовВыданныхУпрощенныйУчетНДСВключенные",
																						 	ВосстановлениеСАвансовВыданныхУпрощенныйУчетНДСВключенные);
		
	СтруктураВнешнихНаборов.Вставить("ВнешнийНаборДанныхНачислениеВосстановлениеСАвансовВыданныхУпрощенныйУчетНДС",
									 	ВнешнийНаборДанныхНачислениеВосстановлениеСАвансовВыданныхУпрощенныйУчетНДС);
	
КонецПроцедуры // ПолучитьНаборДанныхНачислениеВосстановлениеСАвансовВыданныхУпрощенныйУчетНДС()

// Выполняет запрос к базе данных для получения набора данных, необходимого для формирования расшифровки по
//	начисленному НДС по прочим операциям в случае ведения упрощенного учета НДС
//
// Параметры
//  Запрос  - Запрос - запрос к базе данных
//	СтруктураВнешнихНаборов - структура внешних источников данных для расшифровки.
//
// Возвращаемое значение:
//   Структура   - структура параметров для формирования карты отчета
//
Процедура ПолучитьНаборДанныхНачислениеДругиеОперацииУпрощенныйУчетНДС(Запрос, СтруктураВнешнихНаборов) 

	//	В данном разделе отражаются начисленные пользователем суммы НДС, кроме любых видов авансов
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НДСЗаписиКнигиПродажОбороты.Регистратор КАК ДокументРегистратор,
	|	НДСЗаписиКнигиПродажОбороты.СтавкаНДС КАК СтавкаНДС,
	|	НДСЗаписиКнигиПродажОбороты.СуммаБезНДСОборот КАК Сумма,
	|	НДСЗаписиКнигиПродажОбороты.НДСОборот КАК СуммаНДС,
	|	НДСЗаписиКнигиПродажОбороты.Событие,
	|	НДСЗаписиКнигиПродажОбороты.ВидЦенности,
	|	НДСЗаписиКнигиПродажОбороты.ЗаписьДополнительногоЛиста,
	|	НДСЗаписиКнигиПродажОбороты.НомерСтроки
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПродаж.Обороты(
	|			&НачалоПериода,
	|			&КонецПериодаГраница,
	|			Запись,
	|			Организация В (&Организация)
	|				И НЕ ВидЦенности В (&ВидыЦенностейАвансыВсе)
	|				И НЕ ЗаписьДополнительногоЛиста) КАК НДСЗаписиКнигиПродажОбороты
	|ГДЕ
	|	НДСЗаписиКнигиПродажОбороты.Регистратор ССЫЛКА Документ.ОтражениеНачисленияНДС
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НДСЗаписиКнигиПродажОбороты.Регистратор,
	|	НДСЗаписиКнигиПродажОбороты.СтавкаНДС,
	|	НДСЗаписиКнигиПродажОбороты.СуммаБезНДСОборот,
	|	НДСЗаписиКнигиПродажОбороты.НДСОборот,
	|	НДСЗаписиКнигиПродажОбороты.Событие,
	|	НДСЗаписиКнигиПродажОбороты.ВидЦенности,
	|	НДСЗаписиКнигиПродажОбороты.ЗаписьДополнительногоЛиста,
	|	НДСЗаписиКнигиПродажОбороты.НомерСтроки
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПродаж.Обороты(
	|			&НачалоПериода,
	|			,
	|			Запись,
	|			Организация В (&Организация)
	|				И НЕ ВидЦенности В (&ВидыЦенностейАвансыВсе)
	|				И (ЗаписьДополнительногоЛиста
	|					И (КорректируемыйПериод МЕЖДУ &НачалоПериода И &КонецПериода))) КАК НДСЗаписиКнигиПродажОбороты
	|ГДЕ
	|	НДСЗаписиКнигиПродажОбороты.Регистратор ССЫЛКА Документ.ОтражениеНачисленияНДС";
	
	НачислениеДругиеОперацииУпрощенныйУчетНДС = Запрос.Выполнить();
	
	ВнешнийНаборДанныхНачислениеДругиеОперацииУпрощенныйУчетНДС = Новый Структура;
	ВнешнийНаборДанныхНачислениеДругиеОперацииУпрощенныйУчетНДС.Вставить("НачислениеДругиеОперацииУпрощенныйУчетНДС",
																		 НачислениеДругиеОперацииУпрощенныйУчетНДС);
																			
	СтруктураВнешнихНаборов.Вставить("ВнешнийНаборДанныхНачислениеДругиеОперацииУпрощенныйУчетНДС",
									 ВнешнийНаборДанныхНачислениеДругиеОперацииУпрощенныйУчетНДС);

КонецПроцедуры // ПолучитьНаборДанныхНачислениеДругиеОперацииУпрощенныйУчетНДС()

// Выполняет запрос к базе данных для получения набора данных, необходимого для формирования расшифровки по
//	вычету НДС по приобретенным ценностям в случае ведения упрощенного учета НДС
//
// Параметры
//  Запрос  - Запрос - запрос к базе данных
//  СтруктураВнешнихНаборов - структура внешних источников данных для расшифровки.
//
// Возвращаемое значение:
//   Структура   - структура параметров для формирования карты отчета
//
Процедура ПолучитьНаборДанныхВычетПоПриобретеннымЦенностямУпрощенныйУчетНДС(Запрос, СтруктураВнешнихНаборов) 

	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НДСЗаписиКнигиПокупокОбороты.СчетФактура КАК ДокументРегистратор,
	|	НДСЗаписиКнигиПокупокОбороты.СтавкаНДС,
	|	НДСЗаписиКнигиПокупокОбороты.СуммаБезНДСОборот,
	|	НДСЗаписиКнигиПокупокОбороты.НДСОборот,
	|	НДСЗаписиКнигиПокупокОбороты.Событие,
	|	НДСЗаписиКнигиПокупокОбороты.ВидЦенности,
	|	НДСЗаписиКнигиПокупокОбороты.ЗаписьДополнительногоЛиста,
	|	НДСЗаписиКнигиПокупокОбороты.НомерСтроки
	|ПОМЕСТИТЬ ВТСоставКнигиПокупок
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПокупок.Обороты(
	|			&НачалоПериода,
	|			&КонецПериодаГраница,
	|			Запись,
	|			Организация В (&Организация)
	|				И НЕ ВидЦенности В (&ВидыЦенностейАвансыВсе)
	|				И НЕ ЗаписьДополнительногоЛиста) КАК НДСЗаписиКнигиПокупокОбороты
	|ГДЕ
	|	НЕ НДСЗаписиКнигиПокупокОбороты.Регистратор ССЫЛКА Документ.ФормированиеЗаписейКнигиПокупок
	|	И НЕ НДСЗаписиКнигиПокупокОбороты.Регистратор ССЫЛКА Документ.ОтражениеНДСКВычету
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НДСЗаписиКнигиПокупокОбороты.СчетФактура,
	|	НДСЗаписиКнигиПокупокОбороты.СтавкаНДС,
	|	НДСЗаписиКнигиПокупокОбороты.СуммаБезНДСОборот,
	|	НДСЗаписиКнигиПокупокОбороты.НДСОборот,
	|	НДСЗаписиКнигиПокупокОбороты.Событие,
	|	НДСЗаписиКнигиПокупокОбороты.ВидЦенности,
	|	НДСЗаписиКнигиПокупокОбороты.ЗаписьДополнительногоЛиста,
	|	НДСЗаписиКнигиПокупокОбороты.НомерСтроки
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПокупок.Обороты(
	|			&НачалоПериода,
	|			,
	|			Запись,
	|			Организация В (&Организация)
	|				И НЕ ВидЦенности В (&ВидыЦенностейАвансыВсе)
	|				И (ЗаписьДополнительногоЛиста
	|					И (КорректируемыйПериод МЕЖДУ &НачалоПериода И &КонецПериода))) КАК НДСЗаписиКнигиПокупокОбороты
	|ГДЕ
	|	НЕ НДСЗаписиКнигиПокупокОбороты.Регистратор ССЫЛКА Документ.ФормированиеЗаписейКнигиПокупок
	|	И НЕ НДСЗаписиКнигиПокупокОбороты.Регистратор ССЫЛКА Документ.ОтражениеНДСКВычету
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументРегистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПоступлениеТоваровУслугТовары.Номенклатура КАК Номенклатура,
	|	ПоступлениеТоваровУслугТовары.СтавкаНДС КАК СтавкаНДС,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ПоступлениеТоваровУслугТовары.Номенклатура) КАК НоменклатураКоличество
	|ПОМЕСТИТЬ ТаблицаПоступившихТоваров
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.Товары КАК ПоступлениеТоваровУслугТовары
	|ГДЕ
	|	ПоступлениеТоваровУслугТовары.Ссылка.Организация В(&Организация)
	|	И ПоступлениеТоваровУслугТовары.Номенклатура <> &ПустаяНоменклатура
	|	И ПоступлениеТоваровУслугТовары.СтавкаНДС <> ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
	|	И ПоступлениеТоваровУслугТовары.СтавкаНДС <> ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС0)
	|	И ПоступлениеТоваровУслугТовары.Ссылка В
	|			(ВЫБРАТЬ
	|				ВТСоставКнигиПокупок.ДокументРегистратор
	|			ИЗ
	|				ВТСоставКнигиПокупок)
	|
	|СГРУППИРОВАТЬ ПО
	|	ПоступлениеТоваровУслугТовары.Номенклатура,
	|	ПоступлениеТоваровУслугТовары.СтавкаНДС
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.Номенклатура КАК Номенклатура,
	|	СУММА(ВложенныйЗапрос.НоменклатураКоличество) КАК НоменклатураКоличество
	|ПОМЕСТИТЬ СписокПоступившихТоваров
	|ИЗ
	|	ТаблицаПоступившихТоваров КАК ВложенныйЗапрос
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.Номенклатура
	|
	|ИМЕЮЩИЕ
	|	СУММА(ВложенныйЗапрос.НоменклатураКоличество) > 1
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПоступлениеТоваровУслугУслуги.Номенклатура КАК Номенклатура,
	|	ПоступлениеТоваровУслугУслуги.СтавкаНДС КАК СтавкаНДС,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ПоступлениеТоваровУслугУслуги.Номенклатура) КАК НоменклатураКоличество
	|ПОМЕСТИТЬ ТаблицаПоступившихУслуг
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.Услуги КАК ПоступлениеТоваровУслугУслуги
	|ГДЕ
	|	ПоступлениеТоваровУслугУслуги.Ссылка.Организация В(&Организация)
	|	И ПоступлениеТоваровУслугУслуги.СтавкаНДС <> ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
	|	И ПоступлениеТоваровУслугУслуги.СтавкаНДС <> ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС0)
	|	И ПоступлениеТоваровУслугУслуги.Ссылка В
	|			(ВЫБРАТЬ
	|				ВТСоставКнигиПокупок.ДокументРегистратор
	|			ИЗ
	|				ВТСоставКнигиПокупок)
	|	И ПоступлениеТоваровУслугУслуги.Номенклатура <> &ПустаяНоменклатура
	|
	|СГРУППИРОВАТЬ ПО
	|	ПоступлениеТоваровУслугУслуги.Номенклатура,
	|	ПоступлениеТоваровУслугУслуги.СтавкаНДС
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.Номенклатура КАК Номенклатура,
	|	СУММА(ВложенныйЗапрос.НоменклатураКоличество) КАК НоменклатураКоличество
	|ПОМЕСТИТЬ СписокПоступившихУслуг
	|ИЗ
	|	ТаблицаПоступившихУслуг КАК ВложенныйЗапрос
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.Номенклатура
	|
	|ИМЕЮЩИЕ
	|	СУММА(ВложенныйЗапрос.НоменклатураКоличество) > 1
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТСоставКнигиПокупок.ДокументРегистратор,
	|	ВТСоставКнигиПокупок.СтавкаНДС,
	|	ВТСоставКнигиПокупок.СуммаБезНДСОборот КАК Сумма,
	|	ВТСоставКнигиПокупок.НДСОборот КАК СуммаНДС,
	|	&ТекстБезОшибок КАК ПризнакНаличияОшибок,
	|	&ТекстНетСноски КАК СимволСноски,
	|	ЕСТЬNULL(СчетФактураПолученныйДокументыОснования.Ссылка, СчетФактураВыданныйДокументыОснования.Ссылка) КАК СчетФактура,
	|	&ТекстОтраженыВКнигеПокупок КАК ПризнакОтраженияНДС,
	|	1 КАК КоличествоДокументов,
	|	СчетФактураПолученныйДокументыОснования.Ссылка.НомерВходящегоДокумента КАК НомерВходящегоДокумента,
	|	СчетФактураПолученныйДокументыОснования.Ссылка.ДатаВходящегоДокумента КАК ДатаВходящегоДокумента,
	|	0 КАК КоличествоОшибок,
	|	ВТСоставКнигиПокупок.Событие,
	|	ВТСоставКнигиПокупок.ВидЦенности,
	|	ВТСоставКнигиПокупок.ЗаписьДополнительногоЛиста,
	|	ВТСоставКнигиПокупок.НомерСтроки
	|ИЗ
	|	ВТСоставКнигиПокупок КАК ВТСоставКнигиПокупок
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОперацииСОшибками КАК ВТОперацииСОшибками
	|		ПО ВТСоставКнигиПокупок.ДокументРегистратор = ВТОперацииСОшибками.Ссылка
	|			И ВТСоставКнигиПокупок.СтавкаНДС = ВТОперацииСОшибками.СтавкаНДС
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураПолученный.ДокументыОснования КАК СчетФактураПолученныйДокументыОснования
	|		ПО ВТСоставКнигиПокупок.ДокументРегистратор = СчетФактураПолученныйДокументыОснования.ДокументОснование
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураВыданный.ДокументыОснования КАК СчетФактураВыданныйДокументыОснования
	|		ПО ВТСоставКнигиПокупок.ДокументРегистратор = СчетФактураВыданныйДокументыОснования.ДокументОснование";
					  
	ВычетПоПриобретеннымЦенностямУпрощенныйУчетНДСВключенные = Запрос.Выполнить();
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НДСПредъявленныйОбороты.СчетФактура КАК СчетФактура,
	|	НДСПредъявленныйОбороты.ВидЦенности
	|ПОМЕСТИТЬ ВТСписанныйНДС
	|ИЗ
	|	РегистрНакопления.НДСПредъявленный.Обороты(&НачалоПериода, &КонецПериодаГраница, , Организация В (&Организация)) КАК НДСПредъявленныйОбороты
	|ГДЕ
	|	НДСПредъявленныйОбороты.НДСОборот < 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВозвратТоваровОтПокупателя.Ссылка КАК ДокументРегистратор,
	|	ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС18_118) КАК СтавкаНДС,
	|	ВЫРАЗИТЬ(ВозвратТоваровОтПокупателя.СуммаДокумента * 18 / 118 КАК ЧИСЛО(15, 2)) КАК СуммаНДС,
	|	ВЫРАЗИТЬ(ВозвратТоваровОтПокупателя.СуммаДокумента - ВозвратТоваровОтПокупателя.СуммаДокумента * 18 / 118 КАК ЧИСЛО(15, 2)) КАК Сумма
	|ПОМЕСТИТЬ ВТДокументы
	|ИЗ
	|	Документ.ВозвратТоваровОтПокупателя КАК ВозвратТоваровОтПокупателя
	|ГДЕ
	|	ВозвратТоваровОтПокупателя.Проведен
	|	И ВозвратТоваровОтПокупателя.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|	И ВозвратТоваровОтПокупателя.Организация В(&Организация)
	|	И НЕ ВозвратТоваровОтПокупателя.Ссылка В
	|				(ВЫБРАТЬ
	|					ВТСоставКнигиПокупок.ДокументРегистратор
	|				ИЗ
	|					ВТСоставКнигиПокупок)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПоступлениеДопРасходов.Ссылка,
	|	ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС18_118),
	|	ВЫРАЗИТЬ(ПоступлениеДопРасходов.СуммаДокумента * 18 / 118 КАК ЧИСЛО(15, 2)),
	|	ВЫРАЗИТЬ(ПоступлениеДопРасходов.СуммаДокумента - ПоступлениеДопРасходов.СуммаДокумента * 18 / 118 КАК ЧИСЛО(15, 2))
	|ИЗ
	|	Документ.ПоступлениеДопРасходов КАК ПоступлениеДопРасходов
	|ГДЕ
	|	ПоступлениеДопРасходов.НДСВключенВСтоимость
	|	И ПоступлениеДопРасходов.Проведен
	|	И ПоступлениеДопРасходов.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|	И ПоступлениеДопРасходов.Организация В(&Организация)
	|	И НЕ ПоступлениеДопРасходов.Ссылка В
	|				(ВЫБРАТЬ
	|					ВТСоставКнигиПокупок.ДокументРегистратор
	|				ИЗ
	|					ВТСоставКнигиПокупок)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПоступлениеИзПереработки.Ссылка,
	|	ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС18_118),
	|	ВЫРАЗИТЬ(ПоступлениеИзПереработки.СуммаДокумента * 18 / 118 КАК ЧИСЛО(15, 2)),
	|	ВЫРАЗИТЬ(ПоступлениеИзПереработки.СуммаДокумента - ПоступлениеИзПереработки.СуммаДокумента * 18 / 118 КАК ЧИСЛО(15, 2))
	|ИЗ
	|	Документ.ПоступлениеИзПереработки КАК ПоступлениеИзПереработки
	|ГДЕ
	|	ПоступлениеИзПереработки.НДСВключенВСтоимость
	|	И ПоступлениеИзПереработки.Проведен
	|	И ПоступлениеИзПереработки.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|	И ПоступлениеИзПереработки.Организация В(&Организация)
	|	И НЕ ПоступлениеИзПереработки.Ссылка В
	|				(ВЫБРАТЬ
	|					ВТСоставКнигиПокупок.ДокументРегистратор
	|				ИЗ
	|					ВТСоставКнигиПокупок)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПоступлениеНМА.Ссылка,
	|	ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС18_118),
	|	ВЫРАЗИТЬ(ПоступлениеНМА.СуммаДокумента * 18 / 118 КАК ЧИСЛО(15, 2)),
	|	ВЫРАЗИТЬ(ПоступлениеНМА.СуммаДокумента - ПоступлениеНМА.СуммаДокумента * 18 / 118 КАК ЧИСЛО(15, 2))
	|ИЗ
	|	Документ.ПоступлениеНМА КАК ПоступлениеНМА
	|ГДЕ
	|	ПоступлениеНМА.НДСВключенВСтоимость
	|	И ПоступлениеНМА.Проведен
	|	И ПоступлениеНМА.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|	И ПоступлениеНМА.Организация В(&Организация)
	|	И НЕ ПоступлениеНМА.Ссылка В
	|				(ВЫБРАТЬ
	|					ВТСоставКнигиПокупок.ДокументРегистратор
	|				ИЗ
	|					ВТСоставКнигиПокупок)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПоступлениеТоваровУслуг.Ссылка,
	|	ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС18_118),
	|	ВЫРАЗИТЬ(ПоступлениеТоваровУслуг.СуммаДокумента * 18 / 118 КАК ЧИСЛО(15, 2)),
	|	ВЫРАЗИТЬ(ПоступлениеТоваровУслуг.СуммаДокумента - ПоступлениеТоваровУслуг.СуммаДокумента * 18 / 118 КАК ЧИСЛО(15, 2))
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг КАК ПоступлениеТоваровУслуг
	|ГДЕ
	|	ПоступлениеТоваровУслуг.НДСВключенВСтоимость
	|	И ПоступлениеТоваровУслуг.Проведен
	|	И ПоступлениеТоваровУслуг.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|	И ПоступлениеТоваровУслуг.Организация В(&Организация)
	|	И ПоступлениеТоваровУслуг.ВидОперации <> ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеТоваровУслуг.ВПереработку)
	|	И НЕ ПоступлениеТоваровУслуг.Ссылка В
	|				(ВЫБРАТЬ
	|					ВТСоставКнигиПокупок.ДокументРегистратор
	|				ИЗ
	|					ВТСоставКнигиПокупок)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументРегистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТДокументы.ДокументРегистратор,
	|	ВТДокументы.СтавкаНДС,
	|	ВТДокументы.СуммаНДС,
	|	ВТДокументы.Сумма,
	|	1 КАК КоличествоДокументов,
	|	ВЫБОР
	|		КОГДА ВТСписанныйНДС.СчетФактура ЕСТЬ NULL 
	|			ТОГДА &ТекстВычетНеОтраженВКнигеПокупок
	|		ИНАЧЕ &ТекстНДССписанНаРасходы
	|	КОНЕЦ КАК ПризнакНаличияОшибок,
	|	&ТекстНеОтраженыВКнигеПокупок КАК ПризнакОтраженияНДС,
	|	СчетФактураПолученный.Ссылка КАК СчетФактура,
	|	СчетФактураПолученный.Ссылка.НомерВходящегоДокумента КАК НомерВходящегоДокумента,
	|	СчетФактураПолученный.Ссылка.ДатаВходящегоДокумента КАК ДатаВходящегоДокумента,
	|	ВТСписанныйНДС.ВидЦенности
	|ИЗ
	|	ВТДокументы КАК ВТДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСписанныйНДС КАК ВТСписанныйНДС
	|		ПО (ВТСписанныйНДС.СчетФактура = ВТДокументы.ДокументРегистратор)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураПолученный.ДокументыОснования КАК СчетФактураПолученный
	|		ПО (СчетФактураПолученный.ДокументОснование = ВТДокументы.ДокументРегистратор)";

	ВычетПоПриобретеннымЦенностямУпрощенныйУчетНДСНеВключенные = Запрос.Выполнить();
	
				
	ВнешнийНаборДанныхВычетПоПриобретеннымЦенностямУпрощенныйУчетНДС = Новый Структура;
	ВнешнийНаборДанныхВычетПоПриобретеннымЦенностямУпрощенныйУчетНДС.Вставить("ВычетПоПриобретеннымЦенностямУпрощенныйУчетНДСВключенные",
																			  ВычетПоПриобретеннымЦенностямУпрощенныйУчетНДСВключенные);
	ВнешнийНаборДанныхВычетПоПриобретеннымЦенностямУпрощенныйУчетНДС.Вставить("ВычетПоПриобретеннымЦенностямУпрощенныйУчетНДСНеВключенные",
																			  ВычетПоПриобретеннымЦенностямУпрощенныйУчетНДСНеВключенные);
			
	СтруктураВнешнихНаборов.Вставить("ВнешнийНаборДанныхВычетПоПриобретеннымЦенностямУпрощенныйУчетНДС",
									 ВнешнийНаборДанныхВычетПоПриобретеннымЦенностямУпрощенныйУчетНДС);
	
КонецПроцедуры // ПолучитьНаборДанныхВычетПоПриобретеннымЦенностямУпрощенныйУчетНДС()

// Выполняет запрос к базе данных для получения набора данных, необходимого для формирования расшифровки по
//	вычету НДС с выданных авансов в случае ведения упрощенного учета НДС
//
// Параметры
//  Запрос  - Запрос - запрос к базе данных
//  СтруктураВнешнихНаборов - структура внешних источников данных для расшифровки.
//
// Возвращаемое значение:
//   Структура   - структура параметров для формирования карты отчета
//
Процедура ПолучитьНаборДанныхВычетАвансыВыданныеУпрощенныйУчетНДС(Запрос, СтруктураВнешнихНаборов) 
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ХозрасчетныйОбороты.Регистратор КАК ДокументРегистратор,
	|	ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС18_118) КАК СтавкаНДСПредположительная,
	|	ХозрасчетныйОбороты.Субконто2 КАК ДоговорКонтрагента,
	|	ВЫРАЗИТЬ(ХозрасчетныйОбороты.СуммаОборотДт - ХозрасчетныйОбороты.СуммаОборотДт * 18 / 118 КАК ЧИСЛО(15, 2)) КАК СуммаБезНДС,
	|	ВЫРАЗИТЬ(ХозрасчетныйОбороты.СуммаОборотДт * 18 / 118 КАК ЧИСЛО(15, 2)) КАК СуммаНДС
	|ПОМЕСТИТЬ ВТАвансыВыданныеВременнаяТаблица
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоПериода, &КонецПериодаГраница, Регистратор, Счет В (&СписокСчетовАвансовВыданных), &ВидыСубконтоДокументыДоговоры, Организация В (&Организация), , ) КАК ХозрасчетныйОбороты
	|ГДЕ
	|	ХозрасчетныйОбороты.СуммаОборотДт > 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументРегистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасшифровкаПлатежа.Ссылка КАК Ссылка,
	|	РасшифровкаПлатежа.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	ВЫБОР
	|		КОГДА РасшифровкаПлатежа.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС18)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС18_118)
	|		КОГДА РасшифровкаПлатежа.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС10)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС10_110)
	|	КОНЕЦ КАК СтавкаНДС,
	|	СУММА(РасшифровкаПлатежа.СуммаНДС) КАК СуммаНДС,
	|	СУММА(РасшифровкаПлатежа.СуммаПлатежа - РасшифровкаПлатежа.СуммаНДС) КАК СуммаБезНДС
	|ПОМЕСТИТЬ СуммыПлатежей
	|ИЗ
	|	Документ.СписаниеСРасчетногоСчета.РасшифровкаПлатежа КАК РасшифровкаПлатежа
	|ГДЕ
	|	РасшифровкаПлатежа.Ссылка В
	|			(ВЫБРАТЬ
	|				ВТАвансыВыданныеВременнаяТаблица.ДокументРегистратор
	|			ИЗ
	|				ВТАвансыВыданныеВременнаяТаблица)
	|
	|СГРУППИРОВАТЬ ПО
	|	РасшифровкаПлатежа.Ссылка,
	|	РасшифровкаПлатежа.ДоговорКонтрагента,
	|	РасшифровкаПлатежа.СтавкаНДС
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РасшифровкаПлатежа.Ссылка,
	|	РасшифровкаПлатежа.ДоговорКонтрагента,
	|	ВЫБОР
	|		КОГДА РасшифровкаПлатежа.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС18)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС18_118)
	|		КОГДА РасшифровкаПлатежа.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС10)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС10_110)
	|	КОНЕЦ,
	|	СУММА(РасшифровкаПлатежа.СуммаНДС),
	|	СУММА(РасшифровкаПлатежа.СуммаПлатежа - РасшифровкаПлатежа.СуммаНДС)
	|ИЗ
	|	Документ.РасходныйКассовыйОрдер.РасшифровкаПлатежа КАК РасшифровкаПлатежа
	|ГДЕ
	|	РасшифровкаПлатежа.Ссылка В
	|			(ВЫБРАТЬ
	|				ВТАвансыВыданныеВременнаяТаблица.ДокументРегистратор
	|			ИЗ
	|				ВТАвансыВыданныеВременнаяТаблица)
	|
	|СГРУППИРОВАТЬ ПО
	|	РасшифровкаПлатежа.Ссылка,
	|	РасшифровкаПлатежа.ДоговорКонтрагента,
	|	РасшифровкаПлатежа.СтавкаНДС
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТАвансыВыданныеВременнаяТаблица.ДокументРегистратор КАК ДокументРегистратор,
	|	ВТАвансыВыданныеВременнаяТаблица.ДоговорКонтрагента,
	|	ЕСТЬNULL(СуммыПлатежей.СтавкаНДС, ВТАвансыВыданныеВременнаяТаблица.СтавкаНДСПредположительная) КАК СтавкаНДС,
	|	ЕСТЬNULL(СуммыПлатежей.СуммаБезНДС, ВТАвансыВыданныеВременнаяТаблица.СуммаБезНДС) КАК Сумма,
	|	ЕСТЬNULL(СуммыПлатежей.СуммаНДС, ВТАвансыВыданныеВременнаяТаблица.СуммаНДС) КАК СуммаНДС
	|ПОМЕСТИТЬ ВТАвансыВыданные
	|ИЗ
	|	ВТАвансыВыданныеВременнаяТаблица КАК ВТАвансыВыданныеВременнаяТаблица
	|		ЛЕВОЕ СОЕДИНЕНИЕ СуммыПлатежей КАК СуммыПлатежей
	|		ПО ВТАвансыВыданныеВременнаяТаблица.ДокументРегистратор = СуммыПлатежей.Ссылка
	|			И ВТАвансыВыданныеВременнаяТаблица.ДоговорКонтрагента = СуммыПлатежей.ДоговорКонтрагента
	|ГДЕ
	|	ЕСТЬNULL(СуммыПлатежей.СуммаНДС, ВТАвансыВыданныеВременнаяТаблица.СуммаНДС) > 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументРегистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДСЗаписиКнигиПокупокОбороты.СчетФактура КАК ДокументРегистратор,
	|	НДСЗаписиКнигиПокупокОбороты.СтавкаНДС КАК СтавкаНДС,
	|	НДСЗаписиКнигиПокупокОбороты.СуммаБезНДСОборот КАК Сумма,
	|	НДСЗаписиКнигиПокупокОбороты.НДСОборот КАК СуммаНДС,
	|	НДСЗаписиКнигиПокупокОбороты.Событие,
	|	НДСЗаписиКнигиПокупокОбороты.ВидЦенности,
	|	НДСЗаписиКнигиПокупокОбороты.ЗаписьДополнительногоЛиста,
	|	НДСЗаписиКнигиПокупокОбороты.ДоговорКонтрагента,
	|	НДСЗаписиКнигиПокупокОбороты.НомерСтроки
	|ПОМЕСТИТЬ СоставКнигиПокупокВЧастиАвансов
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПокупок.Обороты(
	|			&НачалоПериода,
	|			&КонецПериодаГраница,
	|			Запись,
	|			Организация В (&Организация)
	|				И ВидЦенности = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.АвансыВыданные)
	|				И НЕ ЗаписьДополнительногоЛиста) КАК НДСЗаписиКнигиПокупокОбороты
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НДСЗаписиКнигиПокупокОбороты.СчетФактура,
	|	НДСЗаписиКнигиПокупокОбороты.СтавкаНДС,
	|	НДСЗаписиКнигиПокупокОбороты.СуммаБезНДСОборот,
	|	НДСЗаписиКнигиПокупокОбороты.НДСОборот,
	|	НДСЗаписиКнигиПокупокОбороты.Событие,
	|	НДСЗаписиКнигиПокупокОбороты.ВидЦенности,
	|	НДСЗаписиКнигиПокупокОбороты.ЗаписьДополнительногоЛиста,
	|	НДСЗаписиКнигиПокупокОбороты.ДоговорКонтрагента,
	|	НДСЗаписиКнигиПокупокОбороты.НомерСтроки
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПокупок.Обороты(
	|			&НачалоПериода,
	|			,
	|			Запись,
	|			Организация В (&Организация)
	|				И ВидЦенности = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.АвансыВыданные)
	|				И (ЗаписьДополнительногоЛиста
	|					И (КорректируемыйПериод МЕЖДУ &НачалоПериода И &КонецПериода))) КАК НДСЗаписиКнигиПокупокОбороты
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументРегистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Авансы.ДокументРегистратор КАК ДокументРегистратор,
	|	СчетФактураПолученныйДокументыОснования.Ссылка КАК СчетФактура,
	|	ЕСТЬNULL(СоставКнигиПокупокВЧастиАвансов.СтавкаНДС, Авансы.СтавкаНДС) КАК СтавкаНДС,
	|	ЕСТЬNULL(СоставКнигиПокупокВЧастиАвансов.Сумма, Авансы.Сумма) КАК Сумма,
	|	ЕСТЬNULL(СоставКнигиПокупокВЧастиАвансов.СуммаНДС, Авансы.СуммаНДС) КАК СуммаНДС,
	|	ВЫБОР
	|		КОГДА СчетФактураПолученныйДокументыОснования.Ссылка ЕСТЬ NULL 
	|			ТОГДА &ТекстВыданныйАвансБезСФ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА СчетФактураПолученныйДокументыОснования.Ссылка.Проведен
	|					ТОГДА ВЫБОР
	|							КОГДА СоставКнигиПокупокВЧастиАвансов.ДокументРегистратор ЕСТЬ NULL 
	|								ТОГДА &ТекстВычетНеОтраженВКнигеПокупок
	|							ИНАЧЕ &ТекстБезОшибок
	|						КОНЕЦ
	|				ИНАЧЕ &ТекстСФНаВыданныйАвансНеПроведен
	|			КОНЕЦ
	|	КОНЕЦ КАК ПризнакНаличияОшибок,
	|	ВЫБОР
	|		КОГДА СоставКнигиПокупокВЧастиАвансов.ДокументРегистратор ЕСТЬ NULL 
	|			ТОГДА &ТекстНеОтраженыВКнигеПокупок
	|		ИНАЧЕ &ТекстОтраженыВКнигеПокупок
	|	КОНЕЦ КАК ПризнакОтраженияНДС,
	|	ВЫБОР
	|		КОГДА СоставКнигиПокупокВЧастиАвансов.ДокументРегистратор ЕСТЬ NULL 
	|			ТОГДА ЕСТЬNULL(СоставКнигиПокупокВЧастиАвансов.СуммаНДС, Авансы.СуммаНДС)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаНеВключеннаяВБазу,
	|	ВЫБОР
	|		КОГДА СоставКнигиПокупокВЧастиАвансов.ДокументРегистратор ЕСТЬ NULL 
	|			ТОГДА 0
	|		ИНАЧЕ ЕСТЬNULL(СоставКнигиПокупокВЧастиАвансов.СуммаНДС, Авансы.СуммаНДС)
	|	КОНЕЦ КАК СуммаВключеннаяВБазу,
	|	1 КАК КоличествоДокументов,
	|	ЕСТЬNULL(СоставКнигиПокупокВЧастиАвансов.ВидЦенности, ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПустаяСсылка)) КАК ВидЦенности,
	|	ЕСТЬNULL(СоставКнигиПокупокВЧастиАвансов.Событие, ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПродажи.ПустаяСсылка)) КАК Событие,
	|	ЕСТЬNULL(СоставКнигиПокупокВЧастиАвансов.ЗаписьДополнительногоЛиста, ЛОЖЬ) КАК ЗаписьДополнительногоЛиста,
	|	СоставКнигиПокупокВЧастиАвансов.НомерСтроки
	|ИЗ
	|	ВТАвансыВыданные КАК Авансы
	|		ЛЕВОЕ СОЕДИНЕНИЕ СоставКнигиПокупокВЧастиАвансов КАК СоставКнигиПокупокВЧастиАвансов
	|		ПО Авансы.ДокументРегистратор = СоставКнигиПокупокВЧастиАвансов.ДокументРегистратор
	|			И Авансы.ДоговорКонтрагента = СоставКнигиПокупокВЧастиАвансов.ДоговорКонтрагента
	|			И Авансы.СтавкаНДС = СоставКнигиПокупокВЧастиАвансов.СтавкаНДС
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураПолученный.ДокументыОснования КАК СчетФактураПолученныйДокументыОснования
	|		ПО Авансы.ДокументРегистратор = СчетФактураПолученныйДокументыОснования.ДокументОснование
	|			И Авансы.ДоговорКонтрагента = СчетФактураПолученныйДокументыОснования.Ссылка.ДоговорКонтрагента
	|			И НЕ СчетФактураПолученныйДокументыОснования.Ссылка.ПометкаУдаления
	|			И (СчетФактураПолученныйДокументыОснования.Ссылка.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыПолученного.НаАванс)
	|				ИЛИ СчетФактураПолученныйДокументыОснования.Ссылка.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыПолученного.НаАвансКомитента))";
	
	ВычетАвансыВыданныеУпрощенныйУчетНДС = Запрос.Выполнить();
	
	ВнешнийНаборДанныхВычетАвансыВыданныеУпрощенныйУчетНДС = Новый Структура;
	
	ВнешнийНаборДанныхВычетАвансыВыданныеУпрощенныйУчетНДС.Вставить("ВычетАвансыВыданныеУпрощенныйУчетНДС",
																		ВычетАвансыВыданныеУпрощенныйУчетНДС);
		
	СтруктураВнешнихНаборов.Вставить("ВнешнийНаборДанныхВычетАвансыВыданныеУпрощенныйУчетНДС",
									 	ВнешнийНаборДанныхВычетАвансыВыданныеУпрощенныйУчетНДС);
	
КонецПроцедуры // ПолучитьНаборДанныхВычетАвансыВыданныеУпрощенныйУчетНДС()

// Выполняет запрос к базе данных для получения набора данных, необходимого для формирования расшифровки по
//	вычету НДС с полученных авансов (по факту реализации) в случае ведения упрощенного учета НДС
//
// Параметры
//  Запрос  - Запрос - запрос к базе данных
//  СтруктураВнешнихНаборов - структура внешних источников данных для расшифровки.
//
// Возвращаемое значение:
//   Структура   - структура параметров для формирования карты отчета
//
Процедура ПолучитьНаборДанныхВычетСАвансовПолученныхУпрощенныйУчетНДС(Запрос, СтруктураВнешнихНаборов) 
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ХозрасчетныйОстатки.Субконто1 КАК Субконто1
	|ПОМЕСТИТЬ ВТХозрасчетныйОстаткиСАвансовПолученныхУпрощенныйУчет
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(&НачалоСледующегоПериода, Счет = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НДСПоАвансамИПредоплатам), ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.СФВыданные), Организация В (&Организация)) КАК ХозрасчетныйОстатки
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Субконто1
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДСЗаписиКнигиПродажОбороты.СчетФактура КАК СчетФактура,
	|	НДСЗаписиКнигиПродажОбороты.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	НДСЗаписиКнигиПродажОбороты.ВидЦенности КАК ВидЦенности,
	|	НДСЗаписиКнигиПродажОбороты.СтавкаНДС КАК СтавкаНДС,
	|	НДСЗаписиКнигиПродажОбороты.Покупатель,
	|	НДСЗаписиКнигиПродажОбороты.СуммаБезНДСОборот,
	|	НДСЗаписиКнигиПродажОбороты.НДСОборот,
	|	НДСЗаписиКнигиПродажОбороты.Событие,
	|	НДСЗаписиКнигиПродажОбороты.ЗаписьДополнительногоЛиста,
	|	НДСЗаписиКнигиПродажОбороты.НомерСтроки
	|ПОМЕСТИТЬ ВТНДСЗаписиКнигиПродажОборотыСАвансовПолученныхУпрощенныйУчет
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПродаж.Обороты(
	|			,
	|			&КонецПериодаГраница,
	|			Запись,
	|			Организация В (&Организация)
	|				И СчетФактура В
	|					(ВЫБРАТЬ
	|						ХозрасчетныйОстатки.Субконто1 КАК СчетФактура
	|					ИЗ
	|						ВТХозрасчетныйОстаткиСАвансовПолученныхУпрощенныйУчет КАК ХозрасчетныйОстатки)
	|				И ВидЦенности В (&ВидыЦенностейАвансыПолученные)
	|				И НЕ ЗаписьДополнительногоЛиста) КАК НДСЗаписиКнигиПродажОбороты
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НДСЗаписиКнигиПродажОбороты.СчетФактура,
	|	НДСЗаписиКнигиПродажОбороты.ДоговорКонтрагента,
	|	НДСЗаписиКнигиПродажОбороты.ВидЦенности,
	|	НДСЗаписиКнигиПродажОбороты.СтавкаНДС,
	|	НДСЗаписиКнигиПродажОбороты.Покупатель,
	|	НДСЗаписиКнигиПродажОбороты.СуммаБезНДСОборот,
	|	НДСЗаписиКнигиПродажОбороты.НДСОборот,
	|	НДСЗаписиКнигиПродажОбороты.Событие,
	|	НДСЗаписиКнигиПродажОбороты.ЗаписьДополнительногоЛиста,
	|	НДСЗаписиКнигиПродажОбороты.НомерСтроки
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПродаж.Обороты(
	|			,
	|			,
	|			Запись,
	|			Организация В (&Организация)
	|				И СчетФактура В
	|					(ВЫБРАТЬ
	|						ХозрасчетныйОстатки.Субконто1 КАК СчетФактура
	|					ИЗ
	|						ВТХозрасчетныйОстаткиСАвансовПолученныхУпрощенныйУчет КАК ХозрасчетныйОстатки)
	|				И ВидЦенности В (&ВидыЦенностейАвансыПолученные)
	|				И (ЗаписьДополнительногоЛиста
	|					И (КорректируемыйПериод МЕЖДУ &НачалоПериода И &КонецПериода))) КАК НДСЗаписиКнигиПродажОбороты
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактура,
	|	ДоговорКонтрагента,
	|	ВидЦенности,
	|	СтавкаНДС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДСЗаписиКнигиПокупокОбороты.СчетФактура КАК СчетФактура,
	|	НДСЗаписиКнигиПокупокОбороты.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	НДСЗаписиКнигиПокупокОбороты.ВидЦенности КАК ВидЦенности,
	|	НДСЗаписиКнигиПокупокОбороты.СтавкаНДС КАК СтавкаНДС,
	|	НДСЗаписиКнигиПокупокОбороты.СуммаБезНДСОборот,
	|	НДСЗаписиКнигиПокупокОбороты.НДСОборот
	|ПОМЕСТИТЬ ВТНДСЗаписиКнигиПокупокОборотыСАвансовПолученныхУпрощенныйУчет
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПокупок.Обороты(
	|			,
	|			&КонецПериодаГраница,
	|			,
	|			Организация В (&Организация)
	|				И СчетФактура В
	|					(ВЫБРАТЬ
	|						ХозрасчетныйОстатки.Субконто1 КАК СчетФактура
	|					ИЗ
	|						ВТХозрасчетныйОстаткиСАвансовПолученныхУпрощенныйУчет КАК ХозрасчетныйОстатки)
	|				И ВидЦенности В (&ВидыЦенностейАвансыПолученные)
	|				И НЕ ЗаписьДополнительногоЛиста) КАК НДСЗаписиКнигиПокупокОбороты
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НДСЗаписиКнигиПокупокОбороты.СчетФактура,
	|	НДСЗаписиКнигиПокупокОбороты.ДоговорКонтрагента,
	|	НДСЗаписиКнигиПокупокОбороты.ВидЦенности,
	|	НДСЗаписиКнигиПокупокОбороты.СтавкаНДС,
	|	НДСЗаписиКнигиПокупокОбороты.СуммаБезНДСОборот,
	|	НДСЗаписиКнигиПокупокОбороты.НДСОборот
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПокупок.Обороты(
	|			,
	|			,
	|			,
	|			Организация В (&Организация)
	|				И СчетФактура В
	|					(ВЫБРАТЬ
	|						ХозрасчетныйОстатки.Субконто1 КАК СчетФактура
	|					ИЗ
	|						ВТХозрасчетныйОстаткиСАвансовПолученныхУпрощенныйУчет КАК ХозрасчетныйОстатки)
	|				И ВидЦенности В (&ВидыЦенностейАвансыПолученные)
	|				И (ЗаписьДополнительногоЛиста
	|					И (КорректируемыйПериод МЕЖДУ &НачалоПериода И &КонецПериода))) КАК НДСЗаписиКнигиПокупокОбороты
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактура,
	|	ДоговорКонтрагента,
	|	ВидЦенности,
	|	СтавкаНДС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ХозрасчетныйОстатки.Субконто1 КАК Субконто1,
	|	ХозрасчетныйОстатки.СуммаОстатокКт
	|ПОМЕСТИТЬ ХозрасчетныйОстаткиСАвансовПолученныхУпрощенныйУчет
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(&НачалоСледующегоПериода, Счет В (&СписокСчетовАвансовПолученных), ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ДокументыРасчетовСКонтрагентами), Организация В (&Организация)) КАК ХозрасчетныйОстатки
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Субконто1
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДСЗаписиКнигиПродажОбороты.СтавкаНДС КАК СтавкаНДС,
	|	НДСЗаписиКнигиПродажОбороты.СчетФактура КАК ДокументРегистратор,
	|	НДСЗаписиКнигиПродажОбороты.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	СчетФактураВыданныйДокументыОснования.Ссылка КАК СчетФактура,
	|	СУММА(ЕСТЬNULL(НДСЗаписиКнигиПродажОбороты.СуммаБезНДСОборот, 0) - ЕСТЬNULL(НДСЗаписиКнигиПокупокОбороты.СуммаБезНДСОборот, 0)) КАК Сумма,
	|	СУММА(ЕСТЬNULL(НДСЗаписиКнигиПродажОбороты.НДСОборот, 0) - ЕСТЬNULL(НДСЗаписиКнигиПокупокОбороты.НДСОборот, 0)) КАК СуммаНДС,
	|	ХозрасчетныйОстатки.СуммаОстатокКт,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(НДСЗаписиКнигиПродажОбороты.СуммаБезНДСОборот, 0) - ЕСТЬNULL(НДСЗаписиКнигиПокупокОбороты.СуммаБезНДСОборот, 0) + ЕСТЬNULL(НДСЗаписиКнигиПродажОбороты.НДСОборот, 0) - ЕСТЬNULL(НДСЗаписиКнигиПокупокОбороты.НДСОборот, 0) - ХозрасчетныйОстатки.СуммаОстатокКт = 0
	|			ТОГДА &ТекстОплаченныеЦенностиНеРеализованы
	|		ИНАЧЕ &ТекстВычетНеОтраженВКнигеПокупок
	|	КОНЕЦ КАК ПризнакНаличияОшибок,
	|	&ТекстНеОтраженыВКнигеПокупок КАК ПризнакОтраженияНДС,
	|	1 КАК КоличествоДокументов,
	|	НДСЗаписиКнигиПродажОбороты.Событие,
	|	НДСЗаписиКнигиПродажОбороты.ВидЦенности,
	|	НДСЗаписиКнигиПродажОбороты.ЗаписьДополнительногоЛиста,
	|	НДСЗаписиКнигиПродажОбороты.НомерСтроки
	|ИЗ
	|	ВТНДСЗаписиКнигиПродажОборотыСАвансовПолученныхУпрощенныйУчет КАК НДСЗаписиКнигиПродажОбороты
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНДСЗаписиКнигиПокупокОборотыСАвансовПолученныхУпрощенныйУчет КАК НДСЗаписиКнигиПокупокОбороты
	|		ПО (НДСЗаписиКнигиПокупокОбороты.СчетФактура = НДСЗаписиКнигиПродажОбороты.СчетФактура)
	|			И (НДСЗаписиКнигиПокупокОбороты.ДоговорКонтрагента = НДСЗаписиКнигиПродажОбороты.ДоговорКонтрагента)
	|			И (НДСЗаписиКнигиПокупокОбороты.ВидЦенности = НДСЗаписиКнигиПродажОбороты.ВидЦенности)
	|			И (НДСЗаписиКнигиПокупокОбороты.СтавкаНДС = НДСЗаписиКнигиПродажОбороты.СтавкаНДС)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ХозрасчетныйОстаткиСАвансовПолученныхУпрощенныйУчет КАК ХозрасчетныйОстатки
	|		ПО НДСЗаписиКнигиПродажОбороты.СчетФактура = ХозрасчетныйОстатки.Субконто1
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураВыданный.ДокументыОснования КАК СчетФактураВыданныйДокументыОснования
	|		ПО НДСЗаписиКнигиПродажОбороты.СчетФактура = СчетФактураВыданныйДокументыОснования.ДокументОснование
	|			И НДСЗаписиКнигиПродажОбороты.ДоговорКонтрагента = СчетФактураВыданныйДокументыОснования.Ссылка.ДоговорКонтрагента
	|			И (СчетФактураВыданныйДокументыОснования.Ссылка.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.НаАванс)
	|				ИЛИ СчетФактураВыданныйДокументыОснования.Ссылка.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.НаАвансКомитента))
	|ГДЕ
	|	ЕСТЬNULL(НДСЗаписиКнигиПродажОбороты.СуммаБезНДСОборот, 0) - ЕСТЬNULL(НДСЗаписиКнигиПокупокОбороты.СуммаБезНДСОборот, 0) > 0
	|	И ЕСТЬNULL(НДСЗаписиКнигиПродажОбороты.НДСОборот, 0) - ЕСТЬNULL(НДСЗаписиКнигиПокупокОбороты.НДСОборот, 0) > 0
	|	И НЕ СчетФактураВыданныйДокументыОснования.Ссылка.ПометкаУдаления
	|
	|СГРУППИРОВАТЬ ПО
	|	НДСЗаписиКнигиПродажОбороты.Покупатель,
	|	НДСЗаписиКнигиПродажОбороты.ДоговорКонтрагента,
	|	НДСЗаписиКнигиПродажОбороты.СчетФактура,
	|	СчетФактураВыданныйДокументыОснования.Ссылка,
	|	НДСЗаписиКнигиПродажОбороты.СтавкаНДС,
	|	НДСЗаписиКнигиПокупокОбороты.СуммаБезНДСОборот,
	|	НДСЗаписиКнигиПокупокОбороты.НДСОборот,
	|	ХозрасчетныйОстатки.СуммаОстатокКт,
	|	НДСЗаписиКнигиПродажОбороты.ВидЦенности,
	|	НДСЗаписиКнигиПродажОбороты.Событие,
	|	НДСЗаписиКнигиПродажОбороты.ЗаписьДополнительногоЛиста,
	|	НДСЗаписиКнигиПродажОбороты.НомерСтроки,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(НДСЗаписиКнигиПродажОбороты.СуммаБезНДСОборот, 0) - ЕСТЬNULL(НДСЗаписиКнигиПокупокОбороты.СуммаБезНДСОборот, 0) + ЕСТЬNULL(НДСЗаписиКнигиПродажОбороты.НДСОборот, 0) - ЕСТЬNULL(НДСЗаписиКнигиПокупокОбороты.НДСОборот, 0) - ХозрасчетныйОстатки.СуммаОстатокКт = 0
	|			ТОГДА &ТекстОплаченныеЦенностиНеРеализованы
	|		ИНАЧЕ &ТекстВычетНеОтраженВКнигеПокупок
	|	КОНЕЦ";
	
	ВычетСАвансовПолученныхУпрощенныйУчетНДСНеВключенные = Запрос.Выполнить();
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НДСЗаписиКнигиПокупок.СчетФактура КАК СчетФактура,
	|	НДСЗаписиКнигиПокупок.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	НДСЗаписиКнигиПокупок.СтавкаНДС,
	|	НДСЗаписиКнигиПокупок.СуммаБезНДСОборот,
	|	НДСЗаписиКнигиПокупок.НДСОборот,
	|	НДСЗаписиКнигиПокупок.Событие,
	|	НДСЗаписиКнигиПокупок.ВидЦенности,
	|	НДСЗаписиКнигиПокупок.ЗаписьДополнительногоЛиста,
	|	НДСЗаписиКнигиПокупок.НомерСтроки
	|ПОМЕСТИТЬ ВТНДСЗаписиКнигиПокупокВЧастиАвансовПолученных
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПокупок.Обороты(
	|			&НачалоПериода,
	|			&КонецПериодаГраница,
	|			Запись,
	|			Организация В (&Организация)
	|				И ВидЦенности В (&ВидыЦенностейАвансыПолученные)
	|				И НЕ ЗаписьДополнительногоЛиста) КАК НДСЗаписиКнигиПокупок
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НДСЗаписиКнигиПокупок.СчетФактура,
	|	НДСЗаписиКнигиПокупок.ДоговорКонтрагента,
	|	НДСЗаписиКнигиПокупок.СтавкаНДС,
	|	НДСЗаписиКнигиПокупок.СуммаБезНДСОборот,
	|	НДСЗаписиКнигиПокупок.НДСОборот,
	|	НДСЗаписиКнигиПокупок.Событие,
	|	НДСЗаписиКнигиПокупок.ВидЦенности,
	|	НДСЗаписиКнигиПокупок.ЗаписьДополнительногоЛиста,
	|	НДСЗаписиКнигиПокупок.НомерСтроки
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПокупок.Обороты(
	|			&НачалоПериода,
	|			,
	|			Запись,
	|			Организация В (&Организация)
	|				И ВидЦенности В (&ВидыЦенностейАвансыПолученные)
	|				И (ЗаписьДополнительногоЛиста
	|					И (КорректируемыйПериод МЕЖДУ &НачалоПериода И &КонецПериода))) КАК НДСЗаписиКнигиПокупок
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДСЗаписиКнигиПокупок.СтавкаНДС КАК СтавкаНДС,
	|	НДСЗаписиКнигиПокупок.СчетФактура КАК ДокументРегистратор,
	|	НДСЗаписиКнигиПокупок.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	СчетФактураВыданныйДокументыОснования.Ссылка КАК СчетФактура,
	|	НДСЗаписиКнигиПокупок.СуммаБезНДСОборот КАК Сумма,
	|	НДСЗаписиКнигиПокупок.НДСОборот КАК СуммаНДС,
	|	ХозрасчетныйОстатки.СуммаОстатокКт,
	|	ВЫБОР
	|		КОГДА ХозрасчетныйОстатки.СуммаОстатокКт ЕСТЬ NULL 
	|			ТОГДА &ТекстНДСПринятКВычетуПолностью
	|		ИНАЧЕ &ТекстНДСПринятКВычетуЧастично
	|	КОНЕЦ КАК ПризнакНаличияОшибок,
	|	&ТекстОтраженыВКнигеПокупок КАК ПризнакОтраженияНДС,
	|	1 КАК КоличествоДокументов,
	|	НДСЗаписиКнигиПокупок.ВидЦенности,
	|	НДСЗаписиКнигиПокупок.Событие,
	|	НДСЗаписиКнигиПокупок.ЗаписьДополнительногоЛиста,
	|	НДСЗаписиКнигиПокупок.НомерСтроки
	|ИЗ
	|	ВТНДСЗаписиКнигиПокупокВЧастиАвансовПолученных КАК НДСЗаписиКнигиПокупок
	|		ЛЕВОЕ СОЕДИНЕНИЕ ХозрасчетныйОстаткиСАвансовПолученныхУпрощенныйУчет КАК ХозрасчетныйОстатки
	|		ПО НДСЗаписиКнигиПокупок.СчетФактура = ХозрасчетныйОстатки.Субконто1
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураВыданный.ДокументыОснования КАК СчетФактураВыданныйДокументыОснования
	|		ПО (НДСЗаписиКнигиПокупок.СчетФактура = СчетФактураВыданныйДокументыОснования.ДокументОснование
	|					И НДСЗаписиКнигиПокупок.ДоговорКонтрагента = СчетФактураВыданныйДокументыОснования.Ссылка.ДоговорКонтрагента
	|				ИЛИ НДСЗаписиКнигиПокупок.СчетФактура = СчетФактураВыданныйДокументыОснования.Ссылка)
	|			И (СчетФактураВыданныйДокументыОснования.Ссылка.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.НаАванс)
	|				ИЛИ СчетФактураВыданныйДокументыОснования.Ссылка.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.НаАвансКомитента))
	|ГДЕ
	|	НЕ СчетФактураВыданныйДокументыОснования.Ссылка.ПометкаУдаления";
	
	ВычетСАвансовПолученныхУпрощенныйУчетНДСВключенные = Запрос.Выполнить();
	
	ВнешнийНаборДанныхВычетСАвансовПолученныхУпрощенныйУчетНДС = Новый Структура;
	
	ВнешнийНаборДанныхВычетСАвансовПолученныхУпрощенныйУчетНДС.Вставить("ВычетСАвансовПолученныхУпрощенныйУчетНДСВключенные",
	ВычетСАвансовПолученныхУпрощенныйУчетНДСВключенные);
	
	ВнешнийНаборДанныхВычетСАвансовПолученныхУпрощенныйУчетНДС.Вставить("ВычетСАвансовПолученныхУпрощенныйУчетНДСНеВключенные",
	ВычетСАвансовПолученныхУпрощенныйУчетНДСНеВключенные);
	
	СтруктураВнешнихНаборов.Вставить("ВнешнийНаборДанныхВычетСАвансовПолученныхУпрощенныйУчетНДС",
	ВнешнийНаборДанныхВычетСАвансовПолученныхУпрощенныйУчетНДС);
		
КонецПроцедуры // ПолучитьНаборДанныхВычетСАвансовПолученныхУпрощенныйУчетНДС()

// Выполняет запрос к базе данных для получения набора данных, необходимого для формирования расшифровки по
//	вычету НДС по прочим операциям в случае ведения упрощенного учета НДС
//
// Параметры
//  Запрос  - Запрос - запрос к базе данных
//  СтруктураВнешнихНаборов - структура внешних источников данных для расшифровки.
//
// Возвращаемое значение:
//   Структура   - структура параметров для формирования карты отчета
//
Процедура ПолучитьНаборДанныхВычетДругиеОперацииУпрощенныйУчетНДС(Запрос, СтруктураВнешнихНаборов) 

	//	В данном разделе отражаются вычеты НДС введенные пользователем, кроме любых видов авансов
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НДСЗаписиКнигиПокупокОбороты.Регистратор КАК ДокументРегистратор,
	|	НДСЗаписиКнигиПокупокОбороты.СтавкаНДС,
	|	НДСЗаписиКнигиПокупокОбороты.СуммаБезНДСОборот КАК Сумма,
	|	НДСЗаписиКнигиПокупокОбороты.НДСОборот КАК СуммаНДС,
	|	НДСЗаписиКнигиПокупокОбороты.Событие,
	|	НДСЗаписиКнигиПокупокОбороты.ВидЦенности,
	|	НДСЗаписиКнигиПокупокОбороты.ЗаписьДополнительногоЛиста,
	|	НДСЗаписиКнигиПокупокОбороты.НомерСтроки
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПокупок.Обороты(
	|			&НачалоПериода,
	|			&КонецПериодаГраница,
	|			Запись,
	|			Организация В (&Организация)
	|				И НЕ ВидЦенности В (&ВидыЦенностейАвансыПолученные, ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.АвансыВыданные))
	|				И НЕ ЗаписьДополнительногоЛиста) КАК НДСЗаписиКнигиПокупокОбороты
	|ГДЕ
	|	НДСЗаписиКнигиПокупокОбороты.Регистратор ССЫЛКА Документ.ОтражениеНДСКВычету
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НДСЗаписиКнигиПокупокОбороты.Регистратор,
	|	НДСЗаписиКнигиПокупокОбороты.СтавкаНДС,
	|	НДСЗаписиКнигиПокупокОбороты.СуммаБезНДСОборот,
	|	НДСЗаписиКнигиПокупокОбороты.НДСОборот,
	|	НДСЗаписиКнигиПокупокОбороты.Событие,
	|	НДСЗаписиКнигиПокупокОбороты.ВидЦенности,
	|	НДСЗаписиКнигиПокупокОбороты.ЗаписьДополнительногоЛиста,
	|	НДСЗаписиКнигиПокупокОбороты.НомерСтроки
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПокупок.Обороты(
	|			&НачалоПериода,
	|			,
	|			Запись,
	|			Организация В (&Организация)
	|				И НЕ ВидЦенности В (&ВидыЦенностейАвансыПолученные, ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.АвансыВыданные))
	|				И (ЗаписьДополнительногоЛиста
	|					И (КорректируемыйПериод МЕЖДУ &НачалоПериода И &КонецПериода))) КАК НДСЗаписиКнигиПокупокОбороты
	|ГДЕ
	|	НДСЗаписиКнигиПокупокОбороты.Регистратор ССЫЛКА Документ.ОтражениеНДСКВычету";
	
	ВычетДругиеОперацииУпрощенныйУчетНДС = Запрос.Выполнить();
	
	ВнешнийНаборДанныхВычетДругиеОперацииУпрощенныйУчетНДС = Новый Структура;
	ВнешнийНаборДанныхВычетДругиеОперацииУпрощенныйУчетНДС.Вставить("ВычетДругиеОперацииУпрощенныйУчетНДС",
																	ВычетДругиеОперацииУпрощенныйУчетНДС);
	
	СтруктураВнешнихНаборов.Вставить("ВнешнийНаборДанныхВычетДругиеОперацииУпрощенныйУчетНДС",
									 ВнешнийНаборДанныхВычетДругиеОперацииУпрощенныйУчетНДС);
	
КонецПроцедуры // ПолучитьНаборДанныхВычетДругиеОперацииУпрощенныйУчетНДС()

//	Наборы при сложном учете НДС

// Выполняет запрос к базе данных для получения набора данных, необходимого для формирования расшифровки по
//	начислению НДС в случае реализации по ставке НДС 0%
//
// Параметры
//  Запрос  - Запрос - запрос к базе данных
//  СтруктураВнешнихНаборов - структура внешних источников данных для расшифровки.
//
// Возвращаемое значение:
//   Структура   - структура параметров для формирования карты отчета
//
Процедура ПолучитьНаборДанныхНачислениеРеализация0(Запрос, СтруктураВнешнихНаборов) 

	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НДСРеализация0Остатки.СчетФактура КАК ДокументРегистратор,
	|	НДСРеализация0Остатки.СтавкаНДС,
	|	ЕСТЬNULL(НДСРеализация0Остатки.СуммаБезНДСОстаток, 0) КАК Сумма,
	|	ЕСТЬNULL(НДСРеализация0Остатки.НДСОстаток, 0) КАК СуммаНДС,
	|	ВЫБОР
	|		КОГДА НДСРеализация0Остатки.Состояние = ЗНАЧЕНИЕ(Перечисление.НДССостоянияРеализация0.ОжидаетсяПодтверждение)
	|			ТОГДА &ТекстВычетНеОтраженДоПодтвержденияСтавки0
	|		ИНАЧЕ &ТекстНачислениеНеОтраженоВКнигеПродаж
	|	КОНЕЦ КАК ПризнакНаличияОшибок,
	|	&ТекстНеОтраженыВКнигеПродаж КАК ПризнакОтраженияНДС,
	|	1 КАК КоличествоДокументов,
	|	0 КАК СуммаВключенные,
	|	ЕСТЬNULL(НДСРеализация0Остатки.НДСОстаток, 0) КАК СуммаНеВключенные,
	|	НДСРеализация0Остатки.ВидЦенности,
	|	ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПродажи.Пустаяссылка) КАК Событие,
	|	ЛОЖЬ КАК ЗаписьДополнительногоЛиста,
	|	1 КАК НомерСтроки
	|ИЗ
	|	РегистрНакопления.НДСРеализация0.Остатки(&НачалоСледующегоПериода, Организация В (&Организация)) КАК НДСРеализация0Остатки
	|ГДЕ
	|	(НДСРеализация0Остатки.СуммаБезНДСОстаток > 0
	|			ИЛИ НДСРеализация0Остатки.НДСОстаток > 0)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КнигаПродаж.СчетФактура,
	|	КнигаПродаж.СтавкаНДС,
	|	КнигаПродаж.СуммаБезНДСОборот,
	|	КнигаПродаж.НДСОборот,
	|	&ТекстБезОшибок,
	|	&ТекстОтраженыВКнигеПродаж,
	|	1,
	|	КнигаПродаж.НДСОборот,
	|	0,
	|	КнигаПродаж.ВидЦенности,
	|	КнигаПродаж.Событие,
	|	КнигаПродаж.ЗаписьДополнительногоЛиста,
	|	КнигаПродаж.НомерСтроки
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПродаж.Обороты(
	|			&НачалоПериода,
	|			&КонецПериодаГраница,
	|			Запись,
	|			Организация В (&Организация)
	|				И НЕ ВидЦенности В (&ВидыЦенностейАвансыВсе, &ВидыЦенностейНалоговыйАгент)
	|				И Событие В (ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПродажи.ПодтвержденаСтавка0), ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПродажи.НеПодтвержденаСтавка0))
	|				И НЕ ЗаписьДополнительногоЛиста) КАК КнигаПродаж
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КнигаПродаж.СчетФактура,
	|	КнигаПродаж.СтавкаНДС,
	|	КнигаПродаж.СуммаБезНДСОборот,
	|	КнигаПродаж.НДСОборот,
	|	&ТекстБезОшибок,
	|	&ТекстОтраженыВКнигеПродаж,
	|	1,
	|	КнигаПродаж.НДСОборот,
	|	0,
	|	КнигаПродаж.ВидЦенности,
	|	КнигаПродаж.Событие,
	|	КнигаПродаж.ЗаписьДополнительногоЛиста,
	|	КнигаПродаж.НомерСтроки
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПродаж.Обороты(
	|			&НачалоПериода,
	|			,
	|			Запись,
	|			Организация В (&Организация)
	|				И НЕ ВидЦенности В (&ВидыЦенностейАвансыВсе, &ВидыЦенностейНалоговыйАгент)
	|				И Событие В (ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПродажи.ПодтвержденаСтавка0), ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПродажи.НеПодтвержденаСтавка0))
	|				И (ЗаписьДополнительногоЛиста
	|					И (КорректируемыйПериод МЕЖДУ &НачалоПериода И &КонецПериода))) КАК КнигаПродаж";
	
	НачислениеРеализация0 = Запрос.Выполнить();
		
	ВнешнийНаборДанныхНачислениеРеализация0 = Новый Структура;
	ВнешнийНаборДанныхНачислениеРеализация0.Вставить("НачислениеРеализация0",	НачислениеРеализация0);
	
	СтруктураВнешнихНаборов.Вставить("ВнешнийНаборДанныхНачислениеРеализация0", ВнешнийНаборДанныхНачислениеРеализация0);
	
КонецПроцедуры // ПолучитьНаборДанныхНачислениеРеализация0()

// Выполняет запрос к базе данных для получения набора данных, необходимого для формирования расшифровки по
//	начисленному НДС по реализации в случае сложного учета НДС
//
// Параметры
//  Запрос  - Запрос - запрос к базе данных
//  СтруктураВнешнихНаборов - структура внешних источников данных для расшифровки.
//
// Возвращаемое значение:
//   Структура   - структура параметров для формирования карты отчета
//
Процедура ПолучитьНаборДанныхНачислениеРеализацияСложныйУчетНДС(Запрос, СтруктураВнешнихНаборов) 
	
	//	Документы включенные в книгу продаж (НДС по реализации)
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КнигаПродаж.СчетФактура КАК СчетФактура,
	|	КнигаПродаж.СтавкаНДС КАК СтавкаНДС,
	|	КнигаПродаж.СуммаБезНДСОборот КАК Сумма,
	|	КнигаПродаж.НДСОборот КАК СуммаНДС,
	|	КнигаПродаж.Событие,
	|	КнигаПродаж.ВидЦенности,
	|	КнигаПродаж.Регистратор,
	|	КнигаПродаж.ЗаписьДополнительногоЛиста КАК ЗаписьДополнительногоЛиста,
	|	КнигаПродаж.НомерСтроки
	|ПОМЕСТИТЬ ВТСоставКнигиПродаж
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПродаж.Обороты(
	|			&НачалоПериода,
	|			&КонецПериодаГраница,
	|			Запись,
	|			Организация В (&Организация)
	|				И НЕ ВидЦенности В (&ВидыЦенностейАвансыВсе, &ВидыЦенностейНалоговыйАгент, ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.СМРСобственнымиСилами))
	|				И НЕ Событие В (ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПродажи.ПодтвержденаСтавка0), ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПродажи.НеПодтвержденаСтавка0), ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПродажи.ВосстановлениеНДС))
	|				И НЕ ЗаписьДополнительногоЛиста) КАК КнигаПродаж
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КнигаПродаж.СчетФактура,
	|	КнигаПродаж.СтавкаНДС,
	|	КнигаПродаж.СуммаБезНДСОборот,
	|	КнигаПродаж.НДСОборот,
	|	КнигаПродаж.Событие,
	|	КнигаПродаж.ВидЦенности,
	|	КнигаПродаж.Регистратор,
	|	КнигаПродаж.ЗаписьДополнительногоЛиста,
	|	КнигаПродаж.НомерСтроки
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПродаж.Обороты(
	|			&НачалоПериода,
	|			,
	|			Запись,
	|			Организация В (&Организация)
	|				И НЕ ВидЦенности В (&ВидыЦенностейАвансыВсе, &ВидыЦенностейНалоговыйАгент, ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.СМРСобственнымиСилами))
	|				И НЕ Событие В (ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПродажи.ПодтвержденаСтавка0), ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПродажи.НеПодтвержденаСтавка0), ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПродажи.ВосстановлениеНДС))
	|				И (ЗаписьДополнительногоЛиста
	|					И (КорректируемыйПериод МЕЖДУ &НачалоПериода И &КонецПериода))) КАК КнигаПродаж
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДСНачисленныйОбороты.СчетФактура КАК СчетФактура,
	|	НДСНачисленныйОбороты.СтавкаНДС
	|ПОМЕСТИТЬ ВТНДСНачисленныйОбороты
	|ИЗ
	|	РегистрНакопления.НДСНачисленный.Обороты(
	|			&НачалоПериода,
	|			&КонецПериодаГраница,
	|			,
	|			Организация В (&Организация)
	|				И НЕ СчетФактура ССЫЛКА Документ.ОтчетОРозничныхПродажах
	|				И СчетФактура В
	|					(ВЫБРАТЬ
	|						КнигаПродаж.СчетФактура КАК СчетФактура
	|					ИЗ
	|						ВТСоставКнигиПродаж КАК КнигаПродаж)) КАК НДСНачисленныйОбороты
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДСНачисленныйОбороты.СчетФактура КАК ДокументРегистратор,
	|	НДСНачисленныйОбороты.СтавкаНДС,
	|	СчетФактураВыданныйДокументыОснования.Ссылка КАК СчетФактура,
	|	2 КАК РангОшибки
	|ПОМЕСТИТЬ ВТОперацииСОшибками
	|ИЗ
	|	ВТСоставКнигиПродаж КАК НДСНачисленныйОбороты
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураВыданный.ДокументыОснования КАК СчетФактураВыданныйДокументыОснования
	|		ПО (НДСНачисленныйОбороты.СчетФактура = СчетФактураВыданныйДокументыОснования.ДокументОснование
	|				ИЛИ НДСНачисленныйОбороты.СчетФактура = СчетФактураВыданныйДокументыОснования.Ссылка)
	|ГДЕ
	|	СчетФактураВыданныйДокументыОснования.Ссылка ЕСТЬ NULL 
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НДСНачисленный.СчетФактура,
	|	НДСНачисленный.СтавкаНДС,
	|	СчетФактураВыданныйДокументыОснования.Ссылка,
	|	1
	|ИЗ
	|	РегистрНакопления.НДСНачисленный КАК НДСНачисленный
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураВыданный.ДокументыОснования КАК СчетФактураВыданныйДокументыОснования
	|		ПО НДСНачисленный.СчетФактура = СчетФактураВыданныйДокументыОснования.ДокументОснование
	|ГДЕ
	|	НДСНачисленный.СтавкаНДС <> ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
	|	И НДСНачисленный.СчетДоходовБУ = &ВыручкаЕНВД
	|	И НДСНачисленный.СчетФактура В
	|			(ВЫБРАТЬ
	|				КнигаПродаж.СчетФактура КАК СчетФактура
	|			ИЗ
	|				ВТСоставКнигиПродаж КАК КнигаПродаж)
	|	И НДСНачисленный.Организация В(&Организация)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументРегистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТСоставКнигиПродаж.СчетФактура КАК СчетФактура,
	|	ВТСоставКнигиПродаж.СтавкаНДС,
	|	ВТОперацииСОшибками.ДокументРегистратор,
	|	МИНИМУМ(ВТСоставКнигиПродаж.Сумма) КАК Сумма,
	|	МИНИМУМ(ВТСоставКнигиПродаж.СуммаНДС) КАК СуммаНДС,
	|	МИНИМУМ(ВТОперацииСОшибками.РангОшибки) КАК РангОшибки,
	|	ВТСоставКнигиПродаж.Событие,
	|	ВТСоставКнигиПродаж.ВидЦенности,
	|	ВТСоставКнигиПродаж.Регистратор,
	|	ВТСоставКнигиПродаж.ЗаписьДополнительногоЛиста,
	|	ВТСоставКнигиПродаж.НомерСтроки
	|ПОМЕСТИТЬ ВТПроведеноРанжированиеОшибок
	|ИЗ
	|	ВТСоставКнигиПродаж КАК ВТСоставКнигиПродаж
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОперацииСОшибками КАК ВТОперацииСОшибками
	|		ПО ВТСоставКнигиПродаж.СчетФактура = ВТОперацииСОшибками.ДокументРегистратор
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТСоставКнигиПродаж.СчетФактура,
	|	ВТСоставКнигиПродаж.СтавкаНДС,
	|	ВТОперацииСОшибками.ДокументРегистратор,
	|	ВТСоставКнигиПродаж.Событие,
	|	ВТСоставКнигиПродаж.ВидЦенности,
	|	ВТСоставКнигиПродаж.Регистратор,
	|	ВТСоставКнигиПродаж.ЗаписьДополнительногоЛиста,
	|	ВТСоставКнигиПродаж.НомерСтроки
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СчетФактураВыданныйДокументыОснования.ДокументОснование КАК ДокументОснование,
	|	СчетФактураВыданныйДокументыОснования.Ссылка,
	|	ВЫБОР
	|		КОГДА СчетФактураВыданныйДокументыОснования.Ссылка.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.НаРеализацию)
	|				ИЛИ СчетФактураВыданныйДокументыОснования.Ссылка.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.Корректировочный)
	|			ТОГДА NULL
	|		ИНАЧЕ СчетФактураВыданныйДокументыОснования.Ссылка.СтавкаНДС
	|	КОНЕЦ КАК СтавкаНДССчетаФактуры
	|ПОМЕСТИТЬ ВТСчетФактураВыданныйДокументыОснования
	|ИЗ
	|	Документ.СчетФактураВыданный.ДокументыОснования КАК СчетФактураВыданныйДокументыОснования
	|ГДЕ
	|	СчетФактураВыданныйДокументыОснования.Ссылка.Организация В(&Организация)
	|	И СчетФактураВыданныйДокументыОснования.Ссылка.ПометкаУдаления = ЛОЖЬ
	|	И ВЫБОР
	|			КОГДА СчетФактураВыданныйДокументыОснования.ДокументОснование ССЫЛКА Документ.ОтчетКомитентуОПродажах
	|				ТОГДА СчетФактураВыданныйДокументыОснования.Ссылка.Продавец = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СчетФактураВыданныйДокументыОснования.Ссылка КАК СчетФактура,
	|	ВТПроведеноРанжированиеОшибок.СтавкаНДС,
	|	ВТПроведеноРанжированиеОшибок.СчетФактура КАК ДокументРегистратор,
	|	ВТПроведеноРанжированиеОшибок.Сумма,
	|	ВТПроведеноРанжированиеОшибок.СуммаНДС,
	|	1 КАК КоличествоДокументов,
	|	ВЫБОР
	|		КОГДА ВТПроведеноРанжированиеОшибок.РангОшибки ЕСТЬ NULL 
	|			ТОГДА &ТекстБезОшибок
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ВТПроведеноРанжированиеОшибок.РангОшибки = 1
	|					ТОГДА &ТекстОшибкаРеализацияЕНВДСНДС
	|				КОГДА ВТПроведеноРанжированиеОшибок.РангОшибки = 2
	|					ТОГДА &ТекстОшибкаРеализацияБезСФ
	|			КОНЕЦ
	|	КОНЕЦ КАК ПризнакНаличияОшибок,
	|	ВЫБОР
	|		КОГДА ВТПроведеноРанжированиеОшибок.РангОшибки = 0
	|			ТОГДА &ТекстСноска
	|		ИНАЧЕ &ТекстНетСноски
	|	КОНЕЦ КАК СимволСноски,
	|	&ТекстОтраженыВКнигеПродаж КАК ПризнакОтраженияНДС,
	|	ВЫБОР
	|		КОГДА ВТПроведеноРанжированиеОшибок.РангОшибки ЕСТЬ NULL 
	|			ТОГДА 0
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК КоличествоОшибок,
	|	ВТПроведеноРанжированиеОшибок.Событие,
	|	ВТПроведеноРанжированиеОшибок.ВидЦенности,
	|	ВТПроведеноРанжированиеОшибок.ЗаписьДополнительногоЛиста,
	|	ВТПроведеноРанжированиеОшибок.НомерСтроки
	|ИЗ
	|	ВТПроведеноРанжированиеОшибок КАК ВТПроведеноРанжированиеОшибок
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСчетФактураВыданныйДокументыОснования КАК СчетФактураВыданныйДокументыОснования
	|		ПО (ВТПроведеноРанжированиеОшибок.СчетФактура = СчетФактураВыданныйДокументыОснования.ДокументОснование
	|				ИЛИ ВТПроведеноРанжированиеОшибок.СчетФактура = СчетФактураВыданныйДокументыОснования.Ссылка
	|					И ВТПроведеноРанжированиеОшибок.СтавкаНДС = ЕСТЬNULL(СчетФактураВыданныйДокументыОснования.СтавкаНДССчетаФактуры, ВТПроведеноРанжированиеОшибок.СтавкаНДС))";
	
	НачисленияПоРеализацииВключенные = Запрос.Выполнить();
					
	//	Документы не включенные в книгу продаж
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НДСНачисленныйОстатки.СчетФактура КАК СчетФактура,
	|	НДСНачисленныйОстатки.СтавкаНДС,
	|	НДСНачисленныйОстатки.СуммаБезНДСОстаток,
	|	НДСНачисленныйОстатки.НДСОстаток,
	|	НДСНачисленныйОстатки.ВидЦенности
	|ПОМЕСТИТЬ ВТНДСНачисленныйОстатки
	|ИЗ
	|	РегистрНакопления.НДСНачисленный.Остатки(
	|			&НачалоСледующегоПериода,
	|			Организация В (&Организация)
	|				И (НЕ ВидНачисления В (&НеОтражаютсяВРазделеНачислениеПоРеализацииСложныйУчет)
	|					И НЕ ВидЦенности В (&ВидыЦенностейНалоговыйАгент)
	|					И НЕ ВидЦенности = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.СМРСобственнымиСилами))) КАК НДСНачисленныйОстатки
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДСНачисленныйОстатки.СчетФактура КАК ДокументРегистратор,
	|	НДСНачисленныйОстатки.СтавкаНДС КАК СтавкаНДС,
	|	СУММА(НДСНачисленныйОстатки.СуммаБезНДСОстаток) КАК Сумма,
	|	СУММА(НДСНачисленныйОстатки.НДСОстаток) КАК СуммаНДС,
	|	СчетФактураВыданныйДокументыОснования.Ссылка КАК СчетФактура,
	|	1 КАК КоличествоДокументов,
	|	ВЫБОР
	|		КОГДА СУММА(НДСНачисленныйОстатки.НДСОстаток) > 0
	|			ТОГДА &ТекстНачислениеНеОтраженоВКнигеПродаж
	|		ИНАЧЕ &ТекстКорректировкаКнигиПродаж
	|	КОНЕЦ КАК ПризнакНаличияОшибок,
	|	&ТекстНеОтраженыВКнигеПродаж КАК ПризнакОтраженияНДС
	|ИЗ
	|	ВТНДСНачисленныйОстатки КАК НДСНачисленныйОстатки
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураВыданный.ДокументыОснования КАК СчетФактураВыданныйДокументыОснования
	|		ПО (НДСНачисленныйОстатки.СчетФактура = СчетФактураВыданныйДокументыОснования.ДокументОснование
	|				ИЛИ НДСНачисленныйОстатки.СчетФактура = СчетФактураВыданныйДокументыОснования.Ссылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	НДСНачисленныйОстатки.СчетФактура,
	|	НДСНачисленныйОстатки.СтавкаНДС,
	|	СчетФактураВыданныйДокументыОснования.Ссылка";
	
	НачисленияПоРеализацииНеВключенныеНДСНеОтражен = Запрос.Выполнить();
	
	ВнешнийНаборДанныхНачислениеРеализация = Новый Структура;
	ВнешнийНаборДанныхНачислениеРеализация.Вставить("НачисленияПоРеализацииВключенные",	
													НачисленияПоРеализацииВключенные);
	ВнешнийНаборДанныхНачислениеРеализация.Вставить("НачисленияПоРеализацииНеВключенныеНДСНеОтражен",	
													НачисленияПоРеализацииНеВключенныеНДСНеОтражен);
					
	СтруктураВнешнихНаборов.Вставить("ВнешнийНаборДанныхНачислениеРеализацияСложныйУчетНДС", ВнешнийНаборДанныхНачислениеРеализация);
		
КонецПроцедуры // ПолучитьНаборДанныхНачислениеРеализацияСложныйУчетНДС()
			
// Выполняет запрос к базе данных для получения набора данных, необходимого для формирования расшифровки по
//	начислению НДС по прочим операциям в случае ведения сложного учета НДС
//
// Параметры
//  Запрос  - Запрос - запрос к базе данных
//  СтруктураВнешнихНаборов - структура внешних источников данных для расшифровки.
//
// Возвращаемое значение:
//   Структура   - структура параметров для формирования карты отчета
//
Процедура ПолучитьНаборДанныхНачислениеВосстановлениеДругиеОперации(Запрос, СтруктураВнешнихНаборов) 

	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НДСНачисленныйОстатки.СчетФактура КАК ДокументРегистратор,
	|	НДСНачисленныйОстатки.СтавкаНДС,
	|	НДСНачисленныйОстатки.СуммаБезНДСОстаток КАК Сумма,
	|	НДСНачисленныйОстатки.НДСОстаток КАК СуммаНДС,
	|	&ТекстНеОтраженыВКнигеПродаж КАК ПризнакОтраженияНДС,
	|	ВЫБОР
	|		КОГДА НДСНачисленныйОстатки.НДСОстаток > 0
	|			ТОГДА &ТекстНачислениеНеОтраженоВКнигеПродаж
	|		ИНАЧЕ &ТекстКорректировкаКнигиПродаж
	|	КОНЕЦ КАК ПризнакНаличияОшибок,
	|	1 КАК КоличествоДокументов,
	|	0 КАК СуммаВключенные,
	|	НДСНачисленныйОстатки.НДСОстаток КАК СуммаНеВключенные,
	|	НДСНачисленныйОстатки.ВидЦенности,
	|	ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПродажи.ПустаяСсылка) КАК Событие,
	|	ЛОЖЬ КАК ЗаписьДополнительногоЛиста,
	|	1 КАК НомерСтроки
	|ИЗ
	|	РегистрНакопления.НДСНачисленный.Остатки(
	|			&НачалоСледующегоПериода,
	|			Организация В (&Организация)
	|				И ВидНачисления = ЗНАЧЕНИЕ(Перечисление.НДСВидНачисления.НДСВосстановлен)) КАК НДСНачисленныйОстатки
	|ГДЕ
	|	НЕ(НДСНачисленныйОстатки.СуммаБезНДСОстаток = 0
	|				И НДСНачисленныйОстатки.НДСОстаток = 0)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НДСЗаписиКнигиПродажОбороты.СчетФактура,
	|	НДСЗаписиКнигиПродажОбороты.СтавкаНДС,
	|	НДСЗаписиКнигиПродажОбороты.СуммаБезНДСОборот,
	|	НДСЗаписиКнигиПродажОбороты.НДСОборот,
	|	&ТекстОтраженыВКнигеПродаж,
	|	&ТекстБезОшибок,
	|	1,
	|	НДСЗаписиКнигиПродажОбороты.НДСОборот,
	|	0,
	|	НДСЗаписиКнигиПродажОбороты.ВидЦенности,
	|	НДСЗаписиКнигиПродажОбороты.Событие,
	|	НДСЗаписиКнигиПродажОбороты.ЗаписьДополнительногоЛиста,
	|	НДСЗаписиКнигиПродажОбороты.НомерСтроки
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПродаж.Обороты(
	|			&НачалоПериода,
	|			&КонецПериодаГраница,
	|			Запись,
	|			Организация В (&Организация)
	|				И Событие = ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПродажи.ВосстановлениеНДС)
	|				И ВидЦенности <> ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.АвансыВыданные)
	|				И НЕ ЗаписьДополнительногоЛиста) КАК НДСЗаписиКнигиПродажОбороты
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НДСЗаписиКнигиПродажОбороты.СчетФактура,
	|	НДСЗаписиКнигиПродажОбороты.СтавкаНДС,
	|	НДСЗаписиКнигиПродажОбороты.СуммаБезНДСОборот,
	|	НДСЗаписиКнигиПродажОбороты.НДСОборот,
	|	&ТекстОтраженыВКнигеПродаж,
	|	&ТекстБезОшибок,
	|	1,
	|	НДСЗаписиКнигиПродажОбороты.НДСОборот,
	|	0,
	|	НДСЗаписиКнигиПродажОбороты.ВидЦенности,
	|	НДСЗаписиКнигиПродажОбороты.Событие,
	|	НДСЗаписиКнигиПродажОбороты.ЗаписьДополнительногоЛиста,
	|	НДСЗаписиКнигиПродажОбороты.НомерСтроки
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПродаж.Обороты(
	|			&НачалоПериода,
	|			,
	|			Запись,
	|			Организация В (&Организация)
	|				И Событие = ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПродажи.ВосстановлениеНДС)
	|				И ВидЦенности <> ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.АвансыВыданные)
	|				И (ЗаписьДополнительногоЛиста
	|					И (КорректируемыйПериод МЕЖДУ &НачалоПериода И &КонецПериода))) КАК НДСЗаписиКнигиПродажОбороты";
	
	НачислениеВосстановлениеДругиеОперации = Запрос.Выполнить();
	
	ВнешнийНаборДанныхНачислениеВосстановлениеДругиеОперации = Новый Структура;
	ВнешнийНаборДанныхНачислениеВосстановлениеДругиеОперации.Вставить("НачислениеВосстановлениеДругиеОперации", 
																	  НачислениеВосстановлениеДругиеОперации);
	
	СтруктураВнешнихНаборов.Вставить("ВнешнийНаборДанныхНачислениеВосстановлениеДругиеОперации",
									 ВнешнийНаборДанныхНачислениеВосстановлениеДругиеОперации);
	
КонецПроцедуры // ПолучитьНаборДанныхНачислениеВосстановлениеДругиеОперации()

// Выполняет запрос к базе данных для получения набора данных, необходимого для формирования расшифровки по
//	операциям, начисление НДС с которых не отражается в книге продаж
//
// Параметры
//  Запрос  - Запрос - запрос к базе данных
//  СтруктураВнешнихНаборов - структура внешних источников данных для расшифровки.
//
// Возвращаемое значение:
//   Структура   - структура параметров для формирования карты отчета
//
Процедура ПолучитьНаборДанныхНачислениеНеОтражаетсяВКнигеПродаж(Запрос, СтруктураВнешнихНаборов) 

	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ФормированиеЗаписейКнигиПродажНеОтражаетсяВКниге.СчетФактура КАК ДокументРегистратор,
	|	ФормированиеЗаписейКнигиПродажНеОтражаетсяВКниге.СтавкаНДС КАК СтавкаНДС,
	|	ФормированиеЗаписейКнигиПродажНеОтражаетсяВКниге.СуммаБезНДС КАК Сумма,
	|	ФормированиеЗаписейКнигиПродажНеОтражаетсяВКниге.НДС КАК СуммаНДС,
	|	ФормированиеЗаписейКнигиПродажНеОтражаетсяВКниге.Событие,
	|	ФормированиеЗаписейКнигиПродажНеОтражаетсяВКниге.ВидЦенности,
	|	ЛОЖЬ КАК ЗаписьДополнительногоЛиста,
	|	ФормированиеЗаписейКнигиПродажНеОтражаетсяВКниге.НомерСтроки
	|ИЗ
	|	Документ.ФормированиеЗаписейКнигиПродаж.НеОтражаетсяВКниге КАК ФормированиеЗаписейКнигиПродажНеОтражаетсяВКниге
	|ГДЕ
	|	ФормированиеЗаписейКнигиПродажНеОтражаетсяВКниге.Ссылка.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|	И ФормированиеЗаписейКнигиПродажНеОтражаетсяВКниге.Ссылка.Проведен
	|	И ФормированиеЗаписейКнигиПродажНеОтражаетсяВКниге.Ссылка.Организация В(&Организация)";
		
	НачислениеНеОтражаетсяВКнигеПродаж = Запрос.Выполнить().Выгрузить();
	
	
	ВнешнийНаборДанныхНачислениеНеОтражаетсяВКнигеПродаж = Новый Структура;
	ВнешнийНаборДанныхНачислениеНеОтражаетсяВКнигеПродаж.Вставить("НачислениеНеОтражаетсяВКнигеПродаж",
																  НачислениеНеОтражаетсяВКнигеПродаж);
	
	СтруктураВнешнихНаборов.Вставить("ВнешнийНаборДанныхНачислениеНеОтражаетсяВКнигеПродаж",
									 ВнешнийНаборДанныхНачислениеНеОтражаетсяВКнигеПродаж);
	
КонецПроцедуры // ПолучитьНаборДанныхНачислениеНеОтражаетсяВКнигеПродаж()

// Выполняет запрос к базе данных для получения набора данных, необходимого для формирования расшифровки по
//	вычету НДС по приобретенным ценностям, реализованным по ставке НДС 0%
//
// Параметры
//  Запрос  - Запрос - запрос к базе данных
//  СтруктураВнешнихНаборов - структура внешних источников данных для расшифровки.
//
// Возвращаемое значение:
//   Структура   - структура параметров для формирования карты отчета
//
Процедура ПолучитьНаборДанныхВычетПоПриобретеннымЦенностям0(Запрос, СтруктураВнешнихНаборов) 
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СоставКнигиПокупок.СчетФактура КАК СчетФактура,
	|	СоставКнигиПокупок.СтавкаНДС,
	|	СоставКнигиПокупок.СуммаБезНДСОборот,
	|	СоставКнигиПокупок.НДСОборот,
	|	СоставКнигиПокупок.Событие,
	|	СоставКнигиПокупок.ВидЦенности,
	|	СоставКнигиПокупок.ЗаписьДополнительногоЛиста,
	|	СоставКнигиПокупок.НомерСтроки
	|ПОМЕСТИТЬ ВТСоставКнигиПокупокПоПриобретеннымЦенностям0
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПокупок.Обороты(
	|			&НачалоПериода,
	|			&КонецПериодаГраница,
	|			Запись,
	|			Организация В (&Организация)
	|				И НЕ ВидЦенности В (&ВидыЦенностейАвансыВсе)
	|				И Событие = ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПокупки.ПредъявленНДСКВычету0)
	|				И НЕ ЗаписьДополнительногоЛиста) КАК СоставКнигиПокупок
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СоставКнигиПокупок.СчетФактура,
	|	СоставКнигиПокупок.СтавкаНДС,
	|	СоставКнигиПокупок.СуммаБезНДСОборот,
	|	СоставКнигиПокупок.НДСОборот,
	|	СоставКнигиПокупок.Событие,
	|	СоставКнигиПокупок.ВидЦенности,
	|	СоставКнигиПокупок.ЗаписьДополнительногоЛиста,
	|	СоставКнигиПокупок.НомерСтроки
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПокупок.Обороты(
	|			&НачалоПериода,
	|			,
	|			Запись,
	|			Организация В (&Организация)
	|				И НЕ ВидЦенности В (&ВидыЦенностейАвансыВсе)
	|				И Событие = ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПокупки.ПредъявленНДСКВычету0)
	|				И (ЗаписьДополнительногоЛиста
	|					И (КорректируемыйПериод МЕЖДУ &НачалоПериода И &КонецПериода))) КАК СоставКнигиПокупок
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДСПредъявленныйРеализация0Остатки.СчетФактура КАК СчетФактура,
	|	НДСПредъявленныйРеализация0Остатки.ДокументОтгрузки,
	|	НДСПредъявленныйРеализация0Остатки.СтавкаНДС КАК СтавкаНДС,
	|	НДСПредъявленныйРеализация0Остатки.СуммаБезНДСОстаток,
	|	НДСПредъявленныйРеализация0Остатки.НДСОстаток,
	|	НДСПредъявленныйРеализация0Остатки.Состояние,
	|	НДСПредъявленныйРеализация0Остатки.СчетУчетаНДС КАК СчетУчетаНДС,
	|	НДСПредъявленныйРеализация0Остатки.ВидЦенности КАК ВидЦенности
	|ПОМЕСТИТЬ ВТНДСПредъявленныйРеализация0Остатки
	|ИЗ
	|	РегистрНакопления.НДСПредъявленныйРеализация0.Остатки(
	|			&НачалоСледующегоПериода,
	|			Организация В (&Организация)
	|				И НЕ ВидЦенности В (&ВидыЦенностейНалоговыйАгент)) КАК НДСПредъявленныйРеализация0Остатки
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактура,
	|	ВидЦенности,
	|	СтавкаНДС,
	|	СчетУчетаНДС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДСПредъявленныйОстатки.СчетФактура КАК СчетФактура,
	|	НДСПредъявленныйОстатки.СчетУчетаНДС КАК СчетУчетаНДС,
	|	НДСПредъявленныйОстатки.СтавкаНДС КАК СтавкаНДС,
	|	НДСПредъявленныйОстатки.ВидЦенности КАК ВидЦенности,
	|	НДСПредъявленныйОстатки.НДСОстаток
	|ПОМЕСТИТЬ ВТНДСПредъявленныйПоПриобретеннымЦенностям0
	|ИЗ
	|	РегистрНакопления.НДСПредъявленный.Остатки(
	|			&НачалоСледующегоПериода,
	|			Организация В (&Организация)
	|				И НЕ ВидЦенности В (&ВидыЦенностейАвансыВсе)) КАК НДСПредъявленныйОстатки
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактура,
	|	ВидЦенности,
	|	СтавкаНДС,
	|	СчетУчетаНДС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СоставКнигиПокупок.СчетФактура КАК ДокументРегистратор,
	|	NULL КАК ДокументОтгрузки,
	|	СоставКнигиПокупок.СтавкаНДС КАК СтавкаНДС,
	|	СоставКнигиПокупок.СуммаБезНДСОборот КАК Сумма,
	|	СоставКнигиПокупок.НДСОборот КАК СуммаНДС,
	|	&ТекстБезОшибок КАК ПризнакНаличияОшибок,
	|	&ТекстОтраженыВКнигеПокупок КАК ПризнакОтраженияНДС,
	|	1 КАК КоличествоДокументов,
	|	СоставКнигиПокупок.НДСОборот КАК СуммаВключенные,
	|	0 КАК СуммаНеВключенные,
	|	ВЫБОР
	|		КОГДА СоставКнигиПокупок.СчетФактура ССЫЛКА Документ.СчетФактураВыданный
	|				ИЛИ СоставКнигиПокупок.СчетФактура ССЫЛКА Документ.ГТДИмпорт
	|				ИЛИ СоставКнигиПокупок.СчетФактура ССЫЛКА Документ.ОтчетОРозничныхПродажах
	|			ТОГДА СоставКнигиПокупок.СчетФактура.Номер
	|		КОГДА СоставКнигиПокупок.СчетФактура ССЫЛКА Документ.ОтчетКомиссионераОПродажах
	|			ТОГДА СчетФактураПолученныйДокументыОснования.Ссылка.НомерВходящегоДокумента
	|		ИНАЧЕ ЕСТЬNULL(СчетФактураПолученныйДокументыОснования.Ссылка.НомерВходящегоДокумента, СчетФактураВыданныйДокументыОснования.Ссылка.Номер)
	|	КОНЕЦ КАК НомерВходящегоДокумента,
	|	ВЫБОР
	|		КОГДА СоставКнигиПокупок.СчетФактура ССЫЛКА Документ.СчетФактураВыданный
	|				ИЛИ СоставКнигиПокупок.СчетФактура ССЫЛКА Документ.ГТДИмпорт
	|				ИЛИ СоставКнигиПокупок.СчетФактура ССЫЛКА Документ.ОтчетОРозничныхПродажах
	|			ТОГДА СоставКнигиПокупок.СчетФактура.Дата
	|		КОГДА СоставКнигиПокупок.СчетФактура ССЫЛКА Документ.ОтчетКомиссионераОПродажах
	|			ТОГДА СчетФактураПолученныйДокументыОснования.Ссылка.ДатаВходящегоДокумента
	|		ИНАЧЕ ЕСТЬNULL(СчетФактураПолученныйДокументыОснования.Ссылка.ДатаВходящегоДокумента, СчетФактураВыданныйДокументыОснования.Ссылка.Дата)
	|	КОНЕЦ КАК ДатаВходящегоДокумента,
	|	ВЫБОР
	|		КОГДА СоставКнигиПокупок.СчетФактура ССЫЛКА Документ.СчетФактураВыданный
	|				ИЛИ СоставКнигиПокупок.СчетФактура ССЫЛКА Документ.ГТДИмпорт
	|				ИЛИ СоставКнигиПокупок.СчетФактура ССЫЛКА Документ.ОтчетОРозничныхПродажах
	|			ТОГДА СоставКнигиПокупок.СчетФактура
	|		КОГДА СоставКнигиПокупок.СчетФактура ССЫЛКА Документ.ОтчетКомиссионераОПродажах
	|			ТОГДА СчетФактураПолученныйДокументыОснования.Ссылка
	|		ИНАЧЕ ЕСТЬNULL(СчетФактураПолученныйДокументыОснования.Ссылка, СчетФактураВыданныйДокументыОснования.Ссылка)
	|	КОНЕЦ КАК СчетФактура,
	|	СоставКнигиПокупок.Событие,
	|	СоставКнигиПокупок.ВидЦенности,
	|	СоставКнигиПокупок.ЗаписьДополнительногоЛиста,
	|	СоставКнигиПокупок.НомерСтроки
	|ИЗ
	|	ВТСоставКнигиПокупокПоПриобретеннымЦенностям0 КАК СоставКнигиПокупок
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураПолученный.ДокументыОснования КАК СчетФактураПолученныйДокументыОснования
	|		ПО (СоставКнигиПокупок.СчетФактура = СчетФактураПолученныйДокументыОснования.ДокументОснование
	|				ИЛИ СоставКнигиПокупок.СчетФактура = СчетФактураПолученныйДокументыОснования.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураВыданный.ДокументыОснования КАК СчетФактураВыданныйДокументыОснования
	|		ПО (СоставКнигиПокупок.СчетФактура = СчетФактураВыданныйДокументыОснования.ДокументОснование
	|				ИЛИ СоставКнигиПокупок.СчетФактура = СчетФактураВыданныйДокументыОснования.Ссылка)
	|			И (СчетФактураВыданныйДокументыОснования.Ссылка.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.НаРеализацию))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НДСПредъявленныйРеализация0Остатки.СчетФактура,
	|	НДСПредъявленныйРеализация0Остатки.ДокументОтгрузки,
	|	НДСПредъявленныйРеализация0Остатки.СтавкаНДС,
	|	НДСПредъявленныйРеализация0Остатки.СуммаБезНДСОстаток,
	|	НДСПредъявленныйРеализация0Остатки.НДСОстаток,
	|	ВЫБОР
	|		КОГДА НДСПредъявленныйРеализация0Остатки.Состояние = ЗНАЧЕНИЕ(Перечисление.НДССостоянияРеализация0.ПодтвержденаРеализация0)
	|			ТОГДА ВЫБОР
	|					КОГДА ВЫБОР
	|								КОГДА НДСПредъявленныйОстатки.СчетФактура ССЫЛКА Документ.СчетФактураВыданный
	|										ИЛИ НДСПредъявленныйОстатки.СчетФактура ССЫЛКА Документ.ГТДИмпорт
	|										ИЛИ НДСПредъявленныйОстатки.СчетФактура ССЫЛКА Документ.ОтчетОРозничныхПродажах
	|									ТОГДА НДСПредъявленныйОстатки.СчетФактура
	|								КОГДА НДСПредъявленныйОстатки.СчетФактура ССЫЛКА Документ.ОтчетКомиссионераОПродажах
	|									ТОГДА СчетФактураПолученныйДокументыОснования.Ссылка
	|								ИНАЧЕ ЕСТЬNULL(СчетФактураПолученныйДокументыОснования.Ссылка, СчетФактураВыданныйДокументыОснования.Ссылка)
	|							КОНЕЦ ЕСТЬ NULL 
	|							ИЛИ ВЫБОР
	|								КОГДА НДСПредъявленныйОстатки.СчетФактура ССЫЛКА Документ.СчетФактураВыданный
	|										ИЛИ НДСПредъявленныйОстатки.СчетФактура ССЫЛКА Документ.ГТДИмпорт
	|										ИЛИ НДСПредъявленныйОстатки.СчетФактура ССЫЛКА Документ.ОтчетОРозничныхПродажах
	|									ТОГДА ЛОЖЬ
	|								КОГДА НДСПредъявленныйОстатки.СчетФактура ССЫЛКА Документ.ОтчетКомиссионераОПродажах
	|									ТОГДА ЕСТЬNULL(СчетФактураПолученныйДокументыОснования.Ссылка.ДатаВходящегоДокумента, &НачалоСледующегоПериода) > &КонецПериода
	|								ИНАЧЕ ЕСТЬNULL(ЕСТЬNULL(СчетФактураПолученныйДокументыОснования.Ссылка.ДатаВходящегоДокумента, СчетФактураВыданныйДокументыОснования.Ссылка.Дата), &НачалоСледующегоПериода) > &КонецПериода
	|							КОНЕЦ
	|						ТОГДА &ТекстОтсутствуетСФПолученный
	|					КОГДА НЕ ВЫБОР
	|								КОГДА НДСПредъявленныйОстатки.СчетФактура ССЫЛКА Документ.СчетФактураВыданный
	|										ИЛИ НДСПредъявленныйОстатки.СчетФактура ССЫЛКА Документ.ГТДИмпорт
	|										ИЛИ НДСПредъявленныйОстатки.СчетФактура ССЫЛКА Документ.ОтчетОРозничныхПродажах
	|									ТОГДА НДСПредъявленныйОстатки.СчетФактура.Проведен
	|								КОГДА НДСПредъявленныйОстатки.СчетФактура ССЫЛКА Документ.ОтчетКомиссионераОПродажах
	|									ТОГДА СчетФактураПолученныйДокументыОснования.Ссылка.Проведен
	|								ИНАЧЕ ЕСТЬNULL(СчетФактураПолученныйДокументыОснования.Ссылка.Проведен, СчетФактураВыданныйДокументыОснования.Ссылка.Проведен)
	|							КОНЕЦ
	|						ТОГДА &ТекстСФПолученныйНеПроведен
	|					ИНАЧЕ ВЫБОР
	|							КОГДА НДСПредъявленныйРеализация0Остатки.НДСОстаток >= 0
	|								ТОГДА &ТекстВычетНеОтраженВКнигеПокупок
	|							ИНАЧЕ &ТекстКорректировкаКнигиПокупок
	|						КОНЕЦ
	|				КОНЕЦ
	|		КОГДА НДСПредъявленныйРеализация0Остатки.Состояние = ЗНАЧЕНИЕ(Перечисление.НДССостоянияРеализация0.ОжидаетсяПодтверждение)
	|			ТОГДА &ТекстВычетНеОтраженДоПодтвержденияСтавки0
	|		ИНАЧЕ &ТекстНеПодтвержденаСтавка
	|	КОНЕЦ,
	|	&ТекстНеОтраженыВКнигеПокупок,
	|	1,
	|	0,
	|	НДСПредъявленныйРеализация0Остатки.НДСОстаток,
	|	ВЫБОР
	|		КОГДА НДСПредъявленныйОстатки.СчетФактура ССЫЛКА Документ.СчетФактураВыданный
	|				ИЛИ НДСПредъявленныйОстатки.СчетФактура ССЫЛКА Документ.ГТДИмпорт
	|				ИЛИ НДСПредъявленныйОстатки.СчетФактура ССЫЛКА Документ.ОтчетОРозничныхПродажах
	|			ТОГДА НДСПредъявленныйОстатки.СчетФактура.Номер
	|		КОГДА НДСПредъявленныйОстатки.СчетФактура ССЫЛКА Документ.ОтчетКомиссионераОПродажах
	|			ТОГДА СчетФактураПолученныйДокументыОснования.Ссылка.НомерВходящегоДокумента
	|		ИНАЧЕ ЕСТЬNULL(СчетФактураПолученныйДокументыОснования.Ссылка.НомерВходящегоДокумента, СчетФактураВыданныйДокументыОснования.Ссылка.Номер)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА НДСПредъявленныйОстатки.СчетФактура ССЫЛКА Документ.СчетФактураВыданный
	|				ИЛИ НДСПредъявленныйОстатки.СчетФактура ССЫЛКА Документ.ГТДИмпорт
	|				ИЛИ НДСПредъявленныйОстатки.СчетФактура ССЫЛКА Документ.ОтчетОРозничныхПродажах
	|			ТОГДА НДСПредъявленныйОстатки.СчетФактура.Дата
	|		КОГДА НДСПредъявленныйОстатки.СчетФактура ССЫЛКА Документ.ОтчетКомиссионераОПродажах
	|			ТОГДА СчетФактураПолученныйДокументыОснования.Ссылка.ДатаВходящегоДокумента
	|		ИНАЧЕ ЕСТЬNULL(СчетФактураПолученныйДокументыОснования.Ссылка.ДатаВходящегоДокумента, СчетФактураВыданныйДокументыОснования.Ссылка.Дата)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА НДСПредъявленныйОстатки.СчетФактура ССЫЛКА Документ.СчетФактураВыданный
	|				ИЛИ НДСПредъявленныйОстатки.СчетФактура ССЫЛКА Документ.ГТДИмпорт
	|				ИЛИ НДСПредъявленныйОстатки.СчетФактура ССЫЛКА Документ.ОтчетОРозничныхПродажах
	|			ТОГДА НДСПредъявленныйОстатки.СчетФактура
	|		КОГДА НДСПредъявленныйОстатки.СчетФактура ССЫЛКА Документ.ОтчетКомиссионераОПродажах
	|			ТОГДА СчетФактураПолученныйДокументыОснования.Ссылка
	|		ИНАЧЕ ЕСТЬNULL(СчетФактураПолученныйДокументыОснования.Ссылка, СчетФактураВыданныйДокументыОснования.Ссылка)
	|	КОНЕЦ,
	|	ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПокупки.ПустаяСсылка),
	|	НДСПредъявленныйРеализация0Остатки.ВидЦенности,
	|	ЛОЖЬ,
	|	1
	|ИЗ
	|	ВТНДСПредъявленныйРеализация0Остатки КАК НДСПредъявленныйРеализация0Остатки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНДСПредъявленныйПоПриобретеннымЦенностям0 КАК НДСПредъявленныйОстатки
	|		ПО НДСПредъявленныйРеализация0Остатки.СчетФактура = НДСПредъявленныйОстатки.СчетФактура
	|			И НДСПредъявленныйРеализация0Остатки.СтавкаНДС = НДСПредъявленныйОстатки.СтавкаНДС
	|			И НДСПредъявленныйРеализация0Остатки.СчетУчетаНДС = НДСПредъявленныйОстатки.СчетУчетаНДС
	|			И НДСПредъявленныйРеализация0Остатки.ВидЦенности = НДСПредъявленныйОстатки.ВидЦенности
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураПолученный.ДокументыОснования КАК СчетФактураПолученныйДокументыОснования
	|		ПО (НДСПредъявленныйОстатки.СчетФактура = СчетФактураПолученныйДокументыОснования.ДокументОснование
	|				ИЛИ НДСПредъявленныйОстатки.СчетФактура = СчетФактураПолученныйДокументыОснования.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураВыданный.ДокументыОснования КАК СчетФактураВыданныйДокументыОснования
	|		ПО (НДСПредъявленныйОстатки.СчетФактура = СчетФактураВыданныйДокументыОснования.ДокументОснование
	|				ИЛИ НДСПредъявленныйОстатки.СчетФактура = СчетФактураВыданныйДокументыОснования.Ссылка)
	|			И (СчетФактураВыданныйДокументыОснования.Ссылка.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.НаРеализацию))
	|ГДЕ
	|	(НДСПредъявленныйРеализация0Остатки.СуммаБезНДСОстаток > 0
	|			ИЛИ НДСПредъявленныйРеализация0Остатки.НДСОстаток > 0)
	|	И НДСПредъявленныйРеализация0Остатки.НДСОстаток <= НДСПредъявленныйОстатки.НДСОстаток";
	
	
	ВычетПоПриобретеннымЦенностям0 = Запрос.Выполнить().Выгрузить();

	ВнешнийНаборДанныхВычетПоПриобретеннымЦенностям0 = Новый Структура;
	ВнешнийНаборДанныхВычетПоПриобретеннымЦенностям0.Вставить("ВычетПоПриобретеннымЦенностям0", 
															  ВычетПоПриобретеннымЦенностям0);
	
	СтруктураВнешнихНаборов.Вставить("ВнешнийНаборДанныхВычетПоПриобретеннымЦенностям0", 
									 ВнешнийНаборДанныхВычетПоПриобретеннымЦенностям0);
		
КонецПроцедуры // ПолучитьНаборДанныхВычетПоПриобретеннымЦенностям0()

// Выполняет запрос к базе данных для получения набора данных, необходимого для формирования расшифровки по
//	вычету НДС при исполнении обязанностей налогового агента при реализации ценностей по ставке НДС 0%
//
// Параметры
//  Запрос  - Запрос - запрос к базе данных
//  СтруктураВнешнихНаборов - структура внешних источников данных для расшифровки.
//
// Возвращаемое значение:
//   Структура   - структура параметров для формирования карты отчета
//
Процедура ПолучитьНаборДанныхВычетНалоговыйАгент0(Запрос, СтруктураВнешнихНаборов) 
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НДСПредъявленныйРеализация0Остатки.СчетФактура КАК СчетФактура,
	|	НДСПредъявленныйРеализация0Остатки.ВидЦенности КАК ВидЦенности,
	|	НДСПредъявленныйРеализация0Остатки.СчетУчетаНДС КАК СчетУчетаНДС,
	|	НДСПредъявленныйРеализация0Остатки.СтавкаНДС КАК СтавкаНДС,
	|	НДСПредъявленныйРеализация0Остатки.ДокументОтгрузки,
	|	НДСПредъявленныйРеализация0Остатки.Состояние,
	|	НДСПредъявленныйРеализация0Остатки.СуммаБезНДСОстаток,
	|	НДСПредъявленныйРеализация0Остатки.НДСОстаток
	|ПОМЕСТИТЬ ВТНДСПредъявленныйРеализация0НалоговыйАгент0
	|ИЗ
	|	РегистрНакопления.НДСПредъявленныйРеализация0.Остатки(
	|			&НачалоСледующегоПериода,
	|			Организация В (&Организация)
	|				И ВидЦенности В (&ВидыЦенностейНалоговыйАгент)) КАК НДСПредъявленныйРеализация0Остатки
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактура,
	|	ВидЦенности,
	|	СтавкаНДС,
	|	СчетУчетаНДС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДСПредъявленныйОстатки.СчетФактура КАК СчетФактура,
	|	НДСПредъявленныйОстатки.ВидЦенности КАК ВидЦенности,
	|	НДСПредъявленныйОстатки.СтавкаНДС КАК СтавкаНДС,
	|	НДСПредъявленныйОстатки.СчетУчетаНДС КАК СчетУчетаНДС,
	|	НДСПредъявленныйОстатки.СуммаБезНДСОстаток,
	|	НДСПредъявленныйОстатки.НДСОстаток
	|ПОМЕСТИТЬ ВТНДСПредъявленныйОстаткиНалоговыйАгент0
	|ИЗ
	|	РегистрНакопления.НДСПредъявленный.Остатки(
	|			&НачалоСледующегоПериода,
	|			Организация В (&Организация)
	|				И ВидЦенности В (&ВидыЦенностейНалоговыйАгент)
	|				И ДоговорКонтрагента <> ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)) КАК НДСПредъявленныйОстатки
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактура,
	|	ВидЦенности,
	|	СтавкаНДС,
	|	СчетУчетаНДС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ХозрасчетныйОбороты.Субконто1 КАК Субконто1,
	|	ХозрасчетныйОбороты.Регистратор КАК Регистратор
	|ПОМЕСТИТЬ ВТХозрасчетныйОборотыНалоговыйАгент0
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(
	|			&НачалоПериода,
	|			&КонецПериодаГраница,
	|			Регистратор,
	|			,
	|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ДокументыРасчетовСКонтрагентами),
	|			Организация В (&Организация)
	|				И Субконто1 В
	|					(ВЫБРАТЬ
	|						НДСПредъявленный.СчетФактура
	|					ИЗ
	|						ВТНДСПредъявленныйОстаткиНалоговыйАгент0 КАК НДСПредъявленный),
	|			КорСчет В (ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетныеСчета), ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ВалютныеСчета)),
	|			) КАК ХозрасчетныйОбороты
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ХозрасчетныйОборотыБюджет.Субконто1 КАК Субконто1,
	|	ХозрасчетныйОборотыБюджет.Регистратор КАК Регистратор
	|ПОМЕСТИТЬ ВТХозрасчетныйОборотыБюджетНалоговыйАгент0
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(, &КонецПериодаГраница, Регистратор, Счет = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НДСНалоговогоАгента), ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ДокументыРасчетовСКонтрагентами), Организация В (&Организация), КорСчет В (ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетныеСчета), ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ВалютныеСчета)), ) КАК ХозрасчетныйОборотыБюджет
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Субконто1
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТХозрасчетныйОборотыНалоговыйАгент0.Субконто1 КАК Субконто1,
	|	ВТХозрасчетныйОборотыНалоговыйАгент0.Регистратор,
	|	ВТХозрасчетныйОборотыБюджетНалоговыйАгент0.Регистратор КАК РегистраторОплатыВБюджет
	|ПОМЕСТИТЬ ВТХозрасчетныйОборотыРезультирующая
	|ИЗ
	|	ВТХозрасчетныйОборотыНалоговыйАгент0 КАК ВТХозрасчетныйОборотыНалоговыйАгент0
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТХозрасчетныйОборотыБюджетНалоговыйАгент0 КАК ВТХозрасчетныйОборотыБюджетНалоговыйАгент0
	|		ПО ВТХозрасчетныйОборотыНалоговыйАгент0.Регистратор = ВТХозрасчетныйОборотыБюджетНалоговыйАгент0.Субконто1
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Субконто1
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СоставКнигиПокупок.СчетФактура КАК СчетФактура,
	|	СоставКнигиПокупок.СтавкаНДС,
	|	СоставКнигиПокупок.ДокументОплаты КАК ДокументОплаты,
	|	СоставКнигиПокупок.СуммаБезНДСОборот,
	|	СоставКнигиПокупок.НДСОборот,
	|	СоставКнигиПокупок.Событие,
	|	СоставКнигиПокупок.ВидЦенности,
	|	СоставКнигиПокупок.ЗаписьДополнительногоЛиста
	|ПОМЕСТИТЬ ВТСоставКнигиПокупокНалоговыйАгент0
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПокупок.Обороты(
	|			&НачалоПериода,
	|			&КонецПериодаГраница,
	|			,
	|			Организация В (&Организация)
	|				И ВидЦенности В (&ВидыЦенностейНалоговыйАгент)
	|				И Событие = ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПокупки.ПредъявленНДСКВычету0)) КАК СоставКнигиПокупок
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументОплаты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДСПредъявленныйРеализация0Остатки.СчетФактура КАК ДокументРегистратор,
	|	НДСПредъявленныйРеализация0Остатки.СтавкаНДС,
	|	НДСПредъявленныйРеализация0Остатки.ДокументОтгрузки КАК ДокументОтгрузки,
	|	ВЫБОР
	|		КОГДА НДСПредъявленныйРеализация0Остатки.Состояние = ЗНАЧЕНИЕ(Перечисление.НДССостоянияРеализация0.ОжидаетсяПодтверждение)
	|			ТОГДА &ТекстВычетНеОтраженДоПодтвержденияСтавки0
	|		ИНАЧЕ &ТекстВычетНеОтраженВКнигеПокупок
	|	КОНЕЦ КАК ПризнакНаличияОшибок,
	|	&ТекстНеОтраженыВКнигеПокупок КАК ПризнакОтраженияНДС,
	|	СУММА(ЕСТЬNULL(НДСПредъявленныйРеализация0Остатки.СуммаБезНДСОстаток, 0)) КАК Сумма,
	|	СУММА(ЕСТЬNULL(НДСПредъявленныйРеализация0Остатки.НДСОстаток, 0)) КАК СуммаНДС,
	|	1 КАК КоличествоДокументов,
	|	СчетФактураВыданныйДокументыОснования.Ссылка КАК СчетФактура,
	|	ХозрасчетныйОбороты.Регистратор КАК ДокументОплатыПоставщику,
	|	ХозрасчетныйОбороты.РегистраторОплатыВБюджет КАК ДокументОплатыВБюджет,
	|	0 КАК СуммаВключенные,
	|	СУММА(ЕСТЬNULL(НДСПредъявленныйРеализация0Остатки.НДСОстаток, 0)) КАК СуммаНеВключенные,
	|	НДСПредъявленныйРеализация0Остатки.ВидЦенности,
	|	ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПродажи.ПустаяСсылка) КАК Событие,
	|	ЛОЖЬ КАК ЗаписьДополнительногоЛиста
	|ИЗ
	|	ВТНДСПредъявленныйРеализация0НалоговыйАгент0 КАК НДСПредъявленныйРеализация0Остатки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТНДСПредъявленныйОстаткиНалоговыйАгент0 КАК НДСПредъявленныйОстатки
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТХозрасчетныйОборотыРезультирующая КАК ХозрасчетныйОбороты
	|			ПО НДСПредъявленныйОстатки.СчетФактура = ХозрасчетныйОбороты.Субконто1
	|		ПО НДСПредъявленныйРеализация0Остатки.СчетФактура = НДСПредъявленныйОстатки.СчетФактура
	|			И НДСПредъявленныйРеализация0Остатки.ВидЦенности = НДСПредъявленныйОстатки.ВидЦенности
	|			И НДСПредъявленныйРеализация0Остатки.СтавкаНДС = НДСПредъявленныйОстатки.СтавкаНДС
	|			И НДСПредъявленныйРеализация0Остатки.СчетУчетаНДС = НДСПредъявленныйОстатки.СчетУчетаНДС
	|			И (НДСПредъявленныйОстатки.СуммаБезНДСОстаток + НДСПредъявленныйОстатки.НДСОстаток > 0)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураВыданный.ДокументыОснования КАК СчетФактураВыданныйДокументыОснования
	|		ПО (ХозрасчетныйОбороты.Регистратор = СчетФактураВыданныйДокументыОснования.ДокументОснование)
	|ГДЕ
	|	(НДСПредъявленныйОстатки.СуммаБезНДСОстаток > 0
	|			ИЛИ НДСПредъявленныйОстатки.НДСОстаток > 0)
	|
	|СГРУППИРОВАТЬ ПО
	|	ХозрасчетныйОбороты.РегистраторОплатыВБюджет,
	|	СчетФактураВыданныйДокументыОснования.Ссылка,
	|	НДСПредъявленныйРеализация0Остатки.ДокументОтгрузки,
	|	НДСПредъявленныйРеализация0Остатки.СчетФактура,
	|	НДСПредъявленныйРеализация0Остатки.СтавкаНДС,
	|	ВЫБОР
	|		КОГДА НДСПредъявленныйРеализация0Остатки.Состояние = ЗНАЧЕНИЕ(Перечисление.НДССостоянияРеализация0.ОжидаетсяПодтверждение)
	|			ТОГДА &ТекстВычетНеОтраженДоПодтвержденияСтавки0
	|		ИНАЧЕ &ТекстВычетНеОтраженВКнигеПокупок
	|	КОНЕЦ,
	|	ХозрасчетныйОбороты.Регистратор,
	|	НДСПредъявленныйРеализация0Остатки.ВидЦенности
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СоставКнигиПокупок.СчетФактура,
	|	СоставКнигиПокупок.СтавкаНДС,
	|	NULL,
	|	&ТекстБезОшибок,
	|	&ТекстОтраженыВКнигеПокупок,
	|	СоставКнигиПокупок.СуммаБезНДСОборот,
	|	СоставКнигиПокупок.НДСОборот,
	|	1,
	|	СчетФактураВыданныйДокументыОснования.Ссылка,
	|	ХозрасчетныйОборотыОплата.Регистратор,
	|	ХозрасчетныйОборотыОплата.РегистраторОплатыВБюджет,
	|	СоставКнигиПокупок.НДСОборот,
	|	0,
	|	СоставКнигиПокупок.ВидЦенности,
	|	СоставКнигиПокупок.Событие,
	|	СоставКнигиПокупок.ЗаписьДополнительногоЛиста
	|ИЗ
	|	ВТСоставКнигиПокупокНалоговыйАгент0 КАК СоставКнигиПокупок
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураВыданный.ДокументыОснования КАК СчетФактураВыданныйДокументыОснования
	|		ПО СоставКнигиПокупок.ДокументОплаты = СчетФактураВыданныйДокументыОснования.ДокументОснование
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТХозрасчетныйОборотыРезультирующая КАК ХозрасчетныйОборотыОплата
	|		ПО СоставКнигиПокупок.СчетФактура = ХозрасчетныйОборотыОплата.Субконто1";
	
	ВычетИсполнениеОНА0 = Запрос.Выполнить();
	
	ВнешнийНаборДанныхВычетИсполнениеОНА0 = Новый Структура;
	ВнешнийНаборДанныхВычетИсполнениеОНА0.Вставить("ВычетИсполнениеОНА0",ВычетИсполнениеОНА0);
	
	СтруктураВнешнихНаборов.Вставить("ВнешнийНаборДанныхВычетИсполнениеОНА0", ВнешнийНаборДанныхВычетИсполнениеОНА0);
	
КонецПроцедуры // ПолучитьНаборДанныхВычетИсполнениеОНА0()

Процедура ПолучитьНаборДанныхВосстановлениеНДСпоОбъектамНедвижимости(Запрос, СтруктураВнешнихНаборов)
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	ВосстановлениеНДСпоОбъектамНедвижимости.ОбъектНедвижимости КАК ОбъектНедвижимости,
	               |	ВосстановлениеНДСпоОбъектамНедвижимостиСчетаФактуры.КодОперацииДляДекларации КАК КодОперацииДляДекларации,
	               |	СУММА(ЕСТЬNULL(ВосстановлениеНДСпоОбъектамНедвижимостиСчетаФактуры.НДС, 0)) КАК СуммаНДС,
	               |	ГОД(ВосстановлениеНДСпоОбъектамНедвижимости.Ссылка.Дата) КАК ГодВосстановленияНДС,
	               |	ВосстановлениеНДСпоОбъектамНедвижимости.ДоляВыручкиНеОблагаемаяНДС КАК ДоляВыручкиНеОблагаемаяНДС,
	               |	СУММА(ЕСТЬNULL(ВосстановлениеНДСпоОбъектамНедвижимостиСчетаФактуры.НДСВосстановлен, 0)) КАК НДСВосстановлен,
	               |	ВосстановлениеНДСпоОбъектамНедвижимости.Ссылка КАК ДокументРегистратор,
	               |	ВосстановлениеНДСпоОбъектамНедвижимостиСчетаФактуры.СчетФактура
	               |ИЗ
	               |	Документ.ВосстановлениеНДСПоОбъектамНедвижимости.ОбъектыНедвижимости КАК ВосстановлениеНДСпоОбъектамНедвижимости
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВосстановлениеНДСПоОбъектамНедвижимости.СчетаФактуры КАК ВосстановлениеНДСпоОбъектамНедвижимостиСчетаФактуры
	               |		ПО ВосстановлениеНДСпоОбъектамНедвижимости.Ссылка = ВосстановлениеНДСпоОбъектамНедвижимостиСчетаФактуры.Ссылка
	               |			И ВосстановлениеНДСпоОбъектамНедвижимости.КлючСтроки = ВосстановлениеНДСпоОбъектамНедвижимостиСчетаФактуры.КлючСтроки
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОсновныеСредства.КонтактнаяИнформация КАК ОсновныеСредстваКонтактнаяИнформация
	               |		ПО ВосстановлениеНДСпоОбъектамНедвижимости.ОбъектНедвижимости = ОсновныеСредстваКонтактнаяИнформация.Ссылка
	               |			И (ОсновныеСредстваКонтактнаяИнформация.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.АдресМестонахожденияОсновныеСредства))
	               |ГДЕ
	               |	ВосстановлениеНДСпоОбъектамНедвижимости.Ссылка.Организация В(&Организация)
	               |	И ВосстановлениеНДСпоОбъектамНедвижимости.Ссылка.Дата <= &КонецПериода
	               |	И ВосстановлениеНДСпоОбъектамНедвижимости.Ссылка.Проведен
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ВосстановлениеНДСпоОбъектамНедвижимости.ОбъектНедвижимости,
	               |	ВосстановлениеНДСпоОбъектамНедвижимостиСчетаФактуры.КодОперацииДляДекларации,
	               |	ВосстановлениеНДСпоОбъектамНедвижимости.ДоляВыручкиНеОблагаемаяНДС,
	               |	ВосстановлениеНДСпоОбъектамНедвижимости.Ссылка,
	               |	ВосстановлениеНДСпоОбъектамНедвижимостиСчетаФактуры.СчетФактура,
	               |	ГОД(ВосстановлениеНДСпоОбъектамНедвижимости.Ссылка.Дата)";

	   ВосстановлениеНДСпоОбъектамНедвижимости = Запрос.Выполнить();
	   
	   ВнешнийНаборДанныхВосстановлениеНДСпоОбъектамНедвижимости = Новый Структура;
	   ВнешнийНаборДанныхВосстановлениеНДСпоОбъектамНедвижимости.Вставить("ВосстановлениеНДСпоОбъектамНедвижимости",ВосстановлениеНДСпоОбъектамНедвижимости);
	   
	   СтруктураВнешнихНаборов.Вставить("ВнешнийНаборДанныхВосстановлениеНДСпоОбъектамНедвижимости", ВнешнийНаборДанныхВосстановлениеНДСпоОбъектамНедвижимости);
				   
 КонецПроцедуры


#КонецЕсли