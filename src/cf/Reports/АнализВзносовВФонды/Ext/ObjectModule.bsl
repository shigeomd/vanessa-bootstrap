#Если Не ТолстыйКлиентУправляемоеПриложение Или Сервер Тогда

Перем ДатаНачалаНП, ДатаКонцаНП;

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	ИнициализироватьОтчет();
	
	СтандартнаяОбработка = ложь;
	ДокументРезультат.Очистить();
	
	НастройкиОтчета = КомпоновщикНастроек.ПолучитьНастройки();
	
	ЗначениеПараметра = НастройкиОтчета.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("Период"));
	Если ЗначениеПараметра <> Неопределено Тогда
		ДатаНачалаНП = Дата(ЗначениеПараметра.Значение.ДатаНачала);
		ДатаКонцаНП  = Дата(ЗначениеПараметра.Значение.ДатаОкончания);
	КонецЕсли;
	
	Если ДатаНачалаНП = '00010101' Тогда
		ДатаНачалаНП = НачалоМесяца(ТекущаяДатаСеанса());
	КонецЕсли;
	Если ДатаКонцаНП = '00010101' Тогда
		ДатаКонцаНП = КонецМесяца(ТекущаяДатаСеанса());
	КонецЕсли;
	
	НастройкиОтчета.Структура.Очистить();
	
	Группировка = НастройкиОтчета.Структура.Добавить(Тип("ГруппировкаКомпоновкиДанных"));
	Группировка.ПоляГруппировки.Элементы.Очистить();
	Группировка.Выбор.Элементы.Очистить();
	Группировка.Выбор.Элементы.Добавить(Тип("АвтоВыбранноеПолеКомпоновкиДанных"));
	
	НастройкиОтчета.Выбор.Элементы.Очистить();
	ОтчетыКлиентСервер.ДобавитьВыбранноеПоле(НастройкиОтчета.Выбор, "Организация");
	ОтчетыКлиентСервер.ДобавитьВыбранноеПоле(НастройкиОтчета.Выбор, "ГоловнаяОрганизация");
	ОтчетыКлиентСервер.ДобавитьВыбранноеПоле(НастройкиОтчета.Выбор, "ФизическоеЛицо");
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, НастройкиОтчета, , , Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
		
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, , , Истина);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
	
	ТаблицаЗначений =  Новый ТаблицаЗначений;
	ПроцессорВывода.УстановитьОбъект(ТаблицаЗначений);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	Если ТаблицаЗначений.Количество() > 0 Тогда
		СформироватьМакет(ДокументРезультат, ТаблицаЗначений, ДанныеРасшифровки);
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ИнициализироватьОтчет() Экспорт
	
	ЗарплатаКадрыОбщиеНаборыДанных.ЗаполнитьОбщиеИсточникиДанныхОтчета(ЭтотОбъект);
	
КонецПроцедуры

// Для общей формы "Форма отчета" подсистемы "Варианты отчетов".
Процедура ОпределитьНастройкиФормы(Форма, КлючВарианта, Настройки) Экспорт
	
	ИнициализироватьОтчет();
	ЗначениеВДанныеФормы(ЭтотОбъект, Форма.Отчет);
	
	Настройки.СоответствиеПериодичностиПараметров.Вставить(Новый ПараметрКомпоновкиДанных("Период"), Перечисления.ДоступныеПериодыОтчета.Месяц);
	
КонецПроцедуры

Процедура СформироватьМакет(ДокументРезультат, ФизЛицаОрганизаций, ДанныеРасшифровки)
	
	// Создание запроса и установка всех необходимых параметров.
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц; 
	Запрос.УстановитьПараметр("ТаблицаФизЛицОрганизаций", ФизЛицаОрганизаций);
	Запрос.УстановитьПараметр("НачалоПериода", ДатаНачалаНП);
	Запрос.УстановитьПараметр("КонецПериода",  ДатаКонцаНП);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаФизЛиц.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ТаблицаФизЛиц.Организация КАК Организация,
	|	ТаблицаФизЛиц.ГоловнаяОрганизация КАК ГоловнаяОрганизация
	|ПОМЕСТИТЬ ВТФизЛицаОрганизаций
	|ИЗ
	|	&ТаблицаФизЛицОрганизаций КАК ТаблицаФизЛиц
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ФизическоеЛицо,
	|	Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ФизЛицаОрганизаций.Организация КАК Организация
	|ИЗ
	|	ВТФизЛицаОрганизаций КАК ФизЛицаОрганизаций";
	СписокОрганизаций = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Организация");
	
	Организация = Неопределено;
	Если СписокОрганизаций.Количество() = 1 Тогда
		Организация = СписокОрганизаций[0];
	КонецЕсли;	
		
	УчетСтраховыхВзносов.СформироватьВТРасширенныеСведенияОДоходахИВзносах(ДатаНачалаНП, ДатаКонцаНП, Организация, Запрос.МенеджерВременныхТаблиц, Истина, Истина, Истина, Истина, Истина, , , , Макс(ТекущаяДатаСеанса(), ДатаКонцаНП), Истина);
	
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ФизЛицаОрганизаций.ФизическоеЛицо
	|ИЗ
	|	ВТФизЛицаОрганизаций КАК ФизЛицаОрганизаций
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ИсчисленныеСтраховыеВзносы КАК ИсчисленныеСтраховыеВзносы
	|		ПО ФизЛицаОрганизаций.ФизическоеЛицо = ИсчисленныеСтраховыеВзносы.ФизическоеЛицо
	|			И ФизЛицаОрганизаций.Организация = ИсчисленныеСтраховыеВзносы.Организация
	|			И ФизЛицаОрганизаций.ГоловнаяОрганизация = ИсчисленныеСтраховыеВзносы.ГоловнаяОрганизация
	|ГДЕ
	|	НАЧАЛОПЕРИОДА(ИсчисленныеСтраховыеВзносы.ДатаПолученияДохода, КВАРТАЛ) < НАЧАЛОПЕРИОДА(ИсчисленныеСтраховыеВзносы.Период, КВАРТАЛ)
	|	И ИсчисленныеСтраховыеВзносы.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ФизЛицаОрганизаций.ФизическоеЛицо
	|ИЗ
	|	ВТФизЛицаОрганизаций КАК ФизЛицаОрганизаций
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.СведенияОДоходахСтраховыеВзносы КАК СведенияОДоходахСтраховыеВзносы
	|		ПО ФизЛицаОрганизаций.ФизическоеЛицо = СведенияОДоходахСтраховыеВзносы.ФизическоеЛицо
	|			И ФизЛицаОрганизаций.Организация = СведенияОДоходахСтраховыеВзносы.Организация
	|			И ФизЛицаОрганизаций.ГоловнаяОрганизация = СведенияОДоходахСтраховыеВзносы.ГоловнаяОрганизация
	|ГДЕ
	|	НАЧАЛОПЕРИОДА(СведенияОДоходахСтраховыеВзносы.ДатаПолученияДохода, КВАРТАЛ) < НАЧАЛОПЕРИОДА(СведенияОДоходахСтраховыеВзносы.Период, КВАРТАЛ)
	|	И СведенияОДоходахСтраховыеВзносы.Период МЕЖДУ &НачалоПериода И &КонецПериода";
	
	Если Не Запрос.Выполнить().Пустой() Тогда
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	СведенияОДоходах.Организация КАК Организация,
		|	СведенияОДоходах.ФизическоеЛицо КАК ФизическоеЛицо,
		|	СведенияОДоходах.Период КАК МесяцПолученияДохода,
		|	СведенияОДоходах.ПериодРегистрации КАК МесяцРегистрацииДохода,
		|	ВЫБОР
		|		КОГДА СведенияОДоходах.Начисление ЕСТЬ NULL 
		|			ТОГДА СведенияОДоходах.ВидДохода
		|		КОГДА СведенияОДоходах.Начисление = НЕОПРЕДЕЛЕНО
		|			ТОГДА СведенияОДоходах.ВидДохода
		|		ИНАЧЕ СведенияОДоходах.Начисление
		|	КОНЕЦ КАК ВидРасчета,
		|	СведенияОДоходах.Инвалид КАК Инвалид,
		|	СведенияОДоходах.НачислятьНакопительнуюЧасть КАК НачислятьНакопительнуюЧасть,
		|	СведенияОДоходах.ВидТарифаСтраховыхВзносов КАК ВидТарифаСтраховыхВзносов,
		|	СведенияОДоходах.ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией,
		|	СведенияОДоходах.КлассУсловийТруда,
		|	СведенияОДоходах.ЗастрахованФССкакИностранец,
		|	СУММА(СведенияОДоходах.Сумма) КАК НачисленоВсего,
		|	СУММА(ВЫБОР
		|			КОГДА СведенияОДоходах.ВидДохода = ЗНАЧЕНИЕ(Справочник.ВидыДоходовПоСтраховымВзносам.НеЯвляетсяОбъектом)
		|				ТОГДА СведенияОДоходах.Сумма
		|			ИНАЧЕ СведенияОДоходах.НеОблагаетсяПФР
		|		КОНЕЦ) КАК НеОблагаетсяПФР,
		|	СУММА(ВЫБОР
		|			КОГДА СведенияОДоходах.ВидДохода = ЗНАЧЕНИЕ(Справочник.ВидыДоходовПоСтраховымВзносам.НеЯвляетсяОбъектом)
		|				ТОГДА СведенияОДоходах.Сумма
		|			ИНАЧЕ СведенияОДоходах.НеОблагаетсяФОМС
		|		КОНЕЦ) КАК НеОблагаетсяФОМС,
		|	СУММА(ВЫБОР
		|			КОГДА СведенияОДоходах.ВидДохода = ЗНАЧЕНИЕ(Справочник.ВидыДоходовПоСтраховымВзносам.НеЯвляетсяОбъектом)
		|				ТОГДА СведенияОДоходах.Сумма
		|			ИНАЧЕ СведенияОДоходах.НеОблагаетсяФСС
		|		КОНЕЦ) КАК НеОблагаетсяФСС,
		|	СУММА(ВЫБОР
		|			КОГДА СведенияОДоходах.ВидДохода = ЗНАЧЕНИЕ(Справочник.ВидыДоходовПоСтраховымВзносам.НеЯвляетсяОбъектом)
		|				ТОГДА СведенияОДоходах.Сумма
		|			ИНАЧЕ СведенияОДоходах.НеОблагаетсяФСС_НС
		|		КОНЕЦ) КАК НеОблагаетсяФСС_НС,
		|	СУММА(СведенияОДоходах.СуммаПревысившаяПределПФР) КАК СуммаПревысившаяПределПФР,
		|	СУММА(СведенияОДоходах.СуммаПревысившаяПределФОМС) КАК СуммаПревысившаяПределФОМС,
		|	СУММА(СведенияОДоходах.СуммаПревысившаяПределФСС) КАК СуммаПревысившаяПределФСС,
		|	СУММА(СведенияОДоходах.БазаПФР - СведенияОДоходах.СуммаПревысившаяПределПФР) КАК БазаПФР,
		|	СУММА(СведенияОДоходах.БазаПФР) КАК БазаПФРЗанятыхНаРаботахСДосрочнойПенсией,
		|	СУММА(СведенияОДоходах.БазаФОМС - СведенияОДоходах.СуммаПревысившаяПределФОМС) КАК БазаФОМС,
		|	СУММА(СведенияОДоходах.БазаФСС - СведенияОДоходах.СуммаПревысившаяПределФСС) КАК БазаФСС,
		|	СУММА(СведенияОДоходах.БазаФСС_НС) КАК БазаФСС_НС,
		|	СУММА(СведенияОДоходах.ПФРПоСуммарномуТарифу) КАК ПФРПоСуммарномуТарифу,
		|	СУММА(СведенияОДоходах.ПФРСтраховая) КАК ПФРСтраховая,
		|	СУММА(СведенияОДоходах.ПФРССуммПревышения) КАК ПФРССуммПревышения,
		|	СУММА(СведенияОДоходах.ПФРНакопительная) КАК ПФРНакопительная,
		|	СУММА(СведенияОДоходах.ФФОМС) КАК ФФОМС,
		|	СУММА(СведенияОДоходах.ТФОМС) КАК ТФОМС,
		|	СУММА(СведенияОДоходах.ФСС) КАК ФСС,
		|	СУММА(СведенияОДоходах.ФССНесчастныеСлучаи) КАК ФССНесчастныеСлучаи,
		|	СУММА(СведенияОДоходах.ПФРЗаЗанятыхНаРаботахСДосрочнойПенсией) КАК ПФРЗаЗанятыхНаРаботахСДосрочнойПенсией,
		|	СУММА(СведенияОДоходах.ПФРНаДоплатуЛетчикам) КАК ПФРПоДополнительномуТарифу,
		|	СУММА(СведенияОДоходах.ПФРНаДоплатуШахтерам) КАК ПФРНаДоплатуКПенсииШахтерам,
		|	СУММА(СведенияОДоходах.ПособияЗаСчетФСС) КАК ПособиеЗаСчетФСС,
		|	СУММА(СведенияОДоходах.ПособияЗаСчетФСС_НС) КАК ПособиеЗаСчетФСС_НС
		|ПОМЕСТИТЬ ВТДанныеБезОпоздавшихИсправлений
		|ИЗ
		|	ВТРасширенныеСведенияОДоходахИВзносах КАК СведенияОДоходах
		|ГДЕ
		|	СведенияОДоходах.ПериодРегистрации МЕЖДУ &НачалоПериода И &КонецПериода
		|
		|СГРУППИРОВАТЬ ПО
		|	СведенияОДоходах.ФизическоеЛицо,
		|	СведенияОДоходах.НачислятьНакопительнуюЧасть,
		|	СведенияОДоходах.ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией,
		|	СведенияОДоходах.КлассУсловийТруда,
		|	СведенияОДоходах.ЗастрахованФССкакИностранец,
		|	СведенияОДоходах.Организация,
		|	СведенияОДоходах.ВидТарифаСтраховыхВзносов,
		|	СведенияОДоходах.Инвалид,
		|	ВЫБОР
		|		КОГДА СведенияОДоходах.Начисление ЕСТЬ NULL 
		|			ТОГДА СведенияОДоходах.ВидДохода
		|		КОГДА СведенияОДоходах.Начисление = НЕОПРЕДЕЛЕНО
		|			ТОГДА СведенияОДоходах.ВидДохода
		|		ИНАЧЕ СведенияОДоходах.Начисление
		|	КОНЕЦ,
		|	СведенияОДоходах.Период,
		|	СведенияОДоходах.ПериодРегистрации
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТРасширенныеСведенияОДоходахИВзносах
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТРазмерыТарифныхСтавок
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТДанныеОбИнвалидности
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТИспользуемыеТарифы
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТОписаниеТарифов
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТВидыДеятельности
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МАКСИМУМ(ДатыПолученияДохода.ДатаПолученияДохода) КАК ПоздняяДатаПолученияДохода,
		|	МИНИМУМ(ДатыПолученияДохода.ДатаПолученияДохода) КАК РанняяДатаПолученияДохода
		|ИЗ
		|	(ВЫБРАТЬ
		|		ИсчисленныеСтраховыеВзносы.ДатаПолученияДохода КАК ДатаПолученияДохода
		|	ИЗ
		|		ВТФизЛицаОрганизаций КАК ФизЛицаОрганизаций
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ИсчисленныеСтраховыеВзносы КАК ИсчисленныеСтраховыеВзносы
		|			ПО ФизЛицаОрганизаций.ФизическоеЛицо = ИсчисленныеСтраховыеВзносы.ФизическоеЛицо
		|				И ФизЛицаОрганизаций.Организация = ИсчисленныеСтраховыеВзносы.Организация
		|				И ФизЛицаОрганизаций.ГоловнаяОрганизация = ИсчисленныеСтраховыеВзносы.ГоловнаяОрганизация
		|	ГДЕ
		|		ИсчисленныеСтраховыеВзносы.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		СведенияОДоходахСтраховыеВзносы.ДатаПолученияДохода
		|	ИЗ
		|		ВТФизЛицаОрганизаций КАК ФизЛицаОрганизаций
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.СведенияОДоходахСтраховыеВзносы КАК СведенияОДоходахСтраховыеВзносы
		|			ПО ФизЛицаОрганизаций.ФизическоеЛицо = СведенияОДоходахСтраховыеВзносы.ФизическоеЛицо
		|				И ФизЛицаОрганизаций.Организация = СведенияОДоходахСтраховыеВзносы.Организация
		|				И ФизЛицаОрганизаций.ГоловнаяОрганизация = СведенияОДоходахСтраховыеВзносы.ГоловнаяОрганизация
		|	ГДЕ
		|		СведенияОДоходахСтраховыеВзносы.Период МЕЖДУ &НачалоПериода И &КонецПериода) КАК ДатыПолученияДохода";
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() И ЗначениеЗаполнено(Выборка.РанняяДатаПолученияДохода) Тогда
			УточненнаяДатаНачалаНП = Мин(ДатаНачалаНП, Выборка.РанняяДатаПолученияДохода);
			УточненнаяДатаКонцаНП = Макс(ДатаКонцаНП, Выборка.ПоздняяДатаПолученияДохода);
		Иначе 	
			УточненнаяДатаНачалаНП = ДатаНачалаНП;
			УточненнаяДатаКонцаНП = ДатаКонцаНП;
		КонецЕсли;
		
		УчетСтраховыхВзносов.СформироватьВТРасширенныеСведенияОДоходахИВзносах(УточненнаяДатаНачалаНП, УточненнаяДатаКонцаНП, Организация, Запрос.МенеджерВременныхТаблиц, Истина, Истина, Истина, Истина, Истина,,,, Макс(ТекущаяДатаСеанса(), УточненнаяДатаКонцаНП));
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	СведенияОДоходах.Организация КАК Организация,
		|	СведенияОДоходах.ФизическоеЛицо КАК ФизическоеЛицо,
		|	СведенияОДоходах.Период КАК МесяцПолученияДохода,
		|	СведенияОДоходах.ПериодРегистрации КАК МесяцРегистрацииДохода,
		|	ВЫБОР
		|		КОГДА СведенияОДоходах.Начисление ЕСТЬ NULL 
		|			ТОГДА СведенияОДоходах.ВидДохода
		|		КОГДА СведенияОДоходах.Начисление = НЕОПРЕДЕЛЕНО
		|			ТОГДА СведенияОДоходах.ВидДохода
		|		ИНАЧЕ СведенияОДоходах.Начисление
		|	КОНЕЦ КАК ВидРасчета,
		|	СведенияОДоходах.Инвалид КАК Инвалид,
		|	СведенияОДоходах.НачислятьНакопительнуюЧасть КАК НачислятьНакопительнуюЧасть,
		|	СведенияОДоходах.ВидТарифаСтраховыхВзносов КАК ВидТарифаСтраховыхВзносов,
		|	СведенияОДоходах.ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией,
		|	СведенияОДоходах.КлассУсловийТруда,
		|	СведенияОДоходах.ЗастрахованФССкакИностранец,
		|	СУММА(СведенияОДоходах.Сумма) КАК НачисленоВсего,
		|	СУММА(ВЫБОР
		|			КОГДА СведенияОДоходах.ВидДохода = ЗНАЧЕНИЕ(Справочник.ВидыДоходовПоСтраховымВзносам.НеЯвляетсяОбъектом)
		|				ТОГДА СведенияОДоходах.Сумма
		|			ИНАЧЕ СведенияОДоходах.НеОблагаетсяПФР
		|		КОНЕЦ) КАК НеОблагаетсяПФР,
		|	СУММА(ВЫБОР
		|			КОГДА СведенияОДоходах.ВидДохода = ЗНАЧЕНИЕ(Справочник.ВидыДоходовПоСтраховымВзносам.НеЯвляетсяОбъектом)
		|				ТОГДА СведенияОДоходах.Сумма
		|			ИНАЧЕ СведенияОДоходах.НеОблагаетсяФОМС
		|		КОНЕЦ) КАК НеОблагаетсяФОМС,
		|	СУММА(ВЫБОР
		|			КОГДА СведенияОДоходах.ВидДохода = ЗНАЧЕНИЕ(Справочник.ВидыДоходовПоСтраховымВзносам.НеЯвляетсяОбъектом)
		|				ТОГДА СведенияОДоходах.Сумма
		|			ИНАЧЕ СведенияОДоходах.НеОблагаетсяФСС
		|		КОНЕЦ) КАК НеОблагаетсяФСС,
		|	СУММА(ВЫБОР
		|			КОГДА СведенияОДоходах.ВидДохода = ЗНАЧЕНИЕ(Справочник.ВидыДоходовПоСтраховымВзносам.НеЯвляетсяОбъектом)
		|				ТОГДА СведенияОДоходах.Сумма
		|			ИНАЧЕ СведенияОДоходах.НеОблагаетсяФСС_НС
		|		КОНЕЦ) КАК НеОблагаетсяФСС_НС,
		|	СУММА(СведенияОДоходах.СуммаПревысившаяПределПФР) КАК СуммаПревысившаяПределПФР,
		|	СУММА(СведенияОДоходах.СуммаПревысившаяПределФОМС) КАК СуммаПревысившаяПределФОМС,
		|	СУММА(СведенияОДоходах.СуммаПревысившаяПределФСС) КАК СуммаПревысившаяПределФСС,
		|	СУММА(СведенияОДоходах.БазаПФР - СведенияОДоходах.СуммаПревысившаяПределПФР) КАК БазаПФР,
		|	СУММА(СведенияОДоходах.БазаПФР) КАК БазаПФРЗанятыхНаРаботахСДосрочнойПенсией,
		|	СУММА(СведенияОДоходах.БазаФОМС - СведенияОДоходах.СуммаПревысившаяПределФОМС) КАК БазаФОМС,
		|	СУММА(СведенияОДоходах.БазаФСС - СведенияОДоходах.СуммаПревысившаяПределФСС) КАК БазаФСС,
		|	СУММА(СведенияОДоходах.БазаФСС_НС) КАК БазаФСС_НС,
		|	СУММА(СведенияОДоходах.ПФРПоСуммарномуТарифу) КАК ПФРПоСуммарномуТарифу,
		|	СУММА(СведенияОДоходах.ПФРСтраховая) КАК ПФРСтраховая,
		|	СУММА(СведенияОДоходах.ПФРССуммПревышения) КАК ПФРССуммПревышения,
		|	СУММА(СведенияОДоходах.ПФРНакопительная) КАК ПФРНакопительная,
		|	СУММА(СведенияОДоходах.ФФОМС) КАК ФФОМС,
		|	СУММА(СведенияОДоходах.ТФОМС) КАК ТФОМС,
		|	СУММА(СведенияОДоходах.ФСС) КАК ФСС,
		|	СУММА(СведенияОДоходах.ФССНесчастныеСлучаи) КАК ФССНесчастныеСлучаи,
		|	СУММА(СведенияОДоходах.ПФРЗаЗанятыхНаРаботахСДосрочнойПенсией) КАК ПФРЗаЗанятыхНаРаботахСДосрочнойПенсией,
		|	СУММА(СведенияОДоходах.ПФРНаДоплатуЛетчикам) КАК ПФРПоДополнительномуТарифу,
		|	СУММА(СведенияОДоходах.ПФРНаДоплатуШахтерам) КАК ПФРНаДоплатуКПенсииШахтерам,
		|	СУММА(СведенияОДоходах.ПособияЗаСчетФСС) КАК ПособиеЗаСчетФСС,
		|	СУММА(СведенияОДоходах.ПособияЗаСчетФСС_НС) КАК ПособиеЗаСчетФСС_НС,
		|	ЛОЖЬ КАК ВключатьВПервичнуюОтчетностьПФР
		|ПОМЕСТИТЬ ВТДанныеДляОтчета
		|ИЗ
		|	ВТРасширенныеСведенияОДоходахИВзносах КАК СведенияОДоходах
		|ГДЕ
		|	СведенияОДоходах.ПериодРегистрации МЕЖДУ &НачалоПериода И &КонецПериода
		|	И НЕ СведенияОДоходах.ВключатьВПервичнуюОтчетностьПФР
		|
		|СГРУППИРОВАТЬ ПО
		|	СведенияОДоходах.ФизическоеЛицо,
		|	СведенияОДоходах.НачислятьНакопительнуюЧасть,
		|	СведенияОДоходах.ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией,
		|	СведенияОДоходах.КлассУсловийТруда,
		|	СведенияОДоходах.ЗастрахованФССкакИностранец,
		|	СведенияОДоходах.Организация,
		|	СведенияОДоходах.ВидТарифаСтраховыхВзносов,
		|	СведенияОДоходах.Инвалид,
		|	ВЫБОР
		|		КОГДА СведенияОДоходах.Начисление ЕСТЬ NULL 
		|			ТОГДА СведенияОДоходах.ВидДохода
		|		КОГДА СведенияОДоходах.Начисление = НЕОПРЕДЕЛЕНО
		|			ТОГДА СведенияОДоходах.ВидДохода
		|		ИНАЧЕ СведенияОДоходах.Начисление
		|	КОНЕЦ,
		|	СведенияОДоходах.Период,
		|	СведенияОДоходах.ПериодРегистрации
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДанныеБезОпоздавшихИсправлений.Организация,
		|	ДанныеБезОпоздавшихИсправлений.ФизическоеЛицо,
		|	ДанныеБезОпоздавшихИсправлений.МесяцПолученияДохода,
		|	ДанныеБезОпоздавшихИсправлений.МесяцРегистрацииДохода,
		|	ДанныеБезОпоздавшихИсправлений.ВидРасчета,
		|	ДанныеБезОпоздавшихИсправлений.Инвалид,
		|	ДанныеБезОпоздавшихИсправлений.НачислятьНакопительнуюЧасть,
		|	ДанныеБезОпоздавшихИсправлений.ВидТарифаСтраховыхВзносов,
		|	ДанныеБезОпоздавшихИсправлений.ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией,
		|	ДанныеБезОпоздавшихИсправлений.КлассУсловийТруда,
		|	ДанныеБезОпоздавшихИсправлений.ЗастрахованФССкакИностранец,
		|	ДанныеБезОпоздавшихИсправлений.НачисленоВсего,
		|	ДанныеБезОпоздавшихИсправлений.НеОблагаетсяПФР,
		|	ДанныеБезОпоздавшихИсправлений.НеОблагаетсяФОМС,
		|	ДанныеБезОпоздавшихИсправлений.НеОблагаетсяФСС,
		|	ДанныеБезОпоздавшихИсправлений.НеОблагаетсяФСС_НС,
		|	ДанныеБезОпоздавшихИсправлений.СуммаПревысившаяПределПФР,
		|	ДанныеБезОпоздавшихИсправлений.СуммаПревысившаяПределФОМС,
		|	ДанныеБезОпоздавшихИсправлений.СуммаПревысившаяПределФСС,
		|	ДанныеБезОпоздавшихИсправлений.БазаПФР,
		|	ДанныеБезОпоздавшихИсправлений.БазаПФРЗанятыхНаРаботахСДосрочнойПенсией,
		|	ДанныеБезОпоздавшихИсправлений.БазаФОМС,
		|	ДанныеБезОпоздавшихИсправлений.БазаФСС,
		|	ДанныеБезОпоздавшихИсправлений.БазаФСС_НС,
		|	ДанныеБезОпоздавшихИсправлений.ПФРПоСуммарномуТарифу,
		|	ДанныеБезОпоздавшихИсправлений.ПФРСтраховая,
		|	ДанныеБезОпоздавшихИсправлений.ПФРССуммПревышения,
		|	ДанныеБезОпоздавшихИсправлений.ПФРНакопительная,
		|	ДанныеБезОпоздавшихИсправлений.ФФОМС,
		|	ДанныеБезОпоздавшихИсправлений.ТФОМС,
		|	ДанныеБезОпоздавшихИсправлений.ФСС,
		|	ДанныеБезОпоздавшихИсправлений.ФССНесчастныеСлучаи,
		|	ДанныеБезОпоздавшихИсправлений.ПФРЗаЗанятыхНаРаботахСДосрочнойПенсией,
		|	ДанныеБезОпоздавшихИсправлений.ПФРПоДополнительномуТарифу,
		|	ДанныеБезОпоздавшихИсправлений.ПФРНаДоплатуКПенсииШахтерам,
		|	ДанныеБезОпоздавшихИсправлений.ПособиеЗаСчетФСС,
		|	ДанныеБезОпоздавшихИсправлений.ПособиеЗаСчетФСС_НС,
		|	ИСТИНА
		|ИЗ
		|	ВТДанныеБезОпоздавшихИсправлений КАК ДанныеБезОпоздавшихИсправлений";

	Иначе
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	СведенияОДоходах.Организация КАК Организация,
		|	СведенияОДоходах.ФизическоеЛицо КАК ФизическоеЛицо,
		|	СведенияОДоходах.Период КАК МесяцПолученияДохода,
		|	СведенияОДоходах.ПериодРегистрации КАК МесяцРегистрацииДохода,
		|	ВЫБОР
		|		КОГДА СведенияОДоходах.Начисление ЕСТЬ NULL 
		|			ТОГДА СведенияОДоходах.ВидДохода
		|		КОГДА СведенияОДоходах.Начисление = НЕОПРЕДЕЛЕНО
		|			ТОГДА СведенияОДоходах.ВидДохода
		|		ИНАЧЕ СведенияОДоходах.Начисление
		|	КОНЕЦ КАК ВидРасчета,
		|	СведенияОДоходах.Инвалид КАК Инвалид,
		|	СведенияОДоходах.НачислятьНакопительнуюЧасть КАК НачислятьНакопительнуюЧасть,
		|	СведенияОДоходах.ВидТарифаСтраховыхВзносов КАК ВидТарифаСтраховыхВзносов,
		|	СведенияОДоходах.ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией,
		|	СведенияОДоходах.КлассУсловийТруда,
		|	СведенияОДоходах.ЗастрахованФССкакИностранец,
		|	СУММА(СведенияОДоходах.Сумма) КАК НачисленоВсего,
		|	СУММА(ВЫБОР
		|			КОГДА СведенияОДоходах.ВидДохода = ЗНАЧЕНИЕ(Справочник.ВидыДоходовПоСтраховымВзносам.НеЯвляетсяОбъектом)
		|				ТОГДА СведенияОДоходах.Сумма
		|			ИНАЧЕ СведенияОДоходах.НеОблагаетсяПФР
		|		КОНЕЦ) КАК НеОблагаетсяПФР,
		|	СУММА(ВЫБОР
		|			КОГДА СведенияОДоходах.ВидДохода = ЗНАЧЕНИЕ(Справочник.ВидыДоходовПоСтраховымВзносам.НеЯвляетсяОбъектом)
		|				ТОГДА СведенияОДоходах.Сумма
		|			ИНАЧЕ СведенияОДоходах.НеОблагаетсяФОМС
		|		КОНЕЦ) КАК НеОблагаетсяФОМС,
		|	СУММА(ВЫБОР
		|			КОГДА СведенияОДоходах.ВидДохода = ЗНАЧЕНИЕ(Справочник.ВидыДоходовПоСтраховымВзносам.НеЯвляетсяОбъектом)
		|				ТОГДА СведенияОДоходах.Сумма
		|			ИНАЧЕ СведенияОДоходах.НеОблагаетсяФСС
		|		КОНЕЦ) КАК НеОблагаетсяФСС,
		|	СУММА(ВЫБОР
		|			КОГДА СведенияОДоходах.ВидДохода = ЗНАЧЕНИЕ(Справочник.ВидыДоходовПоСтраховымВзносам.НеЯвляетсяОбъектом)
		|				ТОГДА СведенияОДоходах.Сумма
		|			ИНАЧЕ СведенияОДоходах.НеОблагаетсяФСС_НС
		|		КОНЕЦ) КАК НеОблагаетсяФСС_НС,
		|	СУММА(СведенияОДоходах.СуммаПревысившаяПределПФР) КАК СуммаПревысившаяПределПФР,
		|	СУММА(СведенияОДоходах.СуммаПревысившаяПределФОМС) КАК СуммаПревысившаяПределФОМС,
		|	СУММА(СведенияОДоходах.СуммаПревысившаяПределФСС) КАК СуммаПревысившаяПределФСС,
		|	СУММА(СведенияОДоходах.БазаПФР - СведенияОДоходах.СуммаПревысившаяПределПФР) КАК БазаПФР,
		|	СУММА(СведенияОДоходах.БазаПФР) КАК БазаПФРЗанятыхНаРаботахСДосрочнойПенсией,
		|	СУММА(СведенияОДоходах.БазаФОМС - СведенияОДоходах.СуммаПревысившаяПределФОМС) КАК БазаФОМС,
		|	СУММА(СведенияОДоходах.БазаФСС - СведенияОДоходах.СуммаПревысившаяПределФСС) КАК БазаФСС,
		|	СУММА(СведенияОДоходах.БазаФСС_НС) КАК БазаФСС_НС,
		|	СУММА(СведенияОДоходах.ПФРПоСуммарномуТарифу) КАК ПФРПоСуммарномуТарифу,
		|	СУММА(СведенияОДоходах.ПФРСтраховая) КАК ПФРСтраховая,
		|	СУММА(СведенияОДоходах.ПФРССуммПревышения) КАК ПФРССуммПревышения,
		|	СУММА(СведенияОДоходах.ПФРНакопительная) КАК ПФРНакопительная,
		|	СУММА(СведенияОДоходах.ФФОМС) КАК ФФОМС,
		|	СУММА(СведенияОДоходах.ТФОМС) КАК ТФОМС,
		|	СУММА(СведенияОДоходах.ФСС) КАК ФСС,
		|	СУММА(СведенияОДоходах.ФССНесчастныеСлучаи) КАК ФССНесчастныеСлучаи,
		|	СУММА(СведенияОДоходах.ПФРЗаЗанятыхНаРаботахСДосрочнойПенсией) КАК ПФРЗаЗанятыхНаРаботахСДосрочнойПенсией,
		|	СУММА(СведенияОДоходах.ПФРНаДоплатуЛетчикам) КАК ПФРПоДополнительномуТарифу,
		|	СУММА(СведенияОДоходах.ПФРНаДоплатуШахтерам) КАК ПФРНаДоплатуКПенсииШахтерам,
		|	СУММА(СведенияОДоходах.ПособияЗаСчетФСС) КАК ПособиеЗаСчетФСС,
		|	СУММА(СведенияОДоходах.ПособияЗаСчетФСС_НС) КАК ПособиеЗаСчетФСС_НС,
		|	ИСТИНА КАК ВключатьВПервичнуюОтчетностьПФР
		|ПОМЕСТИТЬ ВТДанныеДляОтчета
		|ИЗ
		|	ВТРасширенныеСведенияОДоходахИВзносах КАК СведенияОДоходах
		|ГДЕ
		|	СведенияОДоходах.ПериодРегистрации МЕЖДУ &НачалоПериода И &КонецПериода
		|
		|СГРУППИРОВАТЬ ПО
		|	СведенияОДоходах.ФизическоеЛицо,
		|	СведенияОДоходах.НачислятьНакопительнуюЧасть,
		|	СведенияОДоходах.ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией,
		|	СведенияОДоходах.КлассУсловийТруда,
		|	СведенияОДоходах.ЗастрахованФССкакИностранец,
		|	СведенияОДоходах.Организация,
		|	СведенияОДоходах.ВидТарифаСтраховыхВзносов,
		|	СведенияОДоходах.Инвалид,
		|	ВЫБОР
		|		КОГДА СведенияОДоходах.Начисление ЕСТЬ NULL 
		|			ТОГДА СведенияОДоходах.ВидДохода
		|		КОГДА СведенияОДоходах.Начисление = НЕОПРЕДЕЛЕНО
		|			ТОГДА СведенияОДоходах.ВидДохода
		|		ИНАЧЕ СведенияОДоходах.Начисление
		|	КОНЕЦ,
		|	СведенияОДоходах.Период,
		|	СведенияОДоходах.ПериодРегистрации";
	КонецЕсли;
	Запрос.Выполнить();
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДанныеДляОтчета.ФизическоеЛицо,
		|	ДанныеДляОтчета.МесяцПолученияДохода КАК Период
		|ПОМЕСТИТЬ ВТФизическиеЛицаПериоды
		|ИЗ
		|	ВТДанныеДляОтчета КАК ДанныеДляОтчета
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДанныеДляОтчета.Организация,
		|	ДанныеДляОтчета.МесяцПолученияДохода КАК Период
		|ПОМЕСТИТЬ ВТОрганизациПериоды
		|ИЗ
		|	ВТДанныеДляОтчета КАК ДанныеДляОтчета
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДАТАВРЕМЯ(1, 1, 1) КАК Период,
		|	ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка) КАК ФизическоеЛицо,
		|	ЗНАЧЕНИЕ(Перечисление.ВидыЗастрахованныхЛицОбязательногоСтрахования.ПустаяСсылка) КАК ВидЗастрахованногоЛица
		|ПОМЕСТИТЬ Представления_КадровыеДанныеФизическихЛиц
		|ИЗ
		|	ВТФизическиеЛицаПериоды КАК ФизическиеЛицаПериоды
		|ГДЕ
		|	""ТолькоРазрешенные"" = ИСТИНА";
		
	ЗарплатаКадрыОбщиеНаборыДанных.ЗаменитьЗапросыКПредставлениямВиртуальныхТаблиц(Запрос.Текст);
	Запрос.Выполнить();
	
	// Таблица со взносами на страхование ФСС_НС
	ЗарплатаКадрыОбщиеНаборыДанных.СоздатьВТИмяРегистраСрезПоследних("СтавкаВзносаНаСтрахованиеОтНесчастныхСлучаев",	
		Запрос.МенеджерВременныхТаблиц,
		Истина,
		ЗарплатаКадрыОбщиеНаборыДанных.ОписаниеФильтраДляСоздатьВТИмяРегистра("ВТОрганизациПериоды", "Организация"),,"ВТСтавкиВзносовФСС_НС");

	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДанныеДляОтчета.Организация,
	|	ДанныеДляОтчета.ФизическоеЛицо,
	|	ДанныеДляОтчета.МесяцПолученияДохода,
	|	ДанныеДляОтчета.МесяцРегистрацииДохода,
	|	ДанныеДляОтчета.ВключатьВПервичнуюОтчетностьПФР,
	|	ДанныеДляОтчета.ВидРасчета,
	|	ДанныеДляОтчета.ВидТарифаСтраховыхВзносов,
	|	ДанныеДляОтчета.НачислятьНакопительнуюЧасть,
	|	ДанныеДляОтчета.ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией,
	|	ДанныеДляОтчета.КлассУсловийТруда,
	|	ДанныеДляОтчета.ЗастрахованФССкакИностранец,
	|	КадровыеДанныеФизическихЛиц.ВидЗастрахованногоЛица КАК СтатусЗастрахованногоЛица,
	|	СУММА(ДанныеДляОтчета.НачисленоВсего) КАК НачисленоВсего,
	|	СУММА(ДанныеДляОтчета.НеОблагаетсяПФР) КАК НеОблагаетсяПФР,
	|	СУММА(ДанныеДляОтчета.НеОблагаетсяФОМС) КАК НеОблагаетсяФОМС,
	|	СУММА(ДанныеДляОтчета.НеОблагаетсяФСС) КАК НеОблагаетсяФСС,
	|	СУММА(ДанныеДляОтчета.СуммаПревысившаяПределПФР) КАК СуммаПревысившаяПределПФР,
	|	СУММА(ДанныеДляОтчета.СуммаПревысившаяПределФОМС) КАК СуммаПревысившаяПределФОМС,
	|	СУММА(ДанныеДляОтчета.СуммаПревысившаяПределФСС) КАК СуммаПревысившаяПределФСС,
	|	СУММА(ДанныеДляОтчета.БазаПФР) КАК БазаПФР,
	|	СУММА(ДанныеДляОтчета.БазаПФРЗанятыхНаРаботахСДосрочнойПенсией) КАК БазаПФРЗанятыхНаРаботахСДосрочнойПенсией,
	|	СУММА(ДанныеДляОтчета.БазаФОМС) КАК БазаФОМС,
	|	СУММА(ДанныеДляОтчета.БазаФСС) КАК БазаФСС,
	|	СУММА(ДанныеДляОтчета.ПФРПоСуммарномуТарифу) КАК ПФРПоСуммарномуТарифу,
	|	СУММА(ДанныеДляОтчета.ПФРСтраховая) КАК ПФРСтраховая,
	|	СУММА(ДанныеДляОтчета.ПФРССуммПревышения) КАК ПФРССуммПревышения,
	|	СУММА(ДанныеДляОтчета.ПФРНакопительная) КАК ПФРНакопительная,
	|	СУММА(ДанныеДляОтчета.ФФОМС) КАК ФФОМС,
	|	СУММА(ДанныеДляОтчета.ТФОМС) КАК ТФОМС,
	|	СУММА(ДанныеДляОтчета.ФСС) КАК ФСС,
	|	СУММА(ДанныеДляОтчета.ПФРПоДополнительномуТарифу) КАК ПФРПоДополнительномуТарифу,
	|	СУММА(ДанныеДляОтчета.ПФРНаДоплатуКПенсииШахтерам) КАК ПФРНаДоплатуКПенсииШахтерам,
	|	СУММА(ДанныеДляОтчета.ПФРЗаЗанятыхНаРаботахСДосрочнойПенсией) КАК ПФРЗаЗанятыхНаРаботахСДосрочнойПенсией,
	|	СУММА(ДанныеДляОтчета.ПособиеЗаСчетФСС) КАК ПособиеЗаСчетФСС,
	|	РазмерыТарифныхСтавок.ПФР КАК СтавкаПФР,
	|	РазмерыТарифныхСтавок.ПФРНакопительная КАК СтавкаПФРНакопительная,
	|	РазмерыТарифныхСтавок.ФСС КАК СтавкаФСС,
	|	РазмерыТарифныхСтавок.ФССсИностранцев КАК СтавкаФССсИностранцев,
	|	РазмерыТарифныхСтавок.ФФОМС КАК СтавкаФФОМС,
	|	РазмерыТарифныхСтавок.ТФОМС КАК СтавкаТФОМС,
	|	РазмерыТарифныхСтавок.ПФР + РазмерыТарифныхСтавок.ФСС + РазмерыТарифныхСтавок.ФФОМС + РазмерыТарифныхСтавок.ТФОМС КАК СтавкаСуммарныйТариф
	|ИЗ
	|	ВТДанныеДляОтчета КАК ДанныеДляОтчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТРазмерыТарифныхСтавок КАК РазмерыТарифныхСтавок
	|		ПО ДанныеДляОтчета.ВидТарифаСтраховыхВзносов = РазмерыТарифныхСтавок.ВидТарифа
	|			И (НАЧАЛОПЕРИОДА(ДанныеДляОтчета.МесяцПолученияДохода, ГОД) = РазмерыТарифныхСтавок.Период)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Представления_КадровыеДанныеФизическихЛиц КАК КадровыеДанныеФизическихЛиц
	|		ПО ДанныеДляОтчета.ФизическоеЛицо = КадровыеДанныеФизическихЛиц.ФизическоеЛицо
	|			И ДанныеДляОтчета.МесяцПолученияДохода = КадровыеДанныеФизическихЛиц.Период
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеДляОтчета.Организация,
	|	ДанныеДляОтчета.ФизическоеЛицо,
	|	ДанныеДляОтчета.ВидРасчета,
	|	ДанныеДляОтчета.ВидТарифаСтраховыхВзносов,
	|	ДанныеДляОтчета.НачислятьНакопительнуюЧасть,
	|	ДанныеДляОтчета.ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией,
	|	ДанныеДляОтчета.КлассУсловийТруда,
	|	ДанныеДляОтчета.ЗастрахованФССкакИностранец,
	|	ДанныеДляОтчета.МесяцПолученияДохода,
	|	ДанныеДляОтчета.ВключатьВПервичнуюОтчетностьПФР,
	|	ДанныеДляОтчета.МесяцРегистрацииДохода,
	|	КадровыеДанныеФизическихЛиц.ВидЗастрахованногоЛица,
	|	РазмерыТарифныхСтавок.ПФР,
	|	РазмерыТарифныхСтавок.ПФРНакопительная,
	|	РазмерыТарифныхСтавок.ФСС,
	|	РазмерыТарифныхСтавок.ФССсИностранцев,
	|	РазмерыТарифныхСтавок.ФФОМС,
	|	РазмерыТарифныхСтавок.ТФОМС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеДляОтчета.Организация,
	|	ДанныеДляОтчета.ФизическоеЛицо,
	|	ДанныеДляОтчета.МесяцПолученияДохода,
	|	ДанныеДляОтчета.МесяцРегистрацииДохода,
	|	ДанныеДляОтчета.ВидРасчета,
	|	ДанныеДляОтчета.Инвалид,
	|	КадровыеДанныеФизическихЛиц.ВидЗастрахованногоЛица КАК СтатусЗастрахованногоЛица,
	|	СУММА(ДанныеДляОтчета.НачисленоВсего) КАК НачисленоВсего,
	|	СУММА(ДанныеДляОтчета.НеОблагаетсяФСС_НС) КАК НеОблагаетсяФСС_НС,
	|	СУММА(ДанныеДляОтчета.БазаФСС_НС) КАК БазаФСС_НС,
	|	СУММА(ДанныеДляОтчета.ФССНесчастныеСлучаи) КАК ФССНесчастныеСлучаи,
	|	СУММА(ДанныеДляОтчета.ПособиеЗаСчетФСС_НС) КАК ПособиеЗаСчетФСС_НС,
	|	РазмерыТарифныхСтавок.ФСС КАК СтавкаФСС,
	|	РазмерыТарифныхСтавок.ФССсИностранцев КАК СтавкаФССсИностранцев,
	|	СтавкаВзносаФСС_НС.Ставка КАК СтавкаФСС_НС
	|ИЗ
	|	ВТДанныеДляОтчета КАК ДанныеДляОтчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТРазмерыТарифныхСтавок КАК РазмерыТарифныхСтавок
	|		ПО ДанныеДляОтчета.ВидТарифаСтраховыхВзносов = РазмерыТарифныхСтавок.ВидТарифа
	|			И (НАЧАЛОПЕРИОДА(ДанныеДляОтчета.МесяцПолученияДохода, ГОД) = РазмерыТарифныхСтавок.Период)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Представления_КадровыеДанныеФизическихЛиц КАК КадровыеДанныеФизическихЛиц
	|		ПО ДанныеДляОтчета.ФизическоеЛицо = КадровыеДанныеФизическихЛиц.ФизическоеЛицо
	|			И ДанныеДляОтчета.МесяцПолученияДохода = КадровыеДанныеФизическихЛиц.Период
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтавкиВзносовФСС_НС КАК СтавкаВзносаФСС_НС
	|		ПО (СтавкаВзносаФСС_НС.Организация = ДанныеДляОтчета.Организация)
	|			И (СтавкаВзносаФСС_НС.Период = ДанныеДляОтчета.МесяцПолученияДохода)
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеДляОтчета.Организация,
	|	ДанныеДляОтчета.ФизическоеЛицо,
	|	ДанныеДляОтчета.ВидРасчета,
	|	ДанныеДляОтчета.Инвалид,
	|	ДанныеДляОтчета.МесяцПолученияДохода,
	|	ДанныеДляОтчета.МесяцРегистрацииДохода,
	|	КадровыеДанныеФизическихЛиц.ВидЗастрахованногоЛица,
	|	РазмерыТарифныхСтавок.ФСС,
	|	РазмерыТарифныхСтавок.ФССсИностранцев,
	|	СтавкаВзносаФСС_НС.Ставка";
	
	МассивРезультатовЗапроса = Запрос.ВыполнитьПакет();
	
	КоличествоРезультатов = МассивРезультатовЗапроса.Количество();
	ТаблицаВзносыФСС_НС = МассивРезультатовЗапроса[КоличествоРезультатов-1].Выгрузить();
	ТаблицаВзносы = МассивРезультатовЗапроса[КоличествоРезультатов-2].Выгрузить();
	
	ВнешниеНаборыДанных = Новый Структура;
	ВнешниеНаборыДанных.Вставить("ДанныеПФР", ТаблицаВзносы);
	ВнешниеНаборыДанных.Вставить("ДанныеФОМС", ТаблицаВзносы);
	ВнешниеНаборыДанных.Вставить("ДанныеФСС", ТаблицаВзносы);
	ВнешниеНаборыДанных.Вставить("ДанныеФСС_НС", ТаблицаВзносыФСС_НС);
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	НастройкиКомпоновки = КомпоновщикНастроек.ПолучитьНастройки();
	
	МакетКомпоновки = КомпоновщикМакета.Выполнить(ЭтотОбъект.СхемаКомпоновкиДанных, НастройкиКомпоновки);
		
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, ВнешниеНаборыДанных);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки, истина);
	
	УстановитьПараметрыВыводаТабличногоДокумента(ДокументРезультат);
	
КонецПроцедуры

Процедура УстановитьПараметрыВыводаТабличногоДокумента(ДокументРезультат)
	ДокументРезультат.Автомасштаб 			= 	Истина;
	ДокументРезультат.ОриентацияСтраницы 	= 	ОриентацияСтраницы.Портрет;
	ДокументРезультат.ТолькоПросмотр		= 	Истина;
	ДокументРезультат.ПолеСверху			= 	5;
	ДокументРезультат.ПолеСнизу				= 	0;
	ДокументРезультат.ПолеСлева				= 	15;
КонецПроцедуры

#КонецОбласти

#КонецЕсли