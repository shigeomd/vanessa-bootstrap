#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда


// Выполняет запрос и формирует табличный документ-результат отчета
// в соответствии с настройками, заданными значениями реквизитов отчета.
//
// Параметры:
//	ДокументРезультат - табличный документ, формируемый отчетом
//	ПоказыватьЗаголовок - признак видимости строк с заголовком отчета
//	ВысотаЗаголовка - параметр, через который возвращается высота заголовка в строках 
//  ДокументОбъект - если указан, то печать отчета по документу
//
Процедура СформироватьОтчет(Знач ПараметрыОтчета, АдресХранилища) Экспорт
	
	ДокументРезультат = Новый ТабличныйДокумент;
	
	ДокументРезультат.ОриентацияСтраницы	= ОриентацияСтраницы.Ландшафт;
	ДокументРезультат.АвтоМасштаб			= Истина;
	
	Макет = Отчеты.АнализРаспределенияНДС.ПолучитьМакет("Таблица");
	
	ОбластьЗаголовок  = Макет.ПолучитьОбласть("Заголовок");
	
	Заголовок = "Анализ распределения сумм НДС";
	
	РеквизитыДокументаРаспределения = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ПараметрыОтчета.ДокументРаспределения, "Организация, НачалоПериода");
	ДокументРаспределенияЗаполнен = ЗначениеЗаполнено(ПараметрыОтчета.ДокументРаспределения);
	
	НачалоПериода = ?(НЕ ДокументРаспределенияЗаполнен, НачалоПериода, РеквизитыДокументаРаспределения.НачалоПериода);
	КонецПериода  = ?(НЕ ДокументРаспределенияЗаполнен, КонецПериода, КонецКвартала(РеквизитыДокументаРаспределения.НачалоПериода));
	
	ОбластьЗаголовок.Параметры.Период = "Период: " + ПредставлениеПериода(НачалоДня(ПараметрыОтчета.НачалоПериода), КонецДня(ПараметрыОтчета.КонецПериода)) + ?(НЕ ДокументРаспределенияЗаполнен, "", " по документу """ + ПараметрыОтчета.ДокументРаспределения + """");
	
	СведенияОбОрганизации	= БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(
		?(ДокументРаспределенияЗаполнен, РеквизитыДокументаРаспределения.Организация, ПараметрыОтчета.Организация),
		ПараметрыОтчета.КонецПериода);
		
	ПредставлениеОрганизации = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОбОрганизации, "НаименованиеДляПечатныхФорм,");
	ОбластьЗаголовок.Параметры.НазваниеОрганизации = ПредставлениеОрганизации;
	
	ОбластьЗаголовок.Параметры.Заголовок	= Заголовок;
	
	ОбластьЗаголовок.Параметры.ИННОрганизации	= СокрЛП(СведенияОбОрганизации.ИНН)
												+ ?(ЗначениеЗаполнено(СведенияОбОрганизации.КПП)," / ", "")
												+ СокрЛП(СведенияОбОрганизации.КПП);
	
	ДокументРезультат.Вывести(ОбластьЗаголовок);
	
	ВывестиВОтчетДанныеПоБазеРаспределения(ПараметрыОтчета, ДокументРезультат, Макет);
	
	ВывестиВОтчетДанныеПоРаспределениюКосвенныхРасходов(ПараметрыОтчета, ДокументРезультат, Макет);
	
	ПоместитьВоВременноеХранилище(ДокументРезультат, АдресХранилища);
	
КонецПроцедуры

// Выполняет запрос и формирует таблицу соответствующую базе распределения
// косвенных расходов
//
Процедура ВывестиВОтчетДанныеПоБазеРаспределения(ПараметрыОтчета, ДокументРезультат, Макет)
	
	Запрос = Новый Запрос;
	
	Если НЕ ПараметрыОтчета.ВыручкаПоДокументам Тогда
		
		Запрос.Текст = "ВЫБРАТЬ
		|	РаспределениеНДС.Ссылка КАК ДокументРаспределения,
		|	СУММА(РаспределениеНДС.ВыручкаЕНВД) КАК ВыручкаЕНВД,
		|	СУММА(РаспределениеНДС.ВыручкаБезНДС) КАК ВыручкаБезНДС,
		|	СУММА(РаспределениеНДС.ВыручкаНДС0) КАК ВыручкаНДС0,
		|	СУММА(РаспределениеНДС.ВыручкаНДС) КАК ВыручкаНДС,
		|	СУММА(РаспределениеНДС.ВыручкаЕНВД) + СУММА(РаспределениеНДС.ВыручкаБезНДС) + СУММА(РаспределениеНДС.ВыручкаНДС0) + СУММА(РаспределениеНДС.ВыручкаНДС) КАК ВсегоВыручка
		|ИЗ
		|	Документ.РаспределениеНДС КАК РаспределениеНДС
		|ГДЕ
		|	РаспределениеНДС.Организация = &Организация
		|	И РаспределениеНДС.НачалоПериода МЕЖДУ &НачалоПериода И &КонецПериода
		|	И РаспределениеНДС.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|	И РаспределениеНДС.Проведен = &Проведен
		|
		|СГРУППИРОВАТЬ ПО
		|	РаспределениеНДС.Ссылка";
		
	Иначе
		
		Запрос.Текст = "ВЫБРАТЬ
		|	РаспределениеНДС.Ссылка КАК ДокументРаспределения,
		|	СУММА(РаспределениеНДС.ВыручкаЕНВД) КАК ВыручкаЕНВД,
		|	СУММА(РаспределениеНДС.ВыручкаБезНДС) КАК ВыручкаБезНДС,
		|	СУММА(РаспределениеНДС.ВыручкаНДС0) КАК ВыручкаНДС0,
		|	СУММА(РаспределениеНДС.ВыручкаНДС) КАК ВыручкаНДС,
		|	СУММА(РаспределениеНДС.ВыручкаЕНВД) + СУММА(РаспределениеНДС.ВыручкаБезНДС) + СУММА(РаспределениеНДС.ВыручкаНДС0) + СУММА(РаспределениеНДС.ВыручкаНДС) КАК ВсегоВыручка
		|ИЗ
		|	Документ.РаспределениеНДС КАК РаспределениеНДС
		|ГДЕ
		|	РаспределениеНДС.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	РаспределениеНДС.Ссылка
		|АВТОУПОРЯДОЧИВАНИЕ";
		
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Организация", 	ПараметрыОтчета.Организация);
	Запрос.УстановитьПараметр("НачалоПериода", 	ПараметрыОтчета.НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода", 	КонецДня(ПараметрыОтчета.КонецПериода));
	Запрос.УстановитьПараметр("Проведен", 		Истина);
	Запрос.УстановитьПараметр("Ссылка", 		ПараметрыОтчета.ДокументРаспределения);
	
	Результат = Запрос.Выполнить().Выбрать();
	
	ОбластьШапкаТаблицы  = Макет.ПолучитьОбласть("ПараметрыБазыРаспределения");
	ДокументРезультат.Вывести(ОбластьШапкаТаблицы);
		
	Результат.Сбросить();
	Пока Результат.Следующий() Цикл
		ОбластьШапкаТаблицы  = Макет.ПолучитьОбласть("ДокументБазыРаспределения");
		ОбластьШапкаТаблицы.Параметры.Заполнить(Результат);
		ОбластьШапкаТаблицы.Параметры.Расшифровка = Результат.ДокументРаспределения;
		ДокументРезультат.Вывести(ОбластьШапкаТаблицы);
		
		ОбластьШапкаТаблицы  = Макет.ПолучитьОбласть("СтрокаБазыРаспределения");
		ОбластьШапкаТаблицы.Параметры.Заполнить(Результат);
		ДокументРезультат.Вывести(ОбластьШапкаТаблицы);
	КонецЦикла;
	
КонецПроцедуры

Процедура ВывестиСтрокуТабличнойЧасти(ДокументРезультат, ОбластьСтрока, Выборка)
	
	Если ТипЗнч(Выборка.СчетФактура) = Тип("ДокументСсылка.СчетФактураПолученный") Тогда
		ОбластьСтрока.Параметры.ДатаОперации = "" + Формат(Выборка.СчетФактура.ДатаВходящегоДокумента, "ДФ=dd.MM.yyyy") 
			+ ", №" + Выборка.СчетФактура.НомерВходящегоДокумента;
	Иначе
		Документ = УчетНДСПереопределяемый.НайтиПодчиненныйСчетФактуруПолученный(Выборка.СчетФактура);
		Если Документ = Неопределено Тогда
			Попытка
				ОбластьСтрока.Параметры.ДатаОперации = "" + Формат(Выборка.СчетФактура.Дата, "ДФ=dd.MM.yyyy") 
					+ ", №" + ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(Выборка.СчетФактура.Номер, Истина, Ложь);
			Исключение
				ОбластьСтрока.Параметры.ДатаОперации = "";
			КонецПопытки; 
		Иначе
			ОбластьСтрока.Параметры.ДатаОперации = "" + Формат(Документ.ДатаВходящегоДокумента, "ДФ=dd.MM.yyyy") 
					+ ", №" + Документ.НомерВходящегоДокумента;
		КонецЕсли;
	КонецЕсли;
	
	ОбластьСтрока.Параметры.Заполнить(Выборка);
	ДокументРезультат.Вывести(ОбластьСтрока);
	
КонецПроцедуры

// Выполняет запрос к регистру НДСПоКосвеннымРасходам
// и выбирает информацию по косвенным расходам текущего периода, которые были
// распределены на соответствующие ставки реализации
//
Процедура ВывестиВОтчетДанныеПоРаспределениюКосвенныхРасходов(ПараметрыОтчета, ДокументРезультат, Макет)
	
	Запрос = Новый Запрос;
	
	Если ПараметрыОтчета.РаздельныйУчетНДСНаСчете19 Тогда 
		
		Если НЕ ПараметрыОтчета.ВыручкаПоДокументам Тогда 
			
			Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	РаспределениеНДСРаспределяемыйНДС.Ссылка КАК ДокументРаспределения,
			|	РаспределениеНДСРаспределяемыйНДС.ВидЦенности,
			|	РаспределениеНДСРаспределяемыйНДС.Поставщик,
			|	РаспределениеНДСРаспределяемыйНДС.СчетФактура,
			|	РаспределениеНДСРаспределяемыйНДС.СтавкаНДС,
			|	СУММА(РаспределениеНДСРаспределяемыйНДС.СуммаБезНДС) КАК ВсегоСумма,
			|	СУММА(РаспределениеНДСРаспределяемыйНДС.НДС) КАК ВсегоНДС,
			|	СУММА(РаспределениеНДСРаспределяемыйНДС.СуммаБезНДСУчитываетсяВCтоимости) КАК БезНДССумма,
			|	СУММА(РаспределениеНДСРаспределяемыйНДС.НДСУчитываетсяВCтоимости) КАК БезНДС,
			|	СУММА(РаспределениеНДСРаспределяемыйНДС.СуммаБезНДСДляОперацийПо0) КАК НДС0Сумма,
			|	СУММА(РаспределениеНДСРаспределяемыйНДС.НДСДляОперацийПо0) КАК НДС0,
			|	СУММА(РаспределениеНДСРаспределяемыйНДС.СуммаБезНДСУчитываетсяВCтоимостиЕНВД) КАК ЕНВДСумма,
			|	СУММА(РаспределениеНДСРаспределяемыйНДС.НДСУчитываетсяВCтоимостиЕНВД) КАК ЕНВДНДС,
			|	СУММА(РаспределениеНДСРаспределяемыйНДС.СуммаБезНДСПринимаетсяКВычету) КАК Сумма,
			|	СУММА(РаспределениеНДСРаспределяемыйНДС.НДСПринимаетсяКВычету) КАК НДС
			|ИЗ
			|	Документ.РаспределениеНДС.РаспределяемыйНДС КАК РаспределениеНДСРаспределяемыйНДС
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.НДСВключенныйВСтоимость.Обороты(&НачалоПериода, &КонецПериода, Регистратор, Организация = &Организация) КАК НДСВключенныйВСтоимостьОбороты
			|		ПО РаспределениеНДСРаспределяемыйНДС.Ссылка = НДСВключенныйВСтоимостьОбороты.Регистратор
			|			И РаспределениеНДСРаспределяемыйНДС.Ссылка.Организация = НДСВключенныйВСтоимостьОбороты.Организация
			|			И РаспределениеНДСРаспределяемыйНДС.ВидЦенности = НДСВключенныйВСтоимостьОбороты.ВидЦенности
			|			И РаспределениеНДСРаспределяемыйНДС.СчетФактура = НДСВключенныйВСтоимостьОбороты.СчетФактура
			|			И РаспределениеНДСРаспределяемыйНДС.СтавкаНДС = НДСВключенныйВСтоимостьОбороты.СтавкаНДС
			|			И РаспределениеНДСРаспределяемыйНДС.СчетУчетаНДС = НДСВключенныйВСтоимостьОбороты.СчетУчетаНДС
			|			И РаспределениеНДСРаспределяемыйНДС.Поставщик = НДСВключенныйВСтоимостьОбороты.Поставщик
			|ГДЕ
			|	РаспределениеНДСРаспределяемыйНДС.Ссылка.Проведен = &Проведен
			|	И РаспределениеНДСРаспределяемыйНДС.Ссылка.Организация = &Организация
			|	И РаспределениеНДСРаспределяемыйНДС.Ссылка.НачалоПериода МЕЖДУ &НачалоПериода И &КонецПериода
			|	И РаспределениеНДСРаспределяемыйНДС.Ссылка.Дата МЕЖДУ &НачалоПериода И &КонецПериода
			|
			|СГРУППИРОВАТЬ ПО
			|	РаспределениеНДСРаспределяемыйНДС.Ссылка,
			|	РаспределениеНДСРаспределяемыйНДС.ВидЦенности,
			|	РаспределениеНДСРаспределяемыйНДС.Поставщик,
			|	РаспределениеНДСРаспределяемыйНДС.СчетФактура,
			|	РаспределениеНДСРаспределяемыйНДС.СтавкаНДС
			|ИТОГИ
			|	СУММА(ВсегоСумма),
			|	СУММА(ВсегоНДС),
			|	СУММА(БезНДССумма),
			|	СУММА(БезНДС),
			|	СУММА(НДС0Сумма),
			|	СУММА(НДС0),
			|	СУММА(ЕНВДСумма),
			|	СУММА(ЕНВДНДС),
			|	СУММА(Сумма),
			|	СУММА(НДС)
			|ПО
			|	ОБЩИЕ
			|АВТОУПОРЯДОЧИВАНИЕ";
			
		Иначе
			
			Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	РаспределениеНДСРаспределяемыйНДС.Ссылка КАК ДокументРаспределения,
			|	РаспределениеНДСРаспределяемыйНДС.ВидЦенности,
			|	РаспределениеНДСРаспределяемыйНДС.Поставщик,
			|	РаспределениеНДСРаспределяемыйНДС.СчетФактура,
			|	РаспределениеНДСРаспределяемыйНДС.СтавкаНДС,
			|	СУММА(РаспределениеНДСРаспределяемыйНДС.СуммаБезНДС) КАК ВсегоСумма,
			|	СУММА(РаспределениеНДСРаспределяемыйНДС.НДС) КАК ВсегоНДС,
			|	СУММА(РаспределениеНДСРаспределяемыйНДС.СуммаБезНДСУчитываетсяВCтоимости) КАК БезНДССумма,
			|	СУММА(РаспределениеНДСРаспределяемыйНДС.НДСУчитываетсяВCтоимости) КАК БезНДС,
			|	СУММА(РаспределениеНДСРаспределяемыйНДС.СуммаБезНДСДляОперацийПо0) КАК НДС0Сумма,
			|	СУММА(РаспределениеНДСРаспределяемыйНДС.НДСДляОперацийПо0) КАК НДС0,
			|	СУММА(РаспределениеНДСРаспределяемыйНДС.СуммаБезНДСУчитываетсяВCтоимостиЕНВД) КАК ЕНВДСумма,
			|	СУММА(РаспределениеНДСРаспределяемыйНДС.НДСУчитываетсяВCтоимостиЕНВД) КАК ЕНВДНДС,
			|	СУММА(РаспределениеНДСРаспределяемыйНДС.СуммаБезНДСПринимаетсяКВычету) КАК Сумма,
			|	СУММА(РаспределениеНДСРаспределяемыйНДС.НДСПринимаетсяКВычету) КАК НДС
			|ИЗ
			|	Документ.РаспределениеНДС.РаспределяемыйНДС КАК РаспределениеНДСРаспределяемыйНДС
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.НДСВключенныйВСтоимость.Обороты(&НачалоПериода, &КонецПериода, Регистратор, Организация = &Организация) КАК НДСВключенныйВСтоимостьОбороты
			|		ПО РаспределениеНДСРаспределяемыйНДС.Ссылка = НДСВключенныйВСтоимостьОбороты.Регистратор
			|			И РаспределениеНДСРаспределяемыйНДС.Ссылка.Организация = НДСВключенныйВСтоимостьОбороты.Организация
			|			И РаспределениеНДСРаспределяемыйНДС.ВидЦенности = НДСВключенныйВСтоимостьОбороты.ВидЦенности
			|			И РаспределениеНДСРаспределяемыйНДС.СчетФактура = НДСВключенныйВСтоимостьОбороты.СчетФактура
			|			И РаспределениеНДСРаспределяемыйНДС.СтавкаНДС = НДСВключенныйВСтоимостьОбороты.СтавкаНДС
			|			И РаспределениеНДСРаспределяемыйНДС.СчетУчетаНДС = НДСВключенныйВСтоимостьОбороты.СчетУчетаНДС
			|			И РаспределениеНДСРаспределяемыйНДС.Поставщик = НДСВключенныйВСтоимостьОбороты.Поставщик
			|ГДЕ
			|	РаспределениеНДСРаспределяемыйНДС.Ссылка = &Ссылка
			|
			|СГРУППИРОВАТЬ ПО
			|	РаспределениеНДСРаспределяемыйНДС.Ссылка,
			|	РаспределениеНДСРаспределяемыйНДС.ВидЦенности,
			|	РаспределениеНДСРаспределяемыйНДС.Поставщик,
			|	РаспределениеНДСРаспределяемыйНДС.СчетФактура,
			|	РаспределениеНДСРаспределяемыйНДС.СтавкаНДС
			|ИТОГИ
			|	СУММА(ВсегоСумма),
			|	СУММА(ВсегоНДС),
			|	СУММА(БезНДССумма),
			|	СУММА(БезНДС),
			|	СУММА(НДС0Сумма),
			|	СУММА(НДС0),
			|	СУММА(ЕНВДСумма),
			|	СУММА(ЕНВДНДС),
			|	СУММА(Сумма),
			|	СУММА(НДС)
			|ПО
			|	ОБЩИЕ,
			|	ДокументРаспределения
			|АВТОУПОРЯДОЧИВАНИЕ";
			
		КонецЕсли;
		
	Иначе
		
		Если НЕ ПараметрыОтчета.ВыручкаПоДокументам Тогда 
			
			Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	РаспределениеНДССоставКосвенныхРасходов.Ссылка КАК ДокументРаспределения,
			|	РаспределениеНДССоставКосвенныхРасходов.ВидЦенности,
			|	РаспределениеНДССоставКосвенныхРасходов.Поставщик,
			|	РаспределениеНДССоставКосвенныхРасходов.СчетФактура,
			|	РаспределениеНДССоставКосвенныхРасходов.СтавкаНДС,
			|	СУММА(РаспределениеНДССоставКосвенныхРасходов.СуммаВсего) КАК ВсегоСумма,
			|	СУММА(РаспределениеНДССоставКосвенныхРасходов.НДСВсего) КАК ВсегоНДС,
			|	СУММА(РаспределениеНДССоставКосвенныхРасходов.БезНДССумма) КАК БезНДССумма,
			|	СУММА(РаспределениеНДССоставКосвенныхРасходов.БезНДС) КАК БезНДС,
			|	СУММА(РаспределениеНДССоставКосвенныхРасходов.НДС0Сумма) КАК НДС0Сумма,
			|	СУММА(РаспределениеНДССоставКосвенныхРасходов.НДС0) КАК НДС0,
			|	СУММА(РаспределениеНДССоставКосвенныхРасходов.ЕНВДСумма) КАК ЕНВДСумма,
			|	СУММА(РаспределениеНДССоставКосвенныхРасходов.ЕНВДНДС) КАК ЕНВДНДС,
			|	СУММА(РаспределениеНДССоставКосвенныхРасходов.НДССумма) КАК Сумма,
			|	СУММА(РаспределениеНДССоставКосвенныхРасходов.НДС) КАК НДС,
			|	СУММА(ВЫБОР
			|			КОГДА НДСВключенныйВСтоимостьОбороты.НДСОборот > 0
			|				ТОГДА НДСВключенныйВСтоимостьОбороты.НДСОборот
			|			ИНАЧЕ 0
			|		КОНЕЦ) КАК НДСВключен,
			|	СУММА(ВЫБОР
			|			КОГДА НДСВключенныйВСтоимостьОбороты.НДСОборот < 0
			|				ТОГДА -НДСВключенныйВСтоимостьОбороты.НДСОборот
			|			ИНАЧЕ 0
			|		КОНЕЦ) КАК НДСИсключен
			|ИЗ
			|	Документ.РаспределениеНДС.СоставКосвенныхРасходов КАК РаспределениеНДССоставКосвенныхРасходов
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.НДСВключенныйВСтоимость.Обороты(&НачалоПериода, &КонецПериода, Регистратор, Организация = &Организация) КАК НДСВключенныйВСтоимостьОбороты
			|		ПО РаспределениеНДССоставКосвенныхРасходов.Ссылка = НДСВключенныйВСтоимостьОбороты.Регистратор
			|			И РаспределениеНДССоставКосвенныхРасходов.Ссылка.Организация = НДСВключенныйВСтоимостьОбороты.Организация
			|			И РаспределениеНДССоставКосвенныхРасходов.ВидЦенности = НДСВключенныйВСтоимостьОбороты.ВидЦенности
			|			И РаспределениеНДССоставКосвенныхРасходов.СчетФактура = НДСВключенныйВСтоимостьОбороты.СчетФактура
			|			И РаспределениеНДССоставКосвенныхРасходов.СтавкаНДС = НДСВключенныйВСтоимостьОбороты.СтавкаНДС
			|			И РаспределениеНДССоставКосвенныхРасходов.СчетУчетаНДС = НДСВключенныйВСтоимостьОбороты.СчетУчетаНДС
			|			И РаспределениеНДССоставКосвенныхРасходов.Поставщик = НДСВключенныйВСтоимостьОбороты.Поставщик
			|ГДЕ
			|	РаспределениеНДССоставКосвенныхРасходов.Ссылка.Проведен = &Проведен
			|	И РаспределениеНДССоставКосвенныхРасходов.Ссылка.Организация = &Организация
			|	И РаспределениеНДССоставКосвенныхРасходов.Ссылка.НачалоПериода МЕЖДУ &НачалоПериода И &КонецПериода
			|	И РаспределениеНДССоставКосвенныхРасходов.Ссылка.Дата МЕЖДУ &НачалоПериода И &КонецПериода
			|
			|СГРУППИРОВАТЬ ПО
			|	РаспределениеНДССоставКосвенныхРасходов.Ссылка,
			|	РаспределениеНДССоставКосвенныхРасходов.ВидЦенности,
			|	РаспределениеНДССоставКосвенныхРасходов.Поставщик,
			|	РаспределениеНДССоставКосвенныхРасходов.СчетФактура,
			|	РаспределениеНДССоставКосвенныхРасходов.СтавкаНДС
			|ИТОГИ
			|	СУММА(ВсегоСумма),
			|	СУММА(ВсегоНДС),
			|	СУММА(БезНДССумма),
			|	СУММА(БезНДС),
			|	СУММА(НДС0Сумма),
			|	СУММА(НДС0),
			|	СУММА(ЕНВДСумма),
			|	СУММА(ЕНВДНДС),
			|	СУММА(Сумма),
			|	СУММА(НДС),
			|	СУММА(НДСВключен),
			|	СУММА(НДСИсключен)
			|ПО
			|	ОБЩИЕ
			|АВТОУПОРЯДОЧИВАНИЕ";
			
		Иначе
			
			Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	РаспределениеНДССоставКосвенныхРасходов.Ссылка КАК ДокументРаспределения,
			|	РаспределениеНДССоставКосвенныхРасходов.ВидЦенности,
			|	РаспределениеНДССоставКосвенныхРасходов.Поставщик,
			|	РаспределениеНДССоставКосвенныхРасходов.СчетФактура,
			|	РаспределениеНДССоставКосвенныхРасходов.СтавкаНДС,
			|	СУММА(РаспределениеНДССоставКосвенныхРасходов.СуммаВсего) КАК ВсегоСумма,
			|	СУММА(РаспределениеНДССоставКосвенныхРасходов.НДСВсего) КАК ВсегоНДС,
			|	СУММА(РаспределениеНДССоставКосвенныхРасходов.БезНДССумма) КАК БезНДССумма,
			|	СУММА(РаспределениеНДССоставКосвенныхРасходов.БезНДС) КАК БезНДС,
			|	СУММА(РаспределениеНДССоставКосвенныхРасходов.НДС0Сумма) КАК НДС0Сумма,
			|	СУММА(РаспределениеНДССоставКосвенныхРасходов.НДС0) КАК НДС0,
			|	СУММА(РаспределениеНДССоставКосвенныхРасходов.ЕНВДСумма) КАК ЕНВДСумма,
			|	СУММА(РаспределениеНДССоставКосвенныхРасходов.ЕНВДНДС) КАК ЕНВДНДС,
			|	СУММА(РаспределениеНДССоставКосвенныхРасходов.НДССумма) КАК Сумма,
			|	СУММА(РаспределениеНДССоставКосвенныхРасходов.НДС) КАК НДС,
			|	СУММА(ВЫБОР
			|			КОГДА НДСВключенныйВСтоимостьОбороты.НДСОборот > 0
			|				ТОГДА НДСВключенныйВСтоимостьОбороты.НДСОборот
			|			ИНАЧЕ 0
			|		КОНЕЦ) КАК НДСВключен,
			|	СУММА(ВЫБОР
			|			КОГДА НДСВключенныйВСтоимостьОбороты.НДСОборот < 0
			|				ТОГДА -НДСВключенныйВСтоимостьОбороты.НДСОборот
			|			ИНАЧЕ 0
			|		КОНЕЦ) КАК НДСИсключен
			|ИЗ
			|	Документ.РаспределениеНДС.СоставКосвенныхРасходов КАК РаспределениеНДССоставКосвенныхРасходов
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.НДСВключенныйВСтоимость.Обороты(&НачалоПериода, &КонецПериода, Регистратор, Организация = &Организация) КАК НДСВключенныйВСтоимостьОбороты
			|		ПО РаспределениеНДССоставКосвенныхРасходов.Ссылка = НДСВключенныйВСтоимостьОбороты.Регистратор
			|			И РаспределениеНДССоставКосвенныхРасходов.Ссылка.Организация = НДСВключенныйВСтоимостьОбороты.Организация
			|			И РаспределениеНДССоставКосвенныхРасходов.ВидЦенности = НДСВключенныйВСтоимостьОбороты.ВидЦенности
			|			И РаспределениеНДССоставКосвенныхРасходов.СчетФактура = НДСВключенныйВСтоимостьОбороты.СчетФактура
			|			И РаспределениеНДССоставКосвенныхРасходов.СтавкаНДС = НДСВключенныйВСтоимостьОбороты.СтавкаНДС
			|			И РаспределениеНДССоставКосвенныхРасходов.СчетУчетаНДС = НДСВключенныйВСтоимостьОбороты.СчетУчетаНДС
			|			И РаспределениеНДССоставКосвенныхРасходов.Поставщик = НДСВключенныйВСтоимостьОбороты.Поставщик
			|ГДЕ
			|	РаспределениеНДССоставКосвенныхРасходов.Ссылка = &Ссылка
			|
			|СГРУППИРОВАТЬ ПО
			|	РаспределениеНДССоставКосвенныхРасходов.Ссылка,
			|	РаспределениеНДССоставКосвенныхРасходов.ВидЦенности,
			|	РаспределениеНДССоставКосвенныхРасходов.Поставщик,
			|	РаспределениеНДССоставКосвенныхРасходов.СчетФактура,
			|	РаспределениеНДССоставКосвенныхРасходов.СтавкаНДС
			|ИТОГИ
			|	СУММА(ВсегоСумма),
			|	СУММА(ВсегоНДС),
			|	СУММА(БезНДССумма),
			|	СУММА(БезНДС),
			|	СУММА(НДС0Сумма),
			|	СУММА(НДС0),
			|	СУММА(ЕНВДСумма),
			|	СУММА(ЕНВДНДС),
			|	СУММА(Сумма),
			|	СУММА(НДС),
			|	СУММА(НДСВключен),
			|	СУММА(НДСИсключен)
			|ПО
			|	ОБЩИЕ,
			|	ДокументРаспределения
			|АВТОУПОРЯДОЧИВАНИЕ";
			
		КонецЕсли;
		
	КонецЕсли;

	Запрос.УстановитьПараметр("Организация", 	ПараметрыОтчета.Организация);
	Запрос.УстановитьПараметр("НачалоПериода", 	ПараметрыОтчета.НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода",  	КонецДня(ПараметрыОтчета.КонецПериода));
	Запрос.УстановитьПараметр("Проведен",	  	Истина);
	Запрос.УстановитьПараметр("Ссылка", 		ПараметрыОтчета.ДокументРаспределения);
	
	Результат = Запрос.Выполнить();
	ВыборкаИтоги = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
	
	НаименованиеВертикальнойОбластиМакета = "";
	Если ПараметрыОтчета.РаздельныйУчетНДСНаСчете19 Тогда
		ОбластьШапкаТаблицы  = Макет.ПолучитьОбласть("ШапкаТаблицыРаздельныйУчетНДСНаСчете19");
		НаименованиеВертикальнойОбластиМакета = "|РаздельныйУчетНДСНаСчете19" 
	Иначе
		ОбластьШапкаТаблицы  = Макет.ПолучитьОбласть("ШапкаТаблицыРаспределения");
	КонецЕсли;
	
	ДокументРезультат.Вывести(ОбластьШапкаТаблицы);
	
	ОбластьСтрокаДокумента = Макет.ПолучитьОбласть("СтрокаДокументРаспределения"+НаименованиеВертикальнойОбластиМакета);
	ОбластьСтрока = Макет.ПолучитьОбласть("Строка"+НаименованиеВертикальнойОбластиМакета);
	ОбластьИтоги  = Макет.ПолучитьОбласть("Итоги"+НаименованиеВертикальнойОбластиМакета);
	
	Пока ВыборкаИтоги.Следующий() Цикл
		
		Если ПараметрыОтчета.ВыручкаПоДокументам Тогда
			
			ВыборкаДокументы = ВыборкаИтоги.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
			Пока ВыборкаДокументы.Следующий() Цикл
				
				ОбластьСтрокаДокумента.Параметры.Заполнить(ВыборкаДокументы);
				ОбластьСтрокаДокумента.Параметры.Расшифровка = ВыборкаДокументы.ДокументРаспределения;
				ДокументРезультат.Вывести(ОбластьСтрокаДокумента);
				
				ВыборкаСтроки = ВыборкаДокументы.Выбрать();
				Пока ВыборкаСтроки.Следующий() Цикл
					ВывестиСтрокуТабличнойЧасти(ДокументРезультат, ОбластьСтрока, ВыборкаСтроки);
				КонецЦикла;
				
			КонецЦикла;
			
		Иначе
			
			ВыборкаСтроки = ВыборкаИтоги.Выбрать();
			Пока ВыборкаСтроки.Следующий() Цикл
				ВывестиСтрокуТабличнойЧасти(ДокументРезультат, ОбластьСтрока, ВыборкаСтроки);
			КонецЦикла;
			
		КонецЕсли;
		
		ОбластьИтоги.Параметры.Заполнить(ВыборкаИтоги);
		ДокументРезультат.Вывести(ОбластьИтоги);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецЕсли