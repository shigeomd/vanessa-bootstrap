#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс
	
Функция ПолучитьПараметрыИсполненияОтчета() Экспорт
	
	Возврат Новый Структура("ИспользоватьПередКомпоновкойМакета,
							|ИспользоватьПослеКомпоновкиМакета,
							|ИспользоватьПослеВыводаРезультата,
							|ИспользоватьДанныеРасшифровки,
							|ИспользоватьПередВыводомЭлементаРезультата",
							Истина, Истина, Истина, Истина, Истина);
							
КонецФункции

Функция ПолучитьТекстЗаголовка(ПараметрыОтчета, ОрганизацияВНачале = Истина) Экспорт 
	
	Возврат "Оборотно-сальдовая ведомость" + БухгалтерскиеОтчетыКлиентСервер.ПолучитьПредставлениеПериода(ПараметрыОтчета.НачалоПериода, ПараметрыОтчета.КонецПериода);
	
КонецФункции

// В процедуре можно доработать компоновщик перед выводом в отчет
// Изменения сохранены не будут
Процедура ПередКомпоновкойМакета(ПараметрыОтчета, Схема, КомпоновщикНастроек) Экспорт
	
	// Очищаем структуру отчета, для того чтобы сформировать ее по установленным настройкам
	КомпоновщикНастроек.Настройки.Структура.Очистить();
	КомпоновщикНастроек.Настройки.Выбор.Элементы.Очистить();
	
	БухгалтерскиеОтчетыКлиентСервер.ДобавитьВыбранноеПоле(КомпоновщикНастроек.Настройки.Выбор, "Счет");
	
	// Основой структуры отчета будет таблица
	Структура = КомпоновщикНастроек.Настройки.Структура.Добавить(Тип("ТаблицаКомпоновкиДанных"));
	
	Структура.Выбор.Элементы.Добавить(Тип("АвтоВыбранноеПолеКомпоновкиДанных"));
	
	// Далее добавляем колонки в таблицу
	// Если нужно добавляем отдельную колонку для показателей (БУ,НУ,ПР,ВР и т.д.)
	КоличествоПоказателей = БухгалтерскиеОтчетыВызовСервера.КоличествоПоказателей(ПараметрыОтчета);
	
	Если КоличествоПоказателей > 1 Тогда
		
		Колонка = Структура.Колонки.Добавить();
		Колонка.Имя = "Показатели";
		ГруппаПоказатели = Колонка.Выбор.Элементы.Добавить(Тип("ГруппаВыбранныхПолейКомпоновкиДанных"));
		ГруппаПоказатели.Заголовок     = БухгалтерскиеОтчеты.ЗаголовокГруппыПоказателей();
		ГруппаПоказатели.Использование = Истина;
		ГруппаПоказатели.Расположение  = РасположениеПоляКомпоновкиДанных.Вертикально;
		
		Для Каждого ИмяПоказателя Из ПараметрыОтчета.НаборПоказателей Цикл
			Если ПараметрыОтчета["Показатель" + ИмяПоказателя] Тогда 
				БухгалтерскиеОтчетыКлиентСервер.ДобавитьВыбранноеПоле(ГруппаПоказатели, "Показатели." + ИмяПоказателя);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	// Для начального сальдо, оборотов и конечного сальдо, создадим по отдельной колонке
	// в каждой колонке будет 2 ячейки дебет и кредит
	
	// Колонка Начальное сальдо
	Колонка = Структура.Колонки.Добавить();
	Колонка.Имя = "СальдоНаНачалоПериода";
	ГруппаСальдоНаНачало = Колонка.Выбор.Элементы.Добавить(Тип("ГруппаВыбранныхПолейКомпоновкиДанных"));
	ГруппаСальдоНаНачало.Заголовок     = "Сальдо на начало периода";
	ГруппаСальдоНаНачало.Использование = Истина;
	ГруппаСальдоНаНачалоДт = ГруппаСальдоНаНачало.Элементы.Добавить(Тип("ГруппаВыбранныхПолейКомпоновкиДанных"));
	ГруппаСальдоНаНачалоДт.Заголовок     = "Дебет";
	ГруппаСальдоНаНачалоДт.Использование = Истина;
	ГруппаСальдоНаНачалоДт.Расположение  = РасположениеПоляКомпоновкиДанных.Вертикально;
	ГруппаСальдоНаНачалоКт = ГруппаСальдоНаНачало.Элементы.Добавить(Тип("ГруппаВыбранныхПолейКомпоновкиДанных"));
	ГруппаСальдоНаНачалоКт.Заголовок     = "Кредит";
	ГруппаСальдоНаНачалоКт.Использование = Истина;
	ГруппаСальдоНаНачалоКт.Расположение  = РасположениеПоляКомпоновкиДанных.Вертикально;
	
	// Колонка Обороты
	Колонка = Структура.Колонки.Добавить();
	Колонка.Имя = "ОборотыЗаПериод";
	ГруппаОбороты = Колонка.Выбор.Элементы.Добавить(Тип("ГруппаВыбранныхПолейКомпоновкиДанных"));
	ГруппаОбороты.Заголовок     = "Обороты за период";
	ГруппаОбороты.Использование = Истина;
	ГруппаОборотыДт = ГруппаОбороты.Элементы.Добавить(Тип("ГруппаВыбранныхПолейКомпоновкиДанных"));
	ГруппаОборотыДт.Заголовок     = "Дебет";
	ГруппаОборотыДт.Использование = Истина;
	ГруппаОборотыДт.Расположение  = РасположениеПоляКомпоновкиДанных.Вертикально;
	ГруппаОборотыКт = ГруппаОбороты.Элементы.Добавить(Тип("ГруппаВыбранныхПолейКомпоновкиДанных"));
	ГруппаОборотыКт.Заголовок     = "Кредит";
	ГруппаОборотыКт.Использование = Истина;
	ГруппаОборотыКт.Расположение  = РасположениеПоляКомпоновкиДанных.Вертикально;
	
	// Колонка Конечное сальдо
	Колонка = Структура.Колонки.Добавить();
	Колонка.Имя = "СальдоНаКонецПериода";
	ГруппаСальдоНаКонец = Колонка.Выбор.Элементы.Добавить(Тип("ГруппаВыбранныхПолейКомпоновкиДанных"));
	ГруппаСальдоНаКонец.Заголовок     = "Сальдо на конец периода";
	ГруппаСальдоНаКонец.Использование = Истина;
	ГруппаСальдоНаКонецДт = ГруппаСальдоНаКонец.Элементы.Добавить(Тип("ГруппаВыбранныхПолейКомпоновкиДанных"));
	ГруппаСальдоНаКонецДт.Заголовок     = "Дебет";
	ГруппаСальдоНаКонецДт.Использование = Истина;
	ГруппаСальдоНаКонецДт.Расположение  = РасположениеПоляКомпоновкиДанных.Вертикально;
	ГруппаСальдоНаКонецКт = ГруппаСальдоНаКонец.Элементы.Добавить(Тип("ГруппаВыбранныхПолейКомпоновкиДанных"));
	ГруппаСальдоНаКонецКт.Заголовок     = "Кредит";
	ГруппаСальдоНаКонецКт.Использование = Истина;
	ГруппаСальдоНаКонецКт.Расположение  = РасположениеПоляКомпоновкиДанных.Вертикально;
	
	// Заполняем созданные колонки показателями
	Для Каждого ИмяПоказателя Из ПараметрыОтчета.НаборПоказателей Цикл
		Если ПараметрыОтчета["Показатель" + ИмяПоказателя] Тогда 
			БухгалтерскиеОтчетыКлиентСервер.ДобавитьВыбранноеПоле(ГруппаСальдоНаНачалоДт, "СальдоНаНачалоПериода." + ИмяПоказателя + "НачальныйОстатокДт");
			БухгалтерскиеОтчетыКлиентСервер.ДобавитьВыбранноеПоле(ГруппаСальдоНаНачалоКт, "СальдоНаНачалоПериода." + ИмяПоказателя + "НачальныйОстатокКт");
			БухгалтерскиеОтчетыКлиентСервер.ДобавитьВыбранноеПоле(ГруппаОборотыДт,        "ОборотыЗаПериод."       + ИмяПоказателя + "ОборотДт");
			БухгалтерскиеОтчетыКлиентСервер.ДобавитьВыбранноеПоле(ГруппаОборотыКт,        "ОборотыЗаПериод."       + ИмяПоказателя + "ОборотКт");
			БухгалтерскиеОтчетыКлиентСервер.ДобавитьВыбранноеПоле(ГруппаСальдоНаКонецДт,  "СальдоНаКонецПериода."  + ИмяПоказателя + "КонечныйОстатокДт");
			БухгалтерскиеОтчетыКлиентСервер.ДобавитьВыбранноеПоле(ГруппаСальдоНаКонецКт,  "СальдоНаКонецПериода."  + ИмяПоказателя + "КонечныйОстатокКт");
		КонецЕсли;
	КонецЦикла;
	// Получаем схему компоновки - эталон
	Схема = ПолучитьМакет("СхемаКомпоновкиДанных");
	
	НаборДанных = Схема.НаборыДанных.НаборДанныхОбъединение.Элементы.ПоСубконто; // Набор "ПоСубконто"
	ТекстЗапроса = НаборДанных.Запрос;
	НаборДанных.Запрос = "";
	
	ТекстЗапросДетализацииПоСубконто       = БухгалтерскиеОтчетыВызовСервера.ПолучитьТекстПоМаркерам(ТекстЗапроса, "//Начало ЗапросПоСубконто Детализация", "//Конец ЗапросПоСубконто Детализация");
	ТекстУсловиеСчетаДетализацииПоСубконто = БухгалтерскиеОтчетыВызовСервера.ПолучитьТекстПоМаркерам(ТекстЗапроса, "//Начало УсловиеСчета Детализация", "//Конец УсловиеСчета Детализация");
	ТекстСубконтоДетализацииПоСубконто     = БухгалтерскиеОтчетыВызовСервера.ПолучитьТекстПоМаркерам(ТекстЗапроса, "//Начало Субконто Детализация", "//Конец Субконто Детализация");
	
	СчетаИсключенныеИзЗапросаПоСчетам = Новый СписокЗначений;
	
	// Доработка для детализации
	ЕстьДетализацияПоСубконто = Ложь;
	НужноКорректироватьзапросСКД = Ложь;
	КоличествоСубконтоДетализации = 0;
	
	// Для настройки развернутого сальдо может понадобиться информация о счетах и
	// количестве субконто, по которым нужна детализация
	КоличествоСубконтоДетализацииПоСчетам = Новый Соответствие;
	
	// Определим тескт запроса СКД который далее заменим
	СтарыйТекстЗапросаСКД = "{ВЫБРАТЬ Субконто1Представление, Субконто2Представление, Субконто3Представление}";
	
	// Подготовим таблицу настроек, для этого создадим ее копию и убедимся, что в ней
	// каждый счет упоминается не более 1 раза
	СчетаГруппировки = ПараметрыОтчета.Группировка.Скопировать();
	СчетаГруппировки.Свернуть("Счет");
	
	Если СчетаГруппировки.Количество() <> ПараметрыОтчета.Группировка.Количество() Тогда
		
		// Создаем таблицу из которой дальше будем считывать настройки группировки
		НастройкаГруппировки = ПараметрыОтчета.Группировка.Скопировать();
		НастройкаГруппировки.Индексы.Добавить("Счет");
		
		// Удалим дублирующиеся настройки счетов из таблицы
		Для Каждого Счет Из СчетаГруппировки.ВыгрузитьКолонку("Счет") Цикл
			
			// Ищем строки с таким счетом
			СтрокиГруппировки = НастройкаГруппировки.НайтиСтроки(Новый Структура("Счет", Счет));
			
			// Удаляем все найденные строки с таким счетом кроме первой
			Для Сч = 1 По СтрокиГруппировки.ВГраница() Цикл
				
				НастройкаГруппировки.Удалить(СтрокиГруппировки[Сч]);
				
			КонецЦикла;
				
		КонецЦикла;
		
	Иначе
		
		// Если количестов строк после свертки таблицы, не изменилось, делаем вывод, что в ней нет дублей
		// и можно считывать настройки группировки прямо из нее
		НастройкаГруппировки = ПараметрыОтчета.Группировка;
		
	КонецЕсли;
	
	Для Каждого СтрокаТаблицы Из НастройкаГруппировки Цикл
		СубконтоДетализации = Новый СписокЗначений;
		Если СтрокаТаблицы.Использование И ЗначениеЗаполнено(СтрокаТаблицы.Счет) Тогда	
			ДанныеСчета = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(СтрокаТаблицы.Счет);
			СписокВидовСубконто = Новый СписокЗначений;
			КоличествоСубконто = СтрДлина(СтрокаТаблицы.ПоСубконто) / 2;
			Для Индекс = 1 По КоличествоСубконто Цикл
				СписокВидовСубконто.Добавить(ДанныеСчета["ВидСубконто" + Сред(СтрокаТаблицы.ПоСубконто, Индекс*2, 1)], ДанныеСчета["ВидСубконто" + Сред(СтрокаТаблицы.ПоСубконто, Индекс*2, 1) + "Наименование"], ?(Сред(СтрокаТаблицы.ПоСубконто, Индекс * 2 - 1, 1) = "+", Истина, Ложь));
			КонецЦикла;
			
			Для Каждого СтрокаТаблицыСубконто Из СписокВидовСубконто Цикл
				Если СтрокаТаблицыСубконто.Пометка Тогда
					СубконтоДетализации.Добавить(СтрокаТаблицыСубконто.Значение);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		Если СубконтоДетализации.Количество() > 0 Тогда
			ЕстьДетализацияПоСубконто = Истина;
			
			Если СтрокаТаблицы.ПоСубсчетам ИЛИ ПараметрыОтчета.ПоСубсчетам Тогда
				
				Для Каждого Счет Из БухгалтерскийУчетВызовСервераПовтИсп.СчетаВИерархии(СтрокаТаблицы.Счет) Цикл
					
					КоличествоСубконтоДетализацииПоСчетам.Вставить(Счет, СубконтоДетализации.ВыгрузитьЗначения());
					
				КонецЦикла;
				
			Иначе
				КоличествоСубконтоДетализацииПоСчетам.Вставить(СтрокаТаблицы.Счет, СубконтоДетализации.ВыгрузитьЗначения());
			КонецЕсли;
			
			КоличествоСубконтоДетализации = Макс(КоличествоСубконтоДетализации, СубконтоДетализации.Количество());
			
			СчетаИсключенныеИзЗапросаПоСчетам.Добавить(СтрокаТаблицы.Счет);

			Индекс = НастройкаГруппировки.Индекс(СтрокаТаблицы) + 1;
			
			// Формируем текст параметра УсловиеСчета запроса детализации по субконто
			ТекстДляПодстановкиУсловиеСчетаДетализацииПоСубконто = "Счет В ИЕРАРХИИ (&СчетДетализации" + Индекс + ")
																   | И ((НЕ Счет.Забалансовый)
																   | ИЛИ &ВыводитьЗабалансовыеСчета)";
											  
			// Формируем текст параметра Субконто запроса детализации по субконто
			ТекстДляПодстановкиСубконтоДетализацииПоСубконто = "&СубконтоДетализации" + Индекс;
			
			// Формируем текст запроса для счета детализации
			ТекстДляПодстановкиЗапросДетализацииПоСубконто = СтрЗаменить(ТекстЗапросДетализацииПоСубконто, ТекстУсловиеСчетаДетализацииПоСубконто, ТекстДляПодстановкиУсловиеСчетаДетализацииПоСубконто);
			ТекстДляПодстановкиЗапросДетализацииПоСубконто = СтрЗаменить(ТекстДляПодстановкиЗапросДетализацииПоСубконто, ТекстСубконтоДетализацииПоСубконто, ТекстДляПодстановкиСубконтоДетализацииПоСубконто);
			
			// Доработка текста запроса СКД
			// Для первого запроса секция "{Выбрать..." должна быть 
			Если НужноКорректироватьзапросСКД Тогда
				ТекстДляПодстановкиЗапросДетализацииПоСубконто = СтрЗаменить(ТекстДляПодстановкиЗапросДетализацииПоСубконто, СтарыйТекстЗапросаСКД, "");
			Иначе
				// для последующих запросов секцию "{Выбрать..." нужно убирать
				НужноКорректироватьзапросСКД = Истина;
			КонецЕсли;
			
			// Корректировка текста запроса в зависимости от количества указанных видов субконто
			Если Индекс > 1 Тогда
				// В первом запросе объединения должны присутствовать псевдонимы полей, для последующих запросов удаляем псевдонимы
				Для ИндексПсевдонима = 1 По 3 Цикл
					ТекстДляПодстановкиЗапросДетализацииПоСубконто = СтрЗаменить(ТекстДляПодстановкиЗапросДетализацииПоСубконто, " КАК Субконто" + ИндексПсевдонима + "Представление", "");
					ТекстДляПодстановкиЗапросДетализацииПоСубконто = СтрЗаменить(ТекстДляПодстановкиЗапросДетализацииПоСубконто, " КАК Субконто" + ИндексПсевдонима, "");
				КонецЦикла;
				
				// Обнуляем субконто и его представление не указанные в запросе
				Для ИндексСубконто = СубконтоДетализации.Количество() + 1 По 3 Цикл
					ТекстДляПодстановкиЗапросДетализацииПоСубконто = СтрЗаменить(ТекстДляПодстановкиЗапросДетализацииПоСубконто, "ПРЕДСТАВЛЕНИЕССЫЛКИ(ОстаткиИОбороты.Субконто" + ИндексСубконто + ")", """""");
					ТекстДляПодстановкиЗапросДетализацииПоСубконто = СтрЗаменить(ТекстДляПодстановкиЗапросДетализацииПоСубконто, "ОстаткиИОбороты.Субконто" + ИндексСубконто, "Null");
				КонецЦикла;
				
			Иначе
				// Обнуляем субконто и его представление не указанные в запросе
				Для ИндексСубконто = СубконтоДетализации.Количество() + 1 По 3 Цикл
					ТекстДляПодстановкиЗапросДетализацииПоСубконто = СтрЗаменить(ТекстДляПодстановкиЗапросДетализацииПоСубконто, "ПРЕДСТАВЛЕНИЕССЫЛКИ(ОстаткиИОбороты.Субконто" + ИндексСубконто + ") КАК Субконто" + ИндексСубконто + "Представление" , """"" КАК Субконто" + ИндексСубконто + "Представление");
					ТекстДляПодстановкиЗапросДетализацииПоСубконто = СтрЗаменить(ТекстДляПодстановкиЗапросДетализацииПоСубконто, "ОстаткиИОбороты.Субконто" + ИндексСубконто + " КАК Субконто" + ИндексСубконто, "Null КАК Субконто" + ИндексСубконто);
				КонецЦикла;
			КонецЕсли;
			
			// Добавление и установка значения параметра СчетДетализации{Индекс}
			БухгалтерскиеОтчетыВызовСервера.СкопироватьПараметрСхемыКомпоновкиДанных(Схема, "СчетДетализации" + Индекс, "СчетДетализации", СтрокаТаблицы.Счет);
			
			// Добавление и установка значения параметра СубконтоДетализации{Индекс}
			БухгалтерскиеОтчетыВызовСервера.СкопироватьПараметрСхемыКомпоновкиДанных(Схема, "СубконтоДетализации" + Индекс, "СубконтоДетализации", СубконтоДетализации);

			// Доработка запроса набора данных
			НаборДанных.Запрос = НаборДанных.Запрос + ТекстДляПодстановкиЗапросДетализацииПоСубконто;
			НаборДанных.Запрос = НаборДанных.Запрос + " ОБЪЕДИНИТЬ ВСЕ ";
			
		КонецЕсли;
	КонецЦикла;
	
	// Дорабатываем текст запроса СКД, в соответствии с количеством детализируемых субконто
	ТекстЗапросаСКД = "{ВЫБРАТЬ ";
	Для Индекс = 1 По КоличествоСубконтоДетализации Цикл
		
		ТекстЗапросаСКД = ТекстЗапросаСКД + "Субконто"+ Индекс +"Представление" +", ";
		
	КонецЦикла;
	
	ТекстЗапросаСКД = Лев(ТекстЗапросаСКД, СтрДлина(ТекстЗапросаСКД) - 2) + "}";
	
	НаборДанных.Запрос = СтрЗаменить(НаборДанных.Запрос, СтарыйТекстЗапросаСКД, ТекстЗапросаСКД);
	
	НаборДанных.Запрос = Лев(НаборДанных.Запрос, СтрДлина(НаборДанных.Запрос) - 16);
	
	// Доработка схемы - развернутое сальдо
	НаборДанных = Схема.НаборыДанных.НаборДанныхОбъединение.Элементы.ПоСубконтоРазвернутое; // Набор "ПоСубконтоРазвернутое"
	ТекстЗапроса = НаборДанных.Запрос;
	НаборДанных.Запрос = "";
	
	ТекстЗапросПоСубконтоРазвернутое       = БухгалтерскиеОтчетыВызовСервера.ПолучитьТекстПоМаркерам(ТекстЗапроса, "//Начало ЗапросПоСубконто РазвернутоеСальдо", "//Конец ЗапросПоСубконто РазвернутоеСальдо");
	ТекстУсловиеСчетаПоСубконтоРазвернутое = БухгалтерскиеОтчетыВызовСервера.ПолучитьТекстПоМаркерам(ТекстЗапроса, "//Начало УсловиеСчета РазвернутоеСальдо"    , "//Конец УсловиеСчета РазвернутоеСальдо");
	ТекстСубконтоПоСубконтоРазвернутое     = БухгалтерскиеОтчетыВызовСервера.ПолучитьТекстПоМаркерам(ТекстЗапроса, "//Начало Субконто РазвернутоеСальдо"        , "//Конец Субконто РазвернутоеСальдо");
		
	ВыводитьРазвернутоеСальдо = Ложь;
	
	ТекстУсловие = "Ложь ИЛИ ";
	
	// Подготовим таблицу настроек развернутого сальдо, для этого создадим копию таблицы
	// и убедимся что в ней каждый счет упоминается не более 1 раза
	СчетаРазвернутоеСальдо = ПараметрыОтчета.РазвернутоеСальдо.Скопировать();
	СчетаРазвернутоеСальдо.Свернуть("Счет");
	
	Если СчетаРазвернутоеСальдо.Количество() <> ПараметрыОтчета.РазвернутоеСальдо.Количество() Тогда
		
		// Создаем таблицу из которой дальше будем считывать настройки группировки
		НастройкаРазвернутоеСальдо = ПараметрыОтчета.РазвернутоеСальдо.Скопировать();
		НастройкаРазвернутоеСальдо.Индексы.Добавить("Счет");
		
		// Удалим дублирующиеся настройки счетов из таблицы
		Для Каждого Счет Из СчетаРазвернутоеСальдо.ВыгрузитьКолонку("Счет") Цикл
			
			// Ищем строки с таким счетом
			СтрокиРазвернутоеСальдо = НастройкаРазвернутоеСальдо.НайтиСтроки(Новый Структура("Счет", Счет));
			
			// Удаляем все найденные строки с таким счетом кроме первой
			Для Сч = 1 По СтрокиРазвернутоеСальдо.ВГраница() Цикл
				
				НастройкаРазвернутоеСальдо.Удалить(СтрокиРазвернутоеСальдо[Сч]);
				
			КонецЦикла;
				
		КонецЦикла;
		
	Иначе
		
		// Если количестов строк после свертки таблицы, не изменилось, делаем вывод, что в ней нет дублей
		// и можно считывать настройки развернутого сальдо прямо из нее
		НастройкаРазвернутоеСальдо = ПараметрыОтчета.РазвернутоеСальдо;
		
	КонецЕсли;

	СписокВсехСчетовРазвернутоеСальдо = Новый СписокЗначений;
	Для Каждого СтрокаТаблицы Из НастройкаРазвернутоеСальдо Цикл
		Если СтрокаТаблицы.Использование И ЗначениеЗаполнено(СтрокаТаблицы.Счет) Тогда
			СписокВсехСчетовРазвернутоеСальдо.Добавить(СтрокаТаблицы.Счет);
		КонецЕсли;
	КонецЦикла;
	
	// Создадим массив, в который будем складывать счета, по которым хотим видеть развернутое сальдо
	// по разным субконто, Этот массив будем использовать для генерации выражений полей итогов СКД
	СчетаСРазворотомПоСубконто = Новый Массив;
	
	СчетаСРазворотомПоСубконто1 = Новый Массив;
	СчетаСРазворотомПоСубконто2 = Новый Массив;
	СчетаСРазворотомПоСубконто3 = Новый Массив;
	
	СчетаСРазворотомПоСубконто.Вставить(0, СчетаСРазворотомПоСубконто1);
	СчетаСРазворотомПоСубконто.Вставить(1, СчетаСРазворотомПоСубконто2);
	СчетаСРазворотомПоСубконто.Вставить(2, СчетаСРазворотомПоСубконто3);
	СчетаБезСубконтоСразвернутымСальдо = Новый Массив;
	ЕстьРазворотПоСубконто = Ложь;
	
	// Для составления списка счетов с подходящим составом субконто, нам потребуется,
	// соответствие всех счетов детализации и развернутых счетов и их субконто,
	// КоличествоСубконтоДетализацииПоСчетам - содержит соответствие счетов детализации и их субконто
	// и это соответствие будет учитываться при построении запроса по развернутым счетам, поэтому,
	// для создания полного соответствия скопируем уже имеющееся и в копию будем добавлять соответсвие
	// развернутых счетов и их субконто, для списка счетов с подходящим составом субконто будем использовать эту копию
	
	ПолныйСписокСубконтоДетализацииПоСчетам = Новый Соответствие(Новый ФиксированноеСоответствие(КоличествоСубконтоДетализацииПоСчетам));
	
	Для Каждого СтрокаТаблицы Из НастройкаРазвернутоеСальдо Цикл
		СубконтоРазвернутоеСальдо = Новый СписокЗначений;
		Если СтрокаТаблицы.Использование И ЗначениеЗаполнено(СтрокаТаблицы.Счет) Тогда
			ВыводитьРазвернутоеСальдо = Истина;
			
			ДанныеСчета = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(СтрокаТаблицы.Счет);
			
			ЕстьРазворотПоСубконтоТекущегоСчета = Ложь;
			
			Если Найти(СтрокаТаблицы.ПоСубконто, "+1") <> 0 Тогда
				
				Если СтрокаТаблицы.ПоСубсчетам Тогда
					Для Каждого Счет Из БухгалтерскийУчетВызовСервераПовтИсп.СчетаВИерархии(СтрокаТаблицы.Счет) Цикл
						СчетаСразворотомПоСубконто[0].Добавить(Счет);
					КонецЦикла;
				Иначе
					СчетаСразворотомПоСубконто[0].Добавить(СтрокаТаблицы.Счет);
				КонецЕсли;
				
				СубконтоРазвернутоеСальдо.Добавить(ДанныеСчета.ВидСубконто1);
				
				ЕстьРазворотПоСубконтоТекущегоСчета = Истина;
				ЕстьРазворотПоСубконто = Истина;
				
			КонецЕсли;
			
			Если Найти(СтрокаТаблицы.ПоСубконто, "+2") <> 0 Тогда
				
				Если СтрокаТаблицы.ПоСубсчетам Тогда
					Для Каждого Счет Из БухгалтерскийУчетВызовСервераПовтИсп.СчетаВИерархии(СтрокаТаблицы.Счет) Цикл
						СчетаСразворотомПоСубконто[1].Добавить(Счет);
					КонецЦикла;
				Иначе
					СчетаСразворотомПоСубконто[1].Добавить(СтрокаТаблицы.Счет);
				КонецЕсли;
				
				СубконтоРазвернутоеСальдо.Очистить();
				СубконтоРазвернутоеСальдо.Добавить(ДанныеСчета.ВидСубконто1);
				СубконтоРазвернутоеСальдо.Добавить(ДанныеСчета.ВидСубконто2);
				
				ЕстьРазворотПоСубконтоТекущегоСчета = Истина;
				ЕстьРазворотПоСубконто = Истина;
				
			КонецЕсли;
			
			Если Найти(СтрокаТаблицы.ПоСубконто, "+3") <> 0 Тогда
				
				Если СтрокаТаблицы.ПоСубсчетам Тогда
					Для Каждого Счет Из БухгалтерскийУчетВызовСервераПовтИсп.СчетаВИерархии(СтрокаТаблицы.Счет) Цикл
						СчетаСразворотомПоСубконто[2].Добавить(Счет);
					КонецЦикла;
				Иначе
					СчетаСразворотомПоСубконто[2].Добавить(СтрокаТаблицы.Счет);
				КонецЕсли;
				
				СубконтоРазвернутоеСальдо.Очистить();
				СубконтоРазвернутоеСальдо.Добавить(ДанныеСчета.ВидСубконто1);
				СубконтоРазвернутоеСальдо.Добавить(ДанныеСчета.ВидСубконто2);
				СубконтоРазвернутоеСальдо.Добавить(ДанныеСчета.ВидСубконто3);
				
				ЕстьРазворотПоСубконтоТекущегоСчета = Истина;
				ЕстьРазворотПоСубконто = Истина;
				
			КонецЕсли;
			
			Индекс = НастройкаРазвернутоеСальдо.Индекс(СтрокаТаблицы) + 1;
			
			СубконтоРазвернутоеСальдоКоличество = СубконтоРазвернутоеСальдо.Количество();
			
			Если СтрокаТаблицы.ПоСубсчетам Тогда
				ИндексСубСчета = 0;
				Для Каждого Счет Из БухгалтерскийУчетВызовСервераПовтИсп.СчетаВИерархии(СтрокаТаблицы.Счет) Цикл
					
					Детализация = ПолныйСписокСубконтоДетализацииПоСчетам.Получить(Счет);
					
					Если Детализация = Неопределено Тогда
						
						ПолныйСписокСубконтоДетализацииПоСчетам.Вставить(Счет, СубконтоРазвернутоеСальдо.ВыгрузитьЗначения());
						
					ИначеЕсли Детализация.Количество() < СубконтоРазвернутоеСальдоКоличество Тогда
						
						ПолныйСписокСубконтоДетализацииПоСчетам.Вставить(Счет, СубконтоРазвернутоеСальдо.ВыгрузитьЗначения());
						
					КонецЕсли;
					
					БухгалтерскиеОтчетыВызовСервера.СкопироватьПараметрСхемыКомпоновкиДанных(Схема, "СчетРазвернутоеСальдо" + Индекс  + "_" + ИндексСубСчета, "СчетРазвернутоеСальдо", Счет);
					ТекстУсловие = ТекстУсловие + "Счет = &СчетРазвернутоеСальдо" + Индекс + "_" + ИндексСубСчета + " ИЛИ ";
					ИндексСубСчета = ИндексСубСчета + 1;
					
				КонецЦикла;
			Иначе
				
				Детализация = ПолныйСписокСубконтоДетализацииПоСчетам.Получить(СтрокаТаблицы.Счет);
				
				Если Детализация = Неопределено Тогда
					
					ПолныйСписокСубконтоДетализацииПоСчетам.Вставить(СтрокаТаблицы.Счет, СубконтоРазвернутоеСальдо.ВыгрузитьЗначения());
					
				ИначеЕсли Детализация.Количество() < СубконтоРазвернутоеСальдоКоличество Тогда
					
					ПолныйСписокСубконтоДетализацииПоСчетам.Вставить(СтрокаТаблицы.Счет, СубконтоРазвернутоеСальдо.ВыгрузитьЗначения());
					
				КонецЕсли;
				
				ТекстУсловие = ТекстУсловие + "Счет = &СчетРазвернутоеСальдо" + Индекс + " ИЛИ "; 
				
			КонецЕсли;
			
			
			// Установка параметра СчетРазвернутоеСальдо
			БухгалтерскиеОтчетыВызовСервера.СкопироватьПараметрСхемыКомпоновкиДанных(Схема, "СчетРазвернутоеСальдо" + Индекс, "СчетРазвернутоеСальдо", СтрокаТаблицы.Счет);
			
			// Счета без разворота по субконто будут обработаны общим запросом поэтому отдельный запрос не формируем
			// Модифицируема текст условия для вычисления развернутого сальдо и добавляем соответсвующий параметр
			Если Не ЕстьРазворотПоСубконтоТекущегоСчета Тогда
				// Счета, по которым нужно развернутое сальдо но не в разрезе субконто, нельзя исключать из запроса по счетам
				// Запомним счет
				Если СтрокаТаблицы.ПоСубсчетам Тогда
					ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СчетаБезСубконтоСразвернутымСальдо, СчетаВИерархии(СтрокаТаблицы.Счет));
				Иначе
					СчетаБезСубконтоСРазвернутымСальдо.Добавить(СтрокаТаблицы.Счет);
				КонецЕсли;
				Продолжить;
			КонецЕсли;
						
			// Формируем текст параметра УсловиеСчета запроса детализации по субконто
			ТекстДляПодстановкиУсловиеСчетаПоСубконтоРазвернутое = "Счет В ИЕРАРХИИ (&СчетРазвернутоеСальдо" + Индекс + ")
																	| И Счет НЕ В (&СчетаИсключенныеИзЗапросаПоСчетамРазвернутое" + Индекс + ")
																	| И ((НЕ Счет.Забалансовый)
																	| ИЛИ &ВыводитьЗабалансовыеСчета)";
			
			// Формируем текст параметра Субконто запроса по субконто развернутое
			ТекстДляПодстановкиСубконтоПоСубконтоРазвернутое = "&СубконтоРазвернутый" + Индекс;
			
			СчетаИсключенныеИзЗапросаПоСчетамРазвернутое = СписокВсехСчетовРазвернутоеСальдо.Скопировать();
			СчетаИсключенныеИзЗапросаПоСчетамРазвернутое.Удалить(СчетаИсключенныеИзЗапросаПоСчетамРазвернутое.НайтиПоЗначению(СтрокаТаблицы.Счет));
			// Установка параметра СчетаИсключенныеИзЗапросаПоСчетамРазвернутое
			БухгалтерскиеОтчетыВызовСервера.СкопироватьПараметрСхемыКомпоновкиДанных(Схема, "СчетаИсключенныеИзЗапросаПоСчетамРазвернутое" + Индекс, "СчетаИсключенныеИзЗапросаПоСчетамРазвернутое", СчетаИсключенныеИзЗапросаПоСчетамРазвернутое);
			
			// Установка параметра "СубконтоДетализацииРазвернутый
			БухгалтерскиеОтчетыВызовСервера.СкопироватьПараметрСхемыКомпоновкиДанных(Схема, "СубконтоРазвернутый" + Индекс, "СубконтоРазвернутый", СубконтоРазвернутоеСальдо);
			
			// Формируем текст запроса для счета детализации
			ТекстДляПодстановкиЗапросПоСубконтоРазвернутое = ТекстЗапросПоСубконтоРазвернутое;
			ТекстДляПодстановкиЗапросПоСубконтоРазвернутое = СтрЗаменить(ТекстДляПодстановкиЗапросПоСубконтоРазвернутое, ТекстУсловиеСчетаПоСубконтоРазвернутое, ТекстДляПодстановкиУсловиеСчетаПоСубконтоРазвернутое);
			ТекстДляПодстановкиЗапросПоСубконтоРазвернутое = СтрЗаменить(ТекстДляПодстановкиЗапросПоСубконтоРазвернутое, ТекстСубконтоПоСубконтоРазвернутое, ТекстДляПодстановкиСубконтоПоСубконтоРазвернутое);
			
			// Для разворота сальдо по нужному количеству субконто, включем их во вложенном запросе
			Для Индекс = 1 По СубконтоРазвернутоеСальдо.Количество() Цикл
				
				ТекстДляПодстановкиЗапросПоСубконтоРазвернутое = СтрЗаменить(ТекстДляПодстановкиЗапросПоСубконтоРазвернутое, "//Null КАК Субконто" + Индекс, "ОстаткиИОбороты.Субконто" + Индекс + " КАК Субконто" + Индекс);
				
			КонецЦикла;
			
			// Для детализации могут потребоваться не все субконто по которым разворачиваем
			// Поэтому в конечном запросе берем КоличествоСубконтоДетализации
			Для Индекс = 1 По КоличествоСубконтоДетализации Цикл
				
				КоличествоСубконтоДетализируемыхПоСчету = КоличествоСубконтоДетализацииПоСчетам.Получить(СтрокаТаблицы.Счет);
				Если КоличествоСубконтоДетализируемыхПоСчету = Неопределено Тогда
					
					КоличествоСубконтоДетализируемыхПоСчету = 0;
					
				Иначе
					КоличествоСубконтоДетализируемыхПоСчету = КоличествоСубконтоДетализируемыхПоСчету.Количество();
				КонецЕсли;
				
				Если Индекс > СубконтоРазвернутоеСальдо.Количество() ИЛИ Индекс > КоличествоСубконтоДетализируемыхПоСчету Тогда
					ТекстПодстановки = "Null КАК Субконто" + Индекс;
				Иначе
					ТекстПодстановки = "ВложенныйЗапрос.Субконто" + Индекс + " КАК Субконто" + Индекс;
				КонецЕсли;
				
				ТекстДляПодстановкиЗапросПоСубконтоРазвернутое = СтрЗаменить(ТекстДляПодстановкиЗапросПоСубконтоРазвернутое, "//Null КАК ВложенныйЗапрос.Субконто" + Индекс, ТекстПодстановки);
			КонецЦикла;
			
			// Доработка запроса набора данных
			НаборДанных.Запрос = НаборДанных.Запрос + ТекстДляПодстановкиЗапросПоСубконтоРазвернутое;
			НаборДанных.Запрос = НаборДанных.Запрос + " ОБЪЕДИНИТЬ ВСЕ ";
			
		КонецЕсли;
	КонецЦикла;
	
	// Вычтем из счетов без развернутого сальдо по субконто, 
	// те по которым развернутое сальдо по субконто всетаки включено отдельной настройкой
	Для ИндексСубконто = 0 По 2 Цикл
		СчетаБезСубконтоСРазвернутымСальдо = ОбщегоНазначенияКлиентСервер.СократитьМассив(СчетаБезСубконтоСРазвернутымСальдо, СчетаСРазворотомПоСубконто[ИндексСубконто]);
	КонецЦикла;
	
	// Среди счетов с развернутым сальдо могут быть субсчета по которым не удасться получить развернутое сальдо
	// У некоторых субсчетов может быть отличный состав субконто от родительского счета, определим такие счета
	// и создадим для них отдельный запрос
		СчетаСПодходящимСоставомСубконто = СчетаИсключенныеИзЗапросаПоСчетам(СписокВсехСчетовРазвернутоеСальдо, ПолныйСписокСубконтоДетализацииПоСчетам);
	
	// Подготовим список счетов для отдельного запроса по развернутому сальдо
	// Возьмем список всех счетов с развернутым сальдо, включая их субсчета
	СчетаДляОтдельногоЗапроса = СчетаВИерархии(СписокВсехСчетовРазвернутоеСальдо.ВыгрузитьЗначения());
	// Вычтем из них сами счета с развернутым сальдо
	СчетаДляОтдельногоЗапроса = ОбщегоНазначенияКлиентСервер.СократитьМассив(СчетаДляОтдельногоЗапроса, СписокВсехСчетовРазвернутоеСальдо.ВыгрузитьЗначения());
	// Из оставшихся субсчетов вычитаем счета с подходящим составом субконто
	СчетаДляОтдельногоЗапроса = ОбщегоНазначенияКлиентСервер.СократитьМассив(СчетаДляОтдельногоЗапроса, СчетаСПодходящимСоставомСубконто.ВыгрузитьЗначения());
	// Добавляем счета у которых явно указано что сальдо не разворачивается по субконто
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СчетаДляОтдельногоЗапроса, СчетаБезСубконтоСРазвернутымСальдо);
	
	// Сформируем отдельный запрос для таких счетов
	Если СчетаДляОтдельногоЗапроса.Количество() > 0 Тогда
		
		СписокСчетаБезСубконтоСРазвернутымСальдо = Новый СписокЗначений;
		СписокСчетаБезСубконтоСРазвернутымСальдо.ЗагрузитьЗначения(СчетаДляОтдельногоЗапроса);
		
		// Установка параметра СчетРазвернутоеСальдо
		БухгалтерскиеОтчетыВызовСервера.СкопироватьПараметрСхемыКомпоновкиДанных(Схема, "СчетРазвернутоеСальдо_БезСубконто", "СчетРазвернутоеСальдо", СписокСчетаБезСубконтоСРазвернутымСальдо);
		
		// Формируем текст параметра УсловиеСчета запроса детализации по субконто
		ТекстДляПодстановкиУсловиеСчетаПоСубконтоРазвернутое = "Счет В ИЕРАРХИИ (&СчетРазвернутоеСальдо_БезСубконто)
		| И ((НЕ Счет.Забалансовый)
		| ИЛИ &ВыводитьЗабалансовыеСчета)";
		
		// Формируем текст параметра Субконто запроса по субконто развернутое
		ТекстДляПодстановкиСубконтоПоСубконтоРазвернутое = "";
		
		// Формируем текст запроса для счета детализации
		ТекстДляПодстановкиЗапросПоСубконтоРазвернутое = ТекстЗапросПоСубконтоРазвернутое;
		ТекстДляПодстановкиЗапросПоСубконтоРазвернутое = СтрЗаменить(ТекстДляПодстановкиЗапросПоСубконтоРазвернутое, ТекстУсловиеСчетаПоСубконтоРазвернутое, ТекстДляПодстановкиУсловиеСчетаПоСубконтоРазвернутое);
		ТекстДляПодстановкиЗапросПоСубконтоРазвернутое = СтрЗаменить(ТекстДляПодстановкиЗапросПоСубконтоРазвернутое, ТекстСубконтоПоСубконтоРазвернутое, ТекстДляПодстановкиСубконтоПоСубконтоРазвернутое);
		
		// Добавляем нужное количество субконто
		Для Индекс = 1 По КоличествоСубконтоДетализации Цикл
			ТекстДляПодстановкиЗапросПоСубконтоРазвернутое = СтрЗаменить(ТекстДляПодстановкиЗапросПоСубконтоРазвернутое, "//Null КАК ВложенныйЗапрос.Субконто" + Индекс, "Null КАК Субконто" + Индекс);
		КонецЦикла;
		
		// Доработка запроса набора данных
		НаборДанных.Запрос = НаборДанных.Запрос + ТекстДляПодстановкиЗапросПоСубконтоРазвернутое;
		
	Иначе
		
		// Удаляем Ключесую фразу " ОБЪЕДИНИТЬ ВСЕ " в конце запроса
		НаборДанных.Запрос = Лев(НаборДанных.Запрос, СтрДлина(НаборДанных.Запрос) - 16);
	
	КонецЕсли;
	
	МассивПоказателей = Новый Массив;
	МассивПоказателей.Добавить("БУ");
	МассивПоказателей.Добавить("НУ");
	МассивПоказателей.Добавить("ПР");
	МассивПоказателей.Добавить("ВР");
	
	// Проверим список исключаемых счетов, возможно некоторые субсчета стоит из него исключить
	// Запрос детализации строится с указанием конкретных видов субконто в параметре виртуальной таблицы
	// Поэтому если у какого-либо субсчета детализируемого счета нет нужного набора субконто, 
	// он будет исключен из запроса детализации, такие субсчета не нужно исключать из запроса по счетам
	СчетаИсключенныеИзЗапросаПоСчетам = СчетаИсключенныеИзЗапросаПоСчетам(СчетаИсключенныеИзЗапросаПоСчетам, КоличествоСубконтоДетализацииПоСчетам);
		
	// Для развернутого сальдо модифицируем выражения полей итога, а также укажем группировки по которым они должны расчитывться
	Если ВыводитьРазвернутоеСальдо Тогда
		
		ТекстУсловие = Лев(ТекстУсловие, СтрДлина(ТекстУсловие) - 4);
		
		Для Каждого ИмяПоказателя Из МассивПоказателей Цикл
			
			ПолеИтога = Схема.ПоляИтога.Найти("СальдоНаНачалоПериода." + ИмяПоказателя + "НачальныйОстатокДт");
			ПолеИтога.Выражение = "Выбор Когда " + ТекстУсловие + " Тогда Сумма(СальдоНаНачалоПериода." + ИмяПоказателя + "НачальныйРазвернутыйОстатокДт) Иначе Сумма(СальдоНаНачалоПериода." + ИмяПоказателя + "НачальныйОстатокДт) Конец";
			ЗаполнитьГруппировкиПоляИтога(ПолеИтога);
			
			ПолеИтога = Схема.ПоляИтога.Найти("СальдоНаНачалоПериода." + ИмяПоказателя + "НачальныйОстатокКт");
			ПолеИтога.Выражение = "Выбор Когда " + ТекстУсловие + " Тогда Сумма(СальдоНаНачалоПериода." + ИмяПоказателя + "НачальныйРазвернутыйОстатокКт) Иначе Сумма(СальдоНаНачалоПериода." + ИмяПоказателя + "НачальныйОстатокКт) Конец";
			ЗаполнитьГруппировкиПоляИтога(ПолеИтога);
			
			ПолеИтога = Схема.ПоляИтога.Найти("СальдоНаКонецПериода." + ИмяПоказателя + "КонечныйОстатокДт");
			ПолеИтога.Выражение = "Выбор Когда " + ТекстУсловие + " Тогда Сумма(СальдоНаКонецПериода." + ИмяПоказателя + "КонечныйРазвернутыйОстатокДт) Иначе Сумма(СальдоНаКонецПериода." + ИмяПоказателя + "КонечныйОстатокДт) Конец";
			ЗаполнитьГруппировкиПоляИтога(ПолеИтога);
			
			ПолеИтога = Схема.ПоляИтога.Найти("СальдоНаКонецПериода." + ИмяПоказателя + "КонечныйОстатокКт");
			ПолеИтога.Выражение = "Выбор Когда " + ТекстУсловие + " Тогда Сумма(СальдоНаКонецПериода." + ИмяПоказателя + "КонечныйРазвернутыйОстатокКт) Иначе Сумма(СальдоНаКонецПериода." + ИмяПоказателя + "КонечныйОстатокКт) Конец";
			ЗаполнитьГруппировкиПоляИтога(ПолеИтога);
			
		КонецЦикла;
		
		// Показатель контроль обрабатываем отдельно
		ПолеИтога = Схема.ПоляИтога.Найти("СальдоНаНачалоПериода.КонтрольНачальныйОстатокДт");
		ПолеИтога.Выражение = "Выбор Когда " + ТекстУсловие + " Тогда Сумма(Выбор Когда СальдоНаНачалоПериода.КонтрольНачальныйРазвернутыйОстатокДт - СальдоНаНачалоПериода.КонтрольНачальныйРазвернутыйОстатокКт = 0 Тогда 0 
			|Иначе СальдоНаНачалоПериода.КонтрольНачальныйРазвернутыйОстатокДт Конец) Иначе Сумма(Выбор Когда СальдоНаНачалоПериода.КонтрольНачальныйОстатокДт - СальдоНаНачалоПериода.КонтрольНачальныйОстатокКт = 0 Тогда 0 
			|Иначе СальдоНаНачалоПериода.КонтрольНачальныйОстатокДт Конец) Конец";
		ЗаполнитьГруппировкиПоляИтога(ПолеИтога);
			
		ПолеИтога = Схема.ПоляИтога.Найти("СальдоНаНачалоПериода.КонтрольНачальныйОстатокКт");
		ПолеИтога.Выражение = "Выбор Когда " + ТекстУсловие + " Тогда Сумма(Выбор Когда СальдоНаНачалоПериода.КонтрольНачальныйРазвернутыйОстатокДт - СальдоНаНачалоПериода.КонтрольНачальныйРазвернутыйОстатокКт = 0 Тогда 0 
			|Иначе СальдоНаНачалоПериода.КонтрольНачальныйРазвернутыйОстатокКт Конец) Иначе Сумма(Выбор Когда СальдоНаНачалоПериода.КонтрольНачальныйОстатокДт - СальдоНаНачалоПериода.КонтрольНачальныйОстатокКт = 0 Тогда 0 
			|Иначе СальдоНаНачалоПериода.КонтрольНачальныйОстатокКт Конец) Конец";
		ЗаполнитьГруппировкиПоляИтога(ПолеИтога);
		
		ПолеИтога = Схема.ПоляИтога.Найти("СальдоНаКонецПериода.КонтрольКонечныйОстатокДт");
		ПолеИтога.Выражение = "Выбор Когда " + ТекстУсловие + " Тогда Сумма(Выбор Когда СальдоНаКонецПериода.КонтрольКонечныйРазвернутыйОстатокДт - СальдоНаКонецПериода.КонтрольКонечныйРазвернутыйОстатокКт = 0 Тогда 0 
			|Иначе СальдоНаКонецПериода.КонтрольКонечныйРазвернутыйОстатокДт Конец) Иначе Сумма(Выбор Когда СальдоНаКонецПериода.КонтрольКонечныйОстатокДт - СальдоНаКонецПериода.КонтрольКонечныйОстатокКт = 0 Тогда 0 
			|Иначе СальдоНаКонецПериода.КонтрольКонечныйОстатокДт Конец) Конец";
		ЗаполнитьГруппировкиПоляИтога(ПолеИтога);
			
		ПолеИтога = Схема.ПоляИтога.Найти("СальдоНаКонецПериода.КонтрольКонечныйОстатокКт");
		ПолеИтога.Выражение = "Выбор Когда " + ТекстУсловие + " Тогда Сумма(Выбор Когда СальдоНаКонецПериода.КонтрольКонечныйРазвернутыйОстатокДт - СальдоНаКонецПериода.КонтрольКонечныйРазвернутыйОстатокКт = 0 Тогда 0 
			|Иначе СальдоНаКонецПериода.КонтрольКонечныйРазвернутыйОстатокКт Конец) Иначе Сумма(Выбор Когда СальдоНаКонецПериода.КонтрольКонечныйОстатокДт - СальдоНаКонецПериода.КонтрольКонечныйОстатокКт = 0 Тогда 0 
			|Иначе СальдоНаКонецПериода.КонтрольКонечныйОстатокКт Конец) Конец";
		ЗаполнитьГруппировкиПоляИтога(ПолеИтога);
		
		// Для каждого субконто добавляем свои поля итогов которые расчитываются только по конкретному субконто
		Для СчетчикСубконто = 1 По 3 Цикл
			
			ТекстУсловие = "Ложь ИЛИ ";
			СчетчикСчетов = 1;
			// Составим тескт условия для субконто <СчетчикСубконто>
			Для Каждого СчетСразворотом Из СчетаСРазворотомПоСубконто[СчетчикСубконто - 1] Цикл
				
				ТекстУсловие = ТекстУсловие + "Счет = &СчетСРазворотомПоСубконто" + СчетчикСубконто + "_" + СчетчикСчетов + " ИЛИ ";
				
				// Установка параметра СчетСРазворотомПоСубконто
				БухгалтерскиеОтчетыВызовСервера.СкопироватьПараметрСхемыКомпоновкиДанных(Схема, "СчетСРазворотомПоСубконто" + + СчетчикСубконто + "_" + СчетчикСчетов, "СчетСРазворотомПоСубконто", СчетСразворотом);
				
				СчетчикСчетов = СчетчикСчетов + 1;
				
			КонецЦикла;
			
			ТекстУсловие = Лев(ТекстУсловие, СтрДлина(ТекстУсловие) - 4);
			
			Для Каждого ИмяПоказателя Из МассивПоказателей Цикл
				
				ПолеИтогаНачалоПериодаДт = Схема.ПоляИтога.Добавить();
				ПолеИтогаНачалоПериодаДт.ПутьКДанным = "СальдоНаНачалоПериода." + ИмяПоказателя + "НачальныйОстатокДт";
				ПолеИтогаНачалоПериодаДт.Выражение = "Выбор Когда " + ТекстУсловие + " Тогда Сумма(СальдоНаНачалоПериода." + ИмяПоказателя + "НачальныйРазвернутыйОстатокДт) Иначе Сумма(СальдоНаНачалоПериода." + ИмяПоказателя + "НачальныйОстатокДт) Конец";
				
				ПолеИтогаНачалоПериодаКт = Схема.ПоляИтога.Добавить();
				ПолеИтогаНачалоПериодаКт.ПутьКДанным = "СальдоНаНачалоПериода." + ИмяПоказателя + "НачальныйОстатокКт";
				ПолеИтогаНачалоПериодаКт.Выражение = "Выбор Когда " + ТекстУсловие + " Тогда Сумма(СальдоНаНачалоПериода." + ИмяПоказателя + "НачальныйРазвернутыйОстатокКт) Иначе Сумма(СальдоНаНачалоПериода." + ИмяПоказателя + "НачальныйОстатокКт) Конец";
				
				ПолеИтогаКонецПериодаДт = Схема.ПоляИтога.Добавить();
				ПолеИтогаКонецПериодаДт.ПутьКДанным = "СальдоНаКонецПериода." + ИмяПоказателя + "КонечныйОстатокДт";
				ПолеИтогаКонецПериодаДт.Выражение = "Выбор Когда " + ТекстУсловие + " Тогда Сумма(СальдоНаКонецПериода." + ИмяПоказателя + "КонечныйРазвернутыйОстатокДт) Иначе Сумма(СальдоНаКонецПериода." + ИмяПоказателя + "КонечныйОстатокДт) Конец";
				
				ПолеИтогаКонецПериодаКт = Схема.ПоляИтога.Добавить();
				ПолеИтогаКонецПериодаКт.ПутьКДанным = "СальдоНаКонецПериода." + ИмяПоказателя + "КонечныйОстатокКт";
				ПолеИтогаКонецПериодаКт.Выражение = "Выбор Когда " + ТекстУсловие + " Тогда Сумма(СальдоНаКонецПериода." + ИмяПоказателя + "КонечныйРазвернутыйОстатокКт) Иначе Сумма(СальдоНаКонецПериода." + ИмяПоказателя + "КонечныйОстатокКт) Конец";
				
				ПолеИтогаНачалоПериодаДт.Группировки.Добавить("Субконто" + СчетчикСубконто);
				ПолеИтогаНачалоПериодаКт.Группировки.Добавить("Субконто" + СчетчикСубконто);
				ПолеИтогаКонецПериодаДт.Группировки.Добавить("Субконто" + СчетчикСубконто);
				ПолеИтогаКонецПериодаКт.Группировки.Добавить("Субконто" + СчетчикСубконто);
				
			КонецЦикла;
			
			// Показатель контроль обрабатываем отдельно
			ПолеИтогаНачалоПериодаДт = Схема.ПоляИтога.Добавить();
			ПолеИтогаНачалоПериодаДт.ПутьКДанным = "СальдоНаНачалоПериода.КонтрольНачальныйОстатокДт";
			ПолеИтогаНачалоПериодаДт.Выражение = "Выбор Когда " + ТекстУсловие + " Тогда Сумма(Выбор Когда СальдоНаНачалоПериода.КонтрольНачальныйРазвернутыйОстатокДт - СальдоНаНачалоПериода.КонтрольНачальныйРазвернутыйОстатокКт = 0 Тогда 0 
				|Иначе СальдоНаНачалоПериода.КонтрольНачальныйРазвернутыйОстатокДт Конец) Иначе Сумма(Выбор Когда СальдоНаНачалоПериода.КонтрольНачальныйОстатокДт - СальдоНаНачалоПериода.КонтрольНачальныйОстатокКт = 0 Тогда 0 
				|Иначе СальдоНаНачалоПериода.КонтрольНачальныйОстатокДт Конец) Конец";
			
			ПолеИтогаНачалоПериодаКт = Схема.ПоляИтога.Добавить();
			ПолеИтогаНачалоПериодаКт.ПутьКДанным = "СальдоНаНачалоПериода.КонтрольНачальныйОстатокКт";
			ПолеИтогаНачалоПериодаКт.Выражение = "Выбор Когда " + ТекстУсловие + " Тогда Сумма(Выбор Когда СальдоНаНачалоПериода.КонтрольНачальныйРазвернутыйОстатокДт - СальдоНаНачалоПериода.КонтрольНачальныйРазвернутыйОстатокКт = 0 Тогда 0 
				|Иначе СальдоНаНачалоПериода.КонтрольНачальныйРазвернутыйОстатокКт Конец) Иначе Сумма(Выбор Когда СальдоНаНачалоПериода.КонтрольНачальныйОстатокДт - СальдоНаНачалоПериода.КонтрольНачальныйОстатокКт = 0 Тогда 0 
				|Иначе СальдоНаНачалоПериода.КонтрольНачальныйОстатокКт Конец) Конец";
			
			ПолеИтогаКонецПериодаДт = Схема.ПоляИтога.Добавить();
			ПолеИтогаКонецПериодаДт.ПутьКДанным = "СальдоНаКонецПериода.КонтрольКонечныйОстатокДт";
			ПолеИтогаКонецПериодаДт.Выражение = "Выбор Когда " + ТекстУсловие + " Тогда Сумма(Выбор Когда СальдоНаКонецПериода.КонтрольКонечныйРазвернутыйОстатокДт - СальдоНаКонецПериода.КонтрольКонечныйРазвернутыйОстатокКт = 0 Тогда 0 
				|Иначе СальдоНаКонецПериода.КонтрольКонечныйРазвернутыйОстатокДт Конец) Иначе Сумма(Выбор Когда СальдоНаКонецПериода.КонтрольКонечныйОстатокДт - СальдоНаКонецПериода.КонтрольКонечныйОстатокКт = 0 Тогда 0 
				|Иначе СальдоНаКонецПериода.КонтрольКонечныйОстатокДт Конец) Конец";
			
			ПолеИтогаКонецПериодаКт = Схема.ПоляИтога.Добавить();
			ПолеИтогаКонецПериодаКт.ПутьКДанным = "СальдоНаКонецПериода.КонтрольКонечныйОстатокКт";
			ПолеИтогаКонецПериодаКт.Выражение = "Выбор Когда " + ТекстУсловие + " Тогда Сумма(Выбор Когда СальдоНаКонецПериода.КонтрольКонечныйРазвернутыйОстатокДт - СальдоНаКонецПериода.КонтрольКонечныйРазвернутыйОстатокКт = 0 Тогда 0 
				|Иначе СальдоНаКонецПериода.КонтрольКонечныйРазвернутыйОстатокКт Конец) Иначе Сумма(Выбор Когда СальдоНаКонецПериода.КонтрольКонечныйОстатокДт - СальдоНаКонецПериода.КонтрольКонечныйОстатокКт = 0 Тогда 0 
				|Иначе СальдоНаКонецПериода.КонтрольКонечныйОстатокКт Конец) Конец";
			
			ПолеИтогаНачалоПериодаДт.Группировки.Добавить("Субконто" + СчетчикСубконто);
			ПолеИтогаНачалоПериодаКт.Группировки.Добавить("Субконто" + СчетчикСубконто);
			ПолеИтогаКонецПериодаДт.Группировки.Добавить("Субконто" + СчетчикСубконто);
			ПолеИтогаКонецПериодаКт.Группировки.Добавить("Субконто" + СчетчикСубконто);
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если Не ЕстьДетализацияПоСубконто Тогда
		Схема.НаборыДанных.НаборДанныхОбъединение.Элементы.Удалить(Схема.НаборыДанных.НаборДанныхОбъединение.Элементы.ПоСубконто);
	КонецЕсли;
	Если Не ВыводитьРазвернутоеСальдо ИЛИ (НЕ ЕстьРазворотПоСубконто И СчетаДляОтдельногоЗапроса.Количество() = 0) Тогда
		Схема.НаборыДанных.НаборДанныхОбъединение.Элементы.Удалить(Схема.НаборыДанных.НаборДанныхОбъединение.Элементы.ПоСубконтоРазвернутое);
	КонецЕсли;
	
	БухгалтерскиеОтчеты.УстановитьМинимальнуюШиринуПоказателей(Схема, ПолучитьНаборПоказателей());
	
	КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(Схема));
	
	Для Каждого Параметр Из КомпоновщикНастроек.Настройки.ПараметрыДанных.Элементы Цикл
		Параметр.Использование = Истина;
	КонецЦикла;
	
	СписокСчетовПоСубсчетам = БухгалтерскиеОтчетыВызовСервера.ПолучитьСписокСчетовПоСубсчетам(НастройкаГруппировки);
		
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "СчетаИсключенныеИзЗапросаПоСчетам", СчетаИсключенныеИзЗапросаПоСчетам);
	
	Если ЗначениеЗаполнено(ПараметрыОтчета.НачалоПериода) Тогда
		БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "НачалоПериода", НачалоДня(ПараметрыОтчета.НачалоПериода));
	КонецЕсли;
	Если ЗначениеЗаполнено(ПараметрыОтчета.КонецПериода) Тогда
		БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "КонецПериода", КонецДня(ПараметрыОтчета.КонецПериода));
	КонецЕсли;
	
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "ВыводитьЗабалансовыеСчета", ПараметрыОтчета.ВыводитьЗабалансовыеСчета);
	
	// Дополнительные данные
	БухгалтерскиеОтчетыВызовСервера.ДобавитьДополнительныеПоля(ПараметрыОтчета, КомпоновщикНастроек);
	
	УсловноеОформлениеАвтоотступа = Неопределено;
	Для каждого ЭлементОформления Из КомпоновщикНастроек.Настройки.УсловноеОформление.Элементы Цикл
		Если ЭлементОформления.Представление = "Уменьшенный автоотступ" Тогда
			УсловноеОформлениеАвтоотступа = ЭлементОформления;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если УсловноеОформлениеАвтоотступа = Неопределено Тогда
		УсловноеОформлениеАвтоотступа = КомпоновщикНастроек.Настройки.УсловноеОформление.Элементы.Добавить();
		УсловноеОформлениеАвтоотступа.Представление = "Уменьшенный автоотступ";
		УсловноеОформлениеАвтоотступа.Оформление.УстановитьЗначениеПараметра("Автоотступ", 1);
		УсловноеОформлениеАвтоотступа.Использование = Ложь;
		УсловноеОформлениеАвтоотступа.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.БыстрыйДоступ;
	Иначе
		УсловноеОформлениеАвтоотступа.Поля.Элементы.Очистить();
	КонецЕсли;
	
	// Формирование структуры отчета
	
	// Добавим в структуру отчета 2 группировки по счету с разным отбором и параметрами
	// Группировка по балансовым счетам
	ГруппировкаПоБалансовымСчетам = Структура.Строки.Добавить();
	
	// Заполняем основные параметры группировки
	ПолеГруппировки = ГруппировкаПоБалансовымСчетам.ПоляГруппировки.Элементы.Добавить(Тип("ПолеГруппировкиКомпоновкиДанных"));
	ПолеГруппировки.Использование  = Истина;
	ПолеГруппировки.Поле           = Новый ПолеКомпоновкиДанных("Счет");
	ПолеГруппировки.ТипГруппировки = ТипГруппировкиКомпоновкиДанных.Иерархия;
	ГруппировкаПоБалансовымСчетам.Выбор.Элементы.Добавить(Тип("АвтоВыбранноеПолеКомпоновкиДанных"));
	ГруппировкаПоБалансовымСчетам.Порядок.Элементы.Добавить(Тип("АвтоЭлементПорядкаКомпоновкиДанных"));
	
	// Добавим автоотступ
	ПолеОформления = УсловноеОформлениеАвтоотступа.Поля.Элементы.Добавить();
	ПолеОформления.Поле = ПолеГруппировки.Поле;

	// Установка отбора на выводимый уровень иерархии счета
	ГруппаЭлементовОтбора = ГруппировкаПоБалансовымСчетам.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаЭлементовОтбора.Применение = ТипПримененияОтбораКомпоновкиДанных.Иерархия;
	ГруппаЭлементовОтбора.ТипГруппы  = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
	БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(ГруппаЭлементовОтбора, "ПараметрыДанных.ПоСубсчетам", Истина);
	БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(ГруппаЭлементовОтбора, "СистемныеПоля.УровеньВГруппировке", 1);
	БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(ГруппаЭлементовОтбора, "Счет", СписокСчетовПоСубсчетам, ВидСравненияКомпоновкиДанных.ВСпискеПоИерархии, СписокСчетовПоСубсчетам.Количество() > 0);
	
	// Устанавливаем отбор по признаку Забалансовый
	БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(ГруппировкаПоБалансовымСчетам.Отбор, "Счет.Забалансовый", Ложь);
	
	// Отключим вывод отбора
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметрВывода(ГруппировкаПоБалансовымСчетам, "ВыводитьОтбор", ТипВыводаТекстаКомпоновкиДанных.НеВыводить);
	
	// Группировка по забалансовым счетам
	ГруппировкаПоЗабалансовымСчетам = Структура.Строки.Добавить();
	
	// Заполняем основные параметры группировки
	ПолеГруппировки = ГруппировкаПоЗабалансовымСчетам.ПоляГруппировки.Элементы.Добавить(Тип("ПолеГруппировкиКомпоновкиДанных"));
	ПолеГруппировки.Использование  = Истина;
	ПолеГруппировки.Поле           = Новый ПолеКомпоновкиДанных("Счет");
	ПолеГруппировки.ТипГруппировки = ТипГруппировкиКомпоновкиДанных.Иерархия;
	ГруппировкаПоЗабалансовымСчетам.Выбор.Элементы.Добавить(Тип("АвтоВыбранноеПолеКомпоновкиДанных"));
	ГруппировкаПоЗабалансовымСчетам.Порядок.Элементы.Добавить(Тип("АвтоЭлементПорядкаКомпоновкиДанных"));
	
	// Добавим автоотступ
	ПолеОформления = УсловноеОформлениеАвтоотступа.Поля.Элементы.Добавить();
	ПолеОформления.Поле = ПолеГруппировки.Поле;
	
	// Установка отбора на выводимый уровень иерархии счета
	ГруппаЭлементовОтбора = ГруппировкаПоЗабалансовымСчетам.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаЭлементовОтбора.Применение = ТипПримененияОтбораКомпоновкиДанных.Иерархия;
	ГруппаЭлементовОтбора.ТипГруппы  = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
	БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(ГруппаЭлементовОтбора, "ПараметрыДанных.ПоСубсчетам", Истина);
	БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(ГруппаЭлементовОтбора, "СистемныеПоля.УровеньВГруппировке", 1);
	БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(ГруппаЭлементовОтбора, "Счет", СписокСчетовПоСубсчетам, ВидСравненияКомпоновкиДанных.ВСпискеПоИерархии, СписокСчетовПоСубсчетам.Количество() > 0);
	
	// Устанавливаем отбор по признаку Забалансовый
	БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(ГруппировкаПоЗабалансовымСчетам.Отбор, "Счет.Забалансовый", Истина);
	
	// Отключим вывод отбора и итогов
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметрВывода(ГруппировкаПоЗабалансовымСчетам, "ВыводитьОтбор", ТипВыводаТекстаКомпоновкиДанных.НеВыводить);
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметрВывода(ГруппировкаПоЗабалансовымСчетам, "РасположениеОбщихИтогов", РасположениеИтоговКомпоновкиДанных.Нет);
	
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "ПоСубсчетам", ПараметрыОтчета.ПоСубсчетам);
	
	// Добавленные группировки сложим в массив для удобства дальнейшей доработки структуры
	ГруппировкиПоСчетам = Новый Массив;
	ГруппировкиПоСчетам.Добавить(ГруппировкаПоБалансовымСчетам);
	ГруппировкиПоСчетам.Добавить(ГруппировкаПоЗабалансовымСчетам);
	
	// Для валютных сумм, добавляем группировку по валюте
	Если ПараметрыОтчета.ПоказательВалютнаяСумма Тогда
		
		Для Каждого Группировка Из ГруппировкиПоСчетам Цикл
			
			Структура = Группировка.Структура.Добавить();
			ПолеГруппировки = Структура.ПоляГруппировки.Элементы.Добавить(Тип("ПолеГруппировкиКомпоновкиДанных"));
			ПолеГруппировки.Использование  = Истина;
			ПолеГруппировки.Поле           = Новый ПолеКомпоновкиДанных("Валюта");
			ПолеГруппировки.ТипГруппировки = ТипГруппировкиКомпоновкиДанных.Элементы;
			
			Структура.Выбор.Элементы.Добавить(Тип("АвтоВыбранноеПолеКомпоновкиДанных"));
			Структура.Порядок.Элементы.Добавить(Тип("АвтоЭлементПорядкаКомпоновкиДанных"));
			
			// Устанавливаем отбор по признаку Валютный
			БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(Структура.Отбор, "Счет.Валютный", Истина);
			// Отключим вывод отбора и итогов
			БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметрВывода(Структура, "ВыводитьОтбор", ТипВыводаТекстаКомпоновкиДанных.НеВыводить);
			
			ПолеОформления = УсловноеОформлениеАвтоотступа.Поля.Элементы.Добавить();
			ПолеОформления.Поле = ПолеГруппировки.Поле;
			
		КонецЦикла;
		
	КонецЕсли;
	
	// Добавляем группировки для разворота по субконто
	Если ЕстьДетализацияПоСубконто Тогда
		
		Для Каждого Группировка Из ГруппировкиПоСчетам Цикл
			
			Структура = Группировка;
			
			Для Индекс = 1 По КоличествоСубконтоДетализации Цикл 
				
				Структура = Структура.Структура.Добавить();
				ПолеГруппировки = Структура.ПоляГруппировки.Элементы.Добавить(Тип("ПолеГруппировкиКомпоновкиДанных"));
				ПолеГруппировки.Использование  = Истина;
				ПолеГруппировки.Поле           = Новый ПолеКомпоновкиДанных("Субконто" + Индекс);
				ПолеГруппировки.ТипГруппировки = ТипГруппировкиКомпоновкиДанных.Элементы;
				
				Структура.Выбор.Элементы.Добавить(Тип("АвтоВыбранноеПолеКомпоновкиДанных"));
				Структура.Порядок.Элементы.Добавить(Тип("АвтоЭлементПорядкаКомпоновкиДанных"));
				
				БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(Структура.Отбор, ПолеГруппировки.Поле,"", ВидСравненияКомпоновкиДанных.Заполнено);
				БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметрВывода(Структура, "ВыводитьОтбор", ТипВыводаТекстаКомпоновкиДанных.НеВыводить);
				
				ПолеОформления = УсловноеОформлениеАвтоотступа.Поля.Элементы.Добавить();
				ПолеОформления.Поле = ПолеГруппировки.Поле;
				
				Если ПараметрыОтчета.ПоказательВалютнаяСумма Тогда
					ГруппировкаПоВалюте = Структура.Структура.Добавить();
					ПолеГруппировки = ГруппировкаПоВалюте.ПоляГруппировки.Элементы.Добавить(Тип("ПолеГруппировкиКомпоновкиДанных"));
					ПолеГруппировки.Использование  = Истина;
					ПолеГруппировки.Поле           = Новый ПолеКомпоновкиДанных("Валюта");
					ПолеГруппировки.ТипГруппировки = ТипГруппировкиКомпоновкиДанных.Элементы;
					
					ГруппировкаПоВалюте.Выбор.Элементы.Добавить(Тип("АвтоВыбранноеПолеКомпоновкиДанных"));
					ГруппировкаПоВалюте.Порядок.Элементы.Добавить(Тип("АвтоЭлементПорядкаКомпоновкиДанных"));
					
					// Устанавливаем отбор по признаку Валютный
					БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(ГруппировкаПоВалюте.Отбор, "Счет.Валютный", Истина);
					
					// Отключим вывод отбора и итогов
					БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметрВывода(ГруппировкаПоВалюте, "ВыводитьОтбор", ТипВыводаТекстаКомпоновкиДанных.НеВыводить);
					
					ПолеОформления = УсловноеОформлениеАвтоотступа.Поля.Элементы.Добавить();
					ПолеОформления.Поле = ПолеГруппировки.Поле;
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЕсли;
	
	// Добавляем отбор по организации
	БухгалтерскиеОтчетыВызовСервера.ДобавитьОтборПоОрганизации(ПараметрыОтчета, КомпоновщикНастроек);
	
	БухгалтерскиеОтчетыВызовСервера.ДобавитьОтборДляПоказателяКонтроль(ПараметрыОтчета, КомпоновщикНастроек);
	
	// Добавляем отбор по ресурсам отчета
	ДобавитьОтборПоРесурсам(ПараметрыОтчета, КомпоновщикНастроек);
	
	// Оформление итогов
	ДобавитьОформлениеИтогов(ПараметрыОтчета, КомпоновщикНастроек);
	
	// У счетов по которым не ведется налоговый учет, убрем показатели НУ,ПР, ВР и контроль
	// для этого условным оформлением установим высоту строк для этих показателе равной 1
	// позже при обработке таблицы результата такие строки будут удалены
	// Добавляем элемент условного оформления
	УсловноеОформление = КомпоновщикНастроек.Настройки.УсловноеОформление.Элементы.Добавить();
	
	// Указываем оформляемые поля
	Поле = УсловноеОформление.Поля.Элементы.Добавить();
	Поле.Поле = Новый ПолеКомпоновкиДанных("Показатели.НУ");
	Поле = УсловноеОформление.Поля.Элементы.Добавить();
	Поле.Поле = Новый ПолеКомпоновкиДанных("Показатели.ПР");
	Поле = УсловноеОформление.Поля.Элементы.Добавить();
	Поле.Поле = Новый ПолеКомпоновкиДанных("Показатели.ВР");
	Поле = УсловноеОформление.Поля.Элементы.Добавить();
	Поле.Поле = Новый ПолеКомпоновкиДанных("Показатели.Контроль");
	Поле = УсловноеОформление.Поля.Элементы.Добавить();
	
	// Отключим использование этого элемента условного оформления в общих итогах
	УсловноеОформление.ИспользоватьВОбщемИтоге = ИспользованиеУсловногоОформленияКомпоновкиДанных.НеИспользовать;
	
	БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(УсловноеОформление.Отбор, "Счет.НалоговыйУчет", Ложь);
	
	// Устанавливаем параметр оформления
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(УсловноеОформление.Оформление, "МаксимальнаяВысота", 1);
	
	Если УсловноеОформлениеАвтоотступа.Поля.Элементы.Количество() = 0 Тогда
		УсловноеОформлениеАвтоотступа.Использование = Ложь;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПослеКомпоновкиМакета(ПараметрыОтчета, МакетКомпоновки) Экспорт

	// Определим количество группировок по субконто
	КоличествоГруппировок = 0;
	
	Для Каждого СтрокаТаблицы Из ПараметрыОтчета.Группировка Цикл
		Если СтрокаТаблицы.Использование Тогда
			КоличествоСубконто = СтрЧислоВхождений(СтрокаТаблицы.ПоСубконто, "+");
			КоличествоГруппировок = Макс(КоличествоГруппировок, КоличествоСубконто);
		КонецЕсли;
	КонецЦикла;

	// Для того чтобы отобразить сумму в валюте после каждой группировки подставляется еще групировка по валюте
	// удваивая тем самым общее количество группировок отчета, это нужно учесть
	КоличествоГруппировок = КоличествоГруппировок + ?(ПараметрыОтчета.ПоказательВалютнаяСумма, КоличествоГруппировок, 0);

	КоличествоСтрокШапки = Макс(КоличествоГруппировок, 1);
	ПараметрыОтчета.Вставить("ВысотаШапки", КоличествоСтрокШапки + 1);

	// Обработка шапки отчета
	// Шапка отчета состоит и 4 макетов, макет шапки таблицы, и 3 макета группировок колонок
	// поместим ссылки на них в массив, для дальнейшей обработки
	МакетШапкиТаблицы = БухгалтерскиеОтчетыВызовСервера.ПолучитьМакетШапки(МакетКомпоновки);
	
	МакетыШапкиОтчета = Новый Массив;
	МакетыШапкиОтчета.Добавить(МакетШапкиТаблицы.Имя);

	Для Каждого Колонка Из МакетКомпоновки.Тело[0].Колонки Цикл

		Для Каждого ТелоГруппировки Из Колонка.Тело Цикл
			МакетыШапкиОтчета.Добавить(ТелоГруппировки.Макет);
		КонецЦикла;

	КонецЦикла;

	// Удалим лишние строки из шапки отчета
	МассивДляУдаления = Новый Массив;
	
	Для Каждого ИмяМакетаШапкиОтчета Из МакетыШапкиОтчета Цикл
		
		МакетШапкиОтчета = МакетКомпоновки.Макеты[ИмяМакетаШапкиОтчета];
		
		
		Для Индекс = КоличествоСтрокШапки + 1 По МакетШапкиОтчета.Макет.Количество() - 1 Цикл
			
			МассивДляУдаления.Добавить(МакетШапкиОтчета.Макет[Индекс]);
			
		КонецЦикла;
		
		Для Каждого Элемент Из МассивДляУдаления Цикл
			МакетШапкиОтчета.Макет.Удалить(Элемент);
		КонецЦикла;
		
		// Если группировка только по счету, объединим ячейки заголовка таблицы
		Если МакетШапкиОтчета = МакетШапкиТаблицы И КоличествоСтрокШапки = 1 Тогда
			
			Для Каждого Ячейка Из МакетШапкиОтчета.Макет[МакетШапкиОтчета.Макет.Количество() - 1].Ячейки Цикл
				
				Оформление = Ячейка.Оформление.Элементы.Найти("ОбъединятьПоВертикали");
				Оформление.Значение = Истина;
				Оформление.Использование = Истина;
				
			КонецЦикла;
			
		КонецЕсли;
			
	КонецЦикла;
	
	// Для обработки итогов запомним имена раличных макетов
	МакетГруппировкиСчет = БухгалтерскиеОтчетыВызовСервера.ПолучитьМакетГруппировкиПоПолюГруппировки(МакетКомпоновки, "Счет");
	МакетыРесурсовПодвалаОтчета 		= Новый Массив;
	МакетыРесурсовСчета 				= Новый Массив;
	
	// Соответствие макетов колонкам отчета понадобится позже для подсчета итогов
	СоответствиеМакетовКолонкамОтчета = Новый Соответствие;
	
	ЗаполнитьМакетыРесурсовПодвалаОтчета(МакетКомпоновки.Тело[0], МакетыРесурсовПодвалаОтчета, СоответствиеМакетовКолонкамОтчета);
	ЗаполнитьМакетыРесурсовГруппировки(МакетКомпоновки.Тело[0].Строки, МакетыРесурсовСчета, СоответствиеМакетовКолонкамОтчета, "Счет");
	
	МассивИменМакетовСчет = Новый Массив;
	Для Каждого МакетСчет Из МакетГруппировкиСчет Цикл
		МассивИменМакетовСчет.Добавить(МакетСчет.Имя);
	КонецЦикла;

	// Макеты групировки и ресурсов групировки по валюте,
	// будем использовать для определения пренадлежности элемента Макет, Макета компоновки
	МакетГруппировкиВалюта = БухгалтерскиеОтчетыВызовСервера.ПолучитьМакетГруппировкиПоПолюГруппировки(МакетКомпоновки, "Валюта");
	МакетРесурсовГруппировкиВалюта = Новый Массив;
	
	МассивИменМакетовВалюта = Новый Массив;
	Для Каждого МакетВалюта Из МакетГруппировкиВалюта Цикл
		МассивИменМакетовВалюта.Добавить(МакетВалюта.Имя);
	КонецЦикла;
	
	ЗаполнитьМакетыРесурсовГруппировки(МакетКомпоновки.Тело[0].Строки, МакетРесурсовГруппировкиВалюта, СоответствиеМакетовКолонкамОтчета, "Валюта", Истина);

	// Если в отчете включена валютная сумма, то ее нужно показывать только по тем счетам
	// где есть валюта, для того чтобы убрать вывод показателя валюта там где он не нужен
	// переберем все макеты, и удалим строки предназначенные для вывода валютных сумм у всех макетов
	// кроме макетов группировки по валюте и макетов ресурсов групировки по валюте
	Если ПараметрыОтчета.ПоказательВалютнаяСумма Тогда
		Для Каждого Макет Из МакетКомпоновки.Макеты Цикл 
			// Пропускаем макеты шапки
			Если МакетыШапкиОтчета.Найти(Макет.Имя) = Неопределено Тогда
				
				Если МассивИменМакетовВалюта.Найти(Макет.Имя) <> Неопределено ИЛИ МакетРесурсовГруппировкиВалюта.Найти(Макет.Имя) <> Неопределено Тогда
					
					Если ПараметрыОтчета.ПоказательКонтроль Тогда
						Макет.Макет.Удалить(Макет.Макет.Количество() - 2);
					КонецЕсли;
					
				ИначеЕсли Макет.Макет.Количество() > 1 Тогда // Последний не удаляем
					
					Макет.Макет.Удалить(Макет.Макет.Количество() - 1);
					
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	// Создадим структуру для хранения итогов
	ЗначенияПоказателей = Новый Структура();
	
	// Определим количество показателей выводимых в отчет
	КоличествоПоказателей 	= БухгалтерскиеОтчетыВызовСервера.КоличествоПоказателей(ПараметрыОтчета);
	// На верхнем уровне структура, ключу структуры соответсвует имя макета колонки
	// Значению 2-х мерный массив, размерность первого измерения массива соответсвует количеству показателей отчета
	// размерность второго измерения равна 2, что соответсвует дебету и кредиту
	Для Каждого Колонка Из МакетКомпоновки.Тело[0].Колонки Цикл
		
		// Создаем структуру
		ЗначенияПоказателей.Вставить(Колонка.Тело[0].Макет, Новый Массив(КоличествоПоказателей, 2));
		
		// Заполняем значения массива нолями
		Для Каждого Массив Из ЗначенияПоказателей[Колонка.Тело[0].Макет] Цикл
			Для Индекс = 0 По 1 Цикл
				Массив[Индекс] = 0;
			КонецЦикла;
		КонецЦикла;
		
	КонецЦикла;

	// Создадим структуру для хранения временных данных, которые нужны для расчета итогов при выводе отчета
	ВременныеДанныеОтчета = Новый Структура;

	// Упаковываем временные данные в структуру
	ВременныеДанныеОтчета.Вставить("МакетыРесурсовСчета"					, МакетыРесурсовСчета);
	ВременныеДанныеОтчета.Вставить("МакетыРесурсовПодвалаОтчета"			, МакетыРесурсовПодвалаОтчета);
	ВременныеДанныеОтчета.Вставить("СоответствиеМакетовКолонкамОтчета"		, СоответствиеМакетовКолонкамОтчета);
	ВременныеДанныеОтчета.Вставить("МакетВалюта"							, МассивИменМакетовВалюта);
	ВременныеДанныеОтчета.Вставить("МакетШапкиОтчета"						, МакетыШапкиОтчета);
	ВременныеДанныеОтчета.Вставить("МакетСчет"								, МассивИменМакетовСчет);
	ВременныеДанныеОтчета.Вставить("КоличествоПоказателей"					, КоличествоПоказателей);
	ВременныеДанныеОтчета.Вставить("ЗначенияПоказателей"					, ЗначенияПоказателей);
	ВременныеДанныеОтчета.Вставить("ТекущийСчет"							, Неопределено);
	
	ПараметрыОтчета.Вставить("ВременныеДанныеОтчета", ВременныеДанныеОтчета);
	
КонецПроцедуры

Процедура ПередВыводомЭлементаРезультата(ПараметрыОтчета, МакетКомпоновки, ДанныеРасшифровки, ЭлементРезультата, Отказ = Ложь) Экспорт
	
	// Отсекаем валютные группировки на счетах, по которым не ведется
	// валютный учет
	Если ЭлементРезультата.ЗначенияПараметров.Количество() > 0
		И ЭлементРезультата.ЗначенияПараметров.Найти("П1") <> Неопределено
		И ЗначениеЗаполнено(ЭлементРезультата.Макет)
		И ПараметрыОтчета.ВременныеДанныеОтчета.МакетВалюта.Найти(ЭлементРезультата.Макет) <> Неопределено
		И ЭлементРезультата.ЗначенияПараметров.П1.Значение = Null Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;

	Если ПараметрыОтчета.ПоказательВалютнаяСумма
		И ПараметрыОтчета.ВременныеДанныеОтчета.КоличествоПоказателей > 1 Тогда
		КоличествоПоказателей = ПараметрыОтчета.ВременныеДанныеОтчета.КоличествоПоказателей - 1;
	Иначе
		КоличествоПоказателей = ПараметрыОтчета.ВременныеДанныеОтчета.КоличествоПоказателей;
	КонецЕсли;

	// Обрабатываем элементы, содержащие корневые счета
	Если ЭлементРезультата.ЗначенияПараметров.Количество() > 0 
		И ЭлементРезультата.ЗначенияПараметров.Найти("П1") <> Неопределено
		И ЗначениеЗаполнено(ЭлементРезультата.Макет)
		И ПараметрыОтчета.ВременныеДанныеОтчета.МакетШапкиОтчета.Найти(ЭлементРезультата.Макет) = Неопределено
		И ПараметрыОтчета.ВременныеДанныеОтчета.МакетыРесурсовПодвалаОтчета.Найти(ЭлементРезультата.Макет) = Неопределено Тогда

		// Запомним значение счета
		Если ПараметрыОтчета.ВременныеДанныеОтчета.МакетСчет.Найти(ЭлементРезультата.Макет) <> Неопределено Тогда
			
			ПараметрыОтчета.ВременныеДанныеОтчета.ТекущийСчет = Неопределено;
			
			Для Каждого Параметр Из ЭлементРезультата.ЗначенияПараметров Цикл
				Если ТипЗнч(Параметр.Значение) = Тип("ИдентификаторРасшифровкиКомпоновкиДанных") Тогда
					
					ПараметрыОтчета.ВременныеДанныеОтчета.ТекущийСчет = ДанныеРасшифровки.Элементы[Параметр.Значение].ПолучитьПоля()[0].Значение;;
					Прервать;
					
				КонецЕсли;
			КонецЦикла;
			
		КонецЕсли;
		
		//Накапливаем суммы по корневым счетам
		Если ПараметрыОтчета.ВременныеДанныеОтчета.МакетыРесурсовСчета.Найти(ЭлементРезультата.Макет) <> Неопределено И ЗначениеЗаполнено(ПараметрыОтчета.ВременныеДанныеОтчета.ТекущийСчет) Тогда
			Если Не ЗначениеЗаполнено(ПараметрыОтчета.ВременныеДанныеОтчета.ТекущийСчет.Родитель) И МакетКомпоновки.Макеты[ЭлементРезультата.Макет].Макет.Количество() > 0 Тогда
				
				Для ИндексЭлемента = 0 По КоличествоПоказателей - 1 Цикл
					
					СтрокаМакета = МакетКомпоновки.Макеты[ЭлементРезультата.Макет].Макет[ИндексЭлемента];
					
					Для Каждого Ячейка Из СтрокаМакета.Ячейки Цикл
						
						Если Ячейка.Элементы.Количество() = 0 Тогда
							Продолжить;
						КонецЕсли;
						
						Для Каждого Элемент Из Ячейка.Элементы Цикл
							
							ИмяПараметра = Строка(Элемент.Значение);
							ПараметрРезультата = ЭлементРезультата.ЗначенияПараметров.Найти(ИмяПараметра);
							
							Значение = 0;

							Если НЕ (Не ПараметрыОтчета.ВременныеДанныеОтчета.ТекущийСчет.НалоговыйУчет И ИндексЭлемента = (КоличествоПоказателей - 1)
								И ПараметрыОтчета.ПоказательКонтроль) И ПараметрРезультата <> Неопределено Тогда
								Если ПараметрРезультата.Значение <> Null Тогда
									Значение = ПараметрРезультата.Значение;
								КонецЕсли;
							КонецЕсли;
							
							КолонкаРезультата = ПараметрыОтчета.ВременныеДанныеОтчета.СоответствиеМакетовКолонкамОтчета.Получить(ЭлементРезультата.Макет);
							
							Если КолонкаРезультата <> Неопределено Тогда
								
								ИндексЯчейки = СтрокаМакета.Ячейки.Индекс(Ячейка);
								ТекущееЗначениеИтога = ПараметрыОтчета.ВременныеДанныеОтчета.ЗначенияПоказателей[КолонкаРезультата][ИндексЭлемента][ИндексЯчейки];
								ПараметрыОтчета.ВременныеДанныеОтчета.ЗначенияПоказателей[КолонкаРезультата][ИндексЭлемента][ИндексЯчейки] = ТекущееЗначениеИтога + Значение;
								
							КонецЕсли;
							
						КонецЦикла;
						
					КонецЦикла;
					
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЕсли; 

	// Проставляем накопленные суммы в подвал отчета
	ИначеЕсли ПараметрыОтчета.ВременныеДанныеОтчета.МакетыРесурсовПодвалаОтчета.Найти(ЭлементРезультата.Макет) <> Неопределено Тогда

		КолонкаРезультата = ПараметрыОтчета.ВременныеДанныеОтчета.СоответствиеМакетовКолонкамОтчета.Получить(ЭлементРезультата.Макет);

		Если КолонкаРезультата <> Неопределено Тогда

			Для Каждого СтрокаМакета Из МакетКомпоновки.Макеты[ЭлементРезультата.Макет].Макет Цикл

				ИндексЭлемента = МакетКомпоновки.Макеты[ЭлементРезультата.Макет].Макет.Индекс(СтрокаМакета);

				Для Каждого Ячейка Из СтрокаМакета.Ячейки Цикл

					ИндексЯчейки =  СтрокаМакета.Ячейки.Индекс(Ячейка);

					Для Каждого Элемент Из Ячейка.Элементы Цикл

						ИмяПараметра = Строка(Элемент.Значение);
						ПараметрЭлемента = ЭлементРезультата.ЗначенияПараметров.Найти(ИмяПараметра);

						// Если параметр определен в макете но его нет в элементе результата
						Если ПараметрЭлемента = Неопределено тогда
							
							// Добавляем параметра в элемент результата
							ПараметрЭлемента = ЭлементРезультата.ЗначенияПараметров.Добавить();
							ПараметрЭлемента.Имя = ИмяПараметра;
							
						КонецЕсли;
						
						ПараметрЭлемента.Значение = ПараметрыОтчета.ВременныеДанныеОтчета.ЗначенияПоказателей[КолонкаРезультата][ИндексЭлемента][ИндексЯчейки];
						
					КонецЦикла;

				КонецЦикла;

			КонецЦикла;

		КонецЕсли;

	КонецЕсли;
	
КонецПроцедуры

Процедура ПослеВыводаРезультата(ПараметрыОтчета, Результат) Экспорт
	
	БухгалтерскиеОтчетыВызовСервера.ОбработкаРезультатаОтчета(ПараметрыОтчета.ИдентификаторОтчета, Результат);
	
	Если Результат.Области.Найти("Заголовок") = Неопределено Тогда
		Результат.ФиксацияСверху = ПараметрыОтчета.ВысотаШапки;
	Иначе
		Результат.ФиксацияСверху = Результат.Области.Заголовок.Низ + ПараметрыОтчета.ВысотаШапки;
	КонецЕсли;
	
	Результат.ФиксацияСлева = 0;
	
КонецПроцедуры

Функция ПолучитьНаборПоказателей() Экспорт
	
	НаборПоказателей = Новый Массив;
	НаборПоказателей.Добавить("БУ");
	НаборПоказателей.Добавить("НУ");
	НаборПоказателей.Добавить("ПР");
	НаборПоказателей.Добавить("ВР");
	НаборПоказателей.Добавить("Контроль");
	НаборПоказателей.Добавить("ВалютнаяСумма");
	
	Возврат НаборПоказателей;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СчетаИсключенныеИзЗапросаПоСчетам(СчетаИсключенныеИзЗапросаПоСчетам, КоличествоСубконтоДетализацииПоСчетам);

	Если СчетаИсключенныеИзЗапросаПоСчетам.Количество() = 0 Тогда
		Возврат СчетаИсключенныеИзЗапросаПоСчетам;
	Конецесли;
	
	ШаблонЗапроса = "ВЫБРАТЬ
		|	ХозрасчетныйВидыСубконто.Ссылка
		|ИЗ
		|	ПланСчетов.Хозрасчетный.ВидыСубконто КАК ХозрасчетныйВидыСубконто
		|ГДЕ
		|	ХозрасчетныйВидыСубконто.Ссылка В ИЕРАРХИИ(&[Счет])
		|	И ХозрасчетныйВидыСубконто.Ссылка <> (&[Счет])
		|	И (ХозрасчетныйВидыСубконто.НомерСтроки = 1
		|				И ХозрасчетныйВидыСубконто.ВидСубконто = &[ВидСубконто1]
		|			ИЛИ ХозрасчетныйВидыСубконто.НомерСтроки = 2
		|				И ХозрасчетныйВидыСубконто.ВидСубконто = &[ВидСубконто2]
		|			ИЛИ ХозрасчетныйВидыСубконто.НомерСтроки = 3
		|				И ХозрасчетныйВидыСубконто.ВидСубконто = &[ВидСубконто3])
		|СГРУППИРОВАТЬ ПО
		|	ХозрасчетныйВидыСубконто.Ссылка
		|ИМЕЮЩИЕ
		|	КОЛИЧЕСТВО(ХозрасчетныйВидыСубконто.Ссылка) = &[КоличествоСубконто]";
		
		
	СчетаИсключение = Новый Массив;
		
	Разделитель = "
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|";
		
	Запрос = Новый Запрос;
	
	НужноВыполнятьЗапрос = Ложь;
	
	ВспомогательныйЗапрос = Новый Запрос;
	ВспомогательныйЗапрос.Текст = "ВЫБРАТЬ
		|	ХозрасчетныйВидыСубконто.ВидСубконто.Ссылка КАК ВидСубконто,
		|	ХозрасчетныйВидыСубконто.НомерСтроки КАК НомерСубконто,
		|	ХозрасчетныйВидыСубконто.Ссылка КАК Счет
		|ИЗ
		|	ПланСчетов.Хозрасчетный.ВидыСубконто КАК ХозрасчетныйВидыСубконто
		|ГДЕ
		|	ХозрасчетныйВидыСубконто.Ссылка В(&СчетаИсключенныеИзЗапросаПоСчетам)";
		
	ВспомогательныйЗапрос.УстановитьПараметр("СчетаИсключенныеИзЗапросаПоСчетам", СчетаИсключенныеИзЗапросаПоСчетам);
	
	СчетаИСубконто = ВспомогательныйЗапрос.Выполнить().Выгрузить();
	СчетаИСубконто.Индексы.Добавить("Счет");
	
	Для Каждого ИсключенныйСчет Из СчетаИсключенныеИзЗапросаПоСчетам Цикл
		
		Счет = ИсключенныйСчет.Значение;
		
		ВидыСубконто = КоличествоСубконтоДетализацииПоСчетам.Получить(Счет);
		
		Если ВидыСубконто = Неопределено Тогда
			Продолжить;
		КонецЕсли;
			
		Если Счет.ЗапретитьИспользоватьВПроводках Тогда
			
			ИндексСчета = СчетаИсключенныеИзЗапросаПоСчетам.Индекс(ИсключенныйСчет);
			
			Запрос.УстановитьПараметр("Счет" + ИндексСчета, Счет);
			
			ПараметрыШаблона = Новый Структура();
			ПараметрыШаблона.Вставить("Счет", "Счет" + ИндексСчета);
			
			ВидыСубконтоСчета = СчетаИСубконто.НайтиСтроки(Новый Структура("Счет", Счет));
			
			Для Каждого ВидСубконто ИЗ ВидыСубконтоСчета Цикл
				
				Если ВидыСубконто.Найти(ВидСубконто.ВидСубконто) <> Неопределено Тогда
					
					Запрос.УстановитьПараметр("ВидСубконто" + ВидСубконто.НомерСубконто + "_" + ИндексСчета, ВидСубконто.ВидСубконто);
					ПараметрыШаблона.Вставить("ВидСубконто" + ВидСубконто.НомерСубконто, "ВидСубконто" + ВидСубконто.НомерСубконто + "_" + ИндексСчета);
					
				КонецЕсли;
				
			КонецЦикла;
			
			Запрос.УстановитьПараметр("КоличествоСубконто_" + ИндексСчета, ВидыСубконто.Количество());
			ПараметрыШаблона.Вставить("КоличествоСубконто", "КоличествоСубконто_" + ИндексСчета);
			
			Запрос.Текст = Запрос.Текст + СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(ШаблонЗапроса, ПараметрыШаблона) + Разделитель;
			
			НужноВыполнятьЗапрос = Истина;
			
		Иначе
			
			СчетаИсключение.Добавить(Счет);
			
		КонецЕсли;
		
	КонецЦикла;
	
	СтруктураПараметров = Новый Структура("ВидСубконто1, ВидСубконто2, ВидСубконто3", "ВидСубконто1", "ВидСубконто2", "ВидСубконто3");
	
	Запрос.Текст = СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(Запрос.Текст, СтруктураПараметров);
	Запрос.УстановитьПараметр("ВидСубконто1", Неопределено);
	Запрос.УстановитьПараметр("ВидСубконто2", Неопределено);
	Запрос.УстановитьПараметр("ВидСубконто3", Неопределено);
	
	СписокСчетов = Новый СписокЗначений;
	
	Если НужноВыполнятьЗапрос Тогда
		
		Запрос.Текст = Лев(Запрос.Текст, СтрДлина(Запрос.Текст) - 17);
		
		Результат = Запрос.Выполнить();
		
		Если Не Результат.Пустой() Тогда
			
			СписокСчетов.ЗагрузитьЗначения(Результат.Выгрузить().ВыгрузитьКолонку("Ссылка"));
			
		КонецЕсли;
		
	КонецЕсли;
	
	Для Каждого Счет Из СчетаИсключение Цикл
		
		СписокСчетов.Добавить(Счет);
		
	КонецЦикла;
		
	Возврат СписокСчетов;
	
КонецФункции

Процедура ЗаполнитьГруппировкиПоляИтога(ПолеИтога)
	
	ПолеИтога.Группировки.Добавить("Счет");
	ПолеИтога.Группировки.Добавить("Счет Иерархия");
	ПолеИтога.Группировки.Добавить("Валюта");
	
КонецПроцедуры

Функция ЗаполнитьМакетыРесурсовГруппировки(Таблица, МассивМакетов, СоответствиеМакетовКолонкамОтчета, ПолеГруппировки = Неопределено, ВключатьМакетыВложенныхГруппировок = Ложь, ПрочитатьМакетыРесурсов = Ложь)
	
	// Переребираем все элементы макета
	Для Каждого Группировка Из Таблица Цикл
		
		Если ТипЗнч(Группировка) = Тип("ГруппировкаТаблицыМакетаКомпоновкиДанных") Тогда
			// Если это группировка проверим поле группировки
			Если ПолеГруппировки = Неопределено ИЛИ Группировка.Группировка[0].ИмяПоля = ПолеГруппировки Тогда
				
				Если ВключатьМакетыВложенныхГруппировок Тогда
					// Перебираем вложенные группировки, условие по полю группировки в них не накладываем
					ЗаполнитьМакетыРесурсовГруппировки(Группировка.Тело, МассивМакетов, СоответствиеМакетовКолонкамОтчета,,,Истина);
				Иначе
					ЗаполнитьМакетыРесурсовГруппировки(Группировка.Тело, МассивМакетов, СоответствиеМакетовКолонкамОтчета, ПолеГруппировки, ВключатьМакетыВложенныхГруппировок, Истина);
				КонецЕсли;
				
				// Перебираем иерархию группировки
				Для Каждого ТелоИерархии Из Группировка.ТелоИерархии Цикл
					
					Если ТипЗнч(ТелоИерархии) = Тип("МакетГруппировкиТаблицыМакетаКомпоновкиДанных") Тогда
						
						Для Каждого МакетРесурсов Из ТелоИерархии.МакетРесурсов Цикл
							// Помещаем макеты в массив
							МассивМакетов.Добавить(МакетРесурсов.Макет);
							// Добавляем соответствие макетов ресурсов и колонок отчета
							СоответствиеМакетовКолонкамОтчета.Вставить(МакетРесурсов.Макет, МакетРесурсов.МакетГруппировки);
							
						КонецЦикла;
						
					КонецЕсли;
				
				КонецЦикла;
				
			Иначе
				// Если эта группировка не подошла по условию проверим вложенные группировки
				ЗаполнитьМакетыРесурсовГруппировки(Группировка.Тело, МассивМакетов, СоответствиеМакетовКолонкамОтчета, ПолеГруппировки, ВключатьМакетыВложенныхГруппировок);

			КонецЕсли;
			
		ИначеЕсли ТипЗнч(Группировка) = Тип("МакетГруппировкиТаблицыМакетаКомпоновкиДанных") И ПрочитатьМакетыРесурсов Тогда
			
			Для Каждого МакетРесурсов Из Группировка.МакетРесурсов Цикл
				
				// Помещаем макеты в массив
				МассивМакетов.Добавить(МакетРесурсов.Макет);
				// Добавляем соответствие макетов ресурсов и колонок отчета
				СоответствиеМакетовКолонкамОтчета.Вставить(МакетРесурсов.Макет, МакетРесурсов.МакетГруппировки);
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецФункции

Функция ЗаполнитьМакетыРесурсовПодвалаОтчета(Таблица, МассивМакетов, СоответствиеМакетовКолонкамОтчета)

	Для Каждого Группировка Из Таблица.Строки Цикл

		Для Каждого МакетРесурсов Из Группировка.МакетПодвала.МакетРесурсов Цикл

			МассивМакетов.Добавить(МакетРесурсов.Макет);

			СоответствиеМакетовКолонкамОтчета.Вставить(МакетРесурсов.Макет, МакетРесурсов.МакетГруппировки);

		КонецЦикла;

	КонецЦикла;

КонецФункции

Процедура ДобавитьОтборПоРесурсам(ПараметрыОтчета, КомпоновщикНастроек)
	
	УсловноеОформление = КомпоновщикНастроек.Настройки.УсловноеОформление.Элементы.Добавить();
	
	// Указываем оформляемые поля
	Поле = УсловноеОформление.Поля.Элементы.Добавить();
	Поле.Поле = Новый ПолеКомпоновкиДанных("Счет");
	
	УсловноеОформление.ИспользоватьВЗаголовке = ИспользованиеУсловногоОформленияКомпоновкиДанных.НеИспользовать;
	УсловноеОформление.ИспользоватьВЗаголовкеПолей = ИспользованиеУсловногоОформленияКомпоновкиДанных.НеИспользовать;
	
	// Устанавливаем параметр оформления
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(УсловноеОформление.Оформление, "МаксимальнаяВысота", 1);
	
	Группа = УсловноеОформление.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	Группа.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
	
	Для Каждого ИмяПоказателя Из ПолучитьНаборПоказателей() Цикл
		
		Если ПараметрыОтчета["Показатель" + ИмяПоказателя] Тогда
			
			БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(Группа, "СальдоНаНачалоПериода." + ИмяПоказателя + "НачальныйОстатокДт", 0, ВидСравненияКомпоновкиДанных.Равно);
			
			БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(Группа, "СальдоНаНачалоПериода." + ИмяПоказателя + "НачальныйОстатокКт", 0, ВидСравненияКомпоновкиДанных.Равно);
			
			БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(Группа, "СальдоНаКонецПериода." + ИмяПоказателя + "КонечныйОстатокДт", 0, ВидСравненияКомпоновкиДанных.Равно);
			
			БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(Группа, "СальдоНаКонецПериода." + ИмяПоказателя + "КонечныйОстатокКт", 0, ВидСравненияКомпоновкиДанных.Равно);
					
			БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(Группа, "ОборотыЗаПериод." + ИмяПоказателя + "ОборотДт", 0, ВидСравненияКомпоновкиДанных.НеЗаполнено);
			
			БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(Группа, "ОборотыЗаПериод." + ИмяПоказателя + "ОборотКт", 0, ВидСравненияКомпоновкиДанных.НеЗаполнено);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ДобавитьОформлениеИтогов(ПараметрыОтчета, КомпоновщикНастроек)
	
	УсловноеОформление = КомпоновщикНастроек.Настройки.УсловноеОформление.Элементы.Добавить();
	
	УсловноеОформление.ИспользоватьВЗаголовке 					= ИспользованиеУсловногоОформленияКомпоновкиДанных.НеИспользовать;
	УсловноеОформление.ИспользоватьВЗаголовкеПолей 				= ИспользованиеУсловногоОформленияКомпоновкиДанных.НеИспользовать;
	УсловноеОформление.ИспользоватьВГруппировке 				= ИспользованиеУсловногоОформленияКомпоновкиДанных.НеИспользовать;
	УсловноеОформление.ИспользоватьВЗаголовке 					= ИспользованиеУсловногоОформленияКомпоновкиДанных.НеИспользовать;
	УсловноеОформление.ИспользоватьВЗаголовкеПолей 				= ИспользованиеУсловногоОформленияКомпоновкиДанных.НеИспользовать;
	УсловноеОформление.ИспользоватьВИерархическойГруппировке 	= ИспользованиеУсловногоОформленияКомпоновкиДанных.НеИспользовать;
	УсловноеОформление.ИспользоватьВОтборе 						= ИспользованиеУсловногоОформленияКомпоновкиДанных.НеИспользовать;
	УсловноеОформление.ИспользоватьВПараметрах 					= ИспользованиеУсловногоОформленияКомпоновкиДанных.НеИспользовать;
	
	ЖирныйШрифт = Новый Шрифт(,10,Истина);
	
	// Устанавливаем параметр оформления
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(УсловноеОформление.Оформление, "Шрифт", ЖирныйШрифт);
КонецПроцедуры

Функция СчетаВИерархии(СчетГруппа)
	
	Если НЕ ЗначениеЗаполнено(СчетГруппа) Тогда
		Возврат Новый Массив;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СчетГруппа", СчетГруппа);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Хозрасчетный.Ссылка КАК Счет
	|ИЗ
	|	ПланСчетов.Хозрасчетный КАК Хозрасчетный
	|ГДЕ
	|	Хозрасчетный.Ссылка В ИЕРАРХИИ(&СчетГруппа)
	|	И Хозрасчетный.ЗапретитьИспользоватьВПроводках = ЛОЖЬ";
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Счет");

КонецФункции

#КонецОбласти

#КонецЕсли