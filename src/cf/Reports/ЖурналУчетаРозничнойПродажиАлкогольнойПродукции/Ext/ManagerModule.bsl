#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Функция ПолучитьТекстЗаголовка(ПараметрыОтчета, ОрганизацияВНачале = Истина) Экспорт 

	Возврат НСтр("ru = 'Журнал учета объема розничной продажи алкогольной и спиртосодержащей продукции'") + БухгалтерскиеОтчетыКлиентСервер.ПолучитьПредставлениеПериода(ПараметрыОтчета.НачалоПериода, ПараметрыОтчета.КонецПериода);
	
КонецФункции

Процедура СформироватьОтчет(ПараметрыОтчета, АдресХранилища) Экспорт
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	Макет = Отчеты.ЖурналУчетаРозничнойПродажиАлкогольнойПродукции.ПолучитьМакет("Макет");

	// Выведем заголовок
	СведенияОбОрганизации = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(ПараметрыОтчета.Организация, ПараметрыОтчета.КонецПериода);
	ТекстОрганизация = БухгалтерскиеОтчетыВызовСервераПовтИсп.ПолучитьТекстОрганизация(ПараметрыОтчета.Организация, ПараметрыОтчета.ВключатьОбособленныеПодразделения);
	МОЛВыбранногоСклада = ОтветственныеЛицаБП.ОтветственноеЛицоНаСкладе(ПараметрыОтчета.Склад, КонецДня(ПараметрыОтчета.КонецПериода));

	// Выведем шапку
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
		
	Если ПараметрыОтчета.ВыводитьЗаголовок Тогда
		
		ВывестиТитульныйЛист(ПараметрыОтчета, ТабличныйДокумент, Макет);
		
	КонецЕсли;
	
	ОбластьМакета = Макет.ПолучитьОбласть("Шапка");

	ТабличныйДокумент.Вывести(ОбластьМакета);
	
	МассивСчетов41 = БухгалтерскийУчетВызовСервераПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.Товары);
	МассивСчетов79 = БухгалтерскийУчетВызовСервераПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.ВнутрихозяйственныеРасчеты);
	
	ВидыСубконто = Новый Массив;
	ВидыСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура);
	Если ЗначениеЗаполнено(ПараметрыОтчета.Склад) Тогда
		ВидыСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Склады);
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Счет41", 		МассивСчетов41);
	Запрос.УстановитьПараметр("Счет79", 		МассивСчетов79);
	Запрос.УстановитьПараметр("ВидыСубконто", 	ВидыСубконто);
	Запрос.УстановитьПараметр("Организация", 	ПараметрыОтчета.Организация);
	Запрос.УстановитьПараметр("ОрганизацияНаименование", ТекстОрганизация);
	Запрос.УстановитьПараметр("ОрганизацияИНН",	СведенияОбОрганизации.Инн);
	Запрос.УстановитьПараметр("Склад", 			ПараметрыОтчета.Склад);
	Запрос.УстановитьПараметр("Подразделение", 	ПараметрыОтчета.Подразделение);
	Запрос.УстановитьПараметр("ДатаНач", 		НачалоДня(ПараметрыОтчета.НачалоПериода));
	Запрос.УстановитьПараметр("ДатаКон", 		Новый Граница(КонецДня(ПараметрыОтчета.КонецПериода), ВидГраницы.Включая));
	Текст =
		"ВЫБРАТЬ
		|	СведенияОбАлкогольнойПродукции.Номенклатура КАК Номенклатура,
		|	СведенияОбАлкогольнойПродукции.Номенклатура.НаименованиеПолное КАК ТоварНаименование,
		|	СведенияОбАлкогольнойПродукции.КоэффПересчетаДал * 10 КАК Емкость,
		|	СведенияОбАлкогольнойПродукции.КоэффПересчетаДал КАК КоэффПересчетаДал,
		|	СведенияОбАлкогольнойПродукции.ВидПродукции.Наименование КАК ВидПродукции,
		|	СведенияОбАлкогольнойПродукции.ВидПродукции.КодВидаПродукции КАК КодВидаПродукции
		|ПОМЕСТИТЬ АлкогольнаяПродукция
		|ИЗ
		|	РегистрСведений.СведенияОбАлкогольнойПродукции КАК СведенияОбАлкогольнойПродукции
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА ХозрасчетныйОбороты.КоличествоОборотДт - ХозрасчетныйОбороты.КоличествоОборотКт > 0
		|			ТОГДА АлкогольнаяПродукция.ВидПродукции + "" ("" + АлкогольнаяПродукция.ТоварНаименование + "")""
		|		ИНАЧЕ """"
		|	КОНЕЦ КАК ВидПродукцииПоступление,
		|	ВЫБОР
		|		КОГДА ХозрасчетныйОбороты.КоличествоОборотДт - ХозрасчетныйОбороты.КоличествоОборотКт > 0
		|			ТОГДА АлкогольнаяПродукция.КодВидаПродукции
		|		ИНАЧЕ """"
		|	КОНЕЦ КАК КодВидаПоступление,
		|	ВЫБОР
		|		КОГДА ХозрасчетныйОбороты.КоличествоОборотДт - ХозрасчетныйОбороты.КоличествоОборотКт > 0
		|				И ХозрасчетныйОбороты.Регистратор ССЫЛКА Документ.ВозвратТоваровОтПокупателя
		|			ТОГДА ВЫРАЗИТЬ(ХозрасчетныйОбороты.Регистратор КАК Документ.ВозвратТоваровОтПокупателя).Контрагент.НаименованиеПолное
		|		КОГДА ХозрасчетныйОбороты.КоличествоОборотДт - ХозрасчетныйОбороты.КоличествоОборотКт > 0
		|				И ХозрасчетныйОбороты.КорСубконто1 ССЫЛКА Справочник.Организации
		|			ТОГДА ВЫРАЗИТЬ(ХозрасчетныйОбороты.КорСубконто1 КАК Справочник.Организации).НаименованиеСокращенное
		|		КОГДА ХозрасчетныйОбороты.КоличествоОборотДт - ХозрасчетныйОбороты.КоличествоОборотКт > 0
		|				И ХозрасчетныйОбороты.КорСубконто1 ССЫЛКА Справочник.Контрагенты
		|			ТОГДА ВЫРАЗИТЬ(ХозрасчетныйОбороты.КорСубконто1 КАК Справочник.Контрагенты).НаименованиеПолное
		|		КОГДА ХозрасчетныйОбороты.КоличествоОборотДт - ХозрасчетныйОбороты.КоличествоОборотКт > 0
		|				И ХозрасчетныйОбороты.ПодразделениеКор.ОбособленноеПодразделение
		|			ТОГДА ХозрасчетныйОбороты.ПодразделениеКор.НаименованиеПолное
		|		КОГДА ХозрасчетныйОбороты.КоличествоОборотДт - ХозрасчетныйОбороты.КоличествоОборотКт > 0
		|			ТОГДА &ОрганизацияНаименование
		|		ИНАЧЕ """"
		|	КОНЕЦ КАК Поставщик,
		|	ВЫБОР
		|		КОГДА ХозрасчетныйОбороты.КоличествоОборотДт - ХозрасчетныйОбороты.КоличествоОборотКт > 0
		|				И ХозрасчетныйОбороты.Регистратор ССЫЛКА Документ.ВозвратТоваровОтПокупателя
		|			ТОГДА ВЫРАЗИТЬ(ХозрасчетныйОбороты.Регистратор КАК Документ.ВозвратТоваровОтПокупателя).Контрагент.ИНН
		|		КОГДА ХозрасчетныйОбороты.КоличествоОборотДт - ХозрасчетныйОбороты.КоличествоОборотКт > 0
		|				И ХозрасчетныйОбороты.КорСубконто1 ССЫЛКА Справочник.Организации
		|			ТОГДА ВЫРАЗИТЬ(ХозрасчетныйОбороты.КорСубконто1 КАК Справочник.Организации).ИНН
		|		КОГДА ХозрасчетныйОбороты.КоличествоОборотДт - ХозрасчетныйОбороты.КоличествоОборотКт > 0
		|				И ХозрасчетныйОбороты.КорСубконто1 ССЫЛКА Справочник.Контрагенты
		|			ТОГДА ВЫРАЗИТЬ(ХозрасчетныйОбороты.КорСубконто1 КАК Справочник.Контрагенты).ИНН
		|		КОГДА ХозрасчетныйОбороты.КоличествоОборотДт - ХозрасчетныйОбороты.КоличествоОборотКт > 0
		|			ТОГДА &ОрганизацияИНН
		|		ИНАЧЕ """"
		|	КОНЕЦ КАК ИНН,
		|	ВЫБОР
		|		КОГДА ХозрасчетныйОбороты.КоличествоОборотДт - ХозрасчетныйОбороты.КоличествоОборотКт > 0
		|			ТОГДА ЕСТЬNULL(РеквизитыДокументов.Номер, """")
		|		ИНАЧЕ """"
		|	КОНЕЦ КАК НомерТТН,
		|	ВЫБОР
		|		КОГДА ХозрасчетныйОбороты.КоличествоОборотДт - ХозрасчетныйОбороты.КоличествоОборотКт > 0
		|			ТОГДА ЕСТЬNULL(РеквизитыДокументов.Дата, """")
		|		ИНАЧЕ """"
		|	КОНЕЦ КАК ДатаТТН,
		|	ВЫБОР
		|		КОГДА ХозрасчетныйОбороты.КоличествоОборотДт - ХозрасчетныйОбороты.КоличествоОборотКт > 0
		|			ТОГДА АлкогольнаяПродукция.Емкость
		|		ИНАЧЕ """"
		|	КОНЕЦ КАК ЕмкостьПоступление,
		|	СУММА(ВЫБОР
		|			КОГДА ХозрасчетныйОбороты.КоличествоОборотДт - ХозрасчетныйОбороты.КоличествоОборотКт > 0
		|				ТОГДА ХозрасчетныйОбороты.КоличествоОборотДт - ХозрасчетныйОбороты.КоличествоОборотКт
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК КоличествоПоступление,
		|	СУММА(ВЫБОР
		|			КОГДА ХозрасчетныйОбороты.КоличествоОборотДт - ХозрасчетныйОбороты.КоличествоОборотКт > 0
		|				ТОГДА (ХозрасчетныйОбороты.КоличествоОборотДт - ХозрасчетныйОбороты.КоличествоОборотКт) * АлкогольнаяПродукция.КоэффПересчетаДал
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК ОбъемПоступление,
		|	ВЫБОР
		|		КОГДА ХозрасчетныйОбороты.КоличествоОборотДт - ХозрасчетныйОбороты.КоличествоОборотКт > 0
		|			ТОГДА ХозрасчетныйОбороты.Регистратор
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ДокументПоступление,
		|	ВЫБОР
		|		КОГДА ХозрасчетныйОбороты.КоличествоОборотКт - ХозрасчетныйОбороты.КоличествоОборотДт > 0
		|			ТОГДА ХозрасчетныйОбороты.Регистратор
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ДокументРасход,
		|	ВЫБОР
		|		КОГДА ХозрасчетныйОбороты.Регистратор ССЫЛКА Документ.ОтчетОРозничныхПродажах
		|			ТОГДА ""Проданная продукция""
		|		КОГДА ХозрасчетныйОбороты.Регистратор ССЫЛКА Документ.ВозвратТоваровПоставщику
		|			ТОГДА ""Продукция, возвращенная поставщику""
		|		КОГДА ХозрасчетныйОбороты.Регистратор ССЫЛКА Документ.СписаниеТоваров
		|			ТОГДА ""Недостачи продукции""
		|		КОГДА ХозрасчетныйОбороты.КоличествоОборотКт - ХозрасчетныйОбороты.КоличествоОборотДт > 0
		|				И (ХозрасчетныйОбороты.КорСчет В (&Счет41)
		|					ИЛИ ХозрасчетныйОбороты.КорСчет В (&Счет79))
		|			ТОГДА ""Продукция, переданная в другое подразделение""
		|		КОГДА ХозрасчетныйОбороты.КоличествоОборотКт - ХозрасчетныйОбороты.КоличествоОборотДт > 0
		|			ТОГДА ""Списанная продукция""
		|		ИНАЧЕ """"
		|	КОНЕЦ КАК СодержаниеРасход,
		|	ВЫБОР
		|		КОГДА ХозрасчетныйОбороты.КоличествоОборотКт - ХозрасчетныйОбороты.КоличествоОборотДт > 0
		|			ТОГДА АлкогольнаяПродукция.ВидПродукции + "" ("" + АлкогольнаяПродукция.ТоварНаименование + "")""
		|		ИНАЧЕ """"
		|	КОНЕЦ КАК ВидПродукцииРасход,
		|	ВЫБОР
		|		КОГДА ХозрасчетныйОбороты.КоличествоОборотКт - ХозрасчетныйОбороты.КоличествоОборотДт > 0
		|			ТОГДА АлкогольнаяПродукция.Емкость
		|		ИНАЧЕ """"
		|	КОНЕЦ КАК ЕмкостьРасход,
		|	СУММА(ВЫБОР
		|			КОГДА ХозрасчетныйОбороты.КоличествоОборотКт - ХозрасчетныйОбороты.КоличествоОборотДт > 0
		|				ТОГДА ХозрасчетныйОбороты.КоличествоОборотКт - ХозрасчетныйОбороты.КоличествоОборотДт
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК КоличествоРасход,
		|	СУММА(ВЫБОР
		|			КОГДА ХозрасчетныйОбороты.КоличествоОборотКт - ХозрасчетныйОбороты.КоличествоОборотДт > 0
		|				ТОГДА (ХозрасчетныйОбороты.КоличествоОборотКт - ХозрасчетныйОбороты.КоличествоОборотДт) * АлкогольнаяПродукция.КоэффПересчетаДал
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК ОбъемРасход,
		|	АлкогольнаяПродукция.Номенклатура,
		|	ХозрасчетныйОбороты.Регистратор,
		|	ХозрасчетныйОбороты.Период
		|ПОМЕСТИТЬ ОборотыДляЖурнала
		|ИЗ
		|	АлкогольнаяПродукция КАК АлкогольнаяПродукция
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.Обороты(&ДатаНач, &ДатаКон, Регистратор, Счет В (&Счет41), &ВидыСубконто, &Условие, , ) КАК ХозрасчетныйОбороты
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК РеквизитыДокументов
		|			ПО ХозрасчетныйОбороты.Регистратор = РеквизитыДокументов.Документ
		|		ПО АлкогольнаяПродукция.Номенклатура = ХозрасчетныйОбороты.Субконто1
		|ГДЕ
		|	НЕ ХозрасчетныйОбороты.КорСчет ЕСТЬ NULL 
		|	И НЕ ХозрасчетныйОбороты.КоличествоОборотДт - ХозрасчетныйОбороты.КоличествоОборотКт = 0
		|
		|СГРУППИРОВАТЬ ПО
		|	ВЫБОР
		|		КОГДА ХозрасчетныйОбороты.КоличествоОборотДт - ХозрасчетныйОбороты.КоличествоОборотКт > 0
		|			ТОГДА АлкогольнаяПродукция.ВидПродукции + "" ("" + АлкогольнаяПродукция.ТоварНаименование + "")""
		|		ИНАЧЕ """"
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ХозрасчетныйОбороты.КоличествоОборотДт - ХозрасчетныйОбороты.КоличествоОборотКт > 0
		|			ТОГДА АлкогольнаяПродукция.КодВидаПродукции
		|		ИНАЧЕ """"
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ХозрасчетныйОбороты.КоличествоОборотДт - ХозрасчетныйОбороты.КоличествоОборотКт > 0
		|			ТОГДА ЕСТЬNULL(РеквизитыДокументов.Номер, """")
		|		ИНАЧЕ """"
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ХозрасчетныйОбороты.КоличествоОборотДт - ХозрасчетныйОбороты.КоличествоОборотКт > 0
		|			ТОГДА ЕСТЬNULL(РеквизитыДокументов.Дата, """")
		|		ИНАЧЕ """"
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ХозрасчетныйОбороты.КоличествоОборотДт - ХозрасчетныйОбороты.КоличествоОборотКт > 0
		|			ТОГДА АлкогольнаяПродукция.Емкость
		|		ИНАЧЕ """"
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ХозрасчетныйОбороты.Регистратор ССЫЛКА Документ.ОтчетОРозничныхПродажах
		|			ТОГДА ""Проданная продукция""
		|		КОГДА ХозрасчетныйОбороты.Регистратор ССЫЛКА Документ.ВозвратТоваровПоставщику
		|			ТОГДА ""Продукция, возвращенная поставщику""
		|		КОГДА ХозрасчетныйОбороты.Регистратор ССЫЛКА Документ.СписаниеТоваров
		|			ТОГДА ""Недостачи продукции""
		|		КОГДА ХозрасчетныйОбороты.КоличествоОборотКт - ХозрасчетныйОбороты.КоличествоОборотДт > 0
		|				И (ХозрасчетныйОбороты.КорСчет В (&Счет41)
		|					ИЛИ ХозрасчетныйОбороты.КорСчет В (&Счет79))
		|			ТОГДА ""Продукция, переданная в другое подразделение""
		|		КОГДА ХозрасчетныйОбороты.КоличествоОборотКт - ХозрасчетныйОбороты.КоличествоОборотДт > 0
		|			ТОГДА ""Списанная продукция""
		|		ИНАЧЕ """"
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ХозрасчетныйОбороты.КоличествоОборотКт - ХозрасчетныйОбороты.КоличествоОборотДт > 0
		|			ТОГДА АлкогольнаяПродукция.ВидПродукции + "" ("" + АлкогольнаяПродукция.ТоварНаименование + "")""
		|		ИНАЧЕ """"
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ХозрасчетныйОбороты.КоличествоОборотКт - ХозрасчетныйОбороты.КоличествоОборотДт > 0
		|			ТОГДА АлкогольнаяПродукция.Емкость
		|		ИНАЧЕ """"
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ХозрасчетныйОбороты.КоличествоОборотДт - ХозрасчетныйОбороты.КоличествоОборотКт > 0
		|			ТОГДА ХозрасчетныйОбороты.Регистратор
		|		ИНАЧЕ 0
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ХозрасчетныйОбороты.КоличествоОборотКт - ХозрасчетныйОбороты.КоличествоОборотДт > 0
		|			ТОГДА ХозрасчетныйОбороты.Регистратор
		|		ИНАЧЕ 0
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ХозрасчетныйОбороты.КоличествоОборотДт - ХозрасчетныйОбороты.КоличествоОборотКт > 0
		|				И ХозрасчетныйОбороты.Регистратор ССЫЛКА Документ.ВозвратТоваровОтПокупателя
		|			ТОГДА ВЫРАЗИТЬ(ХозрасчетныйОбороты.Регистратор КАК Документ.ВозвратТоваровОтПокупателя).Контрагент.НаименованиеПолное
		|		КОГДА ХозрасчетныйОбороты.КоличествоОборотДт - ХозрасчетныйОбороты.КоличествоОборотКт > 0
		|				И ХозрасчетныйОбороты.КорСубконто1 ССЫЛКА Справочник.Организации
		|			ТОГДА ВЫРАЗИТЬ(ХозрасчетныйОбороты.КорСубконто1 КАК Справочник.Организации).НаименованиеСокращенное
		|		КОГДА ХозрасчетныйОбороты.КоличествоОборотДт - ХозрасчетныйОбороты.КоличествоОборотКт > 0
		|				И ХозрасчетныйОбороты.КорСубконто1 ССЫЛКА Справочник.Контрагенты
		|			ТОГДА ВЫРАЗИТЬ(ХозрасчетныйОбороты.КорСубконто1 КАК Справочник.Контрагенты).НаименованиеПолное
		|		КОГДА ХозрасчетныйОбороты.КоличествоОборотДт - ХозрасчетныйОбороты.КоличествоОборотКт > 0
		|				И ХозрасчетныйОбороты.ПодразделениеКор.ОбособленноеПодразделение
		|			ТОГДА ХозрасчетныйОбороты.ПодразделениеКор.НаименованиеПолное
		|		КОГДА ХозрасчетныйОбороты.КоличествоОборотДт - ХозрасчетныйОбороты.КоличествоОборотКт > 0
		|			ТОГДА &ОрганизацияНаименование
		|		ИНАЧЕ """"
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ХозрасчетныйОбороты.КоличествоОборотДт - ХозрасчетныйОбороты.КоличествоОборотКт > 0
		|				И ХозрасчетныйОбороты.Регистратор ССЫЛКА Документ.ВозвратТоваровОтПокупателя
		|			ТОГДА ВЫРАЗИТЬ(ХозрасчетныйОбороты.Регистратор КАК Документ.ВозвратТоваровОтПокупателя).Контрагент.ИНН
		|		КОГДА ХозрасчетныйОбороты.КоличествоОборотДт - ХозрасчетныйОбороты.КоличествоОборотКт > 0
		|				И ХозрасчетныйОбороты.КорСубконто1 ССЫЛКА Справочник.Организации
		|			ТОГДА ВЫРАЗИТЬ(ХозрасчетныйОбороты.КорСубконто1 КАК Справочник.Организации).ИНН
		|		КОГДА ХозрасчетныйОбороты.КоличествоОборотДт - ХозрасчетныйОбороты.КоличествоОборотКт > 0
		|				И ХозрасчетныйОбороты.КорСубконто1 ССЫЛКА Справочник.Контрагенты
		|			ТОГДА ВЫРАЗИТЬ(ХозрасчетныйОбороты.КорСубконто1 КАК Справочник.Контрагенты).ИНН
		|		КОГДА ХозрасчетныйОбороты.КоличествоОборотДт - ХозрасчетныйОбороты.КоличествоОборотКт > 0
		|			ТОГДА &ОрганизацияИНН
		|		ИНАЧЕ """"
		|	КОНЕЦ,
		|	АлкогольнаяПродукция.Номенклатура,
		|	ХозрасчетныйОбороты.Регистратор,
		|	ХозрасчетныйОбороты.Период
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОборотыДляЖурнала.Номенклатура,
		|	ОборотыДляЖурнала.Регистратор,
		|	СУММА(ОборотыДляЖурнала.КоличествоПоступление) КАК КоличествоПоступление,
		|	СУММА(ОборотыДляЖурнала.КоличествоРасход) КАК КоличествоРасход
		|ПОМЕСТИТЬ БезВстречныхОборотов
		|ИЗ
		|	ОборотыДляЖурнала КАК ОборотыДляЖурнала
		|
		|СГРУППИРОВАТЬ ПО
		|	ОборотыДляЖурнала.Номенклатура,
		|	ОборотыДляЖурнала.Регистратор
		|
		|ИМЕЮЩИЕ
		|	СУММА(ОборотыДляЖурнала.КоличествоПоступление) - СУММА(ОборотыДляЖурнала.КоличествоРасход) <> 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОборотыДляЖурнала.ВидПродукцииПоступление,
		|	ОборотыДляЖурнала.КодВидаПоступление,
		|	ОборотыДляЖурнала.Поставщик,
		|	ОборотыДляЖурнала.ИНН,
		|	ОборотыДляЖурнала.НомерТТН,
		|	ОборотыДляЖурнала.ДатаТТН,
		|	ОборотыДляЖурнала.ЕмкостьПоступление,
		|	ОборотыДляЖурнала.КоличествоПоступление,
		|	ОборотыДляЖурнала.ОбъемПоступление,
		|	ОборотыДляЖурнала.ДокументПоступление,
		|	ОборотыДляЖурнала.ДокументРасход,
		|	ОборотыДляЖурнала.СодержаниеРасход,
		|	ОборотыДляЖурнала.ВидПродукцииРасход,
		|	ОборотыДляЖурнала.ЕмкостьРасход,
		|	ОборотыДляЖурнала.КоличествоРасход,
		|	ОборотыДляЖурнала.ОбъемРасход,
		|	ОборотыДляЖурнала.Период КАК Период
		|ИЗ
		|	ОборотыДляЖурнала КАК ОборотыДляЖурнала
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ БезВстречныхОборотов КАК БезВстречныхОборотов
		|		ПО ОборотыДляЖурнала.Номенклатура = БезВстречныхОборотов.Номенклатура
		|			И ОборотыДляЖурнала.Регистратор = БезВстречныхОборотов.Регистратор
		|
		|УПОРЯДОЧИТЬ ПО
		|	Период";
	
		
	СтрокаУсловия = "Организация = &Организация";
	
	Если ЗначениеЗаполнено(ПараметрыОтчета.Склад) Тогда
		СтрокаУсловия = СтрокаУсловия + " И Субконто2 = &Склад";
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПараметрыОтчета.Подразделение) Тогда
		СтрокаУсловия = СтрокаУсловия + " И Подразделение = &Подразделение";
	КонецЕсли;
	
	Текст = СтрЗаменить(Текст, "&Условие", СтрокаУсловия);
	
	Запрос.Текст = Текст;
	Результат = Запрос.Выполнить().Выбрать();
	
	НомерСтроки 			= 1;
	ОбъемПоступление		= 0;
	ОбъемРасход 			= 0;
	КоличествоПоступление	= 0;
	КоличествоРасход		= 0;
	
	ОбластьМакета = Макет.ПолучитьОбласть("Строка");

	Пока Результат.Следующий() Цикл
		
		ОбластьМакета.Параметры.НомерСтроки = НомерСтроки;
		ОбластьМакета.Параметры.Заполнить(Результат);
		
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
		ОбъемПоступление 		= ОбъемПоступление + Результат.ОбъемПоступление;
		ОбъемРасход 			= ОбъемРасход + Результат.ОбъемРасход;
		КоличествоПоступление 	= КоличествоПоступление + Результат.КоличествоПоступление;
		КоличествоРасход 		= КоличествоРасход + Результат.КоличествоРасход;

		НомерСтроки 			= НомерСтроки + 1;
		
	КонецЦикла;
	
	ОбластьМакета = Макет.ПолучитьОбласть("Итоги");
	
	ОбластьМакета.Параметры.ОбъемПоступление 		= ОбъемПоступление;
	ОбластьМакета.Параметры.ОбъемРасход 			= ОбъемРасход;
	ОбластьМакета.Параметры.КоличествоПоступление 	= КоличествоПоступление;
	ОбластьМакета.Параметры.КоличествоРасход 		= КоличествоРасход;
	
	ТабличныйДокумент.Вывести(ОбластьМакета);
	
	ТабличныйДокумент.ПовторятьПриПечатиСтроки = ТабличныйДокумент.Область("СтрокиДляПовтора");
	
	Если ПараметрыОтчета.ВыводитьПодвал Тогда
		
		БухгалтерскиеОтчетыВызовСервера.ВывестиПодвалОтчета(ПараметрыОтчета, Неопределено, ТабличныйДокумент);
		
	КонецЕсли;
	
	ПоместитьВоВременноеХранилище(ТабличныйДокумент, АдресХранилища);
	
КонецПроцедуры

Процедура ВывестиТитульныйЛист(ПараметрыОтчета, Результат, Макет)
	
	ОбластьОрганизация      = Макет.ПолучитьОбласть("ТитульныйЛист");
	
	ТекстОрганизация = БухгалтерскиеОтчетыВызовСервераПовтИсп.ПолучитьТекстОрганизация(ПараметрыОтчета.Организация, ПараметрыОтчета.ВключатьОбособленныеПодразделения);
	ОбластьОрганизация.Параметры.НазваниеОрганизации = ТекстОрганизация;
	ОбластьОрганизация.Параметры.ПредставлениеПериода = БухгалтерскиеОтчетыКлиентСервер.ПолучитьПредставлениеПериода(ПараметрыОтчета.НачалоПериода, ПараметрыОтчета.КонецПериода);
	
	// Организация
	Если ЗначениеЗаполнено(ПараметрыОтчета.Склад) Тогда
		ОбластьОрганизация.Параметры.Подразделение = ПараметрыОтчета.Склад;
	ИначеЕсли ЗначениеЗаполнено(ПараметрыОтчета.Подразделение) Тогда
		ОбластьОрганизация.Параметры.Подразделение = ПараметрыОтчета.Подразделение;
	КонецЕсли;
		
	Результат.Вывести(ОбластьОрганизация);
	Результат.ВывестиГоризонтальныйРазделительСтраниц();
	
	Результат.Область("R1:R" + Результат.ВысотаТаблицы).Имя = "ТитульныйЛист";
	
КонецПроцедуры


#КонецЕсли