&НаКлиенте
Перем ПараметрыОбработчикаОжидания;

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ДанныеРасшифровки   = ПоместитьВоВременноеХранилище(ДанныеРасшифровки, УникальныйИдентификатор);
	КэшПараметровОтчета = ПоместитьВоВременноеХранилище(Новый Структура,   УникальныйИдентификатор);
	
	Если НЕ ЗначениеЗаполнено(Отчет.НачалоПериода) Тогда
		Отчет.НачалоПериода = НачалоГода(ОбщегоНазначенияБП.ПолучитьРабочуюДату());
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Отчет.КонецПериода) Тогда
		Отчет.КонецПериода  = КонецГода(ОбщегоНазначенияБП.ПолучитьРабочуюДату());
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Отчет.Организация) Тогда
		Отчет.Организация   = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновнаяОрганизация");
	КонецЕсли;
	
	ВестиУчетПоПодразделениям = ПолучитьФункциональнуюОпцию("ВестиУчетПоПодразделениям");
	
	НастройкиКлиента = ХранилищеСистемныхНастроек.Загрузить("Общее/НастройкиКлиентскогоПриложения");
	Если ТипЗнч(НастройкиКлиента) = Тип("НастройкиКлиентскогоПриложения") 
		И НастройкиКлиента.ВариантИнтерфейсаКлиентскогоПриложения = ВариантИнтерфейсаКлиентскогоПриложения.Версия8_2 Тогда
		СуффиксСхемы = "82";
	Иначе
		СуффиксСхемы = "";
	КонецЕсли;
	
	УстановитьКартуНаСервере();
	
	ОбщегоНазначенияБПВызовСервера.ЗаполнитьСписокОрганизаций(Элементы.ПолеОрганизация, СоответствиеОрганизаций);
	
	ЗавершитьРаботуСОтчетом = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	БухгалтерскиеОтчетыКлиент.ПриОткрытии(ЭтаФорма, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	Если НомерТекущейСхемы <> -1 И НЕ ЗавершитьРаботуСОтчетом Тогда	
		
		Отказ = Истина;
		Оповещение = Новый ОписаниеОповещения("ВопросПередЗакрытиемЗавершение", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, НСтр("ru = 'Завершить работу с отчетом?'"), РежимДиалогаВопрос.ДаНет);
	
	КонецЕсли;
	
	БухгалтерскиеОтчетыКлиент.ПередЗакрытием(ЭтаФорма, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии()
	
	Если ЗначениеЗаполнено(ИдентификаторЗадания) Тогда
		ОтменитьВыполнениеЗадания();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриСохраненииПользовательскихНастроекНаСервере(Настройки)
	
	БухгалтерскиеОтчетыВызовСервера.ПриСохраненииПользовательскихНастроекНаСервере(ЭтаФорма, Настройки, Истина);
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеПользовательскихНастроекНаСервере(Настройки)
	
	БухгалтерскиеОтчетыВызовСервера.ПриЗагрузкеПользовательскихНастроекНаСервере(ЭтаФорма, Настройки);
	
	ОбновитьТекстЗаголовка(ЭтаФорма);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеАктуальность");
	УстановитьКартуНаСервере();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура НачалоПериодаПриИзменении(Элемент)
	ОбновитьТекстЗаголовка(ЭтаФорма);
	Если Не ЗначениеЗаполнено(ИдентификаторЗадания) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеАктуальность");
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура КонецПериодаПриИзменении(Элемент)
	ОбновитьТекстЗаголовка(ЭтаФорма);
	Если Не ЗначениеЗаполнено(ИдентификаторЗадания) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеАктуальность");
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПолеОрганизацияПриИзменении(Элемент)
	
	ОбщегоНазначенияБПКлиент.ПолеОрганизацияПриИзменении(Элемент, ПолеОрганизация,
		Отчет.Организация, Отчет.ВключатьОбособленныеПодразделения);
	
	ОбновитьТекстЗаголовка(ЭтаФорма);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеАктуальность");
	УстановитьКартуНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПолеОрганизацияОткрытие(Элемент, СтандартнаяОбработка)
	
	ОбщегоНазначенияБПКлиент.ПолеОрганизацияОткрытие(Элемент, СтандартнаяОбработка,
		ПолеОрганизация, СоответствиеОрганизаций);
		
КонецПроцедуры
	
&НаКлиенте
Процедура ПолеОрганизацияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ОбщегоНазначенияБПКлиент.ПолеОрганизацияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка, 
		СоответствиеОрганизаций, Отчет.Организация, Отчет.ВключатьОбособленныеПодразделения);
	
КонецПроцедуры

/////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ПОЛЯ ТАБЛИЧНОГО ДОКУМЕНТА

&НаКлиенте
Процедура РезультатПриАктивизацииОбласти(Элемент)
	
	Если ТипЗнч(Результат.ВыделенныеОбласти) = Тип("ВыделенныеОбластиТабличногоДокумента") Тогда
		ИнтервалОжидания = ?(ПолучитьСкоростьКлиентскогоСоединения() = СкоростьКлиентскогоСоединения.Низкая, 1, 0.2);
		ПодключитьОбработчикОжидания("Подключаемый_РезультатПриАктивизацииОбластиПодключаемый", ИнтервалОжидания, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РезультатСхемыВыбор(Элемент)
	
	Если Элемент.ТекущийЭлемент = Неопределено Тогда 		
		Возврат;		
	КонецЕсли;
	
	Если ПараметраОтчетаНеКорректны() Тогда
		Возврат;
	КонецЕсли;
	
	ОтключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеЗадания");
	
	// собираем одинаковые элементы расшифровки
	Отчет.ИмяРасшифровки = СтрЗаменить(Элемент.ТекущийЭлемент.Имя, "_1", "");
	УправляющаяСтруктура = Новый Структура("Главная,ТолькоОшибки", Ложь,Ложь);

	Отказ = Ложь;
	РезультатВыполнения = СформироватьСхемуНаСервере(Отчет.ИмяРасшифровки, УправляющаяСтруктура, Отказ);
	Если РезультатВыполнения = Неопределено Тогда
		Возврат;
	ИначеЕсли РезультатВыполнения.ЗаданиеВыполнено Тогда
		Если НЕ Отказ Тогда
			ОбновитьСтраницы(УправляющаяСтруктура);
		КонецЕсли;
	Иначе
		ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
		ПодключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеЗадания", 1, Истина);
		
		Если УправляющаяСтруктура.Главная ИЛИ УправляющаяСтруктура.ЭтоСхема Тогда
			Элементы.ГруппаРезультатСхема.ТекущаяСтраница = Элементы.СтраницаИндикаторВыполнения;
			ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.ИндикаторВыполнения, "ФормированиеОтчета");
		Иначе
			ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "ФормированиеОтчета");
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РезультатОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
	
	ОтключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеЗадания");
	
	СтандартнаяОбработка = Ложь;
	УправляющаяСтруктура = Новый Структура("Главная,ТолькоОшибки", Ложь,Ложь);
	РезультатВыполнения  = ОбработатьРасшифровкунаСервере(Расшифровка, УправляющаяСтруктура);
	
	Если РезультатВыполнения.ЗаданиеВыполнено Тогда
		Если УправляющаяСтруктура.Свойство("ЭтоСхема") Тогда 		
			ОбновитьСтраницы(УправляющаяСтруктура);		
		ИначеЕсли УправляющаяСтруктура.Свойство("ОткрытьОбъект") Тогда		
			ПоказатьЗначение( , УправляющаяСтруктура.ОткрытьОбъект);		
		ИначеЕсли ТипЗнч(Расшифровка) = Тип("ИдентификаторРасшифровкиКомпоновкиДанных") Тогда
			БухгалтерскиеОтчетыКлиент.ОбработкаРасшифровкиСтандартногоОтчета(ЭтаФорма, Элемент, Расшифровка, СтандартнаяОбработка);
		КонецЕсли;
	Иначе
		ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
		ПодключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеЗадания", 1, Истина);
		
		Если УправляющаяСтруктура.Главная ИЛИ УправляющаяСтруктура.Свойство("ЭтоСхема") Тогда
			Элементы.ГруппаРезультатСхема.ТекущаяСтраница = Элементы.СтраницаИндикаторВыполнения;
			ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.ИндикаторВыполнения, "ФормированиеОтчета");
		Иначе
			ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "ФормированиеОтчета");
		КонецЕсли;
	КонецЕсли;
		
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ВыбратьПериод(Команда)
	
	ПараметрыВыбора = Новый Структура("НачалоПериода,КонецПериода", Отчет.НачалоПериода, Отчет.КонецПериода);
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыбратьПериодЗавершение", ЭтотОбъект);
	ОткрытьФорму("ОбщаяФорма.ВыборСтандартногоПериода", ПараметрыВыбора, Элементы.ВыбратьПериод, , , , ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПериодЗавершение(РезультатВыбора, ДопПараметры) Экспорт
	
	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ЗаполнитьЗначенияСвойств(Отчет, РезультатВыбора, "НачалоПериода,КонецПериода");
	
	ОбновитьТекстЗаголовка(ЭтаФорма); 
	Если Не ЗначениеЗаполнено(ИдентификаторЗадания) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеАктуальность");
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура Назад(Команда)
	
	УправляющаяСтруктура = Новый Структура("Главная,ТолькоОшибки", Истина,Ложь);
	ОбработатьНазадНаСервере(УправляющаяСтруктура);
	ОбновитьСтраницы(УправляющаяСтруктура);
	
КонецПроцедуры

&НаКлиенте
Процедура ПечатьГрафическойСхемы(Команда)
	
	РезультатСхемы.Напечатать();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаСтруктураНалоговойБазы(Команда)
	
	Если ПараметраОтчетаНеКорректны() Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьКартуНаСервере();
	УправляющаяСтруктура = Новый Структура("Главная,ТолькоОшибки", Истина,Ложь);
	ОбновитьСтраницы(УправляющаяСтруктура);
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьРасшифровку(УправляющаяСтруктура)
	
	ОчиститьСообщения();
	
	ОтключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеЗадания");
	
	Отказ = Ложь;
	
	РезультатВыполнения = ОбновитьТаблицуНаСервере(УправляющаяСтруктура, Отказ);
	Если РезультатВыполнения = Неопределено Тогда
		Возврат;
	ИначеЕсли РезультатВыполнения.ЗаданиеВыполнено Тогда
		Если НЕ Отказ Тогда
			ОбновитьСтраницы(УправляющаяСтруктура);	
		КонецЕсли;
	Иначе
		ОбновитьСтраницыНаСервере(УправляющаяСтруктура);
		
		ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
		ПодключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеЗадания", 1, Истина);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "ФормированиеОтчета");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьВсюРасшифровку(Команда)
	
	УправляющаяСтруктура = Новый Структура("Главная,ТолькоОшибки", Ложь, Ложь);
	СформироватьРасшифровку(УправляющаяСтруктура);
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьТолькоОшибки(Команда)
	
	УправляющаяСтруктура = Новый Структура("Главная,ТолькоОшибки", Ложь, Истина);
	СформироватьРасшифровку(УправляющаяСтруктура);

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаКлиенте
Процедура Подключаемый_ПроверитьВыполнениеЗадания()
	
	Попытка
		Если ЗаданиеВыполнено(ИдентификаторЗадания) Тогда
			ЗагрузитьПодготовленныеДанныеНаСервере();
		Иначе
			ДлительныеОперацииКлиент.ОбновитьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
			ПодключитьОбработчикОжидания(
				"Подключаемый_ПроверитьВыполнениеЗадания", 
				ПараметрыОбработчикаОжидания.ТекущийИнтервал, 
				Истина);
		КонецЕсли;
	Исключение
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.ИндикаторВыполнения, "НеИспользовать");
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеИспользовать");
		
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗаданиеВыполнено(ИдентификаторЗадания)
	
	Возврат ДлительныеОперации.ЗаданиеВыполнено(ИдентификаторЗадания);
	
КонецФункции

&НаСервере
Процедура ОтменитьВыполнениеЗадания()
	
	ДлительныеОперации.ОтменитьВыполнениеЗадания(ИдентификаторЗадания);
	
КонецПроцедуры

&НаСервере
Функция СформироватьОтчетНаСервере(ПараметрыОтчета, Расшифровка = Ложь)
	
	ДлительныеОперации.ОтменитьВыполнениеЗадания(ИдентификаторЗадания);
	ИдентификаторЗадания = Неопределено;
	
	Если ОбщегоНазначения.ИнформационнаяБазаФайловая() Тогда
		АдресХранилища = ПоместитьВоВременноеХранилище(Неопределено, УникальныйИдентификатор);
		Если Расшифровка Тогда 
			Отчеты.АнализСостоянияНалоговогоУчетаПоНалогуНаПрибыль.СформироватьРасшифровку(ПараметрыОтчета, АдресХранилища);
		Иначе
			Отчеты.АнализСостоянияНалоговогоУчетаПоНалогуНаПрибыль.СформироватьОтчет(ПараметрыОтчета, АдресХранилища);
		КонецЕсли;
		РезультатВыполнения = Новый Структура("ЗаданиеВыполнено", Истина);
	Иначе
		Если Расшифровка Тогда 
			ИмяМетода = "СформироватьРасшифровку";
		Иначе
			ИмяМетода = "СформироватьОтчет";
		КонецЕсли;
		
		РезультатВыполнения = ДлительныеОперации.ЗапуститьВыполнениеВФоне(
			УникальныйИдентификатор, 
			"Отчеты.АнализСостоянияНалоговогоУчетаПоНалогуНаПрибыль." + ИмяМетода, 
			ПараметрыОтчета, 
			БухгалтерскиеОтчетыКлиентСервер.ПолучитьНаименованиеЗаданияВыполненияОтчета(ЭтаФорма));
			
		АдресХранилища       = РезультатВыполнения.АдресХранилища;
		ИдентификаторЗадания = РезультатВыполнения.ИдентификаторЗадания;		
	КонецЕсли;
	
	Если РезультатВыполнения.ЗаданиеВыполнено Тогда
		ЗагрузитьПодготовленныеДанныеНаСервере();
	КонецЕсли;
	
	Возврат РезультатВыполнения;
	
КонецФункции

&НаСервере
Процедура ЗагрузитьПодготовленныеДанныеНаСервере()

	РезультатВыполнения = ПолучитьИзВременногоХранилища(АдресХранилища);
	
	Если ТипЗнч(РезультатВыполнения) = Тип("Структура") Тогда
		Если РезультатВыполнения.Свойство("РезультатСхемы") Тогда
			РезультатСхемы      = РезультатВыполнения.РезультатСхемы;
			КэшПараметровОтчета = ПоместитьВоВременноеХранилище(РезультатВыполнения.КэшПараметровОтчета, КэшПараметровОтчета);
			Результат           = РезультатВыполнения.Результат;			
		Иначе
			Результат = РезультатВыполнения.Результат;	
			
			Если РезультатВыполнения.Свойство("ДанныеРасшифровки") Тогда
				ДанныеРасшифровки = РезультатВыполнения.ДанныеРасшифровки;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ИдентификаторЗадания = Неопределено;
	
	Если Элементы.ГруппаРезультатСхема.ТекущаяСтраница = Элементы.СтраницаИндикаторВыполнения Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.ИндикаторВыполнения, "НеИспользовать");
		Элементы.ГруппаРезультатСхема.ТекущаяСтраница = Элементы.СтраницаРезультатСхема;
	Иначе
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеИспользовать");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПодготовитьПараметрыОтчета(ТолькоОшибки)
	
	ПараметрыОтчета = Новый Структура;
	ПараметрыОтчета.Вставить("Организация",                       Отчет.Организация);
	ПараметрыОтчета.Вставить("НачалоПериода",                     Отчет.НачалоПериода);
	ПараметрыОтчета.Вставить("КонецПериода",                      Отчет.КонецПериода);
	ПараметрыОтчета.Вставить("НачалоГода",                        НачалоГода(Отчет.НачалоПериода));
	ПараметрыОтчета.Вставить("ВключатьОбособленныеПодразделения", Отчет.ВключатьОбособленныеПодразделения);
    ПараметрыОтчета.Вставить("ОрганизацияРасшифровки",            Отчет.ОрганизацияРасшифровки);
	ПараметрыОтчета.Вставить("РазбитьПоЛистам",                   Отчет.ИмяРасшифровки);
	ПараметрыОтчета.Вставить("ИдентификаторОтчета",               БухгалтерскиеОтчетыКлиентСервер.ПолучитьИдентификаторОбъекта(ЭтаФорма));
	ПараметрыОтчета.Вставить("ДанныеРасшифровки",                 ДанныеРасшифровки);
	ПараметрыОтчета.Вставить("ИмяСхемы",                          ИмяСхемы);
	ПараметрыОтчета.Вставить("ТолькоОшибки",                      ТолькоОшибки);
	ПараметрыОтчета.Вставить("СуффиксСхемы",                      СуффиксСхемы);
		
	Возврат ПараметрыОтчета;
	
КонецФункции

&НаСервере
Процедура УстановитьКартуНаСервере()
	
	ЭтоЕНВД = Ложь;
	НомерТекущейСхемы = - 1;
	
	Если ЗначениеЗаполнено(Отчет.Организация) И ЗначениеЗаполнено(Отчет.КонецПериода) Тогда
		Если УчетнаяПолитика.Существует(Отчет.Организация, Отчет.КонецПериода, Истина) Тогда
			ЭтоЕНВД = УчетнаяПолитика.ПлательщикЕНВД(Отчет.Организация, Отчет.КонецПериода);
		КонецЕсли;
	КонецЕсли;
	
	ИмяСхемы = "Карта" + ?(ЭтоЕНВД, "ЕНВД", "") + СуффиксСхемы;
    РезультатСхемы = Отчеты.АнализСостоянияНалоговогоУчетаПоНалогуНаПрибыль.ПолучитьМакет(ИмяСхемы);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.ИндикаторВыполнения, "НеИспользовать");
	ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеИспользовать");
	
	// Очистим историю
	КэшПараметровОтчета = ПоместитьВоВременноеХранилище(Новый Структура, КэшПараметровОтчета);
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьНазадНаСервере(УправляющаяСтруктура)
	
	ЭтоРасшифровка = НомерТекущейСхемы = 100; 
	
	КэшПараметровОтчетаСтруктура = ПолучитьИзВременногоХранилища(КэшПараметровОтчета);
	
	ЧислоСтраниц = КэшПараметровОтчетаСтруктура.ИсторияОткрытий.Количество() - ?(ЭтоРасшифровка,1,2);
	НомерСхемы   = ?(ЧислоСтраниц < 0, 100, КэшПараметровОтчетаСтруктура.ИсторияОткрытий.Получить(ЧислоСтраниц).Значение);
	
	Если КэшПараметровОтчетаСтруктура.КэшМакетов.Свойство("Макет_" + НомерСхемы) Тогда		
		ПараметрыМакета = КэшПараметровОтчетаСтруктура.КэшМакетов["Макет_" + НомерСхемы];
		
		УправляющаяСтруктура.Вставить("ЭтоСхема", 				 ПараметрыМакета.ЭтоСхема);
		УправляющаяСтруктура.Вставить("ЭтоОтчетПоДокументам", 	 Ложь);
		УправляющаяСтруктура.Вставить("ДоступностьПоДокументам", Ложь);
		
		Если ПараметрыМакета.ЭтоСхема Тогда			
			РезультатСхемы = Отчеты.АнализСостоянияНалоговогоУчетаПоНалогуНаПрибыль.ПолучитьМакет(ПараметрыМакета.Макет);
			ЗаполнитьСхемуИзКэшаНаСервере(РезультатСхемы.ЭлементыГрафическойСхемы, ПараметрыМакета);
			Результат.Очистить();
		Иначе			
			Результат.Очистить();
			Результат.Вывести(ПараметрыМакета.Макет);			
		КонецЕсли;
		
		НомерТекущейСхемы  = НомерСхемы;
		УправляющаяСтруктура.Главная = Ложь;
		
		Если НЕ ЭтоРасшифровка Тогда			
			КэшПараметровОтчетаСтруктура.ИсторияОткрытий.Удалить(ЧислоСтраниц + 1);			
		КонецЕсли;		
	Иначе		
		УстановитьКартуНаСервере();		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСхемуИзКэшаНаСервере(ЭлементыСхемы, ПараметрыМакета)
	
	Для Каждого Параметр из ПараметрыМакета.ОписаниеЗначений Цикл	
		ЭлементыСхемы[Параметр.Ключ].Наименование = Параметр.Значение;		
	КонецЦикла;
	
	Для Каждого Параметр из ПараметрыМакета.НастройкаРамок Цикл		
		ЭлементыСхемы[Параметр.Ключ].Фигура    = ФигурыГрафическойСхемы.Блок;
		ЭлементыСхемы[Параметр.Ключ].ЦветРамки = ?(Параметр.Значение = 1, WebЦвета.Красный,
			WebЦвета.Зеленый);		
	КонецЦикла;
	
	Если ПараметрыМакета.НастройкаРамок.Количество() > 0 Тогда
		Отчеты.АнализСостоянияНалоговогоУчетаПоНалогуНаПрибыль.ВывестиЛегенду(ЭлементыСхемы);		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьТекстЗаголовка(Форма)
	
	Отчет = Форма.Отчет;
	
	ЗаголовокОтчета = "Анализ состояния налогового учета по налогу на прибыль" + БухгалтерскиеОтчетыКлиентСервер.ПолучитьПредставлениеПериода(
		Отчет.НачалоПериода, Отчет.КонецПериода);
	
	Если ЗначениеЗаполнено(Отчет.Организация) И Форма.ИспользуетсяНесколькоОрганизаций Тогда
		ЗаголовокОтчета = ЗаголовокОтчета + " " + БухгалтерскиеОтчетыВызовСервераПовтИсп.ПолучитьТекстОрганизация(
			Отчет.Организация, Отчет.ВключатьОбособленныеПодразделения);
	КонецЕсли;
	
	Форма.Заголовок = ЗаголовокОтчета;
	
КонецПроцедуры

&НаСервере
Функция СформироватьСхемуНаСервере(Ссылка, УправляющаяСтруктура, Отказ)
	
	Если НЕ ПроверитьЗаполнение() Тогда 
		Возврат Неопределено;
	КонецЕсли;
	
	РезультатВыполнения = Новый Структура("ЗаданиеВыполнено", Истина);
 
	УправляющаяСтруктура.Вставить("ЭтоСхема", 				 Истина);
	УправляющаяСтруктура.Вставить("ЭтоОтчетПоДокументам", 	 Ложь);
	УправляющаяСтруктура.Вставить("ДоступностьПоДокументам", Ложь);
	
	ВременныйНомерТекущейСхемы = НомерТекущейСхемы;
	
	КэшПараметровОтчетаСтруктура = ПолучитьИзВременногоХранилища(КэшПараметровОтчета);
	
	ПараметрыОтчета = ПодготовитьПараметрыОтчета(УправляющаяСтруктура.ТолькоОшибки);
	
	Если ВременныйНомерТекущейСхемы = -1 Тогда		
		Если Найти(Ссылка, "Схема") = 0 Тогда
			Возврат РезультатВыполнения;			
		КонецЕсли;
		
		ВременныйНомерТекущейСхемы = Число(СтрЗаменить(Ссылка, "Схема", "")) + 
			?(Отчет.ВключатьОбособленныеПодразделения И НЕ ВременныйНомерТекущейСхемы > 20, 20, 0);
			
		УправляющаяСтруктура.ЭтоСхема = НЕ ВременныйНомерТекущейСхемы > 20;
		НомерТекущейСхемы = ВременныйНомерТекущейСхемы;
		
		ПараметрыОтчета.Вставить("НомерТекущейСхемы",   НомерТекущейСхемы);
		ПараметрыОтчета.Вставить("КэшПараметровОтчета", КэшПараметровОтчетаСтруктура);
		ПараметрыОтчета.Вставить("Расшифровка",         ДанныеРасшифровки);
		
		РезультатВыполнения = СформироватьОтчетНаСервере(ПараметрыОтчета);
		
		ОбновитьСтраницыНаСервере(УправляющаяСтруктура);
				
		Возврат РезультатВыполнения;
		
	ИначеЕсли НЕ КэшПараметровОтчетаСтруктура.Свойство("КэшМакетов") Тогда
		
		УправляющаяСтруктура.Главная = Истина;
		ТекстСообщения = НСтр("ru = 'Отчет обновлен!'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,, Отказ);
		УстановитьКартуНаСервере();
		
		ОбновитьСтраницыНаСервере(УправляющаяСтруктура);
				
		Возврат РезультатВыполнения;
		
	ИначеЕсли КэшПараметровОтчетаСтруктура.КэшМакетов.Свойство("Макет_" + НомерТекущейСхемы) Тогда
			
		ПараметрыМакета = КэшПараметровОтчетаСтруктура.КэшМакетов["Макет_" + НомерТекущейСхемы];
		
		Если ПараметрыМакета.РасшифровкаМакета.Свойство(Ссылка) Тогда

			СоответствиеРасшифровки = ПараметрыМакета.РасшифровкаМакета[Ссылка];
			Если СоответствиеРасшифровки["ВидРасшифровки"] = "Схема" Тогда
				
				ВременныйНомерТекущейСхемы    = СоответствиеРасшифровки["НомерТекущейСхемы"];
				УправляющаяСтруктура.ЭтоСхема = НЕ ВременныйНомерТекущейСхемы > 20;
				НомерТекущейСхемы             = ВременныйНомерТекущейСхемы;
				
				ПараметрыОтчета.Вставить("НомерТекущейСхемы",   НомерТекущейСхемы);
				ПараметрыОтчета.Вставить("КэшПараметровОтчета", КэшПараметровОтчетаСтруктура);
		        ПараметрыОтчета.Вставить("Расшифровка",         СоответствиеРасшифровки);
				
				РезультатВыполнения = СформироватьОтчетНаСервере(ПараметрыОтчета);
				
				ОбновитьСтраницыНаСервере(УправляющаяСтруктура);
				
				Возврат РезультатВыполнения;
			Иначе
				ПоДокументам      = Ложь;
				НомерТекущейСхемы = 100;
				
				УправляющаяСтруктура.ЭтоСхема             = Ложь;
				УправляющаяСтруктура.ЭтоОтчетПоДокументам = Истина;
				
				СхемаКомпоновкиДанных = Отчеты.АнализСостоянияНалоговогоУчетаПоНалогуНаПрибыль.ПолучитьМакет(
					СоответствиеРасшифровки["НомерТекущейСхемы"]);
				УправляющаяСтруктура.ДоступностьПоДокументам =
					СхемаКомпоновкиДанных.ВариантыНастроек.Найти("БезРегистратора") <> Неопределено;
				
				СоответствиеРасшифровки.Вставить("НеРазворачиватьПоРегистраторам", НЕ ПоДокументам);
				
				// закэшируем расшифровку для возможности переформирования
				КэшПараметровОтчетаСтруктура.Вставить("ТекущаяРасшифровка", СоответствиеРасшифровки);
				
				ПараметрыОтчета.Вставить("НомерТекущейСхемы",   НомерТекущейСхемы);
				ПараметрыОтчета.Вставить("КэшПараметровОтчета", КэшПараметровОтчетаСтруктура);
		
				РезультатВыполнения = СформироватьОтчетНаСервере(ПараметрыОтчета, Истина);
				
				ОбновитьСтраницыНаСервере(УправляющаяСтруктура);
				
				Возврат РезультатВыполнения;
			КонецЕсли;			
		КонецЕсли;				
	КонецЕсли;
	
	КэшПараметровОтчета = ПоместитьВоВременноеХранилище(КэшПараметровОтчетаСтруктура, КэшПараметровОтчета);
	
	ОбновитьСтраницыНаСервере(УправляющаяСтруктура);
	
	Возврат РезультатВыполнения;
	
КонецФункции

&НаСервере
Функция ОбновитьТаблицуНаСервере(УправляющаяСтруктура, Отказ)
	
	УправляющаяСтруктура.Вставить("ЭтоСхема",                Ложь);
	УправляющаяСтруктура.Вставить("ЭтоОтчетПоДокументам",    Истина);
	УправляющаяСтруктура.Вставить("ДоступностьПоДокументам", Истина);
	
	РезультатВыполнения = Новый Структура("ЗаданиеВыполнено", Ложь);
	
	КэшПараметровОтчетаСтруктура = ПолучитьИзВременногоХранилища(КэшПараметровОтчета);
	
	Если КэшПараметровОтчетаСтруктура.Свойство("ИсторияОткрытий")
		И КэшПараметровОтчетаСтруктура.ИсторияОткрытий.Количество() > 0 Тогда
		//Получим последний номер схемы из истории - это там Схема, которую мы расшифровали
		НомерТекущейСхемыРасшифровки = КэшПараметровОтчетаСтруктура.ИсторияОткрытий.Получить(
			КэшПараметровОтчетаСтруктура.ИсторияОткрытий.Количество() - 1);
	Иначе
		Отказ = Ложь;
		Возврат РезультатВыполнения;	
	КонецЕсли;
	
	ПараметрыОтчета = ПодготовитьПараметрыОтчета(УправляющаяСтруктура.ТолькоОшибки);
	
	Если КэшПараметровОтчетаСтруктура.КэшМакетов.Свойство("Макет_" + НомерТекущейСхемыРасшифровки) Тогда
		
		НомерТекущейСхемы = 100;
		ПараметрыМакета   = КэшПараметровОтчетаСтруктура.КэшМакетов["Макет_" + НомерТекущейСхемыРасшифровки];
		
		СоответствиеРасшифровки = КэшПараметровОтчетаСтруктура.ТекущаяРасшифровка;
		
		СхемаКомпоновкиДанных = Отчеты.АнализСостоянияНалоговогоУчетаПоНалогуНаПрибыль.ПолучитьМакет(
			СоответствиеРасшифровки["НомерТекущейСхемы"]);
		УправляющаяСтруктура.ДоступностьПоДокументам =
			СхемаКомпоновкиДанных.ВариантыНастроек.Найти("БезРегистратора") <> Неопределено;
		СоответствиеРасшифровки.Вставить("НеРазворачиватьПоРегистраторам", НЕ ПоДокументам);
		
		// закэшируем расшифровку для возможности переформирования
		КэшПараметровОтчетаСтруктура.Вставить("ТекущаяРасшифровка", СоответствиеРасшифровки);
		
		ПараметрыОтчета.Вставить("НомерТекущейСхемы",   НомерТекущейСхемы);
		ПараметрыОтчета.Вставить("КэшПараметровОтчета", КэшПараметровОтчетаСтруктура);
		ПараметрыОтчета.Вставить("Расшифровка",         СоответствиеРасшифровки);
		
		// сформируем расшифровку
		РезультатВыполнения = СформироватьОтчетНаСервере(ПараметрыОтчета, Истина);
		
		КэшПараметровОтчета = ПоместитьВоВременноеХранилище(КэшПараметровОтчетаСтруктура, КэшПараметровОтчета);
		
		ОбновитьСтраницыНаСервере(УправляющаяСтруктура);
		
	Иначе
		
		Отказ = Ложь;
		
	КонецЕсли;
	
	Возврат РезультатВыполнения;
		
КонецФункции

&НаСервере
Функция ОбработатьРасшифровкуНаСервере(Расшифровка, УправляющаяСтруктура, ОткрытоИзИстории = Ложь)
	
	НомерТекущейСхемы = НомерТекущейСхемы;
	КэшПараметровОтчетаСтруктура = ПолучитьИзВременногоХранилища(КэшПараметровОтчета);
	
	УправляющаяСтруктура.Вставить("ЭтоОтчетПоДокументам" ,    Ложь);
	УправляющаяСтруктура.Вставить("ДоступностьПоДокументам",  Ложь);
	
	РезультатВыполнения = Новый Структура("ЗаданиеВыполнено", Истина);
	Если ТипЗнч(Расшифровка) = Тип("Соответствие") Тогда
		НомерТекущейСхемы = Расшифровка["НомерТекущейСхемы"];
		
		ПараметрыОтчета = ПодготовитьПараметрыОтчета(УправляющаяСтруктура.ТолькоОшибки);
		ПараметрыОтчета.Вставить("НомерТекущейСхемы", НомерТекущейСхемы);
		ПараметрыОтчета.Вставить("КэшПараметровОтчета", КэшПараметровОтчетаСтруктура);
		ПараметрыОтчета.Вставить("Расшифровка", Расшифровка);
		
		РезультатВыполнения = СформироватьОтчетНаСервере(ПараметрыОтчета);
		
		УправляющаяСтруктура.Вставить("ЭтоСхема",  Расшифровка["ВидРасшифровки"] = "Схема");		
		
		ОбновитьСтраницыНаСервере(УправляющаяСтруктура);
	ИначеЕсли ОбщегоНазначения.ЭтоСсылка(ТипЗнч(Расшифровка)) Тогда 
		УправляющаяСтруктура.Вставить("ОткрытьОбъект", Расшифровка);
	КонецЕсли;
	
	Возврат РезультатВыполнения;
		
КонецФункции

&НаСервере
Процедура ОбновитьСтраницыНаСервере(УправляющаяСтруктура)
	
	// Установим режим видимости объектов для карты и расшифровок
	Элементы.ГруппаБыстрыеОтборы.Видимость 			  = УправляющаяСтруктура.Главная;
	Элементы.ГруппаОсновнаяКоманднаяПанель.Видимость  = НЕ УправляющаяСтруктура.Главная;
	
	Элементы.ПечатьСразу.Видимость = НЕ УправляющаяСтруктура.ЭтоСхема; 
	Элементы.ПечатьГрафическойСхемы.Видимость = УправляющаяСтруктура.ЭтоСхема;
	
	Если УправляющаяСтруктура.Главная Тогда		
		Элементы.Сформировать.Видимость = Ложь;
		Элементы.СформироватьВсеДействия.Видимость = Ложь;
		Элементы.СформироватьТолькоОшибки.Видимость = Ложь;
		Элементы.ГруппаДополнительныеНастройки.Видимость = Ложь;
		Элементы.ГруппаНаборРезультатов.ТекущаяСтраница = Элементы.ГруппаСхемы;
		
		Возврат;		
	Иначе 		
		Элементы.Сформировать.Видимость = УправляющаяСтруктура.ЭтоОтчетПоДокументам;
		Элементы.СформироватьВсеДействия.Видимость = УправляющаяСтруктура.ЭтоОтчетПоДокументам;
		Элементы.СформироватьТолькоОшибки.Видимость = УправляющаяСтруктура.ЭтоОтчетПоДокументам;
	    Элементы.ГруппаДополнительныеНастройки.Видимость = УправляющаяСтруктура.ЭтоОтчетПоДокументам;
		Элементы.ГруппаНаборРезультатов.ТекущаяСтраница = ?(УправляющаяСтруктура.ЭтоСхема, Элементы.ГруппаСхемы, 
			Элементы.ГруппаТабличногоДокумента);
		
		Элементы.ПоДокументам.Доступность = УправляющаяСтруктура.ДоступностьПоДокументам;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСтраницы(УправляющаяСтруктура)
	
	// Установим режим видимости объектов для карты и расшифровок
	Элементы.ГруппаБыстрыеОтборы.Видимость 			  = УправляющаяСтруктура.Главная;
	Элементы.ГруппаОсновнаяКоманднаяПанель.Видимость  = НЕ УправляющаяСтруктура.Главная;
	
	Если УправляющаяСтруктура.Главная Тогда
		
		Элементы.Сформировать.Видимость = Ложь;
		Элементы.СформироватьВсеДействия.Видимость = Ложь;
		Элементы.СформироватьТолькоОшибки.Видимость = Ложь;
		Элементы.ГруппаДополнительныеНастройки.Видимость = Ложь;
		Элементы.ГруппаНаборРезультатов.ТекущаяСтраница = Элементы.ГруппаСхемы;
		
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.ИндикаторВыполнения, "НеИспользовать");
		Элементы.ГруппаРезультатСхема.ТекущаяСтраница = Элементы.СтраницаРезультатСхема;
		
		Элементы.ГруппаСохранитьЗагрузить.Видимость = Истина;
		
	Иначе
		
		Элементы.Сформировать.Видимость = УправляющаяСтруктура.ЭтоОтчетПоДокументам;
		Элементы.СформироватьВсеДействия.Видимость = УправляющаяСтруктура.ЭтоОтчетПоДокументам;
		Элементы.СформироватьТолькоОшибки.Видимость = УправляющаяСтруктура.ЭтоОтчетПоДокументам;
		Элементы.ГруппаДополнительныеНастройки.Видимость = УправляющаяСтруктура.ЭтоОтчетПоДокументам;
		Элементы.ГруппаНаборРезультатов.ТекущаяСтраница = ?(УправляющаяСтруктура.ЭтоСхема, Элементы.ГруппаСхемы, 
			Элементы.ГруппаТабличногоДокумента);
		
		Элементы.ПоДокументам.Доступность = УправляющаяСтруктура.ДоступностьПоДокументам;
		
		Если УправляющаяСтруктура.Главная ИЛИ УправляющаяСтруктура.ЭтоСхема Тогда
			ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.ИндикаторВыполнения, "НеИспользовать");
			Элементы.ГруппаРезультатСхема.ТекущаяСтраница = Элементы.СтраницаРезультатСхема;
		Иначе
			ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеИспользовать");
		КонецЕсли;
		
		Элементы.ГруппаСохранитьЗагрузить.Видимость = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ПараметраОтчетаНеКорректны()
	
	СтатусВозврата = Ложь;
	
	Если НЕ ЗначениеЗаполнено(Отчет.Организация) Тогда
		ТекстСообщения
			= ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(,, "Организация");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, "Отчет.Организация");
		СтатусВозврата = Истина;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Отчет.НачалоПериода) Тогда
		ТекстСообщения
			= ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(,, "НачалоПериода");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, "Отчет.НачалоПериода");
		СтатусВозврата = Истина;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Отчет.КонецПериода) Тогда
		ТекстСообщения
			= ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(,, "КонецПериода");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, "Отчет.КонецПериода");
		СтатусВозврата = Истина;
	КонецЕсли;
	
	Если Отчет.КонецПериода < Отчет.НачалоПериода Тогда
		ТекстСообщения = "Дата окончания периода не может быть меньше даты начала периода";
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, "Отчет.КонецПериода");
		СтатусВозврата = Истина;
	КонецЕсли;
	
	Возврат СтатусВозврата;
	
КонецФункции

&НаКлиенте
Процедура ВопросПередЗакрытиемЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ЗавершитьРаботуСОтчетом = Истина;
		Закрыть();
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ПоДокументамПриИзменении(Элемент)
	
	ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеАктуальность");
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодНачалоВыбораЗавершение(СтруктураПериода, ДополнительныеПараметры) Экспорт
	
	// Установим полученный период
	Если СтруктураПериода <> Неопределено Тогда
		ВидПериода = СтруктураПериода.ВидПериода;
		Период = СтруктураПериода.Период;
		Отчет.НачалоПериода = СтруктураПериода.НачалоПериода;
		Отчет.КонецПериода = СтруктураПериода.КонецПериода;
	КонецЕсли;
		
	ОбновитьТекстЗаголовка(ЭтаФорма);
	ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеАктуальность");

КонецПроцедуры

&НаСервере
Процедура ВычислитьСуммуВыделенныхЯчеекТабличногоДокументаВКонтекстеНаСервере()
	
	ПолеСумма = БухгалтерскиеОтчетыВызовСервера.ВычислитьСуммуВыделенныхЯчеекТабличногоДокумента(
		Результат, КэшВыделеннойОбласти);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_РезультатПриАктивизацииОбластиПодключаемый()
	
	НеобходимоВычислятьНаСервере = Ложь;
	БухгалтерскиеОтчетыКлиент.ВычислитьСуммуВыделенныхЯчеекТабличногоДокумента(
		ПолеСумма, Результат, КэшВыделеннойОбласти, НеобходимоВычислятьНаСервере);
	
	Если НеобходимоВычислятьНаСервере Тогда
		ВычислитьСуммуВыделенныхЯчеекТабличногоДокументаВКонтекстеНаСервере();
	КонецЕсли;
	
	ОтключитьОбработчикОжидания("Подключаемый_РезультатПриАктивизацииОбластиПодключаемый");
	
КонецПроцедуры

