////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Заголовок") Тогда
		Заголовок = Параметры.Заголовок;
	КонецЕсли;
	
	Элементы.ГруппаРаздел.Видимость = НЕ Параметры.Свойство("Раздел");
	
	РазделыДляТакси = Константы.ИнтерфейсТакси.Получить();
	ЗаполнитьСписокРазделов();
		
	Если Параметры.Свойство("Раздел") Тогда
		
		Раздел = Параметры.Раздел;
		
	Иначе
		
		РазделПоУмолчанию = Элементы.Раздел.СписокВыбора[0].Значение;
		ТекущийРаздел = ОбщегоНазначенияВызовСервера.ХранилищеСистемныхНастроекЗагрузить("ПолезнаяИнформация", "ТекущийРаздел", РазделПоУмолчанию);
		Если Элементы.Раздел.СписокВыбора.НайтиПоЗначению(ТекущийРаздел) <> Неопределено Тогда
			Раздел = ТекущийРаздел;
		Иначе
			Раздел = РазделПоУмолчанию;
		КонецЕсли;
		
	КонецЕсли;
	
	Для каждого ЭлементСписка Из Элементы.Раздел.СписокВыбора Цикл
		
		Форма = ЭтаФорма;
		
		ИмяРаздела = ЭлементСписка.Значение;
		ПредставлениеРаздела = ЭлементСписка.Представление;
		
		// Страница раздела.
		СтраницаРаздела = Форма.Элементы.Добавить("ГруппаСтраница" + ИмяРаздела, Тип("ГруппаФормы"), Элементы.ГруппаСтраницы); 
		СтраницаРаздела.Заголовок = ПредставлениеРаздела;
		СтраницаРаздела.Вид = ВидГруппыФормы.Страница;
		СтраницаРаздела.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Горизонтальная;
		СтраницаРаздела.ОтображатьЗаголовок = Ложь;
		
		// Колонка "Актуальные материалы".
		КолонкаАктуальныеМатериалы = Форма.Элементы.Добавить("ГруппаСтраница" + ИмяРаздела + "АктуальныеМатериалы", Тип("ГруппаФормы"), СтраницаРаздела); 
		КолонкаАктуальныеМатериалы.Заголовок = ПредставлениеРаздела + НСтр("ru=': актуальные материалы'");
		КолонкаАктуальныеМатериалы.Вид = ВидГруппыФормы.ОбычнаяГруппа;
		КолонкаАктуальныеМатериалы.Отображение = ОтображениеОбычнойГруппы.Нет;
		КолонкаАктуальныеМатериалы.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
		КолонкаАктуальныеМатериалы.ОтображатьЗаголовок = Ложь;
		КолонкаАктуальныеМатериалы.РастягиватьПоГоризонтали = Ложь;
		КолонкаАктуальныеМатериалы.Ширина = 40;

		// Заголовок "Актуальные материалы"
		ЗаголовокАктуальныеМатериалы = Форма.Элементы.Добавить("Заголовок" + ИмяРаздела + "АктуальныеМатериалы", Тип("ДекорацияФормы"), КолонкаАктуальныеМатериалы); 
		ЗаголовокАктуальныеМатериалы.Заголовок = НСтр("ru='Актуальные материалы'");
		ЗаголовокАктуальныеМатериалы.Вид = ВидДекорацииФормы.Надпись;
		ЗаголовокАктуальныеМатериалы.РастягиватьПоГоризонтали = Истина;
		ЗаголовокАктуальныеМатериалы.ЦветТекста = Новый Цвет(179, 128, 0);
		ЗаголовокАктуальныеМатериалы.Шрифт = Новый Шрифт(, 10, Истина);
		ЗаголовокАктуальныеМатериалы.Ширина = 1;
		
		// Колонка "Быстрое освоение".
		КолонкаБыстроеОсвоение = Форма.Элементы.Добавить("ГруппаСтраница" + ИмяРаздела + "БыстроеОсвоение", Тип("ГруппаФормы"), СтраницаРаздела); 
		КолонкаБыстроеОсвоение.Заголовок = ПредставлениеРаздела + НСтр("ru=': быстрое освоение'");
		КолонкаБыстроеОсвоение.Вид = ВидГруппыФормы.ОбычнаяГруппа;
		КолонкаБыстроеОсвоение.Отображение = ОтображениеОбычнойГруппы.Нет;
		КолонкаБыстроеОсвоение.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
		КолонкаБыстроеОсвоение.ОтображатьЗаголовок = Ложь;
		КолонкаБыстроеОсвоение.РастягиватьПоГоризонтали = Ложь;
		КолонкаБыстроеОсвоение.Ширина = 40;
		
		// Заголовок "Быстрое освоение"
		ЗаголовокБыстроеОсвоение = Форма.Элементы.Добавить("Заголовок" + ИмяРаздела + "БыстроеОсвоение", Тип("ДекорацияФормы"), КолонкаБыстроеОсвоение); 
		ЗаголовокБыстроеОсвоение.Заголовок = НСтр("ru='Быстрое освоение'");
		ЗаголовокБыстроеОсвоение.Вид = ВидДекорацииФормы.Надпись;
		ЗаголовокБыстроеОсвоение.РастягиватьПоГоризонтали = Истина;
		ЗаголовокБыстроеОсвоение.ЦветТекста = Новый Цвет(179, 128, 0);
		ЗаголовокБыстроеОсвоение.Шрифт = Новый Шрифт(, 10, Истина);
		ЗаголовокБыстроеОсвоение.Ширина = 1;
		
	 	ПолезнаяИнформация.ДобавитьГиперссылки(ЭтаФорма, КолонкаАктуальныеМатериалы, ИмяРаздела, "АктуальныеМатериалы");
	 	ПолезнаяИнформация.ДобавитьГиперссылки(ЭтаФорма, КолонкаБыстроеОсвоение, ИмяРаздела, "БыстроеОсвоение");
		
		КолонкаАктуальныеМатериалы.Видимость = КолонкаАктуальныеМатериалы.ПодчиненныеЭлементы.Количество() > 1;
		КолонкаБыстроеОсвоение.Видимость = КолонкаБыстроеОсвоение.ПодчиненныеЭлементы.Количество() > 1;
		
	КонецЦикла;
	
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы["ГруппаСтраница" + Раздел];
		
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура РазделПриИзменении(Элемент)
	
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы["ГруппаСтраница" + Раздел];
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура Подключаемый_ОткрытьГиперссылкуПолезнойИнформации(Команда)
	
	ПолезнаяИнформацияКлиент.ОткрытьГиперссылку(ЭтаФорма, Команда);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОткрытьВсеМатериалыПолезнойИнформации(Команда)
	
	ПолезнаяИнформацияКлиент.ОткрытьВсеМатериалы(ЭтаФорма, Команда);
		
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии()
	
	ОбщегоНазначенияВызовСервера.ХранилищеСистемныхНастроекСохранить("ПолезнаяИнформация", "ТекущийРаздел", Раздел);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере
Процедура ЗаполнитьСписокРазделов()

	Если РазделыДляТакси Тогда
		Элементы.Раздел.СписокВыбора.Добавить("Главное",           "1С:Бухгалтерия 8");
		Элементы.Раздел.СписокВыбора.Добавить("БанкИКасса",        "Банк и касса");
		Элементы.Раздел.СписокВыбора.Добавить("Покупки",           "Покупки");
		Элементы.Раздел.СписокВыбора.Добавить("Продажи",           "Продажи");
		Элементы.Раздел.СписокВыбора.Добавить("Склад",             "Склад");
		Элементы.Раздел.СписокВыбора.Добавить("Производство",      "Производство");
		Элементы.Раздел.СписокВыбора.Добавить("ОСИНМА",            "ОС и НМА");
		Элементы.Раздел.СписокВыбора.Добавить("ЗарплатаИКадры",    "Зарплата и кадры");
		Элементы.Раздел.СписокВыбора.Добавить("Операции",          "Операции");
		Элементы.Раздел.СписокВыбора.Добавить("Отчеты",            "Отчеты");
		Элементы.Раздел.СписокВыбора.Добавить("Администрирование", "Администрирование");
	Иначе
		Элементы.Раздел.СписокВыбора.Добавить("РабочийСтол82",                "1С:Бухгалтерия 8");
		Элементы.Раздел.СписокВыбора.Добавить("БанкИКасса82",                 "Банк и касса");
		Элементы.Раздел.СписокВыбора.Добавить("ПокупкиИПродажи82",            "Покупки и продажи");
		Элементы.Раздел.СписокВыбора.Добавить("НоменклатураИСклад82",         "Номенклатура и склад");
		Элементы.Раздел.СписокВыбора.Добавить("Производство82",               "Производство");
		Элементы.Раздел.СписокВыбора.Добавить("ОсновныеСредстваИНМА82",       "Основные средства и НМА");
		Элементы.Раздел.СписокВыбора.Добавить("СотрудникиИЗарплата82",        "Сотрудники и зарплата");
		Элементы.Раздел.СписокВыбора.Добавить("УчетНалогиОтчетность82",       "Учет, налоги, отчетность");
		Элементы.Раздел.СписокВыбора.Добавить("СправочникиИНастройкиУчета82", "Справочники и настройки учета");
		Элементы.Раздел.СписокВыбора.Добавить("Администрирование",            "Администрирование");
	КонецЕсли;

КонецПроцедуры 

