#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ВосстановитьНастройки();
	УправлениеЭУ();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ФлажокМеханизмВключенПриИзменении(Элемент)
	
	ФлажокМеханизмВключенПриИзмененииСервер();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы
//Код процедур и функций
#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура НастроитьПараметрыПроксиСервера(Команда)
	
	ОткрытьФорму("ОбщаяФорма.ПараметрыПроксиСервера");
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьДоступКСервису(Команда)
	
	ТекстПредупреждения = РезультатПроверкиПараметровДоступа();
	ПоказатьПредупреждение(, ТекстПредупреждения);
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьРегистр(Команда)
	ПроверкаКонтрагентовВызовСервера.ОчиститьСохраненныеРезультатыПроверкиКонтрагентов();
КонецПроцедуры


#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ФлажокМеханизмВключенПриИзмененииСервер()
	
	ПроверкаКонтрагентов.СохранитьЗначенияНастроек(ИспользоватьСервис);
	Если ИспользоватьСервис Тогда
		ПроверкаКонтрагентов.ПроверитьКонтрагентовПослеВключенияПроверкиФоновоеЗадание();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УправлениеЭУ()
	
	Элементы.ПредупреждениеПроТестовыйРежим.Заголовок = ПроверкаКонтрагентов.ТекстПредупрежденияПроТестовыйРежимРаботыСервиса();
	
КонецПроцедуры

&НаСервере
Процедура ВосстановитьНастройки()
	
	НастройкиСервиса 	= ПроверкаКонтрагентов.ЗначенияНастроек();
	ИспользоватьСервис 	= НастройкиСервиса.ИспользоватьСервис;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция РезультатПроверкиПараметровДоступа()
	
	Если НЕ ПроверкаКонтрагентов.ЕстьДоступКВебСервисуФНС() Тогда
		ТекстПредупреждения = НСтр("ru = 'Доступ к веб-сервису отсутствует'");
		Возврат ТекстПредупреждения;
	КонецЕсли;
	
	ТекстПредупреждения = НСтр("ru = 'Проверка доступа к веб-сервису успешно пройдена!'");
	Возврат ТекстПредупреждения;
	
КонецФункции

&НаКлиенте
Процедура ИспользоватьСервисРасширеннаяПодсказкаОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылка, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ПроверкаКонтрагентовКлиент.ОткрытьИнструкциюПоИспользованиюСервиса(СтандартнаяОбработка);
КонецПроцедуры

#КонецОбласти

