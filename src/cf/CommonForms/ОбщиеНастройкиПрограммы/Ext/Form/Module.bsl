&НаКлиенте
Процедура ИспользоватьПолнотекстовыйПоискПриИзменении(Элемент)
	
	ТекстВопроса = НСтр("ru = 'Для продолжения требуется записать настройки.
	|
	|Записать?'");
	Оповещение = Новый ОписаниеОповещения("ВопросИспользоватьПолнотекстовыйПоискЗавершение", ЭтотОбъект);
	ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросИспользоватьПолнотекстовыйПоискЗавершение(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		Попытка
			Записать();
		Исключение
			НаборКонстант.ИспользоватьПолнотекстовыйПоиск = Не НаборКонстант.ИспользоватьПолнотекстовыйПоиск;
			ВызватьИсключение КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		КонецПопытки;
	Иначе
		НаборКонстант.ИспользоватьПолнотекстовыйПоиск = Не НаборКонстант.ИспользоватьПолнотекстовыйПоиск;
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// ПолнотекстовыйПоиск
	Если ТекущийОбъект.ИспользоватьПолнотекстовыйПоиск <> Константы.ИспользоватьПолнотекстовыйПоиск.Получить() Тогда
		Режим = ?(НаборКонстант.ИспользоватьПолнотекстовыйПоиск, РежимПолнотекстовогоПоиска.Разрешить, РежимПолнотекстовогоПоиска.Запретить);
		ПолнотекстовыйПоиск.УстановитьРежимПолнотекстовогоПоиска(Режим);
	КонецЕсли;
	// Конец ПолнотекстовыйПоиск
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ОбновитьПовторноИспользуемыеЗначения();
	ОбновитьИнтерфейс();
	
КонецПроцедуры
