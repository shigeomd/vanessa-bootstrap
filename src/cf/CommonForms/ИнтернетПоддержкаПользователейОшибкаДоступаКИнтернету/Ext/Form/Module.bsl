
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	МестоЗапуска = Параметры.МестоЗапуска;
	
	ОписаниеОшибки = Параметры.ОписаниеОшибки;
	ОписаниеОшибкиЗаполнено = НЕ ПустаяСтрока(ОписаниеОшибки);
	
	КлючСохраненияПоложенияОкна = МестоЗапуска + Строка(ОписаниеОшибкиЗаполнено);
	
	Если ТипЗнч(Параметры.СтартовыеПараметры) = Тип("Структура") Тогда
		СтартовыеПараметры = Параметры.СтартовыеПараметры;
	Иначе
		СтартовыеПараметры = Новый Структура;
	КонецЕсли;
	
	Элементы.ОписаниеОшибки.Видимость = ОписаниеОшибкиЗаполнено;
	
	Элементы.ДекорацияПараметры.Видимость =
		ИнтернетПоддержкаПользователейВызовСервера.ДоступнаНастройкаПараметровПодключенияКИнтернетПоддержке();
	
	Если Параметры.ПриНачалеРаботыСистемы Тогда
		ЗапускатьПриСтарте = Истина;
	Иначе
		Элементы.ЗапускатьПриСтарте.Видимость = Ложь;
	КонецЕсли;
	
	// Настройка внешнего вида формы
	Если ТекущийВариантИнтерфейсаКлиентскогоПриложения() = ВариантИнтерфейсаКлиентскогоПриложения.Такси Тогда
		Элементы.ГруппаЗаголовка.Отображение           = ОтображениеОбычнойГруппы.СлабоеВыделение;
		Элементы.ГруппаИнформационнойЧасти.Отображение = ОтображениеОбычнойГруппы.СлабоеВыделение;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если Элементы.ДекорацияПараметры.Видимость Тогда
		Элементы.ДекорацияПараметры.Видимость = НЕ ФормаПараметровИнтернетПоддержкиОткрыта();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДекорацияПараметрыОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылка, СтандартнаяОбработка)
	
	Если НавигационнаяСсылка = "WebITSParams" Тогда
		СтандартнаяОбработка = Ложь;
		ИнтернетПоддержкаПользователейКлиент.ОткрытьФормуНастроекПараметровПодключения(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьСообщенияВТехПоддержкуОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылка, СтандартнаяОбработка)
	
	Если НавигационнаяСсылка = "SendMail" Тогда
		
		СтандартнаяОбработка = Ложь;
		
		#Если ВебКлиент Тогда
		
		// Асинхронный запуск с подключением расширения для работы с файлами
		Оповещение = Новый ОписаниеОповещения("ОтправитьСообщениеВТехПоддержку", ЭтотОбъект);
		ТекстСообщения = НСтр("ru = 'Для отправки сообщения необходимо подключить расширение работы с файлами.'");
		ОбщегоНазначенияКлиент.ПроверитьРасширениеРаботыСФайламиПодключено(Оповещение, ТекстСообщения);
		
		#Иначе
		
		// Синхронный запуск
		Попытка
			ЗапуститьПриложение("mailto:webits-info@1c.ru");
		Исключение
			ИнтернетПоддержкаПользователейВызовСервера.ЗаписатьОшибкуВЖурналРегистрации(
				НСтр("ru = 'Не удалось открыть почтовый клиент:'")
					+ " " + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ПоказатьПредупреждение(,
				НСтр("ru = 'Не удалось запустить приложение для работы с электронной почтой.'"));
		КонецПопытки;
		
		#КонецЕсли
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапускатьПриСтартеПриИзменении(Элемент)
	
	УстановитьНастройкуЗапускатьПриСтарте(ЗапускатьПриСтарте);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПовторитьПопыткуПодключения(Команда)
	
	Закрыть();
	
	ИнтернетПоддержкаПользователейКлиент.СтартоватьМеханизм(МестоЗапуска, СтартовыеПараметры, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция ФормаПараметровИнтернетПоддержкиОткрыта()
	
	ПараметрыОповещения = Новый Структура("ФормаОткрыта", Ложь);
	Оповестить("ПроверитьОткрытиеФормыПараметровИнтернетПоддержки", ПараметрыОповещения);
	Возврат ПараметрыОповещения.ФормаОткрыта;
	
КонецФункции

&НаСервереБезКонтекста
Процедура УстановитьНастройкуЗапускатьПриСтарте(ЗначениеНастройки)
	
	ХранилищеОбщихНастроек.Сохранить(
		"ИнтернетПоддержкаПользователей",
		"ВсегдаПоказыватьПриСтартеПрограммы",
		ЗначениеНастройки);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьСообщениеВТехПоддержку(РасширениеПодключено, ДопПараметры) Экспорт
	
	ОповещениеОЗапускеПриложения = Новый ОписаниеОповещения(
		"ЗапускПриложенияЗавершение",
		ЭтотОбъект,
		,
		"ЗапускПриложенияИсключение",
		ЭтотОбъект);
	
	НачатьЗапускПриложения(ОповещениеОЗапускеПриложения, "mailto:webits-info@1c.ru");
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапускПриложенияЗавершение(КодВозврата, ДопПараметры) Экспорт
	
	Возврат;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапускПриложенияИсключение(ИнфОшибка, СтандартнаяОбработка, ДопПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	ИнтернетПоддержкаПользователейВызовСервера.ЗаписатьОшибкуВЖурналРегистрации(
		НСтр("ru = 'Не удалось открыть почтовый клиент.'")
			+ " " + ПодробноеПредставлениеОшибки(ИнфОшибка));
	
	ПоказатьПредупреждение(,
		НСтр("ru = 'Не удалось запустить приложение для работы с электронной почтой.'"));
	
КонецПроцедуры

#КонецОбласти
