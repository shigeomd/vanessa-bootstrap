#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

Процедура ПрименитьПослеРасчетаПоказателей(Показатели) Экспорт
	
	Формулы = НайтиФормулы(Показатели);
	
	Для Каждого Формула Из Формулы Цикл
		
		Вычесть(Формула);
		
	КонецЦикла;
	
	УдалитьИспользованныеЭлементыФормул(Формулы, Показатели);
	
КонецПроцедуры

Процедура ПрименитьДоРасчетаПоказателей(Показатели) Экспорт
	
	
	Формулы = НайтиФормулы(Показатели);
	
	Для Каждого Формула Из Формулы Цикл
		
		ФормироватьТолькоЗаГод(Показатели, Формула);
		ДобавитьИтоговыйПоказательРозницаВсего(Показатели, Формула);
		
	КонецЦикла;
	
	УдалитьИспользованныеЭлементыФормул(Формулы, Показатели);
	
КонецПроцедуры

Функция КлючевыеПоля(Алгоритм) Экспорт
	
	// Алгоритм выполняется для группы показателей, 
	// содержащих одинаковые значения ключевых полей
	// См. также ЗаполнениеФормСтатистики.НовыйОписаниеПоказателей()
	
	Если Алгоритм = Вычесть Тогда
		Возврат "ИмяПоля, Аналитика, Организация"; 
	ИначеЕсли Алгоритм = ДобавитьИтоговыйПоказательРозницаВсего Тогда
		Возврат "ИмяПоля, ОбъектНаблюдения, Организация";
	ИначеЕсли Алгоритм = ФормироватьТолькоЗаГод Тогда
		Возврат "КонецПериода, ДополнительныйАлгоритм";
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

////////////////////////////////////////////////////////////////////////////////
// Алгоритмы

Процедура Вычесть(Формула)
	
	Если Формула.ДополнительныйАлгоритм <> Вычесть Тогда
		Возврат;
	КонецЕсли;
			
	ЭлементыФормулы = Формула.Элементы;
	Результат = 0;
	ИндексРезультата = Неопределено; // Индекс строки, в которую будет помещен результат
	КоличествоЭлементов = ЭлементыФормулы.Количество();
	Для Индекс = 0 По КоличествоЭлементов - 1 Цикл
		
		ЭлементФормулы = ЭлементыФормулы[Индекс];
		ЭлементФормулы.Удалить = Истина; // Один оставим, какой именно - определим ниже. Постараемся, чтобы это был показатель без доп.алгоритма, обычно он лучше характеризует суть показателя
		
		Показатель     = ЭлементФормулы.Показатель;
		
		Если ТипЗнч(Показатель.Значение) <> Тип("Число") Тогда
			Продолжить;
		КонецЕсли;
		
		Если Показатель.ДополнительныйАлгоритм = Вычесть Тогда
			Результат = Результат - Показатель.Значение;
		Иначе
			Результат = Результат + Показатель.Значение;
		КонецЕсли;
		
		Если ИндексРезультата = Неопределено 
			И Не ЗначениеЗаполнено(Показатель.ДополнительныйАлгоритм) Тогда
			ИндексРезультата = Индекс;
		КонецЕсли;
		
	КонецЦикла;
	
	Если ИндексРезультата = Неопределено Тогда
		ИндексРезультата = 0;
	КонецЕсли;
	
	ЭлементыФормулы[ИндексРезультата].Удалить = Ложь;
	ЭлементыФормулы[ИндексРезультата].Показатель.Значение = Результат;
		
КонецПроцедуры

Процедура ДобавитьИтоговыйПоказательРозницаВсего(Показатели, Формула)
	
	Если Формула.ДополнительныйАлгоритм <> ДобавитьИтоговыйПоказательРозницаВсего Тогда
		Возврат;
	КонецЕсли;
	
	// Убедимся, что показатель не добавлен пользователем
	Для Каждого ЭлементФормулы Из Формула.Элементы Цикл
		Если ЭлементФормулы.Показатель.Аналитика = Справочники.КлассификаторПродукцииПоВидамДеятельности.РозницаВсего Тогда
			Возврат;
		КонецЕсли;
	КонецЦикла;
	
	// Добавим показатель
	НовыйПоказатель = Показатели.Добавить();
	ЗаполнитьЗначенияСвойств(НовыйПоказатель, Формула.Элементы[0].Показатель, , "ДополнительныйАлгоритм");
	НовыйПоказатель.Аналитика = Справочники.КлассификаторПродукцииПоВидамДеятельности.РозницаВсего;
	НовыйПоказатель.Настройка = ""; // Без отбора
	
КонецПроцедуры

Процедура ФормироватьТолькоЗаГод(Показатели, Формула)
	
	Если Формула.ДополнительныйАлгоритм <> ФормироватьТолькоЗаГод Тогда
		Возврат;
	КонецЕсли;
	
	Если КонецДня(Формула.КонецПериода) = КонецГода(Формула.КонецПериода) Тогда
		Возврат;
	КонецЕсли;
	
	// Удалим показатель
	Для Каждого ЭлементФормулы Из Формула.Элементы Цикл
		ЭлементФормулы.Удалить = Истина;
	КонецЦикла;
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// Прочее

Функция НовыйЭлементыФормулы()
	
	ЭлементыФормулы = Новый ТаблицаЗначений;
	ЭлементыФормулы.Колонки.Добавить("Показатель"); // Ссылка на строку таблицы значений Показатели
	ЭлементыФормулы.Колонки.Добавить("Удалить", Новый ОписаниеТипов("Булево")); // См. УдалитьИспользованныеПоказатели()
	
	Возврат ЭлементыФормулы;
	
КонецФункции

Функция НайтиФормулы(Показатели)
	
	// В каждой формуле участвует набор полей, указывающих на одну и ту же ячейку - элементов формул
	ИндексыОтбораЭлементовФормул = Новый Соответствие; // Ключ - Алгоритм, Значение - Индекс;  Индексы удалим в конце процедуры
	
	// Сначала определим состав формул (выражений),
	// а потом их элементы
	Формулы = Показатели.СкопироватьКолонки();
	Для Каждого Показатель Из Показатели Цикл
		
		Алгоритм = Показатель.ДополнительныйАлгоритм;
		
		Если Не ЗначениеЗаполнено(Алгоритм) Тогда
			Продолжить;
		КонецЕсли;
		
		КлючевыеПоля = КлючевыеПоля(Алгоритм);
		Если Не ЗначениеЗаполнено(КлючевыеПоля) Тогда
			ЗаполнитьЗначенияСвойств(Формулы.Добавить(), Показатель);
		Иначе
			
			ЗаполнитьЗначенияСвойств(Формулы.Добавить(), Показатель, КлючевыеПоля + ", ДополнительныйАлгоритм");
			
			ИндексОтбораЭлементовФормул = ИндексыОтбораЭлементовФормул[Алгоритм];
			Если ИндексОтбораЭлементовФормул = Неопределено Тогда
				Индекс = Показатели.Индексы.Добавить(КлючевыеПоля);
				ИндексыОтбораЭлементовФормул.Вставить(Алгоритм, Индекс);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	КолонкиГруппировок = Новый Массив; // Все колонки, за исключением тех, по которым в принципе группировать нельзя
	Для Каждого Колонка Из Показатели.Колонки Цикл
		
		ЕстьЗапрещенныеТипы = Ложь;
		Для Каждого Тип Из Колонка.ТипЗначения.Типы() Цикл
			Если Тип = Тип("Число")
				Или Тип = Тип("Строка") 
				Или Тип = Тип("Дата") 
				Или ОбщегоНазначения.ЭтоСсылка(Тип) Тогда
				Продолжить;
			КонецЕсли;
			ЕстьЗапрещенныеТипы = Истина;
		КонецЦикла;
		
		Если ЕстьЗапрещенныеТипы Тогда
			Продолжить;
		КонецЕсли;
		
		КолонкиГруппировок.Добавить(Колонка.Имя);
		
	КонецЦикла;
	КолонкиГруппировок = СтроковыеФункцииКлиентСервер.СтрокаИзМассиваПодстрок(КолонкиГруппировок,",");
	
	Формулы.Свернуть(КолонкиГруппировок); // Для каждой формулы заполнен свой набор полей
	
	// ... определяем состав элементов формул
	Формулы.Колонки.Добавить("Элементы", Новый ОписаниеТипов("ТаблицаЗначений")); // См. НовыйЭлементыФормулы
	
	Для Каждого Формула Из Формулы Цикл
		
		Алгоритм = Формула.ДополнительныйАлгоритм;
		КлючевыеПоля = КлючевыеПоля(Алгоритм);
		
		ОтборЭлементовФормул = Новый Структура(КлючевыеПоля);
		
		ЗаполнитьЗначенияСвойств(ОтборЭлементовФормул, Формула);
		Формула.Элементы = НовыйЭлементыФормулы();
		Для Каждого Показатель Из Показатели.НайтиСтроки(ОтборЭлементовФормул) Цикл
			НоваяСтрока = Формула.Элементы.Добавить();
			НоваяСтрока.Показатель = Показатель;
		КонецЦикла;
		
	КонецЦикла;
	
	Для Каждого Индекс Из ИндексыОтбораЭлементовФормул Цикл
		Показатели.Индексы.Удалить(Индекс.Значение);
	КонецЦикла;
	
	Возврат Формулы;
	
КонецФункции

Процедура УдалитьИспользованныеЭлементыФормул(Формулы, Показатели)
	
	// Для расчета значения в поле может использоваться несколько показателей.
	// После того, как они поучаствовали в расчете, часть из них может оказаться лишними.
	// Например, если показатели суммируются, то для каждого поля останется одна строка с суммой.
	// Один показатель может входить в разные формулы, перед удалением нужно проверять
	// не был ли показатель уже удален в предидущих формулах	
	Для Каждого Формула Из Формулы Цикл
		Для Каждого ЭлементФормулы Из Формула.Элементы Цикл
			Если ЭлементФормулы.Удалить Тогда
				Показатель = ЭлементФормулы.Показатель;
				
				// Перед удалением проверим существует ди показатель
				ИндексПоказателя = Показатели.Индекс(Показатель);
				// Если не удается получить индекс показателя, то и удалять нечего
				Если ИндексПоказателя <> -1 Тогда
					
					Показатели.Удалить(ИндексПоказателя);
					
				КонецЕсли;
				
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

#КонецЕсли