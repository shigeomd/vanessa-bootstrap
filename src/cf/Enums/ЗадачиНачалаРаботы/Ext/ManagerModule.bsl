#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

// Содержит перечень задач, без выполнения которых нельзя выполнять регулярные задачи.
// 
Функция НельзяИзбежать() Экспорт
	
	НельзяИзбежать = Новый Массив;
	НельзяИзбежать.Добавить(РеквизитыОрганизации);
	НельзяИзбежать.Добавить(ДоступКОрганизациям);
	
	Возврат Новый ФиксированныйМассив(НельзяИзбежать);
	
КонецФункции

// Возвращает массив задач начала работы, которые нельзя пропустить
//
Функция ОбязательныеЗадачиНачалаРаботы() Экспорт
	
	ЗадачиНачалаРаботы = Новый Массив;
	
	Если Не ОбщегоНазначенияБПВызовСервера.ПроверитьНаличиеДоступныхОрганизаций() Тогда
		// У пользователя нет доступных организаций и нет прав на добавление таких организаций
		ЗадачиНачалаРаботы.Добавить(Перечисления.ЗадачиНачалаРаботы.ДоступКОрганизациям);
	ИначеЕсли Справочники.Организации.КоличествоОрганизаций() = 0 Тогда
		// Доступ к добавлению организаций есть. Возможно, следует добавить организацию.
		ЗадачиНачалаРаботы.Добавить(Перечисления.ЗадачиНачалаРаботы.РеквизитыОрганизации);
	КонецЕсли;
	
	Возврат ЗадачиНачалаРаботы;
	
КонецФункции

// Возвращает представление команды выполнения задачи
//
Функция ПредставлениеКомандыВыполнить(Задача) Экспорт
	
	Если Задача = Перечисления.ЗадачиНачалаРаботы.РеквизитыОрганизации Тогда
		
		Команда = НСтр("ru = 'Заполнить реквизиты'");
		
	ИначеЕсли Задача = Перечисления.ЗадачиНачалаРаботы.ПараметрыУчета Тогда
		
		Команда = НСтр("ru = 'Открыть параметры'");
		
	ИначеЕсли Задача = Перечисления.ЗадачиНачалаРаботы.УчетнаяПолитика
		Или   Задача = Перечисления.ЗадачиНачалаРаботы.НастройкаЗаполненияФормСтатистики Тогда
		
		Команда = НСтр("ru = 'Открыть настройки'");
		
	ИначеЕсли Задача = Перечисления.ЗадачиНачалаРаботы.СписокНалоговОтчетов Тогда
		
		Команда = НСтр("ru = 'Открыть список'");
		
	ИначеЕсли Задача = Перечисления.ЗадачиНачалаРаботы.НачальныеОстатки
		Или   Задача = Перечисления.ЗадачиНачалаРаботы.Подключение1СОтчетности Тогда
		
		Команда = НСтр("ru = 'Открыть помощник'");
		
	Иначе
		
		Команда = НСтр("ru = 'Выполнить задачу'");
		
	КонецЕсли;
	
	Возврат Команда;
	
КонецФункции

#КонецЕсли
