#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ВидыНалоговыхПлатежей() Экспорт
	
	ВидыПлатежей = Новый Массив;
	ВидыПлатежей.Добавить(Перечисления.ВидыПлатежейВГосБюджет.Налог);
	ВидыПлатежей.Добавить(Перечисления.ВидыПлатежейВГосБюджет.НалогАкт);
	ВидыПлатежей.Добавить(Перечисления.ВидыПлатежейВГосБюджет.НалогСам);
	
	Возврат ВидыПлатежей;
	
КонецФункции

// Формирует список выбора видов налоговых обязательств в зависимости от вида налога
//
// Параметры:
//   ВидНалога - СправочникСсылка.ВидыНалоговИПлатежейВБюджет
//   Период    - Дата
//
// Возвращаемое значение:
//  СписокЗначений - список видов налоговых обязательств (ПеречислениеСсылка.ВидыПлатежейВГосБюджет)
//
Функция СписокВидовНалоговыхОбязательств(Налог, Знач Период = Неопределено) Экспорт
	
	СписокВыбора = Новый СписокЗначений;
	
	Если Период = Неопределено Тогда
		Период = ТекущаяДатаСеанса();
	КонецЕсли;
	
	ВидГосударственногоОргана = Справочники.ВидыНалоговИПлатежейВБюджет.ВидГосударственногоОргана(Налог);
	Если  ВидГосударственногоОргана <> Перечисления.ВидыГосударственныхОрганов.ОрганПФР
		И ВидГосударственногоОргана <> Перечисления.ВидыГосударственныхОрганов.ОрганФСС Тогда
		СписокВыбора.Добавить(Перечисления.ВидыПлатежейВГосБюджет.Налог, НСтр("ru = 'Налог'"));
	КонецЕсли;
	
	Если ВидГосударственногоОргана <> Перечисления.ВидыГосударственныхОрганов.НалоговыйОрган Тогда
		СписокВыбора.Добавить(Перечисления.ВидыПлатежейВГосБюджет.Налог, НСтр("ru = 'Взносы'"));
	КонецЕсли;
	
	СписокВыбора.Добавить(Перечисления.ВидыПлатежейВГосБюджет.ПениСам, НСтр("ru = 'Пени'"));
	
	Если НачалоДня(Период) >= '20150101'
		И ВидГосударственногоОргана <> Перечисления.ВидыГосударственныхОрганов.ОрганПФР
		И ВидГосударственногоОргана <> Перечисления.ВидыГосударственныхОрганов.ОрганФСС Тогда
		СписокВыбора.Добавить(Перечисления.ВидыПлатежейВГосБюджет.Проценты, НСтр("ru = 'Проценты'"));
	КонецЕсли;
	
	СписокВыбора.Добавить(Перечисления.ВидыПлатежейВГосБюджет.Штраф,   НСтр("ru = 'Штраф'"));
	
	Возврат СписокВыбора;
	
КонецФункции

// Возвращает представление налогового обязательства для генерации назначения платежа
//
// Параметры:
//   ВидНалога                  - СправочникСсылка.ВидыНалоговИПлатежейВБюджет
//   ВидНалоговогоОбязательства - ПеречислениеСсылка.ВидыПлатежейВГосБюджет
//
// Возвращаемое значение:
//  Строка - представление налогового обязательства
//
Функция ПредставлениеНалоговогоОбязательства(ВидНалога, ВидНалоговогоОбязательства) Экспорт
	
	Если Не ЗначениеЗаполнено(ВидНалоговогоОбязательства) Тогда
		Возврат "";
	КонецЕсли;
	
	ВидГосударственногоОргана = Справочники.ВидыНалоговИПлатежейВБюджет.ВидГосударственногоОргана(ВидНалога);
	
	Если ВидГосударственногоОргана = Перечисления.ВидыГосударственныхОрганов.НалоговыйОрган Тогда
		
		Если ЭтоПени(ВидНалоговогоОбязательства) Тогда
			Представление = НСтр("ru = 'Пени по налогу'");
		ИначеЕсли ЭтоПроценты(ВидНалоговогоОбязательства) Тогда
			Представление = НСтр("ru = 'Проценты по налогу'");
		ИначеЕсли ЭтоШтраф(ВидНалоговогоОбязательства) Тогда
			Представление = НСтр("ru = 'Штраф по налогу'");
		Иначе
			Представление = НСтр("ru = 'Налог'");
		КонецЕсли;
		
	ИначеЕсли ВидГосударственногоОргана = Перечисления.ВидыГосударственныхОрганов.ОрганПФР Тогда
		
		Если ЭтоПени(ВидНалоговогоОбязательства) Тогда
			Представление = НСтр("ru = 'Пени по страховым взносам'");
		ИначеЕсли ЭтоШтраф(ВидНалоговогоОбязательства) Тогда
			Представление = НСтр("ru = 'Штраф по страховым взносам'");
		Иначе
			Представление = НСтр("ru = 'Страховые взносы'");
		КонецЕсли;
		
	ИначеЕсли ВидГосударственногоОргана = Перечисления.ВидыГосударственныхОрганов.ОрганФСС Тогда
		
		Если ЭтоПени(ВидНалоговогоОбязательства) Тогда
			Представление = НСтр("ru = 'Пени по взносам'");
		ИначеЕсли ЭтоШтраф(ВидНалоговогоОбязательства) Тогда
			Представление = НСтр("ru = 'Штраф по взносам'");
		Иначе
			Представление = НСтр("ru = 'Взносы'");
		КонецЕсли;
		
	Иначе
		Представление = НСтр("ru = 'Платеж'");
	КонецЕсли;
	
	Возврат Представление;
	
КонецФункции

Функция ЭтоПени(ВидНалоговогоОбязательства) Экспорт
	
	Возврат ВидНалоговогоОбязательства = Перечисления.ВидыПлатежейВГосБюджет.ПениАкт
		Или ВидНалоговогоОбязательства = Перечисления.ВидыПлатежейВГосБюджет.ПениСам;
	
КонецФункции

Функция ЭтоПроценты(ВидНалоговогоОбязательства) Экспорт
	
	Возврат ВидНалоговогоОбязательства = Перечисления.ВидыПлатежейВГосБюджет.Проценты;
	
КонецФункции

Функция ЭтоШтраф(ВидНалоговогоОбязательства) Экспорт
	
	Возврат ВидНалоговогоОбязательства = Перечисления.ВидыПлатежейВГосБюджет.Штраф;
	
КонецФункции

#КонецОбласти

#КонецЕсли