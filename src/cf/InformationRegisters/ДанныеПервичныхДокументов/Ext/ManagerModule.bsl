#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

// Обработчик обновления на версию 3.0.38
// Заполняет недостающие данные по документам в регистре сведений "ДанныеПервичныхДокументов"
//
Процедура ЗаполнитьНомераРасходныхКассовыйОрдеров() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеПервичныхДокументов.Организация,
	|	ДанныеПервичныхДокументов.Документ,
	|	ДанныеПервичныхДокументов.Номер,
	|	ДанныеПервичныхДокументов.Дата,
	|	ДанныеПервичныхДокументов.ДатаРегистратора,
	|	ДанныеПервичныхДокументов.НомерРегистратора
	|ИЗ
	|	РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РасходныйКассовыйОрдер КАК РасходныйКассовыйОрдер
	|		ПО ДанныеПервичныхДокументов.Организация = РасходныйКассовыйОрдер.Организация
	|			И ДанныеПервичныхДокументов.Документ = РасходныйКассовыйОрдер.Ссылка
	|ГДЕ
	|	ДанныеПервичныхДокументов.Номер = """"
	|	И РасходныйКассовыйОрдер.ВидОперации <> ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРКО.УплатаНалога)";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		НаборЗаписей = РегистрыСведений.ДанныеПервичныхДокументов.СоздатьНаборЗаписей();
		
		НаборЗаписей.Отбор.Документ.Значение = Выборка.Документ;
		НаборЗаписей.Отбор.Документ.Использование = Истина;
		
		Запись = НаборЗаписей.Добавить();
		ЗаполнитьЗначенияСвойств(Запись, Выборка);
		Запись.Номер = ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(Выборка.НомерРегистратора, Истина, Ложь);
		
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписей);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецЕсли
