#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

// Процедура выполняет первоначальное заполнение сведений 
// о минимальном размере оплаты труда в РФ.
//
Процедура НачальноеЗаполнение() Экспорт
	
	Если ОбменДаннымиПовтИсп.ЭтоАвтономноеРабочееМесто() Тогда
		Возврат;
	КонецЕсли;
	
	КлассификаторXML = РегистрыСведений.МинимальнаяОплатаТрудаРФ.ПолучитьМакет("МинимальнаяОплатаТрудаРФ").ПолучитьТекст();
	
	КлассификаторТаблица = ОбщегоНазначения.ПрочитатьXMLВТаблицу(КлассификаторXML).Данные;
	
	НаборЗаписей = РегистрыСведений.МинимальнаяОплатаТрудаРФ.СоздатьНаборЗаписей();
	НаборЗаписей.ДополнительныеСвойства.Вставить("ЗаписьОбщихДанных");
	Для Каждого СтрокаКлассификатора Из КлассификаторТаблица Цикл
		СтрокаНабора = НаборЗаписей.Добавить();
		СтрокаНабора.Период 				= Дата(СтрокаКлассификатора.Date);
		СтрокаНабора.Размер					= Число(СтрокаКлассификатора.Rate);
	КонецЦикла;
	НаборЗаписей.ОбменДанными.Загрузка = Истина;
	НаборЗаписей.Записать();
	
КонецПроцедуры

// Функция предназначена для получения данных о минимальном размере оплаты труда в РФ.
// Данные включают в себя сам размер и дату, с которой он действует.
//
// Параметры:
//	ДатаАктуальности - дата, на которую нужно получить размер МРОТ (необязательный), 
//						если не заполнен, возвращается последнее действующее значение.
//
// Возвращаемое значение - структура, полями которой являются Размер и Период.
//
Функция ДанныеМинимальногоРазмераОплатыТрудаРФ(ДатаАктуальности = Неопределено) Экспорт
	
	ДанныеМРОТ = Новый Структура("Размер, Период");
	
	СрезПоследних = РегистрыСведений.МинимальнаяОплатаТрудаРФ.СрезПоследних(ДатаАктуальности);
	Если СрезПоследних.Количество() = 0 Тогда
		ВызватьИсключение НСтр("ru = 'Сведения о минимальном размере оплаты труда РФ не заполнены'");
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ДанныеМРОТ, СрезПоследних[0]);
	
	Возврат ДанныеМРОТ;
	
КонецФункции

// Проверяет заполненность данных о минимальном размере оплаты труда в РФ.
//
// Возвращаемое значение - булево, Истина - если есть хотя бы одно значение.
//
Функция МинимальныйРазмерОплатыТрудаРФЗаполнен() Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИСТИНА КАК Поле1
	|ИЗ
	|	РегистрСведений.МинимальнаяОплатаТрудаРФ КАК МинимальнаяОплатаТрудаРФ");
	
	Возврат Не Запрос.Выполнить().Пустой();
	
КонецФункции

#КонецОбласти

#КонецЕсли