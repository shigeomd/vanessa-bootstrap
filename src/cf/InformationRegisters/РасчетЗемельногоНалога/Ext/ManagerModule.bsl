#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

// ОБНОВЛЕНИЕ ИНФОРМАЦИОННОЙ БАЗЫ

// Перенос Периода строительства из ресурсов в измерения.
//
Процедура ПеренестиПериодСтроительстваИзРесурсовВИзмерения() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РасчетЗемельногоНалога.Регистратор
	|ИЗ
	|	РегистрСведений.РасчетЗемельногоНалога КАК РасчетЗемельногоНалога
	|ГДЕ
	|	РасчетЗемельногоНалога.УдалитьПериодСтроительства <> """"";
	
	ВыборкаЗаписи = Запрос.Выполнить().Выбрать();
	
	Пока ВыборкаЗаписи.Следующий() Цикл
	
		НаборЗаписей = РегистрыСведений.РасчетЗемельногоНалога.СоздатьНаборЗаписей();
		
		НаборЗаписей.Отбор.Регистратор.Установить(ВыборкаЗаписи.Регистратор);
		НаборЗаписей.Прочитать();
		
		Для каждого СтрокаНабора Из НаборЗаписей Цикл
			Если ЗначениеЗаполнено(СтрокаНабора.УдалитьПериодСтроительства) Тогда
				Если НЕ ЗначениеЗаполнено(СтрокаНабора.ПериодСтроительства) Тогда
					СтрокаНабора.ПериодСтроительства = СтрокаНабора.УдалитьПериодСтроительства;
				КонецЕсли;
				СтрокаНабора.УдалитьПериодСтроительства = "";
			КонецЕсли;
		КонецЦикла;
		
		Попытка
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписей);
		Исключение
			ТекстСообщения = НСтр("ru = 'Не удалось обновить расчет земельного налога'");
			ЗаписьЖурналаРегистрации(ТекстСообщения, УровеньЖурналаРегистрации.Ошибка, , , ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		КонецПопытки;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецЕсли