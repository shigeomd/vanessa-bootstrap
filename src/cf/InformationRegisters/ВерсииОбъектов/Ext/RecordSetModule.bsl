#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого Запись Из ЭтотОбъект Цикл
		Запись.РазмерДанных = РазмерДанных(Запись.ВерсияОбъекта);
		ВерсияОбъекта = Запись.ВерсияОбъекта.Получить();
		Запись.ЕстьДанныеВерсии = ВерсияОбъекта <> Неопределено;
		Если Запись.ЕстьДанныеВерсии Тогда
			Запись.КонтрольнаяСумма = ВерсионированиеОбъектов.КонтрольнаяСумма(ВерсияОбъекта);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция РазмерДанных(Данные)
	Возврат Base64Значение(СериализаторXDTO.XMLСтрока(Данные)).Размер();
КонецФункции

#КонецОбласти

#КонецЕсли