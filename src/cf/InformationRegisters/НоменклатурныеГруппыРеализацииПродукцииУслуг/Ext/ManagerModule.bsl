#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
Процедура ЗаполнитьОрганизациюПриОднофирменномУчете() Экспорт
	
	// Когда используется одна организация, то поле Организация регистра не отображается пользователю.
	// В версиях ранее 3.0.33 в этом случае оно и не заполнялось, могли быть добавлены записи с незаполненной организацией.
	// Такие записи неверные, не имеют смысла. 
	// Однако, пользователь не мог увидеть, что они ошибочные, так как визуально они не отличаются от корректных.
	// 
	// Обработчик ЗаполнитьОрганизациюПриОднофирменномУчете() вызывается при обновлении на версию 3.0.33 
	// и исправляет эту ошибку в данных: заполняет организацию при однофирменном учете.
	
	Если Справочники.Организации.ИспользуетсяНесколькоОрганизаций() Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	НоменклатурныеГруппыРеализацииПродукцииУслуг.Организация КАК Организация
	|ИЗ
	|	РегистрСведений.НоменклатурныеГруппыРеализацииПродукцииУслуг КАК НоменклатурныеГруппыРеализацииПродукцииУслуг
	|ГДЕ
	|	НоменклатурныеГруппыРеализацииПродукцииУслуг.Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	НоменклатурныеГруппыРеализацииПродукцииУслуг.Организация КАК Организация
	|ИЗ
	|	РегистрСведений.НоменклатурныеГруппыРеализацииПродукцииУслуг КАК НоменклатурныеГруппыРеализацииПродукцииУслуг
	|ГДЕ
	|	НоменклатурныеГруппыРеализацииПродукцииУслуг.Организация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)";
	Результат = Запрос.ВыполнитьПакет();
	Если Результат[0].Пустой() Тогда
		// Все данные корректны
		Возврат;
	КонецЕсли;
	
	Если Не Результат[1].Пустой() Тогда
		// Есть и корректные и некорректные данные. 
		// Автоматически исправить не можем.
		Возврат;
	КонецЕсли;
	
	Организация = Справочники.Организации.ОрганизацияПоУмолчанию();
	Если Не ЗначениеЗаполнено(Организация) Тогда
		// Исправить данные в регистре не можем
		Возврат;
	КонецЕсли;
	
	// Изменить нужно все записи, поэтому используем набор записей без отбора
	Записи = РегистрыСведений.НоменклатурныеГруппыРеализацииПродукцииУслуг.СоздатьНаборЗаписей();
	Записи.Прочитать();
	Для Каждого Запись Из Записи Цикл
		Запись.Организация = Организация;
	КонецЦикла;
	ОбновлениеИнформационнойБазы.ЗаписатьДанные(Записи);
		
КонецПроцедуры
	
#КонецЕсли
