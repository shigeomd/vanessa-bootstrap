#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОрганизацииСтруктурныхЕдиниц = Новый Соответствие();
	Для Каждого Запись Из ЭтотОбъект Цикл
		Если НЕ ЗначениеЗаполнено(Запись.Организация) Тогда
			
			Организация = ОрганизацииСтруктурныхЕдиниц.Получить(Запись.СтруктурнаяЕдиница);
			Если Организация = Неопределено Тогда
				Организация = ВладелецРегистрацииВНалоговомОргане(Запись.СтруктурнаяЕдиница);
				ОрганизацииСтруктурныхЕдиниц.Вставить(Запись.СтруктурнаяЕдиница, Организация);
			КонецЕсли;
			
			Запись.Организация = Организация;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ВладелецРегистрацииВНалоговомОргане(СтруктурнаяЕдиница)
	
	Если НЕ ЗначениеЗаполнено(СтруктурнаяЕдиница) Тогда
		Возврат Справочники.Организации.ПустаяСсылка();
	КонецЕсли;
	
	Если ТипЗнч(СтруктурнаяЕдиница) = Тип("СправочникСсылка.Организации") Тогда
		Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтруктурнаяЕдиница, "ГоловнаяОрганизация");
	ИначеЕсли ТипЗнч(СтруктурнаяЕдиница) = Тип("СправочникСсылка.ПодразделенияОрганизаций") Тогда
		Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтруктурнаяЕдиница, "Владелец.ГоловнаяОрганизация")
	Иначе
		Возврат Справочники.Организации.ПустаяСсылка();
	КонецЕсли;
	
КонецФункции

#КонецЕсли