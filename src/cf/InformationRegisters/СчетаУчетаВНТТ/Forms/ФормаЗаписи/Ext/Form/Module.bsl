////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Ключ.Пустой() Тогда
		Запись.Период = '19800101';
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Запись.ТорговаяТочка) Тогда
		Элементы.ГруппаТорговаяТочка.ТекущаяСтраница = Элементы.ГруппаТорговаяТочкаВыбрана;
	Иначе
		Элементы.ГруппаТорговаяТочка.ТекущаяСтраница = Элементы.ГруппаВыборТорговойТочки;
	КонецЕсли;
	
	ОпределятьПоУчетнойПолитике = НЕ ЗначениеЗаполнено(Запись.СчетДоходовОтРеализации)
		И НЕ ЗначениеЗаполнено(Запись.СчетРасходовОтРеализации);
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если НЕ Запись.ИсходныйКлючЗаписи.Пустой() Тогда
		
		СчетаУчетаВНТТ					= РегистрыСведений.СчетаУчетаВНТТ.СоздатьМенеджерЗаписи();
		СчетаУчетаВНТТ.Период			= Запись.ИсходныйКлючЗаписи.Период;
		СчетаУчетаВНТТ.ТорговаяТочка	= Запись.ИсходныйКлючЗаписи.ТорговаяТочка;
		СчетаУчетаВНТТ.Прочитать();
		
		СтруктураСтаройЗаписи	= Новый Структура("Период, ТорговаяТочка, СчетДоходовОтРеализации, СчетРасходовОтРеализации");
		ЗаполнитьЗначенияСвойств(СтруктураСтаройЗаписи, СчетаУчетаВНТТ);
		ПараметрыЗаписи.Вставить("СтруктураСтаройЗаписи", СтруктураСтаройЗаписи);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если ПараметрыЗаписи.Свойство("СтруктураСтаройЗаписи") Тогда
		
		// Если была изменена дата и хотя бы одно из полей, тогда сохраним прежнюю запись	
		Если (НЕ ТекущийОбъект.Период = ПараметрыЗаписи.СтруктураСтаройЗаписи.Период
				И (НЕ ТекущийОбъект.СчетДоходовОтРеализации = ПараметрыЗаписи.СтруктураСтаройЗаписи.СчетДоходовОтРеализации
					ИЛИ НЕ ТекущийОбъект.СчетРасходовОтРеализации = ПараметрыЗаписи.СтруктураСтаройЗаписи.СчетРасходовОтРеализации)) Тогда
					
			СчетаУчетаВНТТ	= РегистрыСведений.СчетаУчетаВНТТ.СоздатьМенеджерЗаписи();
			ЗаполнитьЗначенияСвойств(СчетаУчетаВНТТ, ТекущийОбъект);
			ЗаполнитьЗначенияСвойств(СчетаУчетаВНТТ, ПараметрыЗаписи.СтруктураСтаройЗаписи);
			СчетаУчетаВНТТ.Записать();
			
		КонецЕсли;
	
	КонецЕсли;
	
	
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если НЕ ОпределятьПоУчетнойПолитике Тогда
		
		Если НЕ ЗначениеЗаполнено(Запись.СчетДоходовОтРеализации) Тогда
			ТекстСообщения = НСтр("ru = 'Поле ""Счет доходов по реализации"" не заполнено'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "СчетДоходовОтРеализации", "Запись", Отказ);
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Запись.СчетРасходовОтРеализации) Тогда
			ТекстСообщения = НСтр("ru = 'Поле ""Счет расходов по реализации"" не заполнено'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "СчетРасходовОтРеализации", "Запись", Отказ);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура ОпределятьПоУчетнойПолитикеПриИзменении(Элемент)
	
	Если ОпределятьПоУчетнойПолитике Тогда
		Запись.СчетДоходовОтРеализации  = Неопределено;
		Запись.СчетРасходовОтРеализации = Неопределено;
	КонецЕсли;
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ИсторияИзменений(Команда)

	Отбор = Новый Структура("ТорговаяТочка", Запись.ТорговаяТочка);
	ОткрытьФорму("РегистрСведений.СчетаУчетаВНТТ.ФормаСписка", Новый Структура("Отбор", Отбор));

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	
	Элементы.СчетДоходовОтРеализации.Доступность  = НЕ Форма.ОпределятьПоУчетнойПолитике;
	Элементы.СчетРасходовОтРеализации.Доступность = НЕ Форма.ОпределятьПоУчетнойПолитике;
	
КонецПроцедуры
