&НаКлиенте
Перем ОтветПередЗакрытием;

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	ЗакрыватьПриЗакрытииВладельца = Истина;
	
	ИППорядокПризнанияРасходовПоОперациямПрошлогоГода = ?(Параметры.ПризнаватьРасходыПоОперациямПрошлогоГодаИП, 1, 0);
	ДляПризнанияРасходовТребуетсяПолучениеДоходаИП = Параметры.ДляПризнанияРасходовТребуетсяПолучениеДоходаИП;
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	Если НЕ Модифицированность Тогда
		Возврат;
	КонецЕсли;
	
	Если ОтветПередЗакрытием <> Истина Тогда
		
		Отказ = Истина;
		
		Оповещение = Новый ОписаниеОповещения(
			"ПередЗакрытиемЗавершение",
			ЭтотОбъект);
			
		ТекстВопроса = НСтр("ru='Сохранить изменения?'");
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена);
		
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура ДляПризнанияРасходовТребуетсяПолучениеДоходаИППриИзменении(Элемент)
	
	Если НЕ ДляПризнанияРасходовТребуетсяПолучениеДоходаИП Тогда
		ИППорядокПризнанияРасходовПоОперациямПрошлогоГода = 0;
	КонецЕсли;
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ОК(Команда)

	Модифицированность = Ложь;
	ПараметрыЗакрытия = ПолучитьПараметрыЗакрытия();
	Закрыть(ПараметрыЗакрытия);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Модифицированность = Ложь;
	Закрыть();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаКлиенте
Процедура ПередЗакрытиемЗавершение(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ОтветПередЗакрытием	= Истина;
		ПараметрыЗакрытия = ПолучитьПараметрыЗакрытия();
		Закрыть(ПараметрыЗакрытия);
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		ОтветПередЗакрытием	= Истина;
		Закрыть();
	КонецЕсли;   
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	
 	Элементы.ИППорядокПризнанияРасходовПоОперациямПрошлогоГодаНезависимо.Доступность =  Форма.ДляПризнанияРасходовТребуетсяПолучениеДоходаИП;
 	Элементы.ИППорядокПризнанияРасходовПоОперациямПрошлогоГодаВПериодеПолученияДохода.Доступность =  Форма.ДляПризнанияРасходовТребуетсяПолучениеДоходаИП;
	
КонецПроцедуры

&НаКлиенте
Функция ПолучитьПараметрыЗакрытия()
	
	ПараметрыЗакрытия = Новый Структура;
	
	ПараметрыЗакрытия.Вставить("ПризнаватьРасходыПоОперациямПрошлогоГодаИП", ?(ИППорядокПризнанияРасходовПоОперациямПрошлогоГода = 1, Истина, Ложь));
	ПараметрыЗакрытия.Вставить("ДляПризнанияРасходовТребуетсяПолучениеДоходаИП", ДляПризнанияРасходовТребуетсяПолучениеДоходаИП);
	
	Возврат ПараметрыЗакрытия;
	
КонецФункции