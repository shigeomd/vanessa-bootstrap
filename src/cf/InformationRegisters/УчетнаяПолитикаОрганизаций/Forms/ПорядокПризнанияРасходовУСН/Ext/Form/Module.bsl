&НаКлиенте
Перем ОтветПередЗакрытием;

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	ЗакрыватьПриЗакрытииВладельца = Истина;
	
	ПорядокПризнанияМатериальныхРасходов	= Параметры.ПорядокПризнанияМатериальныхРасходов;
	ПорядокПризнанияРасходовПоТоварам		= Параметры.ПорядокПризнанияРасходовПоТоварам;
	ПорядокПризнанияРасходовПоНДС			= Параметры.ПорядокПризнанияРасходовПоНДС;
	ПорядокПризнанияДопРасходов				= Параметры.ПорядокПризнанияДопРасходов;
	ПорядокПризнанияТаможенныхПлатежей		= Параметры.ПорядокПризнанияТаможенныхПлатежей;
	
	ПолучитьПараметрыРасходовУСН();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	Если НЕ Модифицированность Тогда
		Возврат;
	КонецЕсли;
	
	Если ОтветПередЗакрытием <> Истина Тогда
		
		Отказ = Истина;
		
		Оповещение = Новый ОписаниеОповещения(
			"ПередЗакрытиемЗавершение",
			ЭтотОбъект);
			
		ТекстВопроса = НСтр("ru='Сохранить изменения?'");
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена);
		
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ОК(Команда)

	Модифицированность = Ложь;
	ПараметрыЗакрытия = ПолучитьПараметрыЗакрытия();
	Закрыть(ПараметрыЗакрытия);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Модифицированность = Ложь;
	Закрыть();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаКлиенте
Процедура ПередЗакрытиемЗавершение(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ОтветПередЗакрытием	= Истина;
		ПараметрыЗакрытия = ПолучитьПараметрыЗакрытия();
		Закрыть(ПараметрыЗакрытия);
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		ОтветПередЗакрытием	= Истина;
		Закрыть();
	КонецЕсли;   
	
КонецПроцедуры

&НаКлиенте
Функция ПолучитьПараметрыЗакрытия()
	
	УстановитьПараметрыРасходовУСН();
	
	ПараметрыЗакрытия = Новый Структура;
	ПараметрыЗакрытия.Вставить("ПорядокПризнанияМатериальныхРасходов",	ПорядокПризнанияМатериальныхРасходов);
	ПараметрыЗакрытия.Вставить("ПорядокПризнанияРасходовПоТоварам",		ПорядокПризнанияРасходовПоТоварам);
	ПараметрыЗакрытия.Вставить("ПорядокПризнанияРасходовПоНДС",			ПорядокПризнанияРасходовПоНДС);
	ПараметрыЗакрытия.Вставить("ПорядокПризнанияДопРасходов",			ПорядокПризнанияДопРасходов);
	ПараметрыЗакрытия.Вставить("ПорядокПризнанияТаможенныхПлатежей",	ПорядокПризнанияТаможенныхПлатежей);
	
	Возврат ПараметрыЗакрытия;
	
КонецФункции

&НаСервере
Процедура ПолучитьПараметрыРасходовУСН()

	ПоступлениеМатериалов = Истина;
	ОплатаМатериалов = Истина;

	Если ПорядокПризнанияМатериальныхРасходов = ПредопределенноеЗначение("Перечисление.ПорядокПризнанияМатериальныхРасходов.УменьшатьРасходыНаОстатокНЗП") Тогда
		ПередачаВПроизводство = Истина;
		УменьшатьНаНЗП = Истина;
	ИначеЕсли ПорядокПризнанияМатериальныхРасходов = ПредопределенноеЗначение("Перечисление.ПорядокПризнанияМатериальныхРасходов.ПоОплатеПоставщику") Тогда
		ПередачаВПроизводство = Ложь;
		УменьшатьНаНЗП = Ложь;
	Иначе
		ПередачаВПроизводство = Истина;
		УменьшатьНаНЗП = Ложь;
	КонецЕсли;

	ПоступлениеТоваров = Истина;
	ОплатаТоваров      = Истина;

	Если ПорядокПризнанияРасходовПоТоварам = ПредопределенноеЗначение("Перечисление.ПорядокПризнанияРасходовПоТоварам.ПоФактуПолученияДохода") Тогда
		Реализация = Истина;
		ПолучениеДохода	= Истина;
	ИначеЕсли ПорядокПризнанияРасходовПоТоварам = ПредопределенноеЗначение("Перечисление.ПорядокПризнанияРасходовПоТоварам.ПоОплатеПоставщику") Тогда
		Реализация = Ложь;
		ПолучениеДохода	= Ложь;
	Иначе
		Реализация = Истина;
		ПолучениеДохода = Ложь;
	КонецЕсли;

	НДСПредъявленПоставщиком = Истина;
	ОплатаНДС = Истина;

	Если ПорядокПризнанияРасходовПоНДС = ПредопределенноеЗначение("Перечисление.ПорядокПризнанияРасходовПоНДС.ВключатьВСтоимость") Тогда
		ПризнаниеРасхода = Истина;
	Иначе
		ПризнаниеРасхода = Ложь;
	КонецЕсли;
	
	Если ПорядокПризнанияДопРасходов = ПредопределенноеЗначение("Перечисление.ПорядокПризнанияДопРасходов.ВключатьВСтоимость") Тогда
		ДопРасходыПризнаниеРасхода = Истина;
	Иначе
		ДопРасходыПризнаниеРасхода = Ложь;
	КонецЕсли;
	
	Если ПорядокПризнанияТаможенныхПлатежей
		= ПредопределенноеЗначение("Перечисление.ПорядокПризнанияТаможенныхПлатежей.ВключатьВСтоимость") Тогда
		ТаможенныеПлатежиПризнаниеРасхода = Истина;
	Иначе
		ТаможенныеПлатежиПризнаниеРасхода = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПараметрыРасходовУСН()

	Если УменьшатьНаНЗП Тогда
		Если ПорядокПризнанияМатериальныхРасходов <> ПредопределенноеЗначение("Перечисление.ПорядокПризнанияМатериальныхРасходов.УменьшатьРасходыНаОстатокНЗП") Тогда
			ПорядокПризнанияМатериальныхРасходов = ПредопределенноеЗначение("Перечисление.ПорядокПризнанияМатериальныхРасходов.УменьшатьРасходыНаОстатокНЗП");
		КонецЕсли;
	ИначеЕсли ПередачаВПроизводство Тогда
		Если ПорядокПризнанияМатериальныхРасходов <> ПредопределенноеЗначение("Перечисление.ПорядокПризнанияМатериальныхРасходов.ПоФактуСписания") Тогда
			ПорядокПризнанияМатериальныхРасходов = ПредопределенноеЗначение("Перечисление.ПорядокПризнанияМатериальныхРасходов.ПоФактуСписания");
		КонецЕсли;
	Иначе
		Если ПорядокПризнанияМатериальныхРасходов <> ПредопределенноеЗначение("Перечисление.ПорядокПризнанияМатериальныхРасходов.ПоОплатеПоставщику") Тогда
			ПорядокПризнанияМатериальныхРасходов = ПредопределенноеЗначение("Перечисление.ПорядокПризнанияМатериальныхРасходов.ПоОплатеПоставщику");
		КонецЕсли;
	КонецЕсли;

	Если ПолучениеДохода Тогда
		Если ПорядокПризнанияРасходовПоТоварам <> ПредопределенноеЗначение("Перечисление.ПорядокПризнанияРасходовПоТоварам.ПоФактуПолученияДохода") Тогда
			ПорядокПризнанияРасходовПоТоварам = ПредопределенноеЗначение("Перечисление.ПорядокПризнанияРасходовПоТоварам.ПоФактуПолученияДохода");
		КонецЕсли;
	ИначеЕсли Реализация Тогда
		Если ПорядокПризнанияРасходовПоТоварам <> ПредопределенноеЗначение("Перечисление.ПорядокПризнанияРасходовПоТоварам.ПоФактуРеализации") Тогда
			ПорядокПризнанияРасходовПоТоварам = ПредопределенноеЗначение("Перечисление.ПорядокПризнанияРасходовПоТоварам.ПоФактуРеализации");
		КонецЕсли;
	Иначе
		Если ПорядокПризнанияРасходовПоТоварам <> ПредопределенноеЗначение("Перечисление.ПорядокПризнанияРасходовПоТоварам.ПоОплатеПоставщику") Тогда
			ПорядокПризнанияРасходовПоТоварам = ПредопределенноеЗначение("Перечисление.ПорядокПризнанияРасходовПоТоварам.ПоОплатеПоставщику");
		КонецЕсли;
	КонецЕсли;

	Если ПризнаниеРасхода Тогда
		Если ПорядокПризнанияРасходовПоНДС <> ПредопределенноеЗначение("Перечисление.ПорядокПризнанияРасходовПоНДС.ВключатьВСтоимость") Тогда
			ПорядокПризнанияРасходовПоНДС = ПредопределенноеЗначение("Перечисление.ПорядокПризнанияРасходовПоНДС.ВключатьВСтоимость");
		КонецЕсли;
	Иначе
		Если ПорядокПризнанияРасходовПоНДС <> ПредопределенноеЗначение("Перечисление.ПорядокПризнанияРасходовПоНДС.ПоОплатеПоставщику") Тогда
			ПорядокПризнанияРасходовПоНДС = ПредопределенноеЗначение("Перечисление.ПорядокПризнанияРасходовПоНДС.ПоОплатеПоставщику");
		КонецЕсли;
	КонецЕсли;
	
	Если ДопРасходыПризнаниеРасхода Тогда
		Если ПорядокПризнанияДопРасходов <> ПредопределенноеЗначение("Перечисление.ПорядокПризнанияДопРасходов.ВключатьВСтоимость") Тогда
			ПорядокПризнанияДопРасходов = ПредопределенноеЗначение("Перечисление.ПорядокПризнанияДопРасходов.ВключатьВСтоимость");
		КонецЕсли;
	Иначе
		Если ПорядокПризнанияДопРасходов <> ПредопределенноеЗначение("Перечисление.ПорядокПризнанияДопРасходов.ПоОплатеПоставщику") Тогда
			ПорядокПризнанияДопРасходов = ПредопределенноеЗначение("Перечисление.ПорядокПризнанияДопРасходов.ПоОплатеПоставщику");
		КонецЕсли;
	КонецЕсли;
	
	Если ТаможенныеПлатежиПризнаниеРасхода Тогда
		Если ПорядокПризнанияТаможенныхПлатежей <> ПредопределенноеЗначение("Перечисление.ПорядокПризнанияТаможенныхПлатежей.ВключатьВСтоимость") Тогда
			ПорядокПризнанияТаможенныхПлатежей = ПредопределенноеЗначение("Перечисление.ПорядокПризнанияТаможенныхПлатежей.ВключатьВСтоимость");
		КонецЕсли;
	Иначе
		Если ПорядокПризнанияТаможенныхПлатежей <> ПредопределенноеЗначение("Перечисление.ПорядокПризнанияТаможенныхПлатежей.ПоОплате") Тогда
			ПорядокПризнанияТаможенныхПлатежей = ПредопределенноеЗначение("Перечисление.ПорядокПризнанияТаможенныхПлатежей.ПоОплате");
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры
