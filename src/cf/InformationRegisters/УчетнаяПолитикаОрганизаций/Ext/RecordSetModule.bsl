#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)

	Справочники.Организации.ДополнитьДанныеЗаполненияПриОднофирменномУчете(ДанныеЗаполнения);
	
	ЗаписьУчетнойПолитики = ЭтотОбъект[0];
	
	Если ДанныеЗаполнения = Неопределено
		ИЛИ ТипЗнч(ДанныеЗаполнения) = Тип("Структура")  Тогда
		РегистрыСведений.УчетнаяПолитикаОрганизаций.УстановкаПараметровУчетнойПолитикиПоУмолчанию(
			ЗаписьУчетнойПолитики,
			?(ДанныеЗаполнения = Неопределено, Новый Структура, ДанныеЗаполнения));
	КонецЕсли;

	РегистрыСведений.УчетнаяПолитикаОрганизаций.УстановкаПараметровУчетнойПолитикиПоУмолчаниюНаПериод(ЗаписьУчетнойПолитики);

КонецПроцедуры

Процедура ПередЗаписью(Отказ, Замещение)

	Если Не ЭлементыЗатрат.ПоддерживаетсяУчетПоЭлементамЗатрат() Тогда
		
		// Эти ресурсы используются только в версии КОРП
		
		Для Каждого Строка Из ЭтотОбъект Цикл
			
			Если Строка.УчитыватьРасходыПоСтатьямЗатрат Тогда
				Строка.УчитыватьРасходыПоСтатьямЗатрат = Ложь;
			КонецЕсли;
			
			Если Строка.УчитыватьРасходыПоЭлементамЗатрат Тогда
				Строка.УчитыватьРасходыПоЭлементамЗатрат = Ложь;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ДополнительныеСвойства.Вставить("ВестиУчетПоПодразделениям", ПолучитьФункциональнуюОпцию("ВестиУчетПоПодразделениям"));
	ДополнительныеСвойства.Вставить("ГоловныеОрганизации", Новый Массив);
	
	// При записи пустого набора по головной организации должны очищаться записи по ОП этой организации.
	// Поэтому ОП для организации из отбора набора записей получаем независимо от того, есть ли строки в наборе
	Если ЭтотОбъект.Отбор.Организация.Использование Тогда
		ЭтоОбособленноеПодразделение = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
			ЭтотОбъект.Отбор.Организация.Значение, "ОбособленноеПодразделение");
		Если ЭтоОбособленноеПодразделение = Ложь Тогда // Возможно значение Неопределено при удалении организации
			ДополнительныеСвойства.ГоловныеОрганизации.Добавить(ЭтотОбъект.Отбор.Организация.Значение);
		КонецЕсли;
	КонецЕсли;
	
	Для каждого Строка Из ЭтотОбъект Цикл
		
		РеквизитыОрганизации = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			Строка.Организация, "ЮридическоеФизическоеЛицо,ОбособленноеПодразделение");
			
		Если РеквизитыОрганизации.ОбособленноеПодразделение Тогда
			Продолжить; // Записи по ОП создаются автоматически, их не проверяем
		ИначеЕсли ДополнительныеСвойства.ГоловныеОрганизации.Найти(Строка.Организация) = Неопределено Тогда
			ДополнительныеСвойства.ГоловныеОрганизации.Добавить(Строка.Организация);
		КонецЕсли;
		
		ЭтоФизЛицо = РеквизитыОрганизации.ЮридическоеФизическоеЛицо
			= Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо;
			
		Если НЕ ЭтоФизЛицо И Строка.СистемаНалогообложения = Перечисления.СистемыНалогообложения.ОсобыйПорядок Тогда
			Строка.СистемаНалогообложения = Перечисления.СистемыНалогообложения.Общая;
		КонецЕсли;

		Строка.ПлательщикНалогаНаПрибыль = Строка.СистемаНалогообложения = Перечисления.СистемыНалогообложения.Общая 
			И НЕ ЭтоФизЛицо;

		Строка.ПоддержкаПБУ18 = Строка.ПлательщикНалогаНаПрибыль И Строка.ПоддержкаПБУ18;

		Строка.ПрименяетсяУСН = Строка.СистемаНалогообложения = Перечисления.СистемыНалогообложения.Упрощенная;

		Строка.ПрименяетсяУСНДоходы = Строка.ПрименяетсяУСН
			И Строка.ОбъектНалогообложенияУСН = Перечисления.ОбъектыНалогообложенияПоУСН.Доходы;

		Строка.ПрименяетсяУСНДоходыМинусРасходы = Строка.ПрименяетсяУСН
			И Строка.ОбъектНалогообложенияУСН = Перечисления.ОбъектыНалогообложенияПоУСН.ДоходыМинусРасходы;

		Строка.ПлательщикНДС = Строка.СистемаНалогообложения = Перечисления.СистемыНалогообложения.Общая
			И Строка.ПлательщикНДС;

		Строка.УпрощенныйУчетНДС = Строка.ПлательщикНДС
			И Строка.УпрощенныйУчетНДС;
			
		Строка.РаздельныйУчетНДСДо2014Года = Строка.СложныйУчетНДС
			И НЕ Строка.РаздельныйУчетНДСНаСчете19;
			
		Строка.ПлательщикНДФЛ	= ЭтоФизЛицо
			И Строка.СистемаНалогообложения = Перечисления.СистемыНалогообложения.Общая;
		
		Строка.ПрименяетсяУСНПатент	= ЭтоФизЛицо И Строка.ПрименяетсяУСНПатент;
		
		Если Строка.СистемаНалогообложения = Перечисления.СистемыНалогообложения.ОсобыйПорядок Тогда
			Строка.РозничнаяТорговляОблагаетсяЕНВД			= Истина;
		КонецЕсли;
		
		Строка.ПрименяетсяОсобыйПорядокНалогообложения	=
			(Строка.СистемаНалогообложения = Перечисления.СистемыНалогообложения.ОсобыйПорядок);
		
		Если Строка.ПорядокОтраженияАвансаУСН <> Перечисления.ПорядокОтраженияАвансов.ДоходПатент Тогда
			Строка.ПатентУСН	= Неопределено;
		КонецЕсли;
		
		Если Строка.Период < '20150101' Тогда
		
			Строка.СпособПогашенияСтоимостиСпецодеждыНУ = ПредопределенноеЗначение(
				"Перечисление.СпособыПогашенияСтоимостиНУ.ПриПередачеВЭксплуатацию");

		КонецЕсли;

	КонецЦикла;
	
	Если ДополнительныеСвойства.ВестиУчетПоПодразделениям
		И ДополнительныеСвойства.ГоловныеОрганизации.Количество() > 0 Тогда
		
		ОбособленныеПодразделенияОрганизаций = Новый Соответствие;
		Для каждого ГоловнаяОрганизация Из ДополнительныеСвойства.ГоловныеОрганизации Цикл
			СписокОП = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьСписокОбособленныхПодразделенийОрганизации(ГоловнаяОрганизация);
			ОбособленныеПодразделенияОрганизаций.Вставить(ГоловнаяОрганизация, СписокОП);
		КонецЦикла;
		
		ТаблицаЗаписей = ЭтотОбъект.Выгрузить();
		
		Для каждого ГоловнаяОрганизация Из ДополнительныеСвойства.ГоловныеОрганизации Цикл
			
			Если ОбособленныеПодразделенияОрганизаций[ГоловнаяОрганизация].Количество() = 0 Тогда
				Продолжить;
			КонецЕсли;
			
			ЗаписиПоОрганизации = ТаблицаЗаписей.НайтиСтроки(Новый Структура("Организация", ГоловнаяОрганизация));
			
			Для каждого ЭлементСпискаПодразделений Из ОбособленныеПодразделенияОрганизаций[ГоловнаяОрганизация] Цикл
				
				Подразделение = ЭлементСпискаПодразделений.Значение;
				НаборЗаписейПодразделения = РегистрыСведений.УчетнаяПолитикаОрганизаций.СоздатьНаборЗаписей();
				НаборЗаписейПодразделения.Отбор.Организация.Установить(Подразделение);
				Если ЭтотОбъект.Отбор.Период.Использование Тогда
					НаборЗаписейПодразделения.Отбор.Период.Установить(ЭтотОбъект.Отбор.Период.Значение);
				КонецЕсли;
				
				Для каждого СтрокаТаблицы Из ЗаписиПоОрганизации Цикл
				    НоваяЗапись = НаборЗаписейПодразделения.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяЗапись, СтрокаТаблицы);
					НоваяЗапись.Организация = Подразделение;
				КонецЦикла;
				
				НаборЗаписейПодразделения.Записать(Истина);
				
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЕсли;

КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтотОбъект.Количество() > 0 Тогда
		РегистрыСведений.УчетнаяПолитикаОрганизаций.ОбновитьИспользуемыеСистемыНалогообложения();
		СоздатьПрименяемыеТарифыСтраховыхВзносов();
	КонецЕсли;
	
КонецПроцедуры

Процедура СоздатьПрименяемыеТарифыСтраховыхВзносов()
	
	ВидТарифаОбщийНалоговыйРежим = Справочники.ВидыТарифовСтраховыхВзносов.ОбщийНалоговыйРежим;
	ВидТарифаУпрощенныйНалоговыйРежим = Справочники.ВидыТарифовСтраховыхВзносов.УпрощенныйНалоговыйРежим;
	ВидТарифаУпрощенныйНалоговыйРежимПроизводство = Справочники.ВидыТарифовСтраховыхВзносов.УпрощенныйНалоговыйРежимПроизводство;
	
	Для каждого Строка Из ЭтотОбъект Цикл
		
		// Записи регистра ПрименяемыеТарифыСтраховыхВзносов создаются только по головным организациям
		Если ДополнительныеСвойства.ВестиУчетПоПодразделениям
			И ДополнительныеСвойства.ГоловныеОрганизации.Найти(Строка.Организация) = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПрименяемыеТарифыСтраховыхВзносовСрезПоследних.Период,
		|	ПрименяемыеТарифыСтраховыхВзносовСрезПоследних.ГоловнаяОрганизация,
		|	ПрименяемыеТарифыСтраховыхВзносовСрезПоследних.ВидТарифа
		|ИЗ
		|	РегистрСведений.ПрименяемыеТарифыСтраховыхВзносов.СрезПоследних(&Период, ГоловнаяОрганизация = &Организация) КАК ПрименяемыеТарифыСтраховыхВзносовСрезПоследних";
		Запрос.УстановитьПараметр("Период", Строка.Период);
		Запрос.УстановитьПараметр("Организация", Строка.Организация);
		Выборка = Запрос.Выполнить().Выбрать();
		
		Если Выборка.Количество() > 0 Тогда
			
			Выборка.Следующий();
			
			Если Строка.СистемаНалогообложения = Перечисления.СистемыНалогообложения.Общая
			   И Выборка.ВидТарифа = ВидТарифаОбщийНалоговыйРежим Тогда
			   
				Продолжить; // Изменения в тарифах не требуются.
					
			ИначеЕсли Строка.СистемаНалогообложения = Перечисления.СистемыНалогообложения.Упрощенная
				И (Выборка.ВидТарифа = ВидТарифаУпрощенныйНалоговыйРежим ИЛИ Выборка.ВидТарифа = ВидТарифаУпрощенныйНалоговыйРежимПроизводство) Тогда
				 
			 	Продолжить; // Изменения в тарифах не требуются.
				 
			КонецЕсли;
			
		КонецЕсли;
		
		// Создание (изменение) тарифов.
		
		Если Строка.СистемаНалогообложения = Перечисления.СистемыНалогообложения.Общая Тогда
			 
			Если НЕ ЗначениеЗаполнено(ВидТарифаОбщийНалоговыйРежим) Тогда
				Продолжить;
			КонецЕсли;
			
			ВидТарифа = ВидТарифаОбщийНалоговыйРежим;
			
		ИначеЕсли Строка.СистемаНалогообложения = Перечисления.СистемыНалогообложения.Упрощенная Тогда
			
			Если НЕ ЗначениеЗаполнено(ВидТарифаУпрощенныйНалоговыйРежим) Тогда
				Продолжить;
			КонецЕсли;
		 
			ВидТарифа = ВидТарифаУпрощенныйНалоговыйРежим;
			
		КонецЕсли;
		
		НаборЗаписей = РегистрыСведений.ПрименяемыеТарифыСтраховыхВзносов.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Период.Установить(Строка.Период);
		НаборЗаписей.Отбор.ГоловнаяОрганизация.Установить(Строка.Организация);
		НаборЗаписей.Прочитать();
		НаборЗаписей.Очистить();
		
		Запись = НаборЗаписей.Добавить();
		Запись.ГоловнаяОрганизация = Строка.Организация;
		Запись.Период = Строка.Период;
		Запись.ВидТарифа = ВидТарифа;
		
		НаборЗаписей.ДополнительныеСвойства.Вставить("ПропуститьПроверкуЗапретаИзменения", Истина);		
		НаборЗаписей.Записать();
		
	КонецЦикла;
	
КонецПроцедуры

#КонецЕсли