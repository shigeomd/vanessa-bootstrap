#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Устанавливает первоначальные настройки формирования печатных форм.
//
Процедура НачальноеЗаполнение() Экспорт
	
	Запись = РегистрыСведений.ДополнительныеНастройкиЗарплатаКадры.СоздатьМенеджерЗаписи();
	ЗаполнитьЗначенияСвойств(Запись, НастройкиПечатныхФорм());
	
	Запись.УдалятьПрефиксыОрганизацииИИБИзНомеровКадровыхПриказов = Истина;
	Запись.НастройкаУпорядочиванияСпискаСотрудников = НастройкаУпорядочиванияСпискаСотрудниковПоУмолчанию();
	
	УстановитьНастройки(Запись);
	
КонецПроцедуры

Процедура ЗаполнитьНастройкуУпорядочивания() Экспорт
	
	Запись = РегистрыСведений.ДополнительныеНастройкиЗарплатаКадры.СоздатьМенеджерЗаписи();
	Запись.Прочитать();
	
	Если ПустаяСтрока(Запись.НастройкаУпорядочиванияСпискаСотрудников) Тогда
		Запись.НастройкаУпорядочиванияСпискаСотрудников = НастройкаУпорядочиванияСпискаСотрудниковПоУмолчанию();
		Запись.Записать();
	КонецЕсли; 
	
КонецПроцедуры

// Возвращает настройки печатных форм.
//
Функция НастройкиПечатныхФорм() Экспорт
	
	Запись = РегистрыСведений.ДополнительныеНастройкиЗарплатаКадры.СоздатьМенеджерЗаписи();
	Запись.Прочитать();
	
	Возврат ОбщегоНазначения.СтруктураПоМенеджеруЗаписи(Запись, Метаданные.РегистрыСведений.ДополнительныеНастройкиЗарплатаКадры);
	
КонецФункции

// Сохраняет настройки
//
Процедура УстановитьНастройки(Настройки) Экспорт
	
	Запись = РегистрыСведений.ДополнительныеНастройкиЗарплатаКадры.СоздатьМенеджерЗаписи();
	ЗаполнитьЗначенияСвойств(Запись, Настройки);
	
	Запись.Записать();
	
КонецПроцедуры

// Возвращает массив описаний полей упорядочивания
//
// Параметры:
//		НастройкаУпорядочиванияСпискаСотрудников - Строка, строка с перечисленными полями упорядочивания, если
//													параметр не указывать, то строка будет получена из настроек
//
// Возвращаемое значение;
//		Массив	- Коллекция структур с ключами:
//			* КлючПорядка - Строка, полное имя объекта метаданных по которому производится упорядочивание
//			* ПолеПорядка - Строка, имя реквизита объекта метаданных
//
Функция ПоляУпорядочиванияСпискаСотрудников(НастройкаУпорядочиванияСпискаСотрудников = Неопределено) Экспорт
	
	Если НастройкаУпорядочиванияСпискаСотрудников = Неопределено Тогда
		НастройкаУпорядочиванияСпискаСотрудников = НастройкиПечатныхФорм().НастройкаУпорядочиванияСпискаСотрудников;
	КонецЕсли; 
	
	НастройкаПорядка = Новый Массив;
	МассивПолейПорядка = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(НастройкаУпорядочиванияСпискаСотрудников);
	Для каждого ПолеПорядка Из МассивПолейПорядка Цикл
		
		СловаПорядка = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивСлов(ПолеПорядка);
		
		НомерСлова = 0;
		КлючПорядка = "";
		Пока НомерСлова < СловаПорядка.Количество() - 1 Цикл
			КлючПорядка = ?(ПустаяСтрока(КлючПорядка), "", КлючПорядка + ".") + СловаПорядка[НомерСлова];
			НомерСлова = НомерСлова + 1;
		КонецЦикла; 
		НастройкаПорядка.Добавить(Новый Структура("КлючПорядка,ПолеПорядка", КлючПорядка, СловаПорядка[СловаПорядка.Количество() - 1]));
		
	КонецЦикла;
	
	Возврат НастройкаПорядка;
	
КонецФункции

Функция НастройкаУпорядочиванияСпискаСотрудниковПоУмолчанию()
	Возврат "Справочник.ПодразделенияОрганизаций.РеквизитДопУпорядочиванияИерархического,Справочник.Должности.РеквизитДопУпорядочивания,Справочник.Сотрудники.Наименование";
КонецФункции



#КонецОбласти

#КонецЕсли
