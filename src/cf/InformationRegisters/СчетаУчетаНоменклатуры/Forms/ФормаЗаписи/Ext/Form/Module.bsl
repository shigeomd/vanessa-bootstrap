////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ВидимостьАналитикиУслуги();
	
	ПодготовитьФормуНаСервере();
	
	ОпределятьСчетаРеализацииАвтоматически = НЕ ЗначениеЗаполнено(Запись.СчетДоходовОтРеализации)
		И НЕ ЗначениеЗаполнено(Запись.СчетРасходовОтРеализации);
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура СкладПриИзменении(Элемент)

	Если ЗначениеЗаполнено(Запись.Склад) Тогда
		Запись.ТипСклада = ТипСклада(Запись.Склад);
	Иначе
		Запись.ТипСклада = Неопределено;
	КонецЕсли;
	
	УправлениеФормой(ЭтаФорма);

КонецПроцедуры

&НаКлиенте
Процедура СчетУчетаУслугиПриИзменении(Элемент)

	ВидимостьАналитикиУслуги()

КонецПроцедуры

&НаКлиенте
Процедура СчетУчетаТовараПриИзменении(Элемент)

	ВидимостьАналитикиУслуги();

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере
Процедура ПодготовитьФормуНаСервере()

	УстановитьФункциональныеОпцииФормы();
	УправлениеВидимостью();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьФункциональныеОпцииФормы()

	РаздельныйУчетНДСНаСчете19 = УчетНДСРаздельный.ЕстьУчетнаяПолитикаСРаздельнымУчетомНДСНаСчете19();
	
КонецПроцедуры

&НаСервере
Процедура УправлениеВидимостью()

	Элементы.СпособУчетаНДС.Видимость = РаздельныйУчетНДСНаСчете19;

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)

	Элементы = Форма.Элементы;
	Запись   = Форма.Запись;
	
	Элементы.ТипСклада.Доступность = НЕ ЗначениеЗаполнено(Запись.Склад);
	Элементы.СчетДоходовОтРеализации.Доступность  = НЕ Форма.ОпределятьСчетаРеализацииАвтоматически;
	Элементы.СчетРасходовОтРеализации.Доступность = НЕ Форма.ОпределятьСчетаРеализацииАвтоматически;

КонецПроцедуры

&НаСервере
Процедура ВидимостьАналитикиУслуги()

	Если ЗначениеЗаполнено(Запись.СчетУчета) Тогда
		
		СвойстваСчета = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Запись.СчетУчета);
		
		ЭтоУслуга = НЕ СвойстваСчета.Количественный;
		
	Иначе
		ЭтоУслуга = Ложь;
	КонецЕсли;
	
	Если ЭтоУслуга Тогда
		Элементы.ГруппаСтраницыТовараИУслуги.ТекущаяСтраница = Элементы.ГруппаСтраницаУслуги;
		Элементы.СчетУчетаУслуги.АктивизироватьПоУмолчанию = Истина;
		
	Иначе
		Элементы.ГруппаСтраницыТовараИУслуги.ТекущаяСтраница = Элементы.ГруппаСтраницаТовара;
		Элементы.СчетУчетаТовара.АктивизироватьПоУмолчанию = Истина;
	КонецЕсли;
	
	УстановитьЗаголовкиИДоступностьСубконто();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьЗаголовкиИДоступностьСубконто()

	ПоляФормы = Новый Структура("Субконто1, Субконто2, Субконто3",
		"Субконто1", "Субконто2", "Субконто3");
	
	ЗаголовкиПолей = Новый Структура("Субконто1, Субконто2, Субконто3",
		"ЗаголовокСубконто1", "ЗаголовокСубконто2", "ЗаголовокСубконто3");
	
	БухгалтерскийУчетКлиентСервер.ПриВыбореСчета(Запись.СчетУчета, ЭтаФорма, ПоляФормы, ЗаголовкиПолей);

КонецПроцедуры

&НаСервереБезКонтекста
Функция ТипСклада(Ссылка)
	
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "ТипСклада");
	
КонецФункции

&НаКлиенте
Процедура ОпределятьСчетаРеализацииАвтоматическиПриИзменении(Элемент)
	
	Если ОпределятьСчетаРеализацииАвтоматически Тогда
		Запись.СчетДоходовОтРеализации  = Неопределено;
		Запись.СчетРасходовОтРеализации = Неопределено;
	КонецЕсли;
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры
