#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

// Ограничение: у всех таких налогов налоговый период - год, отчетный период - квартал.

Функция НалогиПорядокКоторыхУстанавливаетсяНаМестах() Экспорт
	
	Налоги = Новый Массив;
	Налоги.Добавить("НалогНаИмущество");
	Налоги.Добавить("ЗемельныйНалог");
	Налоги.Добавить("ТранспортныйНалог");
	// См. также МестныеНалогиВРазрезеНалоговыхОрганов()
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Налоги", Налоги);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЗадачиБухгалтера.Ссылка
	|ИЗ
	|	Справочник.ЗадачиБухгалтера КАК ЗадачиБухгалтера
	|ГДЕ
	|	ЗадачиБухгалтера.Код В(&Налоги)";
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
КонецФункции

Процедура СоздатьНалоговыеОрганыСУстановленнойУплатойАвансов(МенеджерВременныхТаблиц, Организация, Период, Налог) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Период",      Период);
	Запрос.УстановитьПараметр("Налог",       Налог);
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗадачиБухгалтера.Ссылка КАК Налог
	|ПОМЕСТИТЬ ВТ_Налог
	|ИЗ
	|	Справочник.ЗадачиБухгалтера КАК ЗадачиБухгалтера
	|ГДЕ
	|	ЗадачиБухгалтера.Код = &Налог
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Налог
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПорядокУплатыНалоговНаМестахСрезПоследних.Налог КАК Налог,
	|	ПорядокУплатыНалоговНаМестахСрезПоследних.РегистрацияВНалоговомОргане КАК НалоговыйОрган
	|ПОМЕСТИТЬ ВТ_ПорядокУплаты
	|ИЗ
	|	РегистрСведений.ПорядокУплатыНалоговНаМестах.СрезПоследних(
	|			&Период,
	|			Организация = &Организация
	|				И УплачиваютсяАвансы) КАК ПорядокУплатыНалоговНаМестахСрезПоследних
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Налог
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ПорядокУплаты.НалоговыйОрган КАК НалоговыйОрган
	|ПОМЕСТИТЬ ВТ_НалоговыеОрганыСУстановленнойУплатойАвансов
	|ИЗ
	|	ВТ_ПорядокУплаты КАК ВТ_ПорядокУплаты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_Налог КАК ВТ_Налог
	|		ПО ВТ_ПорядокУплаты.Налог = ВТ_Налог.Налог
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НалоговыйОрган
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_Налог
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_ПорядокУплаты";
	
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура СоздатьМестныеНалогиВРазрезеНалоговыхОрганов(МенеджерВременныхТаблиц, Организация) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	НачалоПериода = НачалоГода(ДобавитьМесяц(ТекущаяДатаСеанса(), -3)) - 1;
	КонецПериода = КонецГода(ТекущаяДатаСеанса());
	
	Запрос.УстановитьПараметр("НачалоПериода",     НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода",      КонецПериода);
	Запрос.УстановитьПараметр("Организация",       Организация);
	Запрос.УстановитьПараметр("НалогНаИмущество",  "НалогНаИмущество");
	Запрос.УстановитьПараметр("ЗемельныйНалог",    "ЗемельныйНалог");
	Запрос.УстановитьПараметр("ТранспортныйНалог", "ТранспортныйНалог");
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЗадачиБухгалтера.Код КАК Налог,
	|	ЗадачиБухгалтера.Ссылка КАК Задача
	|ПОМЕСТИТЬ НалогиНаМестах
	|ИЗ
	|	Справочник.ЗадачиБухгалтера КАК ЗадачиБухгалтера
	|ГДЕ
	|	ЗадачиБухгалтера.Код В (&НалогНаИмущество, &ЗемельныйНалог, &ТранспортныйНалог)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Налог
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&НалогНаИмущество КАК Налог,
	|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.НалоговыйОрган КАК НалоговыйОрган
	|ПОМЕСТИТЬ НалоговыеОрганы
	|ИЗ
	|	РегистрСведений.СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.СрезПоследних(
	|			&КонецПериода,
	|			Организация = &Организация
	|				И ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)) КАК СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&НалогНаИмущество,
	|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.НалоговыйОрган
	|ИЗ
	|	РегистрСведений.СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам КАК СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам
	|ГДЕ
	|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.Организация = &Организация
	|	И СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
	|	И СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&ЗемельныйНалог,
	|	РегистрацияЗемельныхУчастковСрезПоследних.НалоговыйОрган
	|ИЗ
	|	РегистрСведений.РегистрацияЗемельныхУчастков.СрезПоследних(
	|			&КонецПериода,
	|			Организация = &Организация
	|				И ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)) КАК РегистрацияЗемельныхУчастковСрезПоследних
	|ГДЕ
	|	РегистрацияЗемельныхУчастковСрезПоследних.ВключатьВНалоговуюБазу = ИСТИНА
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&ЗемельныйНалог,
	|	РегистрацияЗемельныхУчастков.НалоговыйОрган
	|ИЗ
	|	РегистрСведений.РегистрацияЗемельныхУчастков КАК РегистрацияЗемельныхУчастков
	|ГДЕ
	|	РегистрацияЗемельныхУчастков.Организация = &Организация
	|	И РегистрацияЗемельныхУчастков.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
	|	И РегистрацияЗемельныхУчастков.ВключатьВНалоговуюБазу = ИСТИНА
	|	И РегистрацияЗемельныхУчастков.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&ТранспортныйНалог,
	|	РегистрацияТранспортныхСредствСрезПоследних.НалоговыйОрган
	|ИЗ
	|	РегистрСведений.РегистрацияТранспортныхСредств.СрезПоследних(
	|			&КонецПериода,
	|			Организация = &Организация
	|				И ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)) КАК РегистрацияТранспортныхСредствСрезПоследних
	|ГДЕ
	|	РегистрацияТранспортныхСредствСрезПоследних.ВключатьВНалоговуюБазу = ИСТИНА
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&ТранспортныйНалог,
	|	РегистрацияТранспортныхСредств.НалоговыйОрган
	|ИЗ
	|	РегистрСведений.РегистрацияТранспортныхСредств КАК РегистрацияТранспортныхСредств
	|ГДЕ
	|	РегистрацияТранспортныхСредств.Организация = &Организация
	|	И РегистрацияТранспортныхСредств.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
	|	И РегистрацияТранспортныхСредств.ВключатьВНалоговуюБазу = ИСТИНА
	|	И РегистрацияТранспортныхСредств.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Налог
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НалогиНаМестах.Задача КАК Налог,
	|	Организации.РегистрацияВНалоговомОргане КАК НалоговыйОрган
	|ПОМЕСТИТЬ МестныеНалогиВРазрезеНалоговыхОрганов
	|ИЗ
	|	НалогиНаМестах КАК НалогиНаМестах,
	|	Справочник.Организации КАК Организации
	|ГДЕ
	|	Организации.Ссылка = &Организация
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	НалогиНаМестах.Задача,
	|	НалоговыеОрганы.НалоговыйОрган
	|ИЗ
	|	НалогиНаМестах КАК НалогиНаМестах
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ НалоговыеОрганы КАК НалоговыеОрганы
	|		ПО НалогиНаМестах.Налог = НалоговыеОрганы.Налог
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Налог,
	|	НалоговыйОрган
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ НалогиНаМестах
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ НалоговыеОрганы";
	
	Запрос.Выполнить();
	
КонецПроцедуры

Функция ПорядокУплатыРегиональныхМестныхНалогов(Организация, Период) Экспорт
	
	МетаданныеРегистра = Метаданные.РегистрыСведений.ПорядокУплатыНалоговНаМестах;
	
	ПорядокУплаты = Новый ТаблицаЗначений;
	ПорядокУплаты.Колонки.Добавить("Период",                      Новый ОписаниеТипов("Дата"));
	ПорядокУплаты.Колонки.Добавить("Налог",                       МетаданныеРегистра.Измерения.Налог.Тип);
	ПорядокУплаты.Колонки.Добавить("РегистрацияВНалоговомОргане", МетаданныеРегистра.Измерения.РегистрацияВНалоговомОргане.Тип);
	Для Каждого Ресурс Из МетаданныеРегистра.Ресурсы Цикл
		ПорядокУплаты.Колонки.Добавить(Ресурс.Имя, Ресурс.Тип);
	КонецЦикла;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц; // Хранит МестныеНалогиВРазрезеНалоговыхОрганов
	СоздатьМестныеНалогиВРазрезеНалоговыхОрганов(МенеджерВременныхТаблиц, Организация);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Организация",         Организация);
	Запрос.УстановитьПараметр("Период",              Период);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПорядокУплатыНалоговНаМестах.Период КАК Период,
	|	ПорядокУплатыНалоговНаМестах.Налог КАК Налог,
	|	ПорядокУплатыНалоговНаМестах.РегистрацияВНалоговомОргане КАК НалоговыйОрган,
	|	ПорядокУплатыНалоговНаМестах.СрокУплатыНалогаМесяцев,
	|	ПорядокУплатыНалоговНаМестах.СрокУплатыНалогаДней,
	|	ПорядокУплатыНалоговНаМестах.УплачиваютсяАвансы,
	|	ПорядокУплатыНалоговНаМестах.СрокУплатыАвансаМесяцев,
	|	ПорядокУплатыНалоговНаМестах.СрокУплатыАвансаДней,
	|	ПорядокУплатыНалоговНаМестах.Описание
	|ПОМЕСТИТЬ ПравилаЗаданыПользователем
	|ИЗ
	|	РегистрСведений.ПорядокУплатыНалоговНаМестах.СрезПоследних(&Период, Организация = &Организация) КАК ПорядокУплатыНалоговНаМестах
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Налог,
	|	НалоговыйОрган
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МестныеНалогиВРазрезеНалоговыхОрганов.Налог КАК Налог,
	|	МестныеНалогиВРазрезеНалоговыхОрганов.НалоговыйОрган
	|ПОМЕСТИТЬ НалогиПоФедеральнымПравилам
	|ИЗ
	|	МестныеНалогиВРазрезеНалоговыхОрганов КАК МестныеНалогиВРазрезеНалоговыхОрганов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПравилаЗаданыПользователем КАК ПравилаЗаданыПользователем
	|		ПО МестныеНалогиВРазрезеНалоговыхОрганов.Налог = ПравилаЗаданыПользователем.Налог
	|			И МестныеНалогиВРазрезеНалоговыхОрганов.НалоговыйОрган = ПравилаЗаданыПользователем.НалоговыйОрган
	|ГДЕ
	|	ПравилаЗаданыПользователем.Налог ЕСТЬ NULL 
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Налог
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПравилаЗаданыПользователем.Период КАК Период,
	|	МестныеНалогиВРазрезеНалоговыхОрганов.Налог КАК Налог,
	|	МестныеНалогиВРазрезеНалоговыхОрганов.НалоговыйОрган КАК РегистрацияВНалоговомОргане,
	|	ПравилаЗаданыПользователем.СрокУплатыНалогаМесяцев,
	|	ПравилаЗаданыПользователем.СрокУплатыНалогаДней,
	|	ПравилаЗаданыПользователем.УплачиваютсяАвансы,
	|	ПравилаЗаданыПользователем.СрокУплатыАвансаМесяцев,
	|	ПравилаЗаданыПользователем.СрокУплатыАвансаДней,
	|	ПравилаЗаданыПользователем.Описание
	|ИЗ
	|	МестныеНалогиВРазрезеНалоговыхОрганов КАК МестныеНалогиВРазрезеНалоговыхОрганов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПравилаЗаданыПользователем КАК ПравилаЗаданыПользователем
	|		ПО МестныеНалогиВРазрезеНалоговыхОрганов.Налог = ПравилаЗаданыПользователем.Налог
	|			И МестныеНалогиВРазрезеНалоговыхОрганов.НалоговыйОрган = ПравилаЗаданыПользователем.НалоговыйОрган
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НалогиПоФедеральнымПравилам.Налог КАК Налог,
	|	НалогиПоФедеральнымПравилам.НалоговыйОрган КАК РегистрацияВНалоговомОргане,
	|	ВЫБОР
	|		КОГДА ПравилаПредставленияОтчетовУплатыНалогов.ФинансовыйПериод = ПравилаПредставленияОтчетовУплатыНалогов.Периодичность
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК Аванс,
	|	ПравилаПредставленияОтчетовУплатыНалогов.СрокМесяцев,
	|	ПравилаПредставленияОтчетовУплатыНалогов.СрокДней
	|ИЗ
	|	НалогиПоФедеральнымПравилам КАК НалогиПоФедеральнымПравилам
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПравилаПредставленияОтчетовУплатыНалогов КАК ПравилаПредставленияОтчетовУплатыНалогов
	|		ПО НалогиПоФедеральнымПравилам.Налог = ПравилаПредставленияОтчетовУплатыНалогов.Владелец
	|ГДЕ
	|	ПравилаПредставленияОтчетовУплатыНалогов.НачалоДействия <= &Период
	|	И (ПравилаПредставленияОтчетовУплатыНалогов.КонецДействия = ДАТАВРЕМЯ(1, 1, 1)
	|			ИЛИ ПравилаПредставленияОтчетовУплатыНалогов.КонецДействия >= &Период)
	|	И НЕ ПравилаПредставленияОтчетовУплатыНалогов.ПометкаУдаления
	|	И ПравилаПредставленияОтчетовУплатыНалогов.Действие = ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.УплатаНалога)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПравилаПредставленияОтчетовУплатыНалогов.Ссылка
	|ИТОГИ ПО
	|	Налог,
	|	РегистрацияВНалоговомОргане,
	|	Аванс";
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	// Правила, заданные пользователем
	Выборка = РезультатЗапроса[2].Выбрать();
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(ПорядокУплаты.Добавить(), Выборка);
	КонецЦикла;
	
	// Значения по умолчанию
	ВыборкаНалоги = РезультатЗапроса[3].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаНалоги.Следующий() Цикл
		
		ВыборкаРегистрацияВНалоговомОргане = ВыборкаНалоги.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаРегистрацияВНалоговомОргане.Следующий() Цикл
		
			НоваяСтрока = ПорядокУплаты.Добавить();
			НоваяСтрока.Период = НачалоГода(ТекущаяДатаСеанса());
			НоваяСтрока.Налог  = ВыборкаНалоги.Налог;
			НоваяСтрока.УплачиваютсяАвансы = Ложь;
			НоваяСтрока.РегистрацияВНалоговомОргане = ВыборкаРегистрацияВНалоговомОргане.РегистрацияВНалоговомОргане;
			
			ВыборкаАванс = ВыборкаРегистрацияВНалоговомОргане.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаАванс.Следующий() Цикл
				
				Выборка = ВыборкаАванс.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				Пока Выборка.Следующий() Цикл
					
					Если Выборка.Аванс Тогда
						НоваяСтрока.СрокУплатыАвансаМесяцев = Выборка.СрокМесяцев;
						НоваяСтрока.СрокУплатыАвансаДней    = Выборка.СрокДней;
					Иначе
						НоваяСтрока.СрокУплатыНалогаМесяцев = Выборка.СрокМесяцев;
						НоваяСтрока.СрокУплатыНалогаДней    = Выборка.СрокДней;
					КонецЕсли;
					УстановитьОписание(НоваяСтрока);
					Прервать;
				КонецЦикла;
				
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат ПорядокУплаты;
	
КонецФункции

Процедура УстановитьОписание(Запись) Экспорт
	
	День = 24 * 60 * 60;
	
	Описание = Новый Массив;
	
	Если Запись.СрокУплатыНалогаМесяцев = 0 И Запись.СрокУплатыНалогаДней = 0 Тогда
		СрокУплатыНалога = НСтр("ru = 'Срок уплаты налога не установлен'");
	ИначеЕсли Запись.СрокУплатыНалогаМесяцев = 0 И Запись.СрокУплатыНалогаДней > 31 Тогда
		СрокУплатыНалога = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Срок уплаты налога - %1'"),
			СтроковыеФункцииКлиентСервер.ЧислоЦифрамиПредметИсчисленияПрописью(Запись.СрокУплатыНалогаДней, "день,дня,дней"));
	Иначе
		Дата = ДобавитьМесяц('20000101', Запись.СрокУплатыНалогаМесяцев) + Запись.СрокУплатыНалогаДней * День - 1;
		СрокУплатыНалога = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Срок уплаты налога - %1'"),
			Формат(Дата, "ДФ='dd MMMM'"));
	КонецЕсли;
	Описание.Добавить(СрокУплатыНалога);
	
	Если Не Запись.УплачиваютсяАвансы Тогда
		СрокУплатыАванса = НСтр("ru = 'Авансы не уплачиваются'");
	Иначе
		
		Если Запись.СрокУплатыАвансаМесяцев = 0 Тогда
			СтрокаСрокУплатыАвансаМесяцев = НСтр("");
		Иначе
			СтрокаСрокУплатыАвансаМесяцев = СтроковыеФункцииКлиентСервер.ЧислоЦифрамиПредметИсчисленияПрописью(
				Запись.СрокУплатыАвансаМесяцев, 
				НСтр("ru = 'месяц,месяца,месяцев'"));
		КонецЕсли;
		
		Если Запись.СрокУплатыАвансаДней = 0 Тогда
			СтрокаСрокУплатыАвансаДней = НСтр("");
		Иначе
			СтрокаСрокУплатыАвансаДней = СтроковыеФункцииКлиентСервер.ЧислоЦифрамиПредметИсчисленияПрописью(
				Запись.СрокУплатыАвансаДней, 
				НСтр("ru = 'день,дня,дней'"));
		КонецЕсли;
		
		СрокУплатыАванса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			СокрП(НСтр("ru = 'Срок уплаты авансов - %1 %2'")),
			СтрокаСрокУплатыАвансаМесяцев,
			СтрокаСрокУплатыАвансаДней);
	КонецЕсли;
	Описание.Добавить(СрокУплатыАванса);
	
	Запись.Описание = СтроковыеФункцииКлиентСервер.СтрокаИзМассиваПодстрок(Описание, "; ");
	
КонецПроцедуры

Процедура СоздатьПравилаУплатыНалоговНаМестах(МенеджерВременныхТаблиц, Организация) Экспорт
	
	СоздатьМестныеНалогиВРазрезеНалоговыхОрганов(МенеджерВременныхТаблиц, Организация);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПравилаПредставленияОтчетовУплатыНалогов.Владелец КАК Налог,
	|	ПравилаПредставленияОтчетовУплатыНалогов.Ссылка КАК Ссылка,
	|	ПравилаПредставленияОтчетовУплатыНалогов.ФинансовыйПериод КАК ФинансовыйПериод,
	|	ПравилаПредставленияОтчетовУплатыНалогов.Периодичность КАК Периодичность,
	|	ПравилаПредставленияОтчетовУплатыНалогов.Действие КАК Действие,
	|	ПравилаПредставленияОтчетовУплатыНалогов.СрокМесяцев КАК СрокМесяцев,
	|	ПравилаПредставленияОтчетовУплатыНалогов.СрокДней КАК СрокДней,
	|	МестныеНалогиВРазрезеНалоговыхОрганов.НалоговыйОрган КАК РегистрацияВНалоговомОргане
	|ПОМЕСТИТЬ Правила
	|ИЗ
	|	МестныеНалогиВРазрезеНалоговыхОрганов КАК МестныеНалогиВРазрезеНалоговыхОрганов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПравилаПредставленияОтчетовУплатыНалогов КАК ПравилаПредставленияОтчетовУплатыНалогов
	|		ПО МестныеНалогиВРазрезеНалоговыхОрганов.Налог = ПравилаПредставленияОтчетовУплатыНалогов.Владелец
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	РегистрацияВНалоговомОргане,
	|	Налог
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Правила.Ссылка КАК Правило,
	|	Правила.РегистрацияВНалоговомОргане,
	|	ЕСТЬNULL(ПорядокУплатыНалоговНаМестах.Период, ДАТАВРЕМЯ(1, 1, 1)) КАК ПериодДействияПравила,
	|	ИСТИНА КАК Включено,
	|	ЕСТЬNULL(ПорядокУплатыНалоговНаМестах.СрокУплатыНалогаМесяцев, Правила.СрокМесяцев) КАК СрокМесяцев,
	|	ЕСТЬNULL(ПорядокУплатыНалоговНаМестах.СрокУплатыНалогаДней, Правила.СрокДней) КАК СрокДней
	|ПОМЕСТИТЬ ПравилаУплатыНалоговНаМестах
	|ИЗ
	|	Правила КАК Правила
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокУплатыНалоговНаМестах КАК ПорядокУплатыНалоговНаМестах
	|		ПО Правила.РегистрацияВНалоговомОргане = ПорядокУплатыНалоговНаМестах.РегистрацияВНалоговомОргане
	|			И Правила.Налог = ПорядокУплатыНалоговНаМестах.Налог
	|ГДЕ
	|	Правила.ФинансовыйПериод = Правила.Периодичность
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Правила.Ссылка,
	|	Правила.РегистрацияВНалоговомОргане,
	|	ЕСТЬNULL(ПорядокУплатыНалоговНаМестах.Период, ДАТАВРЕМЯ(1, 1, 1)),
	|	ЕСТЬNULL(ПорядокУплатыНалоговНаМестах.УплачиваютсяАвансы, ЛОЖЬ),
	|	ЕСТЬNULL(ПорядокУплатыНалоговНаМестах.СрокУплатыАвансаМесяцев, Правила.СрокМесяцев),
	|	ЕСТЬNULL(ПорядокУплатыНалоговНаМестах.СрокУплатыАвансаДней, Правила.СрокДней)
	|ИЗ
	|	Правила КАК Правила
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокУплатыНалоговНаМестах КАК ПорядокУплатыНалоговНаМестах
	|		ПО Правила.РегистрацияВНалоговомОргане = ПорядокУплатыНалоговНаМестах.РегистрацияВНалоговомОргане
	|			И Правила.Налог = ПорядокУплатыНалоговНаМестах.Налог
	|ГДЕ
	|	Правила.ФинансовыйПериод <> Правила.Периодичность
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Правило,
	|	ПериодДействияПравила
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ Правила
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ МестныеНалогиВРазрезеНалоговыхОрганов";
		
	Запрос.Выполнить();
	
КонецПроцедуры

// ОБНОВЛЕНИЕ ИНФОРМАЦИОННОЙ БАЗЫ

// Вместо общей записи с пустым налоговым органом создаются записи по каждому налоговому
// органу организации с параметрами общей записи.
//
Процедура ПреобразоватьЗаписиСПустымНалоговымОрганом() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПорядокУплатыНалоговНаМестах.Период,
	|	ПорядокУплатыНалоговНаМестах.Организация КАК Организация,
	|	ПорядокУплатыНалоговНаМестах.Налог,
	|	РегистрацииВНалоговомОргане.Ссылка КАК РегистрацияВНалоговомОргане,
	|	ПорядокУплатыНалоговНаМестах.СрокУплатыНалогаМесяцев,
	|	ПорядокУплатыНалоговНаМестах.СрокУплатыНалогаДней,
	|	ПорядокУплатыНалоговНаМестах.УплачиваютсяАвансы,
	|	ПорядокУплатыНалоговНаМестах.СрокУплатыАвансаМесяцев,
	|	ПорядокУплатыНалоговНаМестах.СрокУплатыАвансаДней,
	|	ПорядокУплатыНалоговНаМестах.Описание
	|ПОМЕСТИТЬ ВТ_ОбщиеЗаписи
	|ИЗ
	|	Справочник.РегистрацииВНалоговомОргане КАК РегистрацииВНалоговомОргане
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокУплатыНалоговНаМестах КАК ПорядокУплатыНалоговНаМестах
	|		ПО РегистрацииВНалоговомОргане.Владелец = ПорядокУплатыНалоговНаМестах.Организация
	|ГДЕ
	|	ПорядокУплатыНалоговНаМестах.РегистрацияВНалоговомОргане = ЗНАЧЕНИЕ(Справочник.РегистрацииВНалоговомОргане.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ОбщиеЗаписи.Период,
	|	ВТ_ОбщиеЗаписи.Организация КАК Организация,
	|	ВТ_ОбщиеЗаписи.Налог,
	|	ВТ_ОбщиеЗаписи.РегистрацияВНалоговомОргане,
	|	ВТ_ОбщиеЗаписи.СрокУплатыНалогаМесяцев,
	|	ВТ_ОбщиеЗаписи.СрокУплатыНалогаДней,
	|	ВТ_ОбщиеЗаписи.УплачиваютсяАвансы,
	|	ВТ_ОбщиеЗаписи.СрокУплатыАвансаМесяцев,
	|	ВТ_ОбщиеЗаписи.СрокУплатыАвансаДней,
	|	ВТ_ОбщиеЗаписи.Описание,
	|	ВЫБОР
	|		КОГДА ПорядокУплатыНалоговНаМестах.РегистрацияВНалоговомОргане <> ЗНАЧЕНИЕ(Справочник.РегистрацииВНалоговомОргане.ПустаяСсылка)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК НаличиеЗаписи
	|ПОМЕСТИТЬ ВТ_ОбщиеЗаписиСИндикаторомНаличия
	|ИЗ
	|	ВТ_ОбщиеЗаписи КАК ВТ_ОбщиеЗаписи
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокУплатыНалоговНаМестах КАК ПорядокУплатыНалоговНаМестах
	|		ПО ВТ_ОбщиеЗаписи.Период = ПорядокУплатыНалоговНаМестах.Период
	|			И ВТ_ОбщиеЗаписи.Организация = ПорядокУплатыНалоговНаМестах.Организация
	|			И ВТ_ОбщиеЗаписи.Налог = ПорядокУплатыНалоговНаМестах.Налог
	|			И ВТ_ОбщиеЗаписи.РегистрацияВНалоговомОргане = ПорядокУплатыНалоговНаМестах.РегистрацияВНалоговомОргане
	|			И (ПорядокУплатыНалоговНаМестах.РегистрацияВНалоговомОргане <> ЗНАЧЕНИЕ(Справочник.РегистрацииВНалоговомОргане.ПустаяСсылка))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ОбщиеЗаписиСИндикаторомНаличия.Период,
	|	ВТ_ОбщиеЗаписиСИндикаторомНаличия.Организация КАК Организация,
	|	ВТ_ОбщиеЗаписиСИндикаторомНаличия.Налог,
	|	ВТ_ОбщиеЗаписиСИндикаторомНаличия.РегистрацияВНалоговомОргане,
	|	ВТ_ОбщиеЗаписиСИндикаторомНаличия.СрокУплатыНалогаМесяцев,
	|	ВТ_ОбщиеЗаписиСИндикаторомНаличия.СрокУплатыНалогаДней,
	|	ВТ_ОбщиеЗаписиСИндикаторомНаличия.УплачиваютсяАвансы,
	|	ВТ_ОбщиеЗаписиСИндикаторомНаличия.СрокУплатыАвансаМесяцев,
	|	ВТ_ОбщиеЗаписиСИндикаторомНаличия.СрокУплатыАвансаДней,
	|	ВТ_ОбщиеЗаписиСИндикаторомНаличия.Описание
	|ИЗ
	|	ВТ_ОбщиеЗаписиСИндикаторомНаличия КАК ВТ_ОбщиеЗаписиСИндикаторомНаличия
	|ГДЕ
	|	ВТ_ОбщиеЗаписиСИндикаторомНаличия.НаличиеЗаписи = ЛОЖЬ";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	// Создание записей по налоговым органам.
	НаборЗаписей = РегистрыСведений.ПорядокУплатыНалоговНаМестах.СоздатьНаборЗаписей();
	НаборЗаписей.Прочитать();
	Пока Выборка.Следующий() Цикл
		НоваяЗапись = НаборЗаписей.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяЗапись, Выборка);
	КонецЦикла;
	ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписей);
	
	// Удаление записей с пустым налоговым органом.
	НаборЗаписей = РегистрыСведений.ПорядокУплатыНалоговНаМестах.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.РегистрацияВНалоговомОргане.Установить(Справочники.РегистрацииВНалоговомОргане.ПустаяСсылка());
	НаборЗаписей.Прочитать();
	НаборЗаписей.Очистить();
	ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписей);
	
КонецПроцедуры

#КонецЕсли
