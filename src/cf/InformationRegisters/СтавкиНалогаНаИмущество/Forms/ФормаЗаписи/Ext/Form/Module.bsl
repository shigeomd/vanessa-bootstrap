////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ
//

&НаКлиенте
Перем СтруктураСохраняемыхРеквизитов;

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ
//

&НаКлиенте
Процедура СохранитьЗначениеРеквизитаФормы(ИмяРеквизита, Форма, СтруктураРеквизитов)

	Если НЕ Форма.Элементы[ИмяРеквизита].Доступность Тогда
		Возврат;
	КонецЕсли;

	СтруктураРеквизитов.Вставить(ИмяРеквизита, Запись[ИмяРеквизита]);

	Запись[ИмяРеквизита] = ОбщегоНазначенияБПКлиентСервер.ПустоеЗначениеТипа(ТипЗнч(Запись[ИмяРеквизита]));

КонецПроцедуры

&НаКлиенте
Процедура ВосстановитьЗначениеРеквизитаФормы(ИмяРеквизита, Форма, СтруктураРеквизитов)

	Если СтруктураРеквизитов.Свойство(ИмяРеквизита) Тогда
		Запись[ИмяРеквизита] = СтруктураРеквизитов[ИмяРеквизита];
	КонецЕсли;

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)

	Элементы = Форма.Элементы;
	Объект   = Форма.Запись;

	Элементы.КодНалоговойЛьготыОсвобождениеОтНалогообложения.Доступность               = Объект.ОсвобождениеОтНалогообложения;
	Элементы.КодНалоговойЛьготыОсвобождениеОтНалогообложения.АвтоОтметкаНезаполненного = Объект.ОсвобождениеОтНалогообложения;
	Элементы.КодНалоговойЛьготыОсвобождениеОтНалогообложения.ОтметкаНезаполненного     = Объект.ОсвобождениеОтНалогообложения И ПустаяСтрока(Объект.КодНалоговойЛьготыОсвобождениеОтНалогообложения);

	Элементы.СнижениеНалоговойСтавки.Доступность = НЕ Объект.ОсвобождениеОтНалогообложения;

	Элементы.СниженнаяНалоговаяСтавка.Доступность                 = НЕ Объект.ОсвобождениеОтНалогообложения И Объект.СнижениеНалоговойСтавки;
	Элементы.СниженнаяНалоговаяСтавка.АвтоОтметкаНезаполненного   = НЕ Объект.ОсвобождениеОтНалогообложения И Объект.СнижениеНалоговойСтавки;
	Элементы.СниженнаяНалоговаяСтавка.ОтметкаНезаполненного       = НЕ Объект.ОсвобождениеОтНалогообложения И Объект.СнижениеНалоговойСтавки И (Объект.СниженнаяНалоговаяСтавка = 0);
	Элементы.ДекорацияСниженнаяНалоговаяСтавкаПроцент.Доступность = НЕ Объект.ОсвобождениеОтНалогообложения И Объект.СнижениеНалоговойСтавки;

	Элементы.УменьшениеСуммыНалогаВПроцентах.Доступность = НЕ Объект.ОсвобождениеОтНалогообложения;

	Элементы.ПроцентУменьшения.Доступность                 = НЕ Объект.ОсвобождениеОтНалогообложения И Объект.УменьшениеСуммыНалогаВПроцентах;
	Элементы.ПроцентУменьшения.АвтоОтметкаНезаполненного   = НЕ Объект.ОсвобождениеОтНалогообложения И Объект.УменьшениеСуммыНалогаВПроцентах;
	Элементы.ПроцентУменьшения.ОтметкаНезаполненного       = НЕ Объект.ОсвобождениеОтНалогообложения И Объект.УменьшениеСуммыНалогаВПроцентах И (Объект.ПроцентУменьшения = 0);
	Элементы.ДекорацияПроцентУменьшенияПроцент.Доступность = НЕ Объект.ОсвобождениеОтНалогообложения И Объект.УменьшениеСуммыНалогаВПроцентах;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ
//

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Параметры.Ключ.Пустой() Тогда
		Запись.Период = НачалоГода(ОбщегоНазначенияБП.ПолучитьРабочуюДату());
		Если Запись.Организация.Пустая() Тогда
			Запись.Организация = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновнаяОрганизация");
		КонецЕсли;
		Если Запись.НалоговаяСтавка = 0 Тогда
			Запись.НалоговаяСтавка = 2.2;
		КонецЕсли;
	КонецЕсли;

	// Установить видимость/доступность реквизитов и заголовков колонок.
	УправлениеФормой(ЭтаФорма);

КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)

	Если (НЕ Отказ) И (НЕ ТекущийОбъект.Выбран()) Тогда

		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Период",      Запись.Период);
		Запрос.УстановитьПараметр("Организация", Запись.Организация);
		Запрос.Текст = "
			|ВЫБРАТЬ
			|	ПРЕДСТАВЛЕНИЕ(СтавкиНалогаНаИмущество.НалоговаяСтавка) КАК НалоговаяСтавка
			|ИЗ
			|	РегистрСведений.СтавкиНалогаНаИмущество КАК СтавкиНалогаНаИмущество
			|ГДЕ
			|	СтавкиНалогаНаИмущество.Период = &Период
			|	И СтавкиНалогаНаИмущество.Организация = &Организация
			|";
		Выборка = Запрос.Выполнить().Выбрать();

		Если Выборка.Количество() > 0 Тогда
			Выборка.Следующий();
			ТекстСообщения = НСтр("ru = 'На %Период% в организации <%Организация%> уже введена установка ставки налога на имущество!'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Период%", Формат(Запись.Период, "ДФ=dd.MM.yyyy"));
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Организация%", СокрЛП(Запись.Организация));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "Организация", "Запись", Отказ);
		КонецЕсли;

	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	Справочники.ВидыНалоговИПлатежейВБюджет.СоздатьПоставляемыеЭлементы();
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОсвобождениеОтНалогообложенияПриИзменении(Элемент)

	Если Запись.ОсвобождениеОтНалогообложения Тогда
		ВосстановитьЗначениеРеквизитаФормы("КодНалоговойЛьготыОсвобождениеОтНалогообложения", ЭтаФорма, СтруктураСохраняемыхРеквизитов);
		СохранитьЗначениеРеквизитаФормы("СнижениеНалоговойСтавки",  ЭтаФорма, СтруктураСохраняемыхРеквизитов);
		СохранитьЗначениеРеквизитаФормы("СниженнаяНалоговаяСтавка", ЭтаФорма, СтруктураСохраняемыхРеквизитов);
		СохранитьЗначениеРеквизитаФормы("УменьшениеСуммыНалогаВПроцентах", ЭтаФорма, СтруктураСохраняемыхРеквизитов);
		СохранитьЗначениеРеквизитаФормы("ПроцентУменьшения",               ЭтаФорма, СтруктураСохраняемыхРеквизитов);
	Иначе
		СохранитьЗначениеРеквизитаФормы("КодНалоговойЛьготыОсвобождениеОтНалогообложения", ЭтаФорма, СтруктураСохраняемыхРеквизитов);
		ВосстановитьЗначениеРеквизитаФормы("СнижениеНалоговойСтавки",  ЭтаФорма, СтруктураСохраняемыхРеквизитов);
		ВосстановитьЗначениеРеквизитаФормы("СниженнаяНалоговаяСтавка", ЭтаФорма, СтруктураСохраняемыхРеквизитов);
		ВосстановитьЗначениеРеквизитаФормы("УменьшениеСуммыНалогаВПроцентах", ЭтаФорма, СтруктураСохраняемыхРеквизитов);
		ВосстановитьЗначениеРеквизитаФормы("ПроцентУменьшения",               ЭтаФорма, СтруктураСохраняемыхРеквизитов);
	КонецЕсли;

	УправлениеФормой(ЭтаФорма);

КонецПроцедуры

&НаКлиенте
Процедура СнижениеНалоговойСтавкиПриИзменении(Элемент)

	Если Запись.СнижениеНалоговойСтавки Тогда
		ВосстановитьЗначениеРеквизитаФормы("СниженнаяНалоговаяСтавка", ЭтаФорма, СтруктураСохраняемыхРеквизитов);
	Иначе
		СохранитьЗначениеРеквизитаФормы("СниженнаяНалоговаяСтавка", ЭтаФорма, СтруктураСохраняемыхРеквизитов);
	КонецЕсли;

	УправлениеФормой(ЭтаФорма);

КонецПроцедуры

&НаКлиенте
Процедура УменьшениеСуммыНалогаВПроцентахПриИзменении(Элемент)

	Если Запись.УменьшениеСуммыНалогаВПроцентах Тогда
		ВосстановитьЗначениеРеквизитаФормы("ПроцентУменьшения", ЭтаФорма, СтруктураСохраняемыхРеквизитов);
	Иначе
		СохранитьЗначениеРеквизитаФормы("ПроцентУменьшения", ЭтаФорма, СтруктураСохраняемыхРеквизитов);
	КонецЕсли;

	УправлениеФормой(ЭтаФорма);

КонецПроцедуры

&НаКлиенте
Процедура КодНалоговойЛьготыОсвобождениеОтНалогообложенияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ТипОбъекта",		"РегистрСведений");
	ПараметрыФормы.Вставить("НазваниеОбъекта",	"СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам");
	ПараметрыФормы.Вставить("НазваниеМакета",	"ЛьготыПоНалогуНаИмущество");
	ПараметрыФормы.Вставить("ТекущийПериод",	Запись.Период);

	ОповещениеОЗакрытии = Новый ОписаниеОповещения("КодНалоговойЛьготыОсвобождениеОтНалогообложенияНачалоВыбораЗавершение", ЭтотОбъект);
	
	ОткрытьФорму("ОбщаяФорма.ФормаВыбораКода", ПараметрыФормы,,,,,ОповещениеОЗакрытии);

КонецПроцедуры

&НаКлиенте
Процедура КодНалоговойЛьготыОсвобождениеОтНалогообложенияНачалоВыбораЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	КодЛьготы = РезультатЗакрытия;
	
	Если КодЛьготы <> Неопределено Тогда
		Запись.КодНалоговойЛьготыОсвобождениеОтНалогообложения = КодЛьготы;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)

	Если Запись.ОсвобождениеОтНалогообложения Тогда
		Если ПустаяСтрока(Запись.КодНалоговойЛьготыОсвобождениеОтНалогообложения) Тогда
			ТекстСообщения = НСтр("ru = 'Не указан код налоговой льготы (освобождение от налогообложения)!'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "КодНалоговойЛьготыОсвобождениеОтНалогообложения", , Отказ);
		КонецЕсли;
	КонецЕсли;

	Если Запись.СнижениеНалоговойСтавки И Запись.СниженнаяНалоговаяСтавка = 0 Тогда
		ТекстСообщения = НСтр("ru = 'Не указана сниженная налоговая ставка!'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "СниженнаяНалоговаяСтавка", , Отказ);
	КонецЕсли;

	Если Запись.УменьшениеСуммыНалогаВПроцентах И Запись.ПроцентУменьшения = 0 Тогда
		ТекстСообщения = НСтр("ru = 'Не указан процент уменьшения суммы налога!'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "ПроцентУменьшения", , Отказ);
	КонецЕсли;

КонецПроцедуры

СтруктураСохраняемыхРеквизитов = Новый Структура; // Пустая структура