
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Не Параметры.Ключ.Пустая() Тогда
		ОбъектСправочника = Параметры.Ключ.ПолучитьОбъект();
		ЗначениеВРеквизитФормы(ОбъектСправочника, "Объект");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.МассивОтпечатков) Тогда
		СсылкаНаМассивОтпечатков = ПоместитьВоВременноеХранилище(Параметры.МассивОтпечатков, УникальныйИдентификатор);
	КонецЕсли;
	
	РеквизитыНастройкиЭДО = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		Объект.СоглашениеЭД, "ПрограммаБанка, АутентификацияПоСертификату");
	
	ПрограммаБанка = РеквизитыНастройкиЭДО.ПрограммаБанка;
	АутентификацияПоСертификату = РеквизитыНастройкиЭДО.АутентификацияПоСертификату;
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ЭДОтклонен = ЭДОтклонен();
		ТребуетсяПодпись = НеобходимоПодписать();
		ОбновитьСтатусЭД();
		Заголовок = ЭлектронныеДокументыСлужебный.ПолучитьПредставлениеЭД(Объект.Ссылка);
		ЗаполнитьТаблицуЭП();
		
		Если НЕ Отказ Тогда
			ВыполнитьПросмотрЭДИзБДСервер(Отказ);
		КонецЕсли;
	КонецЕсли;
	
	ПерезаполнитьКомментарии();

	Попытка
		МассивОтпечатков = ЭлектронныеДокументыСлужебныйВызовСервера.МассивОтпечатковСертификатов();
		СсылкаНаМассивОтпечатков = ПоместитьВоВременноеХранилище(МассивОтпечатков, УникальныйИдентификатор);
	Исключение
	КонецПопытки;
	
	ИзменитьВидимостьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если НЕ Отказ И ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Если ЭтоАдресВременногоХранилища(АдресФайлаВХранилище) Тогда
			#Если ВебКлиент Тогда
				ПутьКФайлуПросмотра = АдресФайлаВХранилище;
			#Иначе
				ПутьКФайлуПросмотра = ПолучитьИмяВременногоФайла(РасширениеФайла);
				ДДФайла = ПолучитьИзВременногоХранилища(АдресФайлаВХранилище);
				ДДФайла.Записать(ПутьКФайлуПросмотра);
			#КонецЕсли
			Если Найти("HTML PDF DOCX XLSX", ВРег(РасширениеФайла)) > 0 Тогда
				Элементы.ГруппаСодержимоеДокумента.ТекущаяСтраница = Элементы.СтраницаДругойФормат;
			Иначе
				#Если НЕ ВебКлиент Тогда
					ЗапуститьПриложение(ПутьКФайлуПросмотра);
				#КонецЕсли
				Отказ = Истина;
				Возврат;
			КонецЕсли;
		КонецЕсли;
		Иначе
		Отказ = Истина;
	КонецЕсли;
	
	Если Объект.ВидЭД = ПредопределенноеЗначение("Перечисление.ВидыЭД.ДопДанные") Тогда
		СохранитьЭДНаДиск(Неопределено);
		Отказ = Истина;
	КонецЕсли;
	
	Если Не Отказ Тогда
		Описание = Новый ОписаниеОповещения("ПослеПолученияОтпечатков", ЭтотОбъект);
		ЭлектроннаяПодписьКлиент.ПолучитьОтпечаткиСертификатов(Описание, Истина, Ложь);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ОбновитьСостояниеЭД" Тогда
		ВыполнитьОбработкуОповещенияНаСервере();
		ОбновитьОтображениеДанных();
	ИначеЕсли ИмяСобытия = "ОповеститьОСозданииУведомления" И Параметр = Объект.Ссылка Тогда
		ПоместитьТекстУточненияВОбъект(ТекстУточнения);
		ИзменитьВидимостьДоступность();
		ЭДОтклонен = Истина;
		ИзменитьСтатусОтклонить();
		Оповестить("ОбновитьСостояниеЭД");
	ИначеЕсли ИмяСобытия = "ПроведенаПроверкаЭП" Тогда
		Для Каждого ЭД Из Параметр Цикл
			Если ЭД = Объект.Ссылка Тогда
				ОбновитьОтображениеДанных();
				ЗаполнитьТаблицуЭП();
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	СкрытьДопДанные(ОтключитьВыводДопДанных);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии()
	
	ИДПараметра = "ЭлектронноеВзаимодействие." + УникальныйИдентификатор;
	ПараметрыФормы = ПараметрыПриложения[ИДПараметра];
	Если ПараметрыФормы <> Неопределено Тогда
		ПараметрыПриложения.Удалить(ИДПараметра);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийПолейФормы

&НаКлиенте
Процедура ТекстДокументИБНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПоказатьЗначение(, Объект.ВладелецФайла);
	
КонецПроцедуры

&НаКлиенте
Процедура ПричиныОтклоненияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ПоказатьЗначение(, Элемент.ТекущиеДанные.ПричинаОтклонения);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтключитьВыводДопДанныхПриИзменении(Элемент)
	
	СкрытьДопДанные(ОтключитьВыводДопДанных);
	ОбновитьОтображениеДанных();
	
КонецПроцедуры

&НаКлиенте
Процедура КвитанцияНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если ТипЗнч(Квитанция) = Тип("СправочникСсылка.ЭДПрисоединенныеФайлы") Тогда
		ПараметрыФормы = Новый Структура("Ключ", Квитанция);
		ОткрытьФорму("Справочник.ЭДПрисоединенныеФайлы.Форма.ФормаПросмотраЭД", ПараметрыФормы);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбъектДополнительнаяИнформацияПриИзменении(Элемент)
	
	ИзменитьТекстСопроводительнойЗаписки(Объект.ДополнительнаяИнформация);
	
КонецПроцедуры


#КонецОбласти

#Область ОбработчикиСобытийПолейТаблицыЭП

&НаКлиенте
Процедура ЭППриАктивизацииСтроки(Элемент)
	
	Если Элементы.ЭП.ТекущиеДанные <> Неопределено Тогда
		Элементы.ДоверятьСертификату.Доступность = Элементы.ЭП.ТекущиеДанные.ОтсутствуетВСписке;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЭПВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Найти(Поле.Имя, "ЭПКомуВыданСертификат") > 0 Тогда
		ДобавитьСертификатВДоверенные(Элемент.ТекущиеДанные);
		Если Элемент.ТекущиеДанные <> Неопределено И НЕ Элемент.ТекущиеДанные.ОтсутствуетВСписке
				И НЕ ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.ОбменЧерезДопОбработку")
				И НЕ ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.iBank2") Тогда
			ПоказатьСертификат(Элемент.ТекущиеДанные.НомерСтроки, Элемент.ТекущиеДанные.Отпечаток);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПерезаполнитьДокумент(Команда)
	
	ЭлектронныеДокументыКлиент.ПерезаполнитьДокумент(Объект.ВладелецФайла, ЭтотОбъект, , Объект.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура Отклонить(Команда)
	
	ОтклонитьАннулироватьЭД(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьДокумент(Команда)
	
	Модифицированность = Ложь;
	ТекстВопроса = НСтр("ru = 'Внимание! Не рекомендуется выбирать документ отражения в учете вручную. Продолжить?'");
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыбратьДокументПродолжить", ЭтотОбъект);
	ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет);
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьЭДНаДиск(Команда)
	
	Если ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.СбербанкОнлайн") Тогда
		СохранитьБанковскиеДокументыСбербанка(Объект.Ссылка);
		Возврат;
	КонецЕсли;
	
	ПрисоединенныйФайл = Объект.Ссылка;
	ДанныеФайла = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьДанныеФайла(ПрисоединенныйФайл, УникальныйИдентификатор);
	
	ПрисоединенныеФайлыКлиент.СохранитьВместеСЭП(ПрисоединенныйФайл, ДанныеФайла, УникальныйИдентификатор);
	Если Не ЗначениеЗаполнено(ПрограммаБанка) Тогда
		Возврат;
	КонецЕсли;
		
	Если Объект.ВидЭД = ПредопределенноеЗначение("Перечисление.ВидыЭД.ВыпискаБанка") Тогда
		СсылкаНаХранилище = "";
		ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьДанныеВыписки(ПрисоединенныйФайл, СсылкаНаХранилище);
		Если ЗначениеЗаполнено(СсылкаНаХранилище) Тогда
			ПолучитьФайл(СсылкаНаХранилище, ДанныеФайла.Наименование + ".txt");
		КонецЕсли;
	КонецЕсли;
	
	Если (ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.ОбменЧерезДопОбработку")
				ИЛИ ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.iBank2"))
			И Объект.ВидЭД = ПредопределенноеЗначение("Перечисление.ВидыЭД.ПлатежноеПоручение") Тогда
		ДанныеФайла = ДанныеСлужебногоЭДБанка(ПрисоединенныйФайл, УникальныйИдентификатор);
		Если НЕ ДанныеФайла = Неопределено Тогда
			ПолучитьФайл(ДанныеФайла.СсылкаНаДвоичныеДанныеФайла, ДанныеФайла.ИмяФайла);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДоверятьЭтомуСертификату(Команда)
	
	ДобавитьСертификатВДоверенные(Элементы.ЭП.ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиКЖурналуСобытийЭД(Команда)
	
	ПараметрыФормы = Новый Структура;
	
	Отбор = Новый Структура;
	Отбор.Вставить("ПрисоединенныйФайл", Объект.Ссылка);
	
	ПараметрыФормы.Вставить("Отбор", Отбор);
	ПараметрыФормы.Вставить("РежимОткрытияОкна", РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	ОткрытьФорму("РегистрСведений.ЖурналСобытийЭД.ФормаСписка", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСертификат(Команда)
	
	Если Элементы.ЭП.ТекущиеДанные <> Неопределено Тогда
		ПоказатьСертификат(Элементы.ЭП.ТекущиеДанные.НомерСтроки, Элементы.ЭП.ТекущиеДанные.Отпечаток);
	Иначе
		ОчиститьСообщения();
		ТекстОшибки = НСтр("ru = 'Выберите сертификат в списке установленных подписей.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьПодписи(Команда)
	
	ОчиститьСообщения();
	
	Если ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.СбербанкОнлайн") Тогда
		ПараметрыПроверки = Новый Структура;
		МассивЭД = Новый Массив;
		МассивЭД.Добавить(Объект.Ссылка);
		ПараметрыПроверки.Вставить("МассивЭДДляПроверкиЭПСбербанк", МассивЭД);
		ПараметрыПроверки.Вставить("ИндексПроверкиЭПСбербанка", 0);
		ПараметрыПроверки.Вставить("ОповеститьОПроверкеЭП");
		ЭлектронныеДокументыСлужебныйКлиент.ОпределитьСтатусыПодписейСбербанка(Объект.СоглашениеЭД, ПараметрыПроверки);
		Возврат;
	ИначеЕсли ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.ОбменЧерезДопОбработку") Тогда
		МассивПроверки = Новый Массив;
		МассивПроверки.Добавить(Объект.Ссылка);
		ПараметрыПроверки = Новый Структура;
		ПараметрыПроверки.Вставить("СоглашениеЭД", Объект.СоглашениеЭД);
		ПараметрыПроверки.Вставить("МассивЭДДляПроверкиЧерезДополнительнуюОбработку", МассивПроверки);
		ПараметрыПроверки.Вставить("ТекущийИндексПроверкиПодписейЧерезДополнительнуюОбработку", 0);
		ПараметрыПроверки.Вставить("ОповеститьОПроверкеЭП");
		ЭлектронныеДокументыСлужебныйКлиент.НачатьПроверкуСтатусовПодписейЧерезДополнительнуюОбработку(
			Неопределено, ПараметрыПроверки);
		Возврат;
	ИначеЕсли ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.iBank2") Тогда
		ПараметрыПроверки = Новый Структура;
		ОО = Новый ОписаниеОповещения("ПроверитьПодписиiBank2", ЭтотОбъект, ПараметрыПроверки);
		ПараметрыПроверки.Вставить("ОбработчикПослеПодключенияКомпоненты", ОО);
		ЭлектронныеДокументыСлужебныйКлиент.ПодключитьВнешнююКомпонентуiBank2(ПараметрыПроверки);
		Возврат;
	Иначе
		ЭлектронныеДокументыСлужебныйКлиент.ОпределитьСтатусыПодписей(Объект.Ссылка);
	КонецЕсли;
	
	ОбновитьОтображениеДанных();
	ЗаполнитьТаблицуЭП();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьПодтверждениеЭД(Команда)
	
	ЭлектронныеДокументыСлужебныйКлиент.ОтправитьПодтверждениеЭД(Объект.ВладелецФайла, Объект.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура УтвердитьЭД(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОткрытьВторойТитулЭД", ЭтотОбъект);
	ИДПараметра = "ЭлектронноеВзаимодействие." + УникальныйИдентификатор;
	ПараметрыПриложения.Вставить(ИДПараметра, ОписаниеОповещения);
	ДопПараметры = Новый Структура("ИДПараметра", ИДПараметра);
	ЭлектронныеДокументыСлужебныйКлиент.УтвердитьЭД(Объект.ВладелецФайла, Объект.Ссылка, , ДопПараметры);
	ОбновитьОтображениеДанных();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьВторойТитулЭД(НовыйЭД, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(НовыйЭД) Тогда
		ОткрытьФорму("Справочник.ЭДПрисоединенныеФайлы.Форма.ФормаПросмотраЭД", Новый Структура("Ключ", НовыйЭД),
			ВладелецФормы, УникальныйИдентификатор, Окно);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьЭД(Команда)
	
	ОчиститьСообщения();
	ЭлектронныеДокументыКлиент.СформироватьПодписатьОтправитьЭД(Объект.ВладелецФайла, Объект.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьПовторно(Команда)
	
	ЭлектронныеДокументыКлиент.ОтправитьПовторноЭД(Объект.ВладелецФайла, Объект.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодписатьЭД(Команда)
	
	ОчиститьСообщения();
	ЭлектронныеДокументыКлиент.СформироватьПодписатьОтправитьЭД(Объект.ВладелецФайла, Объект.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодтвердитьПлатеж(Команда)
	
	Если ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.ОбменЧерезДопОбработку") Тогда
		СтруктураПодключения = Новый Структура;
		СтруктураПодключения.Вставить("СоглашениеЭД", Объект.СоглашениеЭД);
		СтруктураПодключения.Вставить("ВыполняласьПопыткаПолученияМодуля", Ложь);
		
		ОбработчикПослеПодключения = Новый ОписаниеОповещения("ПодтвердитьПлатежЧерезДополнительнуюОбработку", ЭтотОбъект);
		СтруктураПодключения.Вставить("ОбработкаПослеПолученияМодуля", ОбработчикПослеПодключения);
		
		ЭлектронныеДокументыСлужебныйКлиент.ПолучитьВнешнийМодульЧерезДополнительнуюОбработку(СтруктураПодключения);
	Иначе
		ПараметрыОбработки = Новый Структура;
		ОО = Новый ОписаниеОповещения("ПодтвердитьПлатежiBank2", ЭтотОбъект, ПараметрыОбработки);
		ПараметрыОбработки.Вставить("ОбработчикПослеПодключенияКомпоненты", ОО);
		ЭлектронныеДокументыСлужебныйКлиент.ПодключитьВнешнююКомпонентуiBank2(Параметры);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура АннулироватьЭД(Команда)
	
	ОтклонитьАннулироватьЭД();
	
КонецПроцедуры

&НаКлиенте
Процедура ПринятьАннулирование(Команда)
	
	ОтклонитьАннулирование = Ложь;
	ОбработатьПредложениеОбАннулировании(ОтклонитьАннулирование);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтклонитьАннулирование(Команда)
	
	ОтклонитьАннулирование = Истина;
	ОбработатьПредложениеОбАннулировании(ОтклонитьАннулирование);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьКомментарий(Команда)
	
	СтруктураПараметров = Новый Структура("Описание", Комментарий);
	ИзменитьЗначенияРеквизитовНаСервере(Объект.Ссылка, СтруктураПараметров);
	Комментарий = "";
	Оповестить("ОбновитьСостояниеЭД");
	
КонецПроцедуры

&НаКлиенте
Процедура Перенаправить(Команда)
	
	ОбработатьПеренаправлениеЭД();
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьЗаписку(Команда)
	
	Если ЗначениеЗаполнено(Объект.ДополнительнаяИнформация) Тогда
		СтруктураПараметров = Новый Структура("ДополнительнаяИнформация", "");
		ИзменитьЗначенияРеквизитовНаСервере(Объект.Ссылка, СтруктураПараметров);
		Оповестить("ОбновитьСостояниеЭД");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапроситьСостояниеЭД(Команда)
	
	Перем ДанныеАвторизации;
	
	Если НЕ ЕстьПоддержкаВидаЭД(Объект.СоглашениеЭД, ПредопределенноеЗначение("Перечисление.ВидыЭД.ЗапросОСостоянииЭД")) Тогда
		ТекстСообщения = Нстр("ru = 'Банк не поддерживает данный функционал'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	Если АутентификацияПоСертификату Тогда
		
		Оповещение = Новый ОписаниеОповещения("ПослеПолученияОтпечатковЗапроситьСостояниеЭД", ЭтотОбъект);
		
		ЭлектроннаяПодписьКлиент.ПолучитьОтпечаткиСертификатов(Оповещение, Истина, Ложь);
		
	Иначе
		Если ЭлектронныеДокументыСлужебныйКлиент.ПолученыДанныеАвторизации(Объект.СоглашениеЭД, ДанныеАвторизации) Тогда
			ОтправитьЗапросСтатусаЭДВБанк(ДанныеАвторизации)
		Иначе
			ОООЗ = Новый ОписаниеОповещения("ОтправитьЗапросСтатусаЭДВБанк", ЭтотОбъект);
			ЭлектронныеДокументыСлужебныйКлиент.ПолучитьДанныеАутентификации(Объект.СоглашениеЭД, ОООЗ);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЕстьПоддержкаВидаЭД(НастройкаЭДО, ВидЭД)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СоглашенияОбИспользованииЭДИсходящиеДокументы.Ссылка
	|ИЗ
	|	Справочник.СоглашенияОбИспользованииЭД.ИсходящиеДокументы КАК СоглашенияОбИспользованииЭДИсходящиеДокументы
	|ГДЕ
	|	СоглашенияОбИспользованииЭДИсходящиеДокументы.ИсходящийДокумент = &ВидЭД
	|	И СоглашенияОбИспользованииЭДИсходящиеДокументы.Ссылка = &НастройкаЭДО";
	
	Запрос.УстановитьПараметр("ВидЭД", ВидЭД);
	Запрос.УстановитьПараметр("НастройкаЭДО", НастройкаЭДО);
	Результат = Запрос.Выполнить();
	
	Возврат НЕ Результат.Пустой();
	
КонецФункции

&НаКлиенте
Процедура ОтозватьЭД(Команда)
		
	Если НЕ ЕстьПоддержкаВидаЭД(Объект.СоглашениеЭД, ПредопределенноеЗначение("Перечисление.ВидыЭД.ЗапросНаОтзывЭД")) Тогда
		ТекстСообщения = Нстр("ru = 'Банк не поддерживает данный функционал'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	Если МожноОтозватьЭД(Объект.Ссылка) Тогда
		ОО = Новый ОписаниеОповещения("НачатьОтзывЭД", ЭтотОбъект);
		ЗаголовокОкна = Нстр("ru = 'Укажите причину отзыва электронного документа'");
		ПоказатьВводСтроки(ОО, , ЗаголовокОкна, , Истина);
	КонецЕсли
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ИзменитьТекстСопроводительнойЗаписки(ТекстЗаписки)
	
	СтруктураПараметров = Новый Структура("ДополнительнаяИнформация", ТекстЗаписки);
	ИзменитьЗначенияРеквизитовНаСервере(Объект.Ссылка, СтруктураПараметров);
	ЭтаФорма.Модифицированность = Ложь;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ИзменитьЗначенияРеквизитовНаСервере(Знач Ссылка, Знач СтруктураПараметров)
	
	Если СтруктураПараметров.Свойство("Описание") И ЗначениеЗаполнено(Ссылка.Ответственный) Тогда
		СтруктураПараметров.Вставить("Ответственный", Ссылка.Ответственный);
	КонецЕсли;
	ЭлектронныеДокументыСлужебный.ИзменитьПоСсылкеПрисоединенныйФайл(Ссылка, СтруктураПараметров, Ложь);
	
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьПеренаправлениеЭД()
	
	МассивЭД = Новый Массив;
	МассивЭД.Добавить(Объект.Ссылка);
	ЭлектронныеДокументыСлужебныйКлиент.ИзменитьОтветственного(МассивЭД, Неопределено);
	
КонецПроцедуры

&НаСервере
Процедура ПерезаполнитьКомментарии()
	
	ВсеКомментарии = "";
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ЖурналСобытийЭД.Пользователь.Представление КАК Пользователь,
		|	ЖурналСобытийЭД.Дата КАК Дата,
		|	ЖурналСобытийЭД.СтатусЭД,
		|	ЖурналСобытийЭД.Ответственный.Представление КАК Ответственный,
		|	ЖурналСобытийЭД.Комментарий
		|ИЗ
		|	РегистрСведений.ЖурналСобытийЭД КАК ЖурналСобытийЭД
		|ГДЕ
		|	ЖурналСобытийЭД.ПрисоединенныйФайл = &Ссылка
		|	И ЖурналСобытийЭД.Комментарий <> &ПустаяСтрока
		|
		|УПОРЯДОЧИТЬ ПО
		|	Дата";
		
	Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
	Запрос.УстановитьПараметр("ПустаяСтрока", "");
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	ШаблонКомментария = НСтр("ru = '%1, %2 (статус - %3, ответственный - %4):
		|%5'");
	ПредыдущийКоммент = "";
	ПервыйКоммент = Истина;
	Массив = Новый Массив;
	Пока Выборка.Следующий() Цикл
		ТекущийКоммент = СокрЛП(Выборка.Комментарий);
		Если ПредыдущийКоммент = ТекущийКоммент Тогда
			Продолжить;
		КонецЕсли;
		ПредыдущийКоммент = ТекущийКоммент;
		СтрокаКомментария = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонКомментария,
				Выборка.Дата, Выборка.Пользователь, Выборка.СтатусЭД, Выборка.Ответственный, ТекущийКоммент);
		Массив.Добавить(СтрокаКомментария);
		ПервыйКоммент = Ложь;
	КонецЦикла;
	Если Массив.Количество() > 0 Тогда
		ПервыйКоммент = Истина;
		Для Сч = -Массив.Количество() + 1 По 0 Цикл
			СтрокаКомментария = Массив[-Сч];
			ВсеКомментарии = ВсеКомментарии
				+ СтрокаКомментария
				+ ?(ПервыйКоммент, Символы.ПС + "------------------------------------", "")
				+ Символы.ПС
				+ Символы.ПС;
			ПервыйКоммент = Ложь;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция УникальныйИДВнешний(ЭД)
	
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЭД, "УникальныйИДВнешний");
	
КонецФункции

&НаСервереБезКонтекста
Процедура УстановитьСтатусДоставлен(ЭД)
	
	СтруктураЭД = Новый Структура("СтатусЭД", Перечисления.СтатусыЭД.Доставлен);
	ЭлектронныеДокументыСлужебный.ИзменитьПоСсылкеПрисоединенныйФайл(ЭД, СтруктураЭД, Ложь);
	
КонецПроцедуры


&НаСервереБезКонтекста
Функция МассивФайловСбербанка(ЭД)
	
	МассивВозврата = Новый Массив;
	
	СтруктураДайджест = Новый Структура;
	ОсноваИмениФайла = ОбщегоНазначенияКлиентСервер.ЗаменитьНедопустимыеСимволыВИмениФайла(ЭД.Наименование);
	
	СтрокаBase64 = ЭлектронныеДокументыСлужебныйВызовСервера.ПодписанныеДанныеBase64(ЭД);
	ИмяФайлаДанных = "doc_" + ОсноваИмениФайла + ".txt";
	ДвоичныеДанные = Base64Значение(СтрокаBase64);
	СсылкаНаДайджест =  ПоместитьВоВременноеХранилище(ДвоичныеДанные);
	СтруктураДайджест = Новый Структура("СсылкаНаФайл, ИмяФайла", СсылкаНаДайджест, ИмяФайлаДанных);
	МассивВозврата.Добавить(СтруктураДайджест);
	
	Для Каждого ЭП из ЭД.ЭлектронныеПодписи Цикл
		ДвоичныеДанныеЭП = ЭП.Подпись.Получить();
		ИмяФайлаДанных = "sign_" + ОсноваИмениФайла+ "_" + ЭП.НомерСтроки + ".txt";
		СсылкаНаПодпись = ПоместитьВоВременноеХранилище(ДвоичныеДанныеЭП);
		МассивВозврата.Добавить(Новый Структура("СсылкаНаФайл, ИмяФайла", СсылкаНаПодпись, ИмяФайлаДанных));
				
		ДвоичныеДанныеСертификата = ЭП.Сертификат.Получить();
		ИмяФайлаДанных = "cert_" + ОсноваИмениФайла + "_" + ЭП.НомерСтроки + ".cer";
		СсылкаНаСертификат = ПоместитьВоВременноеХранилище(ДвоичныеДанныеСертификата);
		МассивВозврата.Добавить(Новый Структура("СсылкаНаФайл, ИмяФайла", СсылкаНаСертификат, ИмяФайлаДанных));
	КонецЦикла;

	ДанныеФайла = ЭлектронныеДокументыСлужебный.ПолучитьДанныеФайла(ЭД);
	ИмяФайлаДанных = ОсноваИмениФайла + ".xml";
	СтруктураФайла = Новый Структура("СсылкаНаФайл, ИмяФайла", ДанныеФайла.СсылкаНаДвоичныеДанныеФайла, ИмяФайлаДанных);
	МассивВозврата.Добавить(СтруктураФайла);
	
	Возврат МассивВозврата;
	
КонецФункции

&НаСервере
Процедура ИзменитьСтатусОтклонить()
	
	ОбъектДокумент = РеквизитФормыВЗначение("Объект");
	СтруктураПараметров = Новый Структура("СтатусЭД", Перечисления.СтатусыЭД.Отклонен);
	ЭлектронныеДокументыСлужебный.ИзменитьПоСсылкеПрисоединенныйФайл(Объект.Ссылка, СтруктураПараметров, Ложь);
	ЗначениеВРеквизитФормы(ОбъектДокумент, "Объект");
	
КонецПроцедуры

&НаСервере
Процедура ИзменитьВидимостьДоступность()
	
	Если ЗначениеЗаполнено(СсылкаНаМассивОтпечатков) Тогда
		МассивОтпечатков = ПолучитьИзВременногоХранилища(СсылкаНаМассивОтпечатков);
	Иначе
		МассивОтпечатков = Новый Массив;
	КонецЕсли;

	ЕстьВозможностьПодписания = (ЗначениеЗаполнено(МассивОтпечатков)
								  ИЛИ ПрограммаБанка = Перечисления.ПрограммыБанка.ОбменЧерезДопОбработку
								  ИЛИ ПрограммаБанка = Перечисления.ПрограммыБанка.iBank2
								  ИЛИ ПрограммаБанка = Перечисления.ПрограммыБанка.СбербанкОнлайн)
								И ДоступныДляПодписиСертификаты(МассивОтпечатков);
	
	Если ЗначениеЗаполнено(Объект.ЭлектронныйДокументВладелец) Тогда
		СсылкаНаЭД = Объект.ЭлектронныйДокументВладелец;
	Иначе
		СсылкаНаЭД = Объект.Ссылка;
	КонецЕсли;
	МожноОтклонитьЭтотЭД = ЭлектронныеДокументыСлужебныйВызовСервера.МожноОтклонитьЭтотЭД(СсылкаНаЭД);
	МожноАннулироватьЭтотЭД = ЭлектронныеДокументыСлужебныйВызовСервера.МожноАннулироватьЭтотЭД(СсылкаНаЭД);
	Элементы.ОтозватьЭД.Видимость = МожноОтозватьЭД(СсылкаНаЭД);
	
	// Отображение страниц:
	Элементы.ГруппаСодержимоеДокумента.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
	Элементы.ГруппаВправо.ПодчиненныеЭлементы.СтраницыКоманд.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
	
	
	Если НЕ Объект.ВидЭД = Перечисления.ВидыЭД.ВыпискаБанка И НЕ Элементы.Найти("КомандаПрочитать") = Неопределено Тогда
		Элементы.КомандаПрочитать.Видимость = Ложь;
	КонецЕсли;
	
	ДОЗакрыт = ЭлектронныеДокументыСлужебныйВызовСервера.ДОЗакрытПринудительно(Объект.ВладелецФайла);
	ЭДОтклонен = ЭлектронныеДокументыСлужебныйВызовСервера.ЭДОтклонен(Объект.СтатусЭД);
	ЭтоСлужебный = ЭлектронныеДокументыСлужебныйВызовСервера.ЭтоСлужебныйДокумент(Объект.Ссылка);
	
	ЭДТитулПродавца = (Объект.ВидЭД = Перечисления.ВидыЭД.ТОРГ12Продавец
					ИЛИ Объект.ВидЭД = Перечисления.ВидыЭД.АктИсполнитель
					ИЛИ Объект.ВидЭД = Перечисления.ВидыЭД.СоглашениеОбИзмененииСтоимостиОтправитель);
	ЭДСчетФактура = Объект.ВидЭД = Перечисления.ВидыЭД.СчетФактура
					ИЛИ Объект.ВидЭД = Перечисления.ВидыЭД.КорректировочныйСчетФактура;
					
	ИспользуетсяКриптография = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.СоглашениеЭД, "ИспользуетсяКриптография");
	Если НЕ ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьЗначениеФункциональнойОпции("ИспользоватьЭлектронныеПодписиЭД")
		ИЛИ НЕ (ИспользуетсяКриптография = Истина)
		И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.СоглашениеЭД, "СпособОбменаЭД") = Перечисления.СпособыОбменаЭД.ЧерезВебРесурсБанка
		И ЭлектронныеДокументыСлужебный.НемедленнаяОтправкаЭД() Тогда
		Элементы.КомандаУтвердитьЭД.Заголовок = Нстр("ru = 'Утвердить и отправить'");
	КонецЕсли;
	
	Если ЭлектронныеДокументыСлужебный.НемедленнаяОтправкаЭД() Тогда
		КомандаПодписиОтправки = Элементы.КомандаОтправитьЭД;
	Иначе
		КомандаПодписиОтправки = Элементы.КомандаПодписать;
	КонецЕсли;
	
	Если Объект.НаправлениеЭД = Перечисления.НаправленияЭД.Входящий Тогда
		
		Элементы.ГруппаВправо.ПодчиненныеЭлементы.СтраницыКоманд.ТекущаяСтраница = Элементы.ГруппаКомандВх;
		
		Если Объект.ВидЭД = Перечисления.ВидыЭД.ИзвещениеОПолучении
			ИЛИ Объект.ВидЭД = Перечисления.ВидыЭД.Подтверждение
			ИЛИ Объект.ВидЭД = Перечисления.ВидыЭД.УведомлениеОбУточнении
			ИЛИ Объект.ВидЭД = Перечисления.ВидыЭД.ПредложениеОбАннулировании
			ИЛИ Объект.ВидЭД = Перечисления.ВидыЭД.ВыпискаБанка
			ИЛИ Объект.ВидЭД = Перечисления.ВидыЭД.Квитанция Тогда
			
			Элементы.ПерезаполнитьДокумент.Видимость = Ложь;
		ИначеЕсли Объект.ВидЭД = Перечисления.ВидыЭД.КаталогТоваров Тогда
			Элементы.ПерезаполнитьДокумент.Заголовок = НСтр("ru = 'Сопоставить номенклатуру'");
		ИначеЕсли Объект.ВидЭД = Перечисления.ВидыЭД.ТОРГ12Покупатель
			ИЛИ Объект.ВидЭД = Перечисления.ВидыЭД.АктЗаказчик
			ИЛИ Объект.ВидЭД = Перечисления.ВидыЭД.СоглашениеОбИзмененииСтоимостиПолучатель
			ИЛИ Объект.Ссылка.ВидЭД = Перечисления.ВидыЭД.ИзвещениеОСостоянииЭД Тогда
			
			Элементы.ПерезаполнитьДокумент.Видимость = Ложь;
		КонецЕсли;
		
		ЭтоПолученныйКаталогТоваров = Объект.ВидЭД = Перечисления.ВидыЭД.КаталогТоваров
										И Объект.СтатусЭД = Перечисления.СтатусыЭД.Получен;
		Элементы.ПерезаполнитьДокумент.Доступность = НЕ ЭДОтклонен И НЕ(ЭтоПолученныйКаталогТоваров);
		
		ОбменЧерезТакском = ОбменЧерезОператора();
		ВерсияФорматаПакета = ЭлектронныеДокументыСлужебный.ВерсияПакетаЭД(Объект.Ссылка);
		ЭтоСчетВерсии30 = (Объект.ВидЭД = Перечисления.ВидыЭД.СчетНаОплату)
			И (ОбменЧерезТакском
				Или (ВерсияФорматаПакета = Перечисления.ВерсииФорматаПакетаЭД.Версия30
					И Не ОбменЧерезТакском));
		
		// КомандаОтправитьПодтверждениеЭД видимость и доступность:
		Элементы.КомандаОтправитьПодтверждениеЭД.Видимость = ЕстьВозможностьПодписания
			И НЕ ЭДСчетФактура
			И НЕ ЭДТитулПродавца
			И Объект.ВидЭД <> Перечисления.ВидыЭД.ТОРГ12Покупатель
			И Объект.ВидЭД <> Перечисления.ВидыЭД.АктЗаказчик
			И Объект.ВидЭД <> Перечисления.ВидыЭД.СоглашениеОбИзмененииСтоимостиПолучатель
			И Не ЭтоСчетВерсии30;
		
		Элементы.КомандаОтправитьПодтверждениеЭД.Доступность = ЕстьВозможностьПодписания
			И НЕ ЭДОтклонен И (Объект.СтатусЭД = Перечисления.СтатусыЭД.Получен
				ИЛИ Объект.СтатусЭД = Перечисления.СтатусыЭД.Утвержден)
			И (ОтраженВУчете ИЛИ ЭтоПолученныйКаталогТоваров);
		//
		
		// КомандаУтвердитьЭД видимость и доступность:
		Элементы.КомандаУтвердитьЭД.Видимость = Объект.ВидЭД <> Перечисления.ВидыЭД.ТОРГ12Покупатель
			И Объект.ВидЭД <> Перечисления.ВидыЭД.АктЗаказчик
			И Объект.ВидЭД <> Перечисления.ВидыЭД.СоглашениеОбИзмененииСтоимостиПолучатель
			И Объект.ВидЭД <> Перечисления.ВидыЭД.ВыпискаБанка
			И Объект.ВидЭД <> Перечисления.ВидыЭД.Квитанция
			И Объект.ВидЭД <> Перечисления.ВидыЭД.ИзвещениеОСостоянииЭД
			И (НЕ ЕстьВозможностьПодписания ИЛИ ЭДСчетФактура
				ИЛИ ЭДТитулПродавца Или ЭтоСчетВерсии30);
		
		Элементы.КомандаУтвердитьЭД.Доступность =  Не ДОЗакрыт
		
			И (Объект.СтатусЭД = Перечисления.СтатусыЭД.Получен
			И НЕ(Объект.ВидЭД = Перечисления.ВидыЭД.ИзвещениеОПолучении
				ИЛИ Объект.ВидЭД = Перечисления.ВидыЭД.Подтверждение
				ИЛИ Объект.ВидЭД = Перечисления.ВидыЭД.ПредложениеОбАннулировании
				ИЛИ Объект.ВидЭД = Перечисления.ВидыЭД.УведомлениеОбУточнении));
		//
		
		Элементы.ЗаголовокОтраженВУчете.Доступность = НЕ ЭДОтклонен;
		
		Если Объект.ВидЭД = Перечисления.ВидыЭД.ВыпискаБанка Тогда
			Элементы.КомандаВыбратьДокумент.Видимость = Ложь;
		КонецЕсли;
		
		// Для входящего с.ф. кнопка отклонение имеет свое название и картинку
		Если Объект.Ссылка.ВидЭД = Перечисления.ВидыЭД.СчетФактура
			Или Объект.Ссылка.ВидЭД = Перечисления.ВидыЭД.КорректировочныйСчетФактура Тогда
			
			Элементы.КомандаОтклонить.Заголовок = НСтр("ru = 'Запросить уточнение по электронному документу'");
			Элементы.КомандаОтклонить.Картинка = БиблиотекаКартинок.ПользовательБезНеобходимыхСвойств;
		КонецЕсли;
		
	ИначеЕсли Объект.НаправлениеЭД = Перечисления.НаправленияЭД.Исходящий Тогда
		
		Элементы.ГруппаВправо.ПодчиненныеЭлементы.СтраницыКоманд.ТекущаяСтраница = Элементы.ГруппаКомандИсх;
		
		Элементы.КомандаОтправитьПодтверждениеЭД.Видимость = Ложь;
		Элементы.КомандаВыбратьДокумент.Видимость = Ложь;
		
		Если Объект.ВидЭД = Перечисления.ВидыЭД.ПлатежноеПоручение ИЛИ Объект.ВидЭД = Перечисления.ВидыЭД.ПлатежноеТребование Тогда
			КоличествоНеустановленныхПодписей =  ПолучитьКоличествоНеустановленныхПодписей();
			ЕстьОшибкаПередачи = (Объект.СтатусЭД = Перечисления.СтатусыЭД.ОшибкаПередачи);
			Если ПрограммаБанка = Перечисления.ПрограммыБанка.СбербанкОнлайн Тогда
				Элементы.КомандаОтправитьЭД.Видимость = Ложь;
				Элементы.КомандаПодписать.Видимость = КоличествоНеустановленныхПодписей > 0 И ЕстьВозможностьПодписания;
				Элементы.ОтправитьПовторно.Видимость = Ложь;
			Иначе
				Элементы.КомандаОтправитьЭД.Видимость = (КоличествоНеустановленныхПодписей = 1) И ЕстьВозможностьПодписания;
				Элементы.КомандаПодписать.Видимость   = КоличествоНеустановленныхПодписей > 1 И ЕстьВозможностьПодписания;
				Элементы.ОтправитьПовторно.Видимость = ЕстьОшибкаПередачи;
			КонецЕсли;
		Иначе
			КомандаПодписиОтправки.Видимость   = ЕстьВозможностьПодписания И ТребуетсяПодпись;
			КомандаПодписиОтправки.Доступность = НЕ ЭДОтклонен И Не ДОЗакрыт
				И (ОтраженВУчете ИЛИ Объект.ВидЭД = Перечисления.ВидыЭД.ТОРГ12Покупатель
					ИЛИ Объект.ВидЭД = Перечисления.ВидыЭД.АктЗаказчик
					ИЛИ Объект.ВидЭД = Перечисления.ВидыЭД.АктНаПередачуПрав
					ИЛИ Объект.ВидЭД = Перечисления.ВидыЭД.СоглашениеОбИзмененииСтоимостиПолучатель
					ИЛИ Объект.ВидЭД = Перечисления.ВидыЭД.ПредложениеОбАннулировании
					ИЛИ Объект.ВидЭД = Перечисления.ВидыЭД.ИзвещениеОПолучении)
				И (Объект.СтатусЭД = Перечисления.СтатусыЭД.Сформирован ИЛИ Объект.СтатусЭД = Перечисления.СтатусыЭД.Утвержден);
			
			
			Если ЭлектронныеДокументыСлужебныйВызовСервера.ЭтоПрямойОбмен(Объект.ПрофильНастроекЭДО.СпособОбменаЭД) Тогда
				Элементы.ОтправитьПовторно.Видимость = Истина;
				Элементы.ОтправитьПовторно.Доступность = НЕ ЭДОтклонен
					И ОтраженВУчете
					ИЛИ (Объект.ВидЭД = Перечисления.ВидыЭД.ИзвещениеОПолучении
						ИЛИ Объект.ВидЭД = Перечисления.ВидыЭД.Подтверждение
						ИЛИ Объект.ВидЭД = Перечисления.ВидыЭД.ПредложениеОбАннулировании
						ИЛИ Объект.ВидЭД = Перечисления.ВидыЭД.УведомлениеОбУточнении);
			КонецЕсли;
			
			ЗапискаДоступна = (НЕ ЭтоСлужебный
				И (Объект.СтатусЭД = Перечисления.СтатусыЭД.Сформирован
					ИЛИ Объект.СтатусЭД = Перечисления.СтатусыЭД.Утвержден
					ИЛИ Объект.СтатусЭД = Перечисления.СтатусыЭД.Подписан
					ИЛИ Объект.СтатусЭД = Перечисления.СтатусыЭД.ПодготовленКОтправке));
			Элементы.ОбъектДополнительнаяИнформация.ТолькоПросмотр = НЕ ЗапискаДоступна;
			Элементы.ОчиститьЗаписку.Доступность = ЗапискаДоступна;
		КонецЕсли;
		
		// КомандаУтвердитьЭД видимость и доступность:
		Элементы.КомандаУтвердитьЭД.Видимость = НЕ (ЕстьВозможностьПодписания И ТребуетсяПодпись)
												И НЕ Объект.Ссылка.ВидЭД = Перечисления.ВидыЭД.ЗапросЗонд;
		Элементы.КомандаУтвердитьЭД.Доступность = Не ДОЗакрыт И (Объект.СтатусЭД = Перечисления.СтатусыЭД.Сформирован);
		//
	ИначеЕсли Объект.НаправлениеЭД = Перечисления.НаправленияЭД.Интеркампани Тогда
		
		Элементы.ГруппаВправо.ПодчиненныеЭлементы.СтраницыКоманд.ТекущаяСтраница = Элементы.ГруппаКомандИсх;
		
		Элементы.КомандаОтправитьПодтверждениеЭД.Видимость = Ложь;
		Элементы.КомандаВыбратьДокумент.Видимость          = Ложь;
		
		Элементы.КомандаПодписать.Видимость     = ЕстьВозможностьПодписания;
		Элементы.КомандаПодписать.Доступность   = (ЕстьВозможностьПодписания
			И НЕ ЭДОтклонен И Объект.СтатусЭД <> Перечисления.СтатусыЭД.ПолностьюПодписан);
		
		Элементы.КомандаУтвердитьЭД.Видимость   = НЕ ЕстьВозможностьПодписания;
		Элементы.КомандаУтвердитьЭД.Доступность = (Объект.СтатусЭД = Перечисления.СтатусыЭД.Сформирован);
		
	КонецЕсли;
	
	Элементы.ФормаАннулироватьЭД.Доступность = МожноАннулироватьЭтотЭД;
	Элементы.КомандаОтклонить.Доступность = НЕ (ЭДОтклонен ИЛИ ЭтоСлужебный ИЛИ ДОЗакрыт) И МожноОтклонитьЭтотЭД;
	Элементы.ФормаГруппаАннулирование.Видимость = Ложь;
	Элементы.ГруппаСтраницыПодвала.ТекущаяСтраница = Элементы.ГруппаСтатусовИСостояния;
	
	Если ЭДОтклонен Тогда
		ПричиныОтклонения.Очистить();
		НовСтрока = ПричиныОтклонения.Добавить();
		НовСтрока.ПричинаОтклонения = Объект.Ссылка.ПричинаОтклонения;
		Элементы.ГруппаСтраницыПодвала.ТекущаяСтраница = Элементы.ГруппаСтраницыПодвала.ПодчиненныеЭлементы.ГруппаСтраницаОтклонение;
		
		Если Объект.СтатусЭД = Перечисления.СтатусыЭД.ОшибкаПередачи Тогда
			Элементы.ПричиныОтклоненияПричинаОтклонения.Заголовок = НСтр("ru = 'Ошибка обмена'");
			НовСтрока.ПричинаОтклонения = Объект.ПричинаОтклонения;
		КонецЕсли;
		
		Если Объект.ВидЭД = Перечисления.ВидыЭД.ПлатежноеПоручение
			И ПрограммаБанка = Перечисления.ПрограммыБанка.СбербанкОнлайн Тогда
			Элементы.ГруппаСтраницыПодвала.ТекущаяСтраница = Элементы.ГруппаКвитанция;
			Квитанция = ЭлектронныеДокументыВнутренний.ПодчиненныйДокумент(Объект.Ссылка, Перечисления.ВидыЭД.Квитанция);
		КонецЕсли;
	ИначеЕсли ЭДОАннулированИлиВПроцессе() Тогда
		ПричиныОтклонения.Очистить();
		НовСтрока = ПричиныОтклонения.Добавить();
		НовСтрока.ПричинаОтклонения = СсылкаНаЭД.ПричинаОтклонения;
		
		Элементы.ГруппаСтраницыПодвала.ТекущаяСтраница = Элементы.ГруппаСтраницыПодвала.ПодчиненныеЭлементы.ГруппаСтраницаОтклонение;
		Элементы.ПричиныОтклоненияПричинаОтклонения.Заголовок = НСтр("ru = 'Причина аннулирования'");
		Элементы.КомандаОтклонить.Доступность = Ложь;
		Если Объект.СтатусЭД = Перечисления.СтатусыЭД.ПолученоПредложениеОбАннулировании
			ИЛИ Объект.ВидЭД = Перечисления.ВидыЭД.ПредложениеОбАннулировании
			И Объект.СтатусЭД = Перечисления.СтатусыЭД.Получен
			ИЛИ ЗначениеЗаполнено(Объект.ЭлектронныйДокументВладелец)
			И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ЭлектронныйДокументВладелец, "СтатусЭД") = Перечисления.СтатусыЭД.ПолученоПредложениеОбАннулировании Тогда
			Элементы.ФормаГруппаАннулирование.Видимость = Истина;
			Элементы.КомандаОтправитьПодтверждениеЭД.Видимость = Ложь;
			Элементы.КомандаОтправитьЭД.Видимость = Ложь;
			Элементы.КомандаОтклонить.Видимость = Ложь;
			Элементы.ФормаАннулироватьЭД.Видимость = Ложь;
			Элементы.ФормаПринятьАннулирование.ТолькоВоВсехДействиях = НЕ (Объект.ВидЭД = Перечисления.ВидыЭД.ПредложениеОбАннулировании);
			Элементы.ФормаОтклонитьАннулирование.ТолькоВоВсехДействиях = НЕ (Объект.ВидЭД = Перечисления.ВидыЭД.ПредложениеОбАннулировании);
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ РазрешеноОтклонитьЭД() Тогда
		Элементы.КомандаОтклонить.Видимость = Ложь;
	КонецЕсли;
	
	Элементы.ФормаПеренаправить.Видимость = НЕ ЭтоСлужебный И НЕ ЗначениеЗаполнено(ПрограммаБанка);
	
	ОчиститьОповещениеОНеактуальности();
	УстановитьЗаголовокФормы();
	
	Элементы.ГруппаПодвалСтраницыКомментарии.Картинка = ОбщегоНазначения.ПолучитьКартинкуКомментария(ВсеКомментарии);
	Элементы.ГруппаПодвалСтраницыЗаписка.Картинка = ОбщегоНазначения.ПолучитьКартинкуКомментария(Объект.ДополнительнаяИнформация);
	
	Элементы.ЗапроситьСостояниеЭД.Видимость = (ПрограммаБанка = Перечисления.ПрограммыБанка.АсинхронныйОбмен)
		И (Объект.СтатусЭД = Перечисления.СтатусыЭД.Принят ИЛИ Объект.СтатусЭД = Перечисления.СтатусыЭД.Отправлен
			ИЛИ Объект.СтатусЭД = Перечисления.СтатусыЭД.Доставлен ИЛИ Объект.СтатусЭД = Перечисления.СтатусыЭД.Приостановлен
			ИЛИ Объект.СтатусЭД = Перечисления.СтатусыЭД.Картотека2);
		
	Элементы.ГруппаПодвалСтраницыЗаписка.Видимость = НЕ ЗначениеЗаполнено(ПрограммаБанка);
	
	Элементы.КомандаПодтвердитьПлатеж.Видимость = (ПрограммаБанка = Перечисления.ПрограммыБанка.ОбменЧерезДопОбработку
													ИЛИ ПрограммаБанка = Перечисления.ПрограммыБанка.iBank2)
												И Объект.Ссылка.СтатусЭД = Перечисления.СтатусыЭД.Отправлен;
												
	Элементы.ФормаПерейтиКЖурналуСобытий.Доступность = Пользователи.ЭтоПолноправныйПользователь();
	
КонецПроцедуры

&НаСервере
Функция ОбменЧерезОператора()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СоглашенияОбИспользованииЭДИсходящиеДокументы.СпособОбменаЭД КАК СпособОбменаЭД
	|ИЗ
	|	Справочник.СоглашенияОбИспользованииЭД.ИсходящиеДокументы КАК СоглашенияОбИспользованииЭДИсходящиеДокументы
	|ГДЕ
	|	СоглашенияОбИспользованииЭДИсходящиеДокументы.Ссылка = &Ссылка
	|	И СоглашенияОбИспользованииЭДИсходящиеДокументы.ИсходящийДокумент = &ИсходящийДокумент";
	Запрос.УстановитьПараметр("Ссылка", Объект.СоглашениеЭД);
	Запрос.УстановитьПараметр("ИсходящийДокумент", Объект.ВидЭД);
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	Выборка.Следующий();
	Если Не ЭлектронныеДокументыСлужебныйВызовСервера.ЭтоПрямойОбмен(Выборка.СпособОбменаЭД) Тогда
		Возврат Истина
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
	
КонецФункции

&НаСервере
Функция РазрешеноОтклонитьЭД()
	
	Если Объект.ВидЭД = Перечисления.ВидыЭД.ПлатежноеПоручение
		ИЛИ Объект.ВидЭД = Перечисления.ВидыЭД.ПлатежноеТребование Тогда
		Если Объект.СтатусЭД = перечисления.СтатусыЭД.НеОтправлен 
			ИЛИ Объект.СтатусЭД = перечисления.СтатусыЭД.Подписан
			ИЛИ Объект.СтатусЭД = перечисления.СтатусыЭД.Сформирован
			ИЛИ Объект.СтатусЭД = перечисления.СтатусыЭД.Утвержден
			ИЛИ Объект.СтатусЭД = перечисления.СтатусыЭД.ЧастичноПодписан Тогда
				Возврат Истина;
		Иначе
			Возврат Ложь;
		КонецЕсли;
	КонецЕсли;
	Возврат Истина;
	
КонецФункции

&НаСервере
Процедура УстановитьЗаголовокФормы()
	
	Если НЕ ОтраженВУчете И НЕ (Объект.Ссылка.ВидЭД = Перечисления.ВидыЭД.ИзвещениеОПолучении
								ИЛИ Объект.Ссылка.ВидЭД = Перечисления.ВидыЭД.Подтверждение
								ИЛИ Объект.Ссылка.ВидЭД = Перечисления.ВидыЭД.УведомлениеОбУточнении
								ИЛИ Объект.Ссылка.ВидЭД = Перечисления.ВидыЭД.ПредложениеОбАннулировании
								ИЛИ Объект.Ссылка.ВидЭД = Перечисления.ВидыЭД.ВыпискаБанка
								ИЛИ Объект.Ссылка.ВидЭД = Перечисления.ВидыЭД.Квитанция
								ИЛИ Объект.Ссылка.ВидЭД = Перечисления.ВидыЭД.ЗапросВыписки
								ИЛИ Объект.Ссылка.ВидЭД = Перечисления.ВидыЭД.ЗапросОСостоянииЭД
								ИЛИ Объект.Ссылка.ВидЭД = Перечисления.ВидыЭД.ИзвещениеОСостоянииЭД
								ИЛИ Объект.Ссылка.ВидЭД = Перечисления.ВидыЭД.ЗапросЗонд) Тогда
		
		Если НЕ ЗначениеЗаполнено(ВерсияЭД) ИЛИ Объект.Ссылка.ДатаФормированияЭДОтправителем <= ВерсияЭД Тогда
			Заголовок = Заголовок + НСтр("ru = ' - неактуальный'");
		ИначеЕсли Объект.ДатаФормированияЭДОтправителем > ВерсияЭД Тогда
			Заголовок = Заголовок + НСтр("ru = ' - новый'");
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОчиститьОповещениеОНеактуальности()
	
	Заголовок = СтрЗаменить(Заголовок, НСтр("ru = ' - неактуальный'"), "");
	Заголовок = СтрЗаменить(Заголовок, НСтр("ru = ' - новый'"), "");
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСтатусЭД(СтатусЭД = Неопределено, ДатаИзмененияСтатусаЭД = Неопределено)
	
	Если НЕ ЗначениеЗаполнено(СтатусЭД) Тогда
		СтатусЭД = Объект.Ссылка.СтатусЭД;
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(ДатаИзмененияСтатусаЭД) Тогда
		ДатаИзмененияСтатусаЭД = Объект.Ссылка.ДатаИзмененияСтатусаЭД;
	КонецЕсли;
	
	ТекстСтатусЭД = " " +  СтатусЭД + ", " + Формат(ДатаИзмененияСтатусаЭД, "ДЛФ=");
	ТекстСостояния = ЭлектронныеДокументыКлиентСервер.ПолучитьТекстСостоянияЭД(Объект.ВладелецФайла);
	ТекстДокументИБ = Строка(Объект.ВладелецФайла);
	
	ЗапросПоОтражению = Новый Запрос;
	ЗапросПоОтражению.УстановитьПараметр("СсылкаНаОбъект", Объект.ВладелецФайла);
	
	ЗапросПоОтражению.Текст =
	"ВЫБРАТЬ
	|	СостоянияЭД.СсылкаНаОбъект,
	|	СостоянияЭД.ЭлектронныйДокумент
	|ИЗ
	|	РегистрСведений.СостоянияЭД КАК СостоянияЭД
	|ГДЕ
	|	СостоянияЭД.СсылкаНаОбъект = &СсылкаНаОбъект";
	
	Выборка = ЗапросПоОтражению.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ВерсияЭД = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Выборка.ЭлектронныйДокумент, "ДатаФормированияЭДОтправителем");
		ОтраженВУчете = (Объект.Ссылка = Выборка.ЭлектронныйДокумент);
	КонецЕсли;
	
	ТаблицаСтатусов = ПолучитьТаблицуСтатусовЭД(Объект.Ссылка);
	Если ЗначениеЗаполнено(ТаблицаСтатусов) Тогда
		ЗначениеВРеквизитФормы(ТаблицаСтатусов, "СтатусыЭД");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуЭП()
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка.СоглашениеЭД)
		ИЛИ НЕ Объект.Ссылка.СоглашениеЭД.ПроверятьСертификатыПодписей Тогда
		
		ТаблицаЭП = РеквизитФормыВЗначение("ЭП");
		ТаблицаЭП.Очистить();
		
		Для Каждого ТекСтрока Из Объект.Ссылка.ЭлектронныеПодписи Цикл
			НоваяСтрока = ТаблицаЭП.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекСтрока);
			ЗаполнитьСтатусПодписи(НоваяСтрока, ТекСтрока);
		КонецЦикла;
		
		ЗначениеВРеквизитФормы(ТаблицаЭП, "ЭП");
		Возврат;
	КонецЕсли;
	
	МассивОтпечатковОжидаемыхСертификатов = ЭлектронныеДокументыСлужебный.ОтпечаткиОжидаемыхСертификатов(Объект.Ссылка);
	
	ТаблицаЭП = РеквизитФормыВЗначение("ЭП");
	ТаблицаЭП.Очистить();
	
	Для Каждого ТекСтрока Из Объект.Ссылка.ЭлектронныеПодписи Цикл
		НоваяСтрока = ТаблицаЭП.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекСтрока);
		Если МассивОтпечатковОжидаемыхСертификатов.Найти(ТекСтрока.Отпечаток) = Неопределено Тогда
			НоваяСтрока.ОтсутствуетВСписке = Истина;
			НоваяСтрока.ВыводКартинки = 1;
		Иначе
			НоваяСтрока.ВыводКартинки = 0;
		КонецЕсли;
		ЗаполнитьСтатусПодписи(НоваяСтрока, ТекСтрока);
	КонецЦикла;
	ЗначениеВРеквизитФормы(ТаблицаЭП, "ЭП");
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСтатусПодписи(НоваяСтрока, ТекСтрока)
	
	Если ЗначениеЗаполнено(ТекСтрока.ДатаПроверкиПодписи) Тогда
		НоваяСтрока.ПодписьВерна = ?(ТекСтрока.ПодписьВерна, НСтр("ru = 'Верна'"), НСтр("ru = 'Неверна'"))
			+" (" + ТекСтрока.ДатаПроверкиПодписи + ")";
	Иначе
		НоваяСтрока.ПодписьВерна = НСтр("ru = 'Не проверена'");
	КонецЕсли
	
КонецПроцедуры

&НаСервере
Функция НеобходимоПодписать()
	
	ФлагПодписи = Ложь;
	// Ответ на заказ никогда не подписывает покупатель или документ отклонен.
	Если НЕ ЭДОтклонен Тогда
		
		Если Не ЭлектронныеДокументыСлужебныйВызовСервера.ЭтоПрямойОбмен(Объект.Ссылка.ПрофильНастроекЭДО.СпособОбменаЭД) Тогда
			ФлагПодписи = Истина;
		Иначе
			
			УстановитьПривилегированныйРежим(Истина);
			
			Если Объект.Ссылка.НаправлениеЭД = Перечисления.НаправленияЭД.Входящий Тогда
				ФлагПодписи = Объект.Ссылка.ПодписанЭП;
			ИначеЕсли Объект.Ссылка.НаправлениеЭД = Перечисления.НаправленияЭД.Исходящий
				ИЛИ Объект.Ссылка.НаправлениеЭД = Перечисления.НаправленияЭД.Интеркампани Тогда
				
				Если Объект.ВидЭД = Перечисления.ВидыЭД.ИзвещениеОПолучении Тогда
					
					ФлагПодписи = Объект.ЭлектронныйДокументВладелец.ПодписанЭП;
					
				Иначе
					
					Запрос = Новый Запрос;
					Запрос.Текст =
					"ВЫБРАТЬ
					|	СоглашенияОбИспользованииЭДИсходящиеДокументы.ИспользоватьЭП
					|ИЗ
					|	Справочник.СоглашенияОбИспользованииЭД.ИсходящиеДокументы КАК СоглашенияОбИспользованииЭДИсходящиеДокументы
					|ГДЕ
					|	СоглашенияОбИспользованииЭДИсходящиеДокументы.ИсходящийДокумент = &ВидЭД
					|	И (&НаправлениеЭД = ЗНАЧЕНИЕ(Перечисление.НаправленияЭД.Исходящий)
					|			ИЛИ &НаправлениеЭД = ЗНАЧЕНИЕ(Перечисление.НаправленияЭД.Интеркампани))
					|	И СоглашенияОбИспользованииЭДИсходящиеДокументы.Ссылка = &Ссылка";
					Запрос.УстановитьПараметр("Ссылка",        Объект.Ссылка.СоглашениеЭД);
					Запрос.УстановитьПараметр("ВидЭД",         Объект.Ссылка.ВидЭД);
					Запрос.УстановитьПараметр("НаправлениеЭД", Объект.Ссылка.НаправлениеЭД);
					
					Результат = Запрос.Выполнить().Выбрать();
					Результат.Следующий();
					
					ФлагПодписи = Результат.ИспользоватьЭП;
					
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ФлагПодписи;
	
КонецФункции

&НаСервере
Функция ДоступныДляПодписиСертификаты(МассивОтпечатков)
	
	ЗапросПоСертификатам = Новый Запрос;
	ЗапросПоСертификатам.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	Сертификаты.Ссылка
	|ИЗ
	|	РегистрСведений.ПодписываемыеВидыЭД КАК ВидыЭДЭП
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СертификатыКлючейЭлектроннойПодписиИШифрования КАК Сертификаты
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ РАЗЛИЧНЫЕ
	|				ПрофилиЭДОСертификаты.Сертификат КАК Сертификат
	|			ИЗ
	|				Справочник.ЭДПрисоединенныеФайлы КАК ЭДПрисоединенныеФайлы
	|					ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПрофилиНастроекЭДО.СертификатыПодписейОрганизации КАК ПрофилиЭДОСертификаты
	|					ПО ЭДПрисоединенныеФайлы.ПрофильНастроекЭДО = ПрофилиЭДОСертификаты.Ссылка
	|			ГДЕ
	|				ЭДПрисоединенныеФайлы.Ссылка = &Ссылка
	|			
	|			ОБЪЕДИНИТЬ ВСЕ
	|			
	|			ВЫБРАТЬ
	|				СоглашенияЭДСертификаты.Сертификат
	|			ИЗ
	|				Справочник.ЭДПрисоединенныеФайлы КАК ЭДПрисоединенныеФайлы
	|					ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СоглашенияОбИспользованииЭД.СертификатыПодписейОрганизации КАК СоглашенияЭДСертификаты
	|					ПО ЭДПрисоединенныеФайлы.СоглашениеЭД = СоглашенияЭДСертификаты.Ссылка
	|			ГДЕ
	|				ЭДПрисоединенныеФайлы.Ссылка = &Ссылка) КАК СертификатыИзНастроекИПрофилей
	|			ПО (СертификатыИзНастроекИПрофилей.Сертификат = Сертификаты.Ссылка)
	|		ПО ВидыЭДЭП.СертификатЭП = Сертификаты.Ссылка
	|ГДЕ
	|	НЕ Сертификаты.Отозван
	|	И (Сертификаты.Пользователь = &ТекущийПользователь
	|			ИЛИ Сертификаты.Пользователь = ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка)
	|			ИЛИ Сертификаты.Пользователь = &ПользовательНеУказан)
	|	И НЕ Сертификаты.ПометкаУдаления
	|	И ВидыЭДЭП.ВидЭД = &ВидДокумента
	|	И ВидыЭДЭП.Использовать
	|	И ИСТИНА";
	
	Если Объект.ВидЭД = Перечисления.ВидыЭД.ПлатежноеПоручение
			И (ПрограммаБанка = Перечисления.ПрограммыБанка.СбербанкОнлайн
				ИЛИ ПрограммаБанка = Перечисления.ПрограммыБанка.ОбменЧерезДопОбработку
				ИЛИ ПрограммаБанка = Перечисления.ПрограммыБанка.iBank2) Тогда

		МассивСертификатовНеобходимыхПодписей = Объект.СоглашениеЭД.СертификатыПодписейОрганизации.ВыгрузитьКолонку(
																										"Сертификат");
		ДопУсловие = " Сертификаты.Ссылка В(&МассивСертификатовНеобходимыхПодписей)";
		ЗапросПоСертификатам.УстановитьПараметр("МассивСертификатовНеобходимыхПодписей",
												МассивСертификатовНеобходимыхПодписей);
	Иначе
		
		ЗапросПоСертификатам.УстановитьПараметр("МассивОтпечатков", МассивОтпечатков);
		ДопУсловие = " Сертификаты.Отпечаток В(&МассивОтпечатков)";
	КонецЕсли;
	
	ЗапросПоСертификатам.Текст = СтрЗаменить(ЗапросПоСертификатам.Текст, "ИСТИНА", ДопУсловие);
	
	ЗапросПоСертификатам.УстановитьПараметр("ТекущийПользователь", Пользователи.АвторизованныйПользователь());
	СвойстваНеуказанногоПользователя = ПользователиСлужебный.СвойстваНеуказанногоПользователя();
	ЗапросПоСертификатам.УстановитьПараметр("ПользовательНеУказан", СвойстваНеуказанногоПользователя.Ссылка);
	ЗапросПоСертификатам.УстановитьПараметр("ВидДокумента", Объект.ВидЭД);
	ЗапросПоСертификатам.УстановитьПараметр("Ссылка", Объект.Ссылка);

	ИспользуютсяЭП = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьЗначениеФункциональнойОпции(
																	"ИспользоватьЭлектронныеПодписиЭД")
						ИЛИ ПрограммаБанка = Перечисления.ПрограммыБанка.ОбменЧерезДопОбработку
						ИЛИ ПрограммаБанка = Перечисления.ПрограммыБанка.СбербанкОнлайн
						ИЛИ ПрограммаБанка = Перечисления.ПрограммыБанка.iBank2;
	
	ВозвращаемыйПараметр = ИспользуютсяЭП И НЕ ЗапросПоСертификатам.Выполнить().Пустой() И ТребуетсяПодпись;
		
	Возврат ВозвращаемыйПараметр;
	
КонецФункции

&НаСервере
Функция ФайлДанныхЭД(СсылкаНаЭД = Неопределено, Знач ИмяФайлаПодчиненногоЭД = Неопределено, Идентификатор = Неопределено)
	
	Если СсылкаНаЭД = Неопределено Тогда
		СсылкаНаЭД = Объект.Ссылка;
	КонецЕсли;
	
	ДопИнформацияПоЭД = ЭлектронныеДокументыСлужебный.ПолучитьДанныеФайла(СсылкаНаЭД, СсылкаНаЭД.УникальныйИдентификатор(), Истина);
	
	Если Идентификатор = Неопределено Тогда
		Идентификатор = ДопИнформацияПоЭД.Наименование;
	КонецЕсли;
	
	Если ДопИнформацияПоЭД.Свойство("СсылкаНаДвоичныеДанныеФайла")
		И ЗначениеЗаполнено(ДопИнформацияПоЭД.СсылкаНаДвоичныеДанныеФайла) Тогда
		
		ДанныеЭД = ПолучитьИзВременногоХранилища(ДопИнформацияПоЭД.СсылкаНаДвоичныеДанныеФайла);
		
		Если ЗначениеЗаполнено(ДопИнформацияПоЭД.Расширение) Тогда
			ИмяФайла = ЭлектронныеДокументыСлужебный.ТекущееИмяВременногоФайла(ДопИнформацияПоЭД.Расширение);
		Иначе
			ИмяФайла = ЭлектронныеДокументыСлужебный.ТекущееИмяВременногоФайла("xml");
		КонецЕсли;
		
		Если ИмяФайла = Неопределено Тогда
			ТекстОшибки = НСтр("ru = 'Не удалось просмотреть электронный документ. Проверьте настройку рабочего каталога'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
			Возврат Неопределено;
		КонецЕсли;
		
		ВыборкаЭДДопДанных = ЭлектронныеДокументыСлужебный.ВыборкаДопДанныеЭД(СсылкаНаЭД);
		Если ВыборкаЭДДопДанных.Следующий() Тогда
			ДопДанныеЭД = ЭлектронныеДокументыСлужебный.ПолучитьДанныеФайла(ВыборкаЭДДопДанных.Ссылка,
				ВыборкаЭДДопДанных.Ссылка.УникальныйИдентификатор(), Истина);
			СсылкаНаДДДопДанныхЭД = "";
			Если ДопДанныеЭД.Свойство("СсылкаНаДвоичныеДанныеФайла", СсылкаНаДДДопДанныхЭД)
				И ЗначениеЗаполнено(СсылкаНаДДДопДанныхЭД) Тогда
				ДанныеДопФайла = ПолучитьИзВременногоХранилища(СсылкаНаДДДопДанныхЭД);
			
				Если ЗначениеЗаполнено(ДопДанныеЭД.Расширение) Тогда
					ИмяФайлаДопДанных = ЭлектронныеДокументыСлужебный.ТекущееИмяВременногоФайла(ДопДанныеЭД.Расширение);
				Иначе
					ИмяФайлаДопДанных = ЭлектронныеДокументыСлужебный.ТекущееИмяВременногоФайла("xml");
				КонецЕсли;
			
				Если ИмяФайлаДопДанных = Неопределено Тогда
					ТекстОшибки = НСтр("ru = 'Не удалось получить доп. данные электронного документа. Проверьте настройку рабочего каталога'");
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
					Возврат Неопределено;
				КонецЕсли;
				ДанныеДопФайла.Записать(ИмяФайлаДопДанных);
			КонецЕсли;
		КонецЕсли;
		
		ДанныеЭД.Записать(ИмяФайла);
		
		Если СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.ТОРГ12Покупатель
			ИЛИ СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.АктЗаказчик
			ИЛИ СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.СоглашениеОбИзмененииСтоимостиПолучатель Тогда
			
			ТабличныйДокумент = ФайлДанныхЭД(СсылкаНаЭД.ЭлектронныйДокументВладелец, ИмяФайла, Идентификатор);
			Возврат ТабличныйДокумент;
		ИначеЕсли Найти(ДопИнформацияПоЭД.Расширение, "zip") > 0 Тогда
			
			ЗИПЧтение = Новый ЧтениеZipФайла(ИмяФайла);
			ПапкаДляРаспаковки = ЭлектронныеДокументыСлужебный.РабочийКаталог(, Новый УникальныйИдентификатор);
			
			Если ПапкаДляРаспаковки = Неопределено Тогда
				ТекстОшибки = НСтр("ru = 'Не удалось просмотреть электронный документ. Проверьте настройку рабочего каталога'");
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
				Возврат Неопределено;
			КонецЕсли;
			
			Попытка
				ЗипЧтение.ИзвлечьВсе(ПапкаДляРаспаковки);
			Исключение
				ТекстОшибки = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
				Если НЕ ЭлектронныеДокументыСлужебный.ВозможноИзвлечьФайлы(ЗипЧтение, ПапкаДляРаспаковки) Тогда
					ТекстСообщения = ЭлектронныеДокументыПовтИсп.ПолучитьСообщениеОбОшибке("006");
				КонецЕсли;
				ЭлектронныеДокументыСлужебныйВызовСервера.ОбработатьИсключениеПоЭДНаСервере(НСтр("ru = 'Распаковка пакета ЭД'"),
					ТекстОшибки, ТекстСообщения);
				УдалитьФайлы(ПапкаДляРаспаковки);
				Возврат Неопределено;
			КонецПопытки;
			
			ФлагПросмотра = Ложь;
			
			Если СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.АктНаПередачуПрав Тогда
				ФайлыАрхиваPDF = НайтиФайлы(ПапкаДляРаспаковки, "*.pdf");
				Для Каждого РаспакованныйФайл Из ФайлыАрхиваPDF Цикл
					ДвоичныеДанныеФайла = Новый ДвоичныеДанные(РаспакованныйФайл.ПолноеИмя);
					СсылкаНаФайл = ПоместитьВоВременноеХранилище(ДвоичныеДанныеФайла, УникальныйИдентификатор);
					УдалитьФайлы(ПапкаДляРаспаковки);
					Возврат СсылкаНаФайл;
				КонецЦикла;
			КонецЕсли;
			
			ФайлыАрхиваXML = НайтиФайлы(ПапкаДляРаспаковки, "*.xml");
			
			Для Каждого РаспакованныйФайл Из ФайлыАрхиваXML Цикл
				
				ИмяФайлаДанных = РаспакованныйФайл.ПолноеИмя;
				Если Найти(РаспакованныйФайл.Имя, "packageDescription") Тогда
					ДвоичныеДанныеФайла = Новый ДвоичныеДанные(ИмяФайла);
					СсылкаНаФайл = ПоместитьВоВременноеХранилище(ДвоичныеДанныеФайла, УникальныйИдентификатор);
					УдалитьФайлы(ПапкаДляРаспаковки);
					Возврат СсылкаНаФайл;
				КонецЕсли;
				
				ТабличныйДокумент = ЭлектронныеДокументыВнутренний.СформироватьПечатнуюФормуЭД(РаспакованныйФайл.ПолноеИмя,
																								СсылкаНаЭД.НаправлениеЭД,
																								СсылкаНаЭД.УникальныйИдентификатор(),
																								,
																								ДопИнформацияПоЭД.Наименование,
																								ИмяФайлаДопДанных);
					
				Если ТипЗнч(ТабличныйДокумент) = Тип("ТабличныйДокумент") Тогда
					УдалитьФайлы(ПапкаДляРаспаковки);
					Возврат ТабличныйДокумент;
				КонецЕсли;
				
			КонецЦикла;
			
			ФайлыАрхиваMXL = НайтиФайлы(ПапкаДляРаспаковки, "*.mxl");
			Для Каждого РаспакованныйФайл Из ФайлыАрхиваMXL Цикл
				ИмяФайлаДанных = РаспакованныйФайл.ПолноеИмя;
				ТабличныйДокумент = Новый ТабличныйДокумент;
				ТабличныйДокумент.Прочитать(ИмяФайлаДанных);
				УдалитьФайлы(ПапкаДляРаспаковки);
				Возврат ТабличныйДокумент;
			КонецЦикла;
			
			ФайлыАрхиваHTML = НайтиФайлы(ПапкаДляРаспаковки, "*.html");
			Для Каждого РаспакованныйФайл Из ФайлыАрхиваHTML Цикл
				ДвоичныеДанныеФайла = Новый ДвоичныеДанные(РаспакованныйФайл.ПолноеИмя);
				СсылкаНаФайл = ПоместитьВоВременноеХранилище(ДвоичныеДанныеФайла, УникальныйИдентификатор);
				УдалитьФайлы(ПапкаДляРаспаковки);
				Возврат СсылкаНаФайл;
			КонецЦикла;
			
			ФайлыАрхиваDOCX = НайтиФайлы(ПапкаДляРаспаковки, "*.docx");
			Для Каждого РаспакованныйФайл Из ФайлыАрхиваDOCX Цикл
				ДвоичныеДанныеФайла = Новый ДвоичныеДанные(РаспакованныйФайл.ПолноеИмя);
				СсылкаНаФайл = ПоместитьВоВременноеХранилище(ДвоичныеДанныеФайла, УникальныйИдентификатор);
				УдалитьФайлы(ПапкаДляРаспаковки);
				Возврат СсылкаНаФайл;
			КонецЦикла;
			
			ФайлыАрхиваXLS = НайтиФайлы(ПапкаДляРаспаковки, "*.xls");
			Для Каждого РаспакованныйФайл Из ФайлыАрхиваXLS Цикл
				ДвоичныеДанныеФайла = Новый ДвоичныеДанные(РаспакованныйФайл.ПолноеИмя);
				СсылкаНаФайл = ПоместитьВоВременноеХранилище(ДвоичныеДанныеФайла, УникальныйИдентификатор);
				УдалитьФайлы(ПапкаДляРаспаковки);
				Возврат СсылкаНаФайл;
			КонецЦикла;
			
			ФайлыАрхиваXML = НайтиФайлы(ПапкаДляРаспаковки, "*.xml");
			Для Каждого РаспакованныйФайл Из ФайлыАрхиваXML Цикл
				ДвоичныеДанныеФайла = Новый ДвоичныеДанные(РаспакованныйФайл.ПолноеИмя);
				СсылкаНаФайл = ПоместитьВоВременноеХранилище(ДвоичныеДанныеФайла, УникальныйИдентификатор);
				УдалитьФайлы(ПапкаДляРаспаковки);
				Возврат СсылкаНаФайл;
			КонецЦикла;
			
		ИначеЕсли Найти(ДопИнформацияПоЭД.Расширение, "xml") > 0 Тогда
			Если СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.Подтверждение
				ИЛИ СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.ИзвещениеОПолучении
				ИЛИ СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.УведомлениеОбУточнении
				ИЛИ СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.ПредложениеОбАннулировании
				ИЛИ СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.ТОРГ12Продавец
				ИЛИ СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.АктИсполнитель
				ИЛИ СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.СоглашениеОбИзмененииСтоимостиОтправитель
				ИЛИ СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.СчетФактура
				ИЛИ СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.КорректировочныйСчетФактура Тогда
			
				ИмяФайлаДанных = ИмяФайла;
				ТабличныйДокумент = ЭлектронныеДокументыВнутренний.СформироватьПечатнуюФормуЭД(
																			ИмяФайла,
																			СсылкаНаЭД.НаправлениеЭД,
																			СсылкаНаЭД.УникальныйИдентификатор(),
																			ИмяФайлаПодчиненногоЭД,
																			ДопИнформацияПоЭД.Наименование,
																			ИмяФайлаДопДанных,, Идентификатор);
			
				Если ТипЗнч(ТабличныйДокумент) = Тип("ТабличныйДокумент") Тогда
					Возврат ТабличныйДокумент;
				КонецЕсли;
			
			Иначе
				
				ИмяФайлаДанных = ИмяФайла;
				ТабличныйДокумент = ЭлектронныеДокументыВнутренний.СформироватьПечатнуюФормуЭД(
																			ИмяФайла,
																			СсылкаНаЭД.НаправлениеЭД,
																			СсылкаНаЭД.УникальныйИдентификатор(),
																			ИмяФайлаПодчиненногоЭД,
																			СсылкаНаЭД.УникальныйИдентификатор());
			
				Если ТипЗнч(ТабличныйДокумент)=Тип("ТабличныйДокумент") Тогда
					Возврат ТабличныйДокумент;
				КонецЕсли;

			КонецЕсли;
		Иначе
			ДвоичныеДанныеФайла = Новый ДвоичныеДанные(ИмяФайла);
			СсылкаНаФайл = ПоместитьВоВременноеХранилище(ДвоичныеДанныеФайла, УникальныйИдентификатор);
			Возврат СсылкаНаФайл;
		КонецЕсли;
		
	КонецЕсли;
	
КонецФункции

&НаСервере
Процедура ВыполнитьПросмотрЭДИзБДСервер(Отказ = Ложь)
	
	ДанныеЭД = ФайлДанныхЭД();
	Если ДанныеЭД = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Если ТипЗнч(ДанныеЭД) = Тип("ТабличныйДокумент") Тогда
		ТабличныйДокументФормы = ДанныеЭД;
		ИсходныйТабличныйДокумент = ДанныеЭД;
		ОпределитьНаличиеДопДанныхСкрытьФлаг();
		Если ЕстьДопДанные Тогда
			СкрытьДопДанные(ОтключитьВыводДопДанных);
		КонецЕсли;
		Элементы.ГруппаСодержимоеДокумента.ТекущаяСтраница = Элементы.СтраницаТабличныйДокумент;
	Иначе
		Если ТипЗнч(ДанныеЭД) = Тип("Строка") Тогда
			АдресФайлаВХранилище = ДанныеЭД;
		Иначе
			Отказ = Истина;
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ВыполнитьОбработкуОповещенияНаСервере()
	
	ВыполнитьПросмотрЭДИзБДСервер();
	ЗначениеВРеквизитФормы(Объект.Ссылка.ПолучитьОбъект(), "Объект");
	ЗаполнитьТаблицуЭП();
	ОбновитьСтатусЭД();
	ПерезаполнитьКомментарии();
	ИзменитьВидимостьДоступность();
	
КонецПроцедуры

&НаСервере
Процедура ПерепривязатьЭлектронныйДокумент(ВыбранноеЗначение)
	
	ЭлектронныйДокумент = РеквизитФормыВЗначение("Объект");
	СтарыйВладелец = ЭлектронныйДокумент.ВладелецФайла;
	ЭлектронныйДокумент.ВладелецФайла = ВыбранноеЗначение;
	Если Не ЗначениеЗаполнено(ЭлектронныйДокумент.Автор) Тогда
		ЭлектронныйДокумент.Автор = Пользователи.АвторизованныйПользователь();
	КонецЕсли;
	ЭлектронныйДокумент.Записать();
	
	ОбновитьСостояниеЭДВладельцев(ЭлектронныйДокумент.Ссылка, СтарыйВладелец, ВыбранноеЗначение);
	ЗначениеВРеквизитФормы(ЭлектронныйДокумент, "Объект");
	
	ТекстДокументИБ = Строка(Объект.ВладелецФайла.Ссылка);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСостояниеЭДВладельцев(ЭД, СтарыйВладелец, НовыйВладелец)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос();
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	СостоянияЭД.СсылкаНаОбъект
	|ИЗ
	|	РегистрСведений.СостоянияЭД КАК СостоянияЭД
	|ГДЕ
	|	СостоянияЭД.ЭлектронныйДокумент = &ЭлектронныйДокумент
	|	И СостоянияЭД.СсылкаНаОбъект = &СсылкаНаСтарыйДокумент
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	СостоянияЭД.СсылкаНаОбъект
	|ИЗ
	|	РегистрСведений.СостоянияЭД КАК СостоянияЭД
	|ГДЕ
	|	(НЕ СостоянияЭД.ЭлектронныйДокумент = ЗНАЧЕНИЕ(Справочник.ЭДПрисоединенныеФайлы.ПустаяСсылка))
	|	И СостоянияЭД.СсылкаНаОбъект = &СсылкаНаНовыйДокумент";
	
	Запрос.УстановитьПараметр("ЭлектронныйДокумент", ЭД);
	Запрос.УстановитьПараметр("СсылкаНаСтарыйДокумент", СтарыйВладелец);
	Запрос.УстановитьПараметр("СсылкаНаНовыйДокумент", НовыйВладелец);
	
	Выборка = Запрос.ВыполнитьПакет();
	
	ЭлектронныеДокументыСлужебныйВызовСервера.УстановитьНовуюВерсиюЭД(СтарыйВладелец, Неопределено);
	
	Результат2 = Выборка[1].Выбрать();
	Если НЕ Результат2.Следующий() Тогда
		ЭлектронныеДокументыСлужебныйВызовСервера.УстановитьНовуюВерсиюЭД(НовыйВладелец, ЭД);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьСтатус(КартаСтатусовЭД, Статус, Значение = Ложь)
	
	НовСтрока = КартаСтатусовЭД.Добавить();
	НовСтрока.Статус = Статус;
	НовСтрока.Пройден = Значение;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьКартуСтатусовЭД(КартаСтатусовЭД, ЭД)
	
	ПараметрыЭД = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ЭД.Ссылка, "ВидЭД, НаправлениеЭД, ВерсияРегламентаЭДО,
		| Организация, Контрагент, СоглашениеЭД, ПрофильНастроекЭДО, ПодписанЭП, СтатусЭД");
	
	НастройкиСтатусов = Новый ТаблицаЗначений;
	НастройкиСтатусов.Колонки.Добавить("СпособОбмена");
	НастройкиСтатусов.Колонки.Добавить("Направление");
	НастройкиСтатусов.Колонки.Добавить("ВидЭД");
	НастройкиСтатусов.Колонки.Добавить("ИспользоватьПодпись");
	НастройкиСтатусов.Колонки.Добавить("ИспользоватьКвитанции");
	НастройкиСтатусов.Колонки.Добавить("ИспользуетсяНесколькоПодписей");
	НастройкиСтатусов.Колонки.Добавить("ВерсияРегламентаЭДО");
	НастройкиСтатусов.Колонки.Добавить("ПрограммаБанка");
	НастройкиСтатусов.Колонки.Добавить("ВерсияФорматаПакета");
	НастройкиСтатусов.Колонки.Добавить("СтатусЭД");
	
	ИспользуетсяЭП = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьЗначениеФункциональнойОпции(
															"ИспользоватьЭлектронныеПодписиЭД");
	
	Если ПараметрыЭД.ВидЭД = Перечисления.ВидыЭД.ИзвещениеОПолучении
		ИЛИ ПараметрыЭД.ВидЭД = Перечисления.ВидыЭД.Подтверждение
		ИЛИ ПараметрыЭД.ВидЭД = Перечисления.ВидыЭД.ПлатежноеПоручение
		ИЛИ ПараметрыЭД.ВидЭД = Перечисления.ВидыЭД.ПлатежноеТребование
		ИЛИ ПараметрыЭД.ВидЭД = Перечисления.ВидыЭД.ЗапросВыписки
		ИЛИ ((ПараметрыЭД.ВидЭД = Перечисления.ВидыЭД.ТОРГ12Покупатель
			ИЛИ ПараметрыЭД.ВидЭД = Перечисления.ВидыЭД.ТОРГ12Продавец
			ИЛИ ПараметрыЭД.ВидЭД = Перечисления.ВидыЭД.АктИсполнитель
			ИЛИ ПараметрыЭД.ВидЭД = Перечисления.ВидыЭД.АктЗаказчик
			ИЛИ ПараметрыЭД.ВидЭД = Перечисления.ВидыЭД.СоглашениеОбИзмененииСтоимостиОтправитель
			ИЛИ ПараметрыЭД.ВидЭД = Перечисления.ВидыЭД.СоглашениеОбИзмененииСтоимостиПолучатель)
			И ПараметрыЭД.НаправлениеЭД = Перечисления.НаправленияЭД.Входящий)
		ИЛИ ПараметрыЭД.ВидЭД = Перечисления.ВидыЭД.ПредложениеОбАннулировании
		ИЛИ ПараметрыЭД.ВидЭД = Перечисления.ВидыЭД.УведомлениеОбУточнении Тогда
		
		РеквизитыСоглашения = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ПараметрыЭД.СоглашениеЭД,
			"СпособОбменаЭД, ПрограммаБанка, ИспользуетсяКриптография");
		РеквизитыПрофиляНастроекЭДО = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ПараметрыЭД.ПрофильНастроекЭДО,
			"СпособОбменаЭД");
		
		НоваяСтрока = НастройкиСтатусов.Добавить();
		НоваяСтрока.СтатусЭД = ПараметрыЭД.СтатусЭД;
		Если РеквизитыСоглашения.СпособОбменаЭД = Перечисления.СпособыОбменаЭД.ЧерезВебРесурсБанка Тогда
			НоваяСтрока.СпособОбмена = РеквизитыСоглашения.СпособОбменаЭД;
		Иначе
			НоваяСтрока.СпособОбмена = РеквизитыПрофиляНастроекЭДО.СпособОбменаЭД;
		КонецЕсли;
		НоваяСтрока.Направление         = ПараметрыЭД.НаправлениеЭД;
		НоваяСтрока.ВидЭД               = ПараметрыЭД.ВидЭД;
		Если РеквизитыСоглашения.СпособОбменаЭД = Перечисления.СпособыОбменаЭД.ЧерезВебРесурсБанка Тогда
			НоваяСтрока.ИспользоватьПодпись = РеквизитыСоглашения.ИспользуетсяКриптография;
		Иначе
			НоваяСтрока.ИспользоватьПодпись = ИспользуетсяЭП;
		КонецЕсли;
		НоваяСтрока.ИспользоватьКвитанции = Ложь;
		НоваяСтрока.ИспользуетсяНесколькоПодписей = ПараметрыЭД.СоглашениеЭД.СертификатыПодписейОрганизации.Количество() > 1;
		НоваяСтрока.ВерсияРегламентаЭДО   = ПараметрыЭД.ВерсияРегламентаЭДО;
		НоваяСтрока.ПрограммаБанка        = РеквизитыСоглашения.ПрограммаБанка;
	Иначе
		
		РеквизитыСоглашения = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ПараметрыЭД.СоглашениеЭД, "ПрограммаБанка");
		
		СпособОбменаЭД = СпособОбменаТЧСоглашения(ПараметрыЭД.СоглашениеЭД, ПараметрыЭД.ВидЭД);
		
		РеквизитыСоглашения.Вставить("СпособОбменаЭД", СпособОбменаЭД);
		
		Если ПараметрыЭД.НаправлениеЭД = Перечисления.НаправленияЭД.Входящий Тогда
			
			
			НоваяСтрока = НастройкиСтатусов.Добавить();
			НоваяСтрока.СпособОбмена        = РеквизитыСоглашения.СпособОбменаЭД;
			НоваяСтрока.Направление         = ПараметрыЭД.НаправлениеЭД;
			НоваяСтрока.ВидЭД               = ПараметрыЭД.ВидЭД;
			НоваяСтрока.ИспользоватьПодпись = ТребуетсяПодпись;
			
			ВерсияПакетаЭД = ЭлектронныеДокументыСлужебный.ВерсияПакетаЭД(ЭД);
			
			НоваяСтрока.ВерсияФорматаПакета = ВерсияПакетаЭД;
			
			Если ВерсияПакетаЭД = Перечисления.ВерсииФорматаПакетаЭД.Версия30 Тогда
				ИспользоватьКвитанции = Ложь;
			Иначе
				ИспользоватьКвитанции = Истина;
			КонецЕсли;
			НоваяСтрока.ИспользоватьКвитанции = ИспользоватьКвитанции;
			
			НоваяСтрока.ИспользуетсяНесколькоПодписей = ПараметрыЭД.СоглашениеЭД.СертификатыПодписейОрганизации.Количество() > 1;
			НоваяСтрока.ВерсияРегламентаЭДО   = ПараметрыЭД.ВерсияРегламентаЭДО;
			НоваяСтрока.ПрограммаБанка        = РеквизитыСоглашения.ПрограммаБанка;
			
		Иначе
			
			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("ВидЭД",           ПараметрыЭД.ВидЭД);
			Запрос.УстановитьПараметр("НаправлениеЭД",   ПараметрыЭД.НаправлениеЭД);
			Запрос.УстановитьПараметр("Контрагент",      ПараметрыЭД.Контрагент);
			Запрос.УстановитьПараметр("Организация",     ПараметрыЭД.Организация);
			Запрос.УстановитьПараметр("ИспользуетсяЭП", ИспользуетсяЭП);
			Запрос.Текст =
			"ВЫБРАТЬ
			|	ВЫБОР
			|		КОГДА &ИспользуетсяЭП
			|			ТОГДА Соглашение.ИспользоватьПодпись
			|		ИНАЧЕ ЛОЖЬ
			|	КОНЕЦ КАК ИспользоватьПодпись,
			|	Соглашение.ИспользоватьКвитанции,
			|	Соглашение.ВидЭД,
			|	Соглашение.Направление,
			|	Соглашение.СпособОбмена,
			|	Соглашение.ВерсияФорматаПакета,
			|	Соглашение.ПрограммаБанка
			|ИЗ
			|	
			|	(ВЫБРАТЬ
			|		СоглашенияОбИспользованииЭДИсходящиеДокументы.ИспользоватьЭП КАК ИспользоватьПодпись,
			|		Истина КАК ИспользоватьКвитанции,
			|		ВЫБОР
			|			КОГДА &НаправлениеЭД = ЗНАЧЕНИЕ(Перечисление.НаправленияЭД.Интеркампани)
			|				ТОГДА ЗНАЧЕНИЕ(Перечисление.НаправленияЭД.Интеркампани)
			|			ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.НаправленияЭД.Исходящий)
			|		КОНЕЦ КАК Направление,
			|		СоглашенияОбИспользованииЭДИсходящиеДокументы.ИсходящийДокумент КАК ВидЭД,
			|		СоглашенияОбИспользованииЭДИсходящиеДокументы.СпособОбменаЭД КАК СпособОбмена,
			|		0 КАК Приоритет,
			|		СоглашенияОбИспользованииЭДИсходящиеДокументы.Ссылка.ВерсияФорматаПакета КАК ВерсияФорматаПакета,
			|		СоглашенияОбИспользованииЭДИсходящиеДокументы.Ссылка.ПрограммаБанка КАК ПрограммаБанка
			|	ИЗ
			|		Справочник.СоглашенияОбИспользованииЭД.ИсходящиеДокументы КАК СоглашенияОбИспользованииЭДИсходящиеДокументы
			|	ГДЕ
			|		СоглашенияОбИспользованииЭДИсходящиеДокументы.Ссылка.Организация = &Организация
			|		И СоглашенияОбИспользованииЭДИсходящиеДокументы.Ссылка.Контрагент = &Контрагент
			|		И СоглашенияОбИспользованииЭДИсходящиеДокументы.ИсходящийДокумент = &ВидЭД
			|		И СоглашенияОбИспользованииЭДИсходящиеДокументы.Ссылка.СтатусПодключения = ЗНАЧЕНИЕ(Перечисление.СтатусыУчастниковОбменаЭД.Присоединен)
			|		И СоглашенияОбИспользованииЭДИсходящиеДокументы.Формировать
			|		И Не СоглашенияОбИспользованииЭДИсходящиеДокументы.Ссылка.ПометкаУдаления
			|	
			|	ОБЪЕДИНИТЬ ВСЕ
			|	
			|	ВЫБРАТЬ
			|		СоглашенияОбИспользованииЭДИсходящиеДокументы.ИспользоватьЭП,
			|		Истина КАК ИспользоватьКвитанции,
			|		ВЫБОР
			|			КОГДА &НаправлениеЭД = ЗНАЧЕНИЕ(Перечисление.НаправленияЭД.Интеркампани)
			|				ТОГДА ЗНАЧЕНИЕ(Перечисление.НаправленияЭД.Интеркампани)
			|			ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.НаправленияЭД.Входящий)
			|		КОНЕЦ КАК Направление,
			|		&ВидЭД КАК ВидЭД,
			|		СоглашенияОбИспользованииЭДИсходящиеДокументы.СпособОбменаЭД КАК СпособОбмена,
			|		0 КАК Приоритет,
			|		NULL,
			|		NULL
			|	ИЗ
			|		Справочник.СоглашенияОбИспользованииЭД.ИсходящиеДокументы КАК СоглашенияОбИспользованииЭДИсходящиеДокументы
			|	ГДЕ
			|		СоглашенияОбИспользованииЭДИсходящиеДокументы.Ссылка.Организация = &Организация
			|		И СоглашенияОбИспользованииЭДИсходящиеДокументы.Ссылка.Контрагент = &Контрагент
			|		И СоглашенияОбИспользованииЭДИсходящиеДокументы.ИсходящийДокумент = &ВидЭД
			|		И СоглашенияОбИспользованииЭДИсходящиеДокументы.Ссылка.СтатусПодключения = ЗНАЧЕНИЕ(Перечисление.СтатусыУчастниковОбменаЭД.Присоединен)
			|		И СоглашенияОбИспользованииЭДИсходящиеДокументы.Формировать
			|		И Не СоглашенияОбИспользованииЭДИсходящиеДокументы.Ссылка.ПометкаУдаления) КАК Соглашение";
			
			Результат = Запрос.Выполнить().Выбрать();
			
			Если Результат.Следующий() Тогда
				НоваяСтрока = НастройкиСтатусов.Добавить();
				НоваяСтрока.СпособОбмена          = Результат.СпособОбмена;
				НоваяСтрока.Направление           = Результат.Направление;
				НоваяСтрока.ВидЭД                 = Результат.ВидЭД;
				НоваяСтрока.ИспользоватьПодпись   = Результат.ИспользоватьПодпись;
				НоваяСтрока.ИспользоватьКвитанции = Результат.ИспользоватьКвитанции;
				
				НоваяСтрока.ВерсияРегламентаЭДО   = ПараметрыЭД.ВерсияРегламентаЭДО;
				НоваяСтрока.ПрограммаБанка        = Результат.ПрограммаБанка;
				НоваяСтрока.ВерсияФорматаПакета   = Результат.ВерсияФорматаПакета;
				
			КонецЕсли;
			
		КонецЕсли;

	КонецЕсли;
	
	Если НастройкиСтатусов.Количество() > 0 Тогда
		УстановитьСтатусы(КартаСтатусовЭД, НастройкиСтатусов[0]);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура УстановитьСтатусы(КартаСтатусовЭД, НастройкиСтатусов)
	
	МассивСтатусов = ЭлектронныеДокументыСлужебный.ВернутьМассивСтатусовЭД(НастройкиСтатусов);
	Для Каждого Элемент Из МассивСтатусов Цикл
		ДобавитьСтатус(КартаСтатусовЭД, Элемент);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьТаблицуСтатусовЭД(ЭД)
	
	КартаСтатусовЭД = Новый ТаблицаЗначений;
	КартаСтатусовЭД.Колонки.Добавить("Статус");
	КартаСтатусовЭД.Колонки.Добавить("Пройден");
	
	Если ЭД.СтатусЭД = Перечисления.СтатусыЭД.Отклонен ИЛИ ЭД.СтатусЭД = Перечисления.СтатусыЭД.ОтклоненПолучателем Тогда
		
		Если ЭД.НаправлениеЭД = Перечисления.НаправленияЭД.Входящий Тогда
		
			Стр = КартаСтатусовЭД.Добавить();
			Стр.Статус = Перечисления.СтатусыЭД.Получен;
			
			Стр = КартаСтатусовЭД.Добавить();
			Стр.Статус = Перечисления.СтатусыЭД.Отклонен;
		Иначе
			
			Стр = КартаСтатусовЭД.Добавить();
			Стр.Статус = Перечисления.СтатусыЭД.Сформирован;
			
			Стр = КартаСтатусовЭД.Добавить();
			Стр.Статус = ЭД.СтатусЭД;
		КонецЕсли;
		
		КартаСтатусовЭД.ЗаполнитьЗначения(Истина, "Пройден");
		
	Иначе
		
		ЗаполнитьКартуСтатусовЭД(КартаСтатусовЭД, ЭД);
		ПризнакПройден = Истина;
		Для Каждого ТекСтрока Из КартаСтатусовЭД Цикл 
			ТекСтрока.Пройден = ПризнакПройден;
			Если ТекСтрока.Статус = Перечисления.СтатусыЭД.Утвержден
				И (ЭД.СтатусЭД = Перечисления.СтатусыЭД.Отклонен ИЛИ ЭД.СтатусЭД = Перечисления.СтатусыЭД.ОтклоненПолучателем) Тогда
				ТекСтрока.Статус = Перечисления.СтатусыЭД.Отклонен;
				Прервать;
			КонецЕсли;
			Если ЭД.СтатусЭД = ТекСтрока.Статус Тогда
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат КартаСтатусовЭД;
	
КонецФункции

&НаКлиенте
Процедура ПоказатьСертификат(НомерСтроки, Отпечаток)
	
	АдресДанныхСертификата = АдресДанныхСертификата(НомерСтроки);
	ДвоичныеДанныеСертификата = ПолучитьИзВременногоХранилища(АдресДанныхСертификата);
	
	ВыбранныйСертификат = Новый СертификатКриптографии(ДвоичныеДанныеСертификата);
	Если ВыбранныйСертификат=Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("Сертификат не найден"));
		Возврат;
	КонецЕсли;
	
	СтруктураСертификата = ЭлектроннаяПодписьКлиентСервер.ЗаполнитьСтруктуруСертификата(ВыбранныйСертификат);
	Если СтруктураСертификата <> Неопределено Тогда
		ПараметрыФормы = Новый Структура("СтруктураСертификата, Отпечаток, АдресСертификата",
			СтруктураСертификата, Отпечаток, АдресДанныхСертификата);
		ОткрытьФорму("ОбщаяФорма.Сертификат", ПараметрыФормы);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция АдресДанныхСертификата(НомерСтроки)
	
	ДвоичныеДанныеСертификата = Объект.Ссылка.ЭлектронныеПодписи[НомерСтроки-1].Сертификат.Получить();
	СсылкаНаХранилищеДанныхСертификата = ПоместитьВоВременноеХранилище(ДвоичныеДанныеСертификата, УникальныйИдентификатор);
	
	Возврат СсылкаНаХранилищеДанныхСертификата;
	
КонецФункции

&НаСервере
Процедура ДобавитьСертификатПодписиВСоглашение(Отпечаток, СертификатДобавлен)
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка.СоглашениеЭД) Тогда
		Возврат ;
	КонецЕсли;
	
	ОбъектЭД = РеквизитФормыВЗначение("Объект");
	НайденнаяСтрока = ОбъектЭД.ЭлектронныеПодписи.Найти(Отпечаток, "Отпечаток");
	Если НЕ НайденнаяСтрока = Неопределено Тогда
		СоглашениеОбъект = Объект.Ссылка.СоглашениеЭД.ПолучитьОбъект();
		
		НоваяСтрока = СоглашениеОбъект.СертификатыПодписейКонтрагента.Добавить();
		НоваяСтрока.Сертификат = НайденнаяСтрока.Сертификат;
		НоваяСтрока.Отпечаток  = Отпечаток;
		СоглашениеОбъект.Записать();
		
		СертификатДобавлен = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьСертификатВДоверенные(ДанныеПодписи)
	
	Если ДанныеПодписи <> Неопределено И ДанныеПодписи.ОтсутствуетВСписке Тогда 
		ТекстВопроса = НСтр("ru = 'Добавить сертификат %1 в список ожидаемых сертификатов контрагента?'");
		ТекстВопроса = СтрЗаменить(ТекстВопроса, "%1", ДанныеПодписи.КомуВыданСертификат);
		ДопДанные = Новый Структура("ДанныеПодписи", ДанныеПодписи);
		ОписаниеОповещения = Новый ОписаниеОповещения("ДобавитьСертификатВДоверенныеЗавершить", ЭтотОбъект, ДопДанные);
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПоместитьТекстУточненияВОбъект(ТекстУточнения)
	
	ЭлектронныйДокумент = Объект.Ссылка.ПолучитьОбъект();
	ЭлектронныйДокумент.ПричинаОтклонения = ТекстУточнения;
	ЭлектронныйДокумент.Записать();
	ЗначениеВРеквизитФормы(ЭлектронныйДокумент, "Объект");
	
КонецПроцедуры

&НаСервере
Функция ПолучитьКоличествоНеустановленныхПодписей()
	
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	ЭДПрисоединенныеФайлыЭлектронныеПодписи.Отпечаток
	               |ПОМЕСТИТЬ УстановленныеПодписи
	               |ИЗ
	               |	Справочник.ЭДПрисоединенныеФайлы.ЭлектронныеПодписи КАК ЭДПрисоединенныеФайлыЭлектронныеПодписи
	               |ГДЕ
	               |	ЭДПрисоединенныеФайлыЭлектронныеПодписи.Ссылка = &СсылкаНаЭД
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ СоглашенияОбИспользованииЭДСертификатыПодписейОрганизации.Сертификат) КАК КоличествоНеобходимыхПодписей
	               |ИЗ
	               |	Справочник.СоглашенияОбИспользованииЭД.СертификатыПодписейОрганизации КАК СоглашенияОбИспользованииЭДСертификатыПодписейОрганизации
	               |ГДЕ
	               |	НЕ СоглашенияОбИспользованииЭДСертификатыПодписейОрганизации.Сертификат.Отпечаток В
	               |				(ВЫБРАТЬ
	               |					УстановленныеПодписи.Отпечаток
	               |				ИЗ
	               |					УстановленныеПодписи КАК УстановленныеПодписи)
	               |	И СоглашенияОбИспользованииЭДСертификатыПодписейОрганизации.Ссылка = &СоглашениеЭД";
	Запрос.УстановитьПараметр("СсылкаНаЭД", Объект.Ссылка);
	Запрос.УстановитьПараметр("СоглашениеЭД", Объект.Ссылка.СоглашениеЭД);
	РезультатЗапроса = Запрос.Выполнить().Выбрать();
	РезультатЗапроса.Следующий();
	
	Возврат РезультатЗапроса.КоличествоНеобходимыхПодписей;
	
КонецФункции

&НаСервере
Функция ЭДОтклонен()
	
	ЭДОтклонен = (Объект.Ссылка.СтатусЭД = Перечисления.СтатусыЭД.Отклонен
					ИЛИ Объект.Ссылка.СтатусЭД = Перечисления.СтатусыЭД.ОтклоненПолучателем
					ИЛИ Объект.Ссылка.СтатусЭД = Перечисления.СтатусыЭД.ОтклоненБанком
					ИЛИ Объект.Ссылка.СтатусЭД = Перечисления.СтатусыЭД.ОшибкаПередачи
					ИЛИ Объект.Ссылка.СтатусЭД = Перечисления.СтатусыЭД.ОтказанАБС
					ИЛИ Объект.Ссылка.СтатусЭД = Перечисления.СтатусыЭД.ЭПНеВерна
					ИЛИ Объект.Ссылка.СтатусЭД = Перечисления.СтатусыЭД.ОшибкаРеквизитов);
	Возврат ЭДОтклонен
	
КонецФункции

&НаСервере
Функция СкрытьОбластьТабличногоДокумента(ИмяОбласти)
	
	// Находим область дополнительных данных и скрываем ее.
	// Если область ДД имеет левую и/или правую границы, то это вертикальная область, иначе горизонтальная область.
	// Горизонтальная область выводится внизу таблицы, поэтому находим шапку горизонтальной области ДД и скрываем строки
	// от шапки ДД до конца всей таблицы.
	// Вертикальная область присоединяется к основной таблице данных, при этом, если таблица не помещается на одной странице,
	// то именуемая область будет присутствовать только на последней странице. В этом случае алгоритм определения скрываемой
	// области следующий:
	// 1. находим именованную область ДД (например ОбластьДД).
	// 2. вычисляем верхнюю границу области ДД: находим область шапки, следующая строка за областью шапки - будет верхней
	//  строкой области ДД, поэтому берем низ области шапки + 1.
	// 3. т.к. доп.данные присоединяются к основным данным справа, то в качестве нижней границы можно взять высоту табличного документа.
	//
	// Если область "Шапка" не существует, то область ДД может скрываться некорректно/неполностью.
	ЕстьОбластьДД = ТабличныйДокументФормы.Области.Найти(ИмяОбласти);
	Если ЕстьОбластьДД <> Неопределено Тогда
		ОбластьДДТаблицы = ТабличныйДокументФормы.Область(ИмяОбласти);
		ОбластьШапки = ТабличныйДокументФормы.Области.Найти("Шапка");
		Верх = ?(ОбластьДДТаблицы.Лево = 0 И ОбластьДДТаблицы.Право = 0 ИЛИ ОбластьШапки = Неопределено,
			ОбластьДДТаблицы.Верх, ОбластьШапки.Низ + 1);
		УдаляемаяОбласть = ТабличныйДокументФормы.Область(Верх, ОбластьДДТаблицы.Лево,
			ТабличныйДокументФормы.ВысотаТаблицы, ОбластьДДТаблицы.Право);
		ТабличныйДокументФормы.УдалитьОбласть(УдаляемаяОбласть, ТипСмещенияТабличногоДокумента.ПоГоризонтали);
	КонецЕсли;
	
КонецФункции

&НаСервере
Процедура СкрытьДопДанные(Скрыть)
	
	Если Скрыть Тогда
		СкрытьОбластьТабличногоДокумента("ОбластьДД");
		СкрытьОбластьТабличногоДокумента("ОбластьДДСЭП");
		СкрытьОбластьТабличногоДокумента("ОбластьДДБезЭП");
		
		СкрытьОбластьТабличногоДокумента("ОбластьДДСЭП_УС");
		СкрытьОбластьТабличногоДокумента("ОбластьДДСЭП_У");
		СкрытьОбластьТабличногоДокумента("ОбластьДДСЭП_С");
		
		СкрытьОбластьТабличногоДокумента("ДопДанныеШапки_Шапка");
	Иначе
		ТабличныйДокументФормы = ИсходныйТабличныйДокумент.ПолучитьОбласть();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОпределитьНаличиеДопДанныхСкрытьФлаг()
	
	СсылкаНаЭД = Объект.Ссылка;
	
	Если СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.АктЗаказчик 
		ИЛИ СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.АктИсполнитель
		ИЛИ СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.ТОРГ12Покупатель
		ИЛИ СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.ТОРГ12Продавец
		ИЛИ СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.СчетФактура
		ИЛИ СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.КорректировочныйСчетФактура
		ИЛИ СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.СоглашениеОбИзмененииСтоимостиОтправитель
		ИЛИ СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.СоглашениеОбИзмененииСтоимостиПолучатель
		
		ИЛИ СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.ЗаказТовара
		ИЛИ СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.ОтветНаЗаказ
		ИЛИ СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.СчетНаОплату
		ИЛИ СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.ПрайсЛист
		ИЛИ СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.КаталогТоваров
		ИЛИ СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.ОтчетОПродажахКомиссионногоТовара
		ИЛИ СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.ОтчетОСписанииКомиссионногоТовара
		
		Тогда
		
		ЕстьДопДанные = Истина;
	Иначе
		
		ЕстьДопДанные = Ложь;
	КонецЕсли;
	Элементы.ОтключитьВыводДопДанных.Видимость = ЕстьДопДанные;
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьБанковскиеДокументыСбербанка(ЭД)
	
	КоличествоВыгруженныхДокументов = 0;
	
	МассивФайловСбербанка = МассивФайловСбербанка(ЭД);
	
	Для Каждого Элемент Из МассивФайловСбербанка Цикл
		ПолучитьФайл(Элемент.СсылкаНаФайл, Элемент.ИмяФайла);
		КоличествоВыгруженныхДокументов = КоличествоВыгруженныхДокументов + 1;
	КонецЦикла;
	
	ТекстОповещения = Нстр ("ru = 'Выгружено файлов: (" + КоличествоВыгруженныхДокументов + ")'");
	ПоказатьОповещениеПользователя(ТекстОповещения);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ДанныеСлужебногоЭДБанка(Знач ЭД, Знач УникальныйИдентификатор)

	ДанныеФайла = Неопределено;
	СлужебныйЭДБанка = ЭлектронныеДокументыСлужебный.СлужебныйЭДБанка(ЭД);
	Если ЗначениеЗаполнено(СлужебныйЭДБанка) Тогда
		ДанныеФайла = ЭлектронныеДокументыСлужебный.ПолучитьДанныеФайла(СлужебныйЭДБанка, УникальныйИдентификатор);
	КонецЕсли;
	Возврат ДанныеФайла;
	
КонецФункции

&НаСервере
Функция ЭДОАннулированИлиВПроцессе()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИСТИНА КАК Аннулирован
	|ИЗ
	|	Справочник.ЭДПрисоединенныеФайлы КАК ЭДПрисоединенныеФайлы
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ЭДПрисоединенныеФайлы КАК ЭДВладелецЭД
	|		ПО ЭДПрисоединенныеФайлы.ЭлектронныйДокументВладелец = ЭДВладелецЭД.Ссылка
	|ГДЕ
	|	ЭДПрисоединенныеФайлы.Ссылка = &ЭД
	|	И ВЫБОР
	|			КОГДА ЭДВладелецЭД.Ссылка ЕСТЬ NULL 
	|				ТОГДА ЭДПрисоединенныеФайлы.СтатусЭД В (&СписокСтатусовСАннулированием)
	|			ИНАЧЕ ЭДВладелецЭД.СтатусЭД В (&СписокСтатусовСАннулированием)
	|		КОНЕЦ";
		
	МассивСостояний = Новый Массив;
	МассивСостояний.Добавить(Перечисления.СтатусыЭД.Аннулирован);
	МассивСостояний.Добавить(Перечисления.СтатусыЭД.ОтправленоПредложениеОбАннулировании);
	МассивСостояний.Добавить(Перечисления.СтатусыЭД.СформированоПредложениеОбАннулировании);
	МассивСостояний.Добавить(Перечисления.СтатусыЭД.ПолученоПредложениеОбАннулировании);
	Запрос.УстановитьПараметр("СписокСтатусовСАннулированием", МассивСостояний);
	Запрос.УстановитьПараметр("ЭД", Объект.Ссылка);
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		ВозвращаемоеЗначение = Ложь;
	Иначе
		ВозвращаемоеЗначение = Истина;
	КонецЕсли;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

&НаКлиенте
Процедура ОбработатьПредложениеОбАннулировании(ОтклонитьАннулирование)
	
	Если ЗначениеЗаполнено(Объект.ЭлектронныйДокументВладелец) Тогда
		СсылкаНаЭД = Объект.ЭлектронныйДокументВладелец;
	Иначе
		СсылкаНаЭД = Объект.Ссылка;
	КонецЕсли;
	ЭлектронныеДокументыСлужебныйКлиент.ОбработатьПредложениеОбАннулировании(СсылкаНаЭД, ОтклонитьАннулирование);
	
	ИзменитьВидимостьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтклонитьАннулироватьЭД(Отклонить = Ложь)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОтклонитьАннулироватьЭДПродолжить", ЭтотОбъект);
	Если ЗначениеЗаполнено(Объект.ЭлектронныйДокументВладелец) Тогда
		СсылкаНаЭД = Объект.ЭлектронныйДокументВладелец;
	Иначе
		СсылкаНаЭД = Объект.Ссылка;
	КонецЕсли;
	ПараметрыЭД = Новый Структура("Организация, Отклонить, ОписаниеОповещения",
		Объект.Организация, Отклонить, ОписаниеОповещения);
	ЭлектронныеДокументыСлужебныйКлиент.ОбработатьОтклонениеАннулированиеЭД(СсылкаНаЭД, ПараметрыЭД);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиАсинхронныхДиалогов

&НаКлиенте
Процедура ПослеПолученияОтпечатков(Отпечатки, Параметры = Неопределено) Экспорт
	
	МассивОтпечатков = Новый Массив;
	Если ТипЗнч(Отпечатки) = Тип("Соответствие") Тогда
		Для Каждого КлючЗначение Из Отпечатки Цикл
			МассивОтпечатков.Добавить(КлючЗначение.Ключ);
		КонецЦикла
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СсылкаНаМассивОтпечатков) Тогда
		МассивОтпечатковСервера = ПолучитьИзВременногоХранилища(СсылкаНаМассивОтпечатков);
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивОтпечатков, МассивОтпечатковСервера, Истина);
	КонецЕсли;
	
	СсылкаНаМассивОтпечатков = ПоместитьВоВременноеХранилище(МассивОтпечатков, УникальныйИдентификатор);
	
	ИзменитьВидимостьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура ПодтвердитьПлатежЗавершитьiBank2(Знач Результат, Знач ДополнительныеПараметры) Экспорт
	
	Сессия = Неопределено;
	СертификатXML = Неопределено;
	
	Если Результат <> Неопределено
		И ТипЗнч(ДополнительныеПараметры) = Тип("Структура") И ДополнительныеПараметры.Свойство("Сессия", Сессия)
		И ДополнительныеПараметры.Свойство("СертификатXML", СертификатXML) Тогда
		Пароль = Результат;
		ПараметрыЗапроса = Новый Структура("Способ, Пароль, Сессия");
		ПараметрыЗапроса.Способ = "SMS";
		ПараметрыЗапроса.Пароль = Пароль;
		ПараметрыЗапроса.Сессия = Сессия;
		
		Результат = ЭлектронныеДокументыСлужебныйКлиент.ОтправитьЗапросiBank2("5", ПараметрыЗапроса);
		
		Если Не ЗначениеЗаполнено(Результат) Тогда
			Возврат;
		КонецЕсли;
		
		Если Не ПустаяСтрока(Результат.ТекстОшибки) ИЛИ Результат.Статус = "30" Тогда
			ТекстСообщения = НСтр("ru = 'Ошибка при подтверждении платежного поручения: '") + Результат.ТекстОшибки;
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			Возврат;
		КонецЕсли;
		
		УстановитьСтатусДоставлен(Объект.Ссылка);
		
		Оповестить("ОбновитьСостояниеЭД");
		
		ПоказатьОповещениеПользователя(НСтр("ru = 'Документ подтвержден'"));
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодтвердитьПлатежЗавершитьЧерезДополнительнуюОбработку(Знач Результат, Знач ДополнительныеПараметры) Экспорт
	
	Сессия = Неопределено;
	ВнешнийПодключаемыйМодуль = Неопределено;
	СертификатXML = Неопределено;
	Если Результат <> Неопределено
		И ТипЗнч(ДополнительныеПараметры) = Тип("Структура")
		И ДополнительныеПараметры.Свойство("Сессия", Сессия)
		И ДополнительныеПараметры.Свойство("ВнешнийПодключаемыйМодуль", ВнешнийПодключаемыйМодуль)
		И ДополнительныеПараметры.Свойство("СертификатXML", СертификатXML) Тогда
		Пароль = Результат;
		ПараметрыЗапроса = Новый Структура("Способ, Пароль, Сессия");
		ПараметрыЗапроса.Способ = "SMS";
		ПараметрыЗапроса.Пароль = Пароль;
		ПараметрыЗапроса.Сессия = Сессия;
			
		Попытка
			Результат = ВнешнийПодключаемыйМодуль.ОтправитьЗапрос(СертификатXML, 5, ПараметрыЗапроса);
		Исключение
			ШаблонОшибки = НСтр("ru = 'Ошибка подтверждения платежного поручения.
									|Код ошибки: %1
									|%2'");
			ДеталиОшибки = ВнешнийПодключаемыйМодуль.ДеталиОшибки();
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
																ШаблонОшибки,
																ДеталиОшибки.Код,
																ДеталиОшибки.Сообщение);
			Операция = НСтр("ru = 'Подтверждение платежного поручения'");
			ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			ЭлектронныеДокументыСлужебныйВызовСервера.ОбработатьИсключениеПоЭДНаСервере(Операция,
				ПодробноеПредставлениеОшибки, ТекстСообщения, 1);
			Возврат;
		КонецПопытки;
		
		Если Результат.Количество() = 0 Тогда
			ПоказатьОповещениеПользователя(НСтр("ru = 'Нет данных для подтверждения'"));
		КонецЕсли;
		
		Если Не ПустаяСтрока(Результат[0].ТекстОшибки) Тогда
			ТекстСообщения = НСтр("ru = 'Ошибка при подтверждении платежного поручения: '") + Результат[0].ТекстОшибки;
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			Возврат;
		КонецЕсли;
		
		УстановитьСтатусДоставлен(Объект.Ссылка);
		
		Оповестить("ОбновитьСостояниеЭД");
		
		ПоказатьОповещениеПользователя(НСтр("ru = 'Документ подтвержден'"));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодтвердитьПлатежЧерезДополнительнуюОбработку(ВнешнийПодключаемыйМодуль, ДополнительныеПараметры = Неопределено) Экспорт
	
	ОчиститьСообщения();
	Если ВнешнийПодключаемыйМодуль <> Неопределено Тогда
		ПарольУстановленРанее = Ложь;
		МассивСертификатов = Новый Массив;
		ДоступныеСертификаты = ЭлектронныеДокументыСлужебныйВызовСервера.ДоступныеСертификаты(Объект.СоглашениеЭД);
		
		ДанныеСертификата = Неопределено;
		Для Каждого Элемент ИЗ ДоступныеСертификаты Цикл
			ПарольУстановленРанее = ЭлектронныеДокументыСлужебныйКлиент.УстановленПарольСертификатаЧерезДополнительнуюОбработку(
																	ВнешнийПодключаемыйМодуль, Элемент.Значение.ДанныеСертификата);
			Если ПарольУстановленРанее Тогда
				ВыбранныйСертификат = Элемент.Ключ;
				ДанныеСертификата = Элемент.Значение;
				ДанныеСертификата.Вставить("ВыбранныйСертификат", ВыбранныйСертификат);
				СертификатXML = Элемент.Значение.ДанныеСертификата;
				Прервать;
			КонецЕсли;
			МассивСертификатов.Добавить(Элемент.Ключ);
		КонецЦикла;
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("ПарольУстановленРанее", ПарольУстановленРанее);
		ДополнительныеПараметры.Вставить("СоотвСертификатовИИхСтруктур", ДоступныеСертификаты);
		ДополнительныеПараметры.Вставить("ВнешнийПодключаемыйМодуль", ВнешнийПодключаемыйМодуль);
		
		Если ПарольУстановленРанее Тогда
			ПродолжитьПодтверждениеПлатежаПослеВводаПароляКСертификатуЧерезДополнительнуюОбработку(ДанныеСертификата,
				ДополнительныеПараметры);
		Иначе
			ВидОперации = НСтр("ru = 'Аутентификация на ресурсе банка'");
			Если ЭлектронныеДокументыСлужебныйКлиент.ПарольКСертификатуПолучен2(ДоступныеСертификаты, ВидОперации) Тогда
				ДополнительныеПараметры.Вставить("СоотвСертификатовИИхСтруктур", ДоступныеСертификаты);
			Иначе
				ДополнительныеПараметры.Вставить("СоотвСертификатовИИхСтруктур", ДоступныеСертификаты);
				ОООЗ = Новый ОписаниеОповещения(
					"ПродолжитьПодтверждениеПлатежаПослеВводаПароляКСертификатуЧерезДополнительнуюОбработку", ЭтотОбъект,
					ДополнительныеПараметры);
				ДополнительныеПараметры.Вставить("ВызватьОповещение", ОООЗ);
				ЭлектронныеДокументыСлужебныйКлиент.ПолучитьПарольКСертификату(ДоступныеСертификаты,
					ВидОперации, , , ДополнительныеПараметры);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьДокументЗавершить(Знач Результат, Знач ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(Результат) Тогда
		ПерепривязатьЭлектронныйДокумент(Результат);
		Оповестить("ОбновитьСостояниеЭД");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьДокументПродолжить(Знач Результат, Знач ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ВыбЗнач = Объект.ВладелецФайла;
		ОписаниеОповещения = Новый ОписаниеОповещения("ВыбратьДокументЗавершить", ЭтотОбъект);
		Подсказка = НСтр("ru = 'Укажите документ отражения в учете'");
		ПоказатьВводЗначения(ОписаниеОповещения, ВыбЗнач, Подсказка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьСертификатВДоверенныеЗавершить(Знач Результат, Знач ДополнительныеПараметры) Экспорт
	
	ДанныеПодписи = Неопределено;
	Если Результат = КодВозвратаДиалога.Нет Тогда
		ПоказатьСертификат(Элементы.ЭП.ТекущиеДанные.НомерСтроки,Элементы.ЭП.ТекущиеДанные.Отпечаток);
	ИначеЕсли ТипЗнч(ДополнительныеПараметры) = Тип("Структура")
		И ДополнительныеПараметры.Свойство("ДанныеПодписи", ДанныеПодписи)
		И ЗначениеЗаполнено(ДанныеПодписи) Тогда
		// Добавим сертификат в Соглашение.
		СертификатДобавлен = Ложь;
		ДобавитьСертификатПодписиВСоглашение(ДанныеПодписи.Отпечаток, СертификатДобавлен);
		Если НЕ СертификатДобавлен Тогда 
			ТекстСообщения = НСтр("ru = 'Ошибка добавления сертификата подписи в список ожидаемых сертификатов!'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Иначе
			ЗаполнитьТаблицуЭП();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтклонитьАннулироватьЭДПродолжить(Знач Результат, Знач ДополнительныеПараметры) Экспорт
	
	Если Результат = Истина Тогда
		ИзменитьВидимостьДоступность();
		Оповестить("ОбновитьСостояниеЭД");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьПодписиiBank2(ВнешнийПодключаемыйМодуль, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если ВнешнийПодключаемыйМодуль = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	МассивПроверки = Новый Массив;
	МассивПроверки.Добавить(Объект.Ссылка);
	ПараметрыПроверки = Новый Структура;
	ПараметрыПроверки.Вставить("МассивЭДДляПроверкиiBank2", МассивПроверки);
	ПараметрыПроверки.Вставить("ТекущийИндексПроверкиПодписейiBank2", 0);
	ПараметрыПроверки.Вставить("ОповеститьОПроверкеЭП");
	ЭлектронныеДокументыСлужебныйКлиент.НачатьПроверкуСтатусовПодписейiBank2(ПараметрыПроверки);
		
КонецПроцедуры

&НаКлиенте
Процедура ПодтвердитьПлатежiBank2(ВнешнийПодключаемыйМодуль, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если ВнешнийПодключаемыйМодуль = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОчиститьСообщения();
	ДоступныеСертификаты = ЭлектронныеДокументыСлужебныйВызовСервера.ДоступныеСертификаты(Объект.СоглашениеЭД);
	ПарольУстановленРанее = Ложь;
	МассивСертификатов = Новый Массив;
	
	Для Каждого Элемент ИЗ ДоступныеСертификаты Цикл
		ПарольУстановленРанее = ЭлектронныеДокументыСлужебныйКлиент.УстановленПарольСертификатаiBank2(
																		Элемент.Значение.ДвоичныеДанныеСертификата);
		Если ПарольУстановленРанее Тогда
			ВыбранныйСертификат = Элемент.Ключ;
			СертификатXML = Элемент.Значение.ДвоичныеДанныеСертификата;
			Прервать;
		КонецЕсли;
		МассивСертификатов.Добавить(Элемент.Ключ);
	КонецЦикла;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ПарольУстановленРанее", ПарольУстановленРанее);
	ДополнительныеПараметры.Вставить("СоотвСертификатовИИхСтруктур", ДоступныеСертификаты);
	
	Если Не ПарольУстановленРанее Тогда
		ВидОперации = НСтр("ru = 'Аутентификация на ресурсе банка'");
		
		Если НЕ ЭлектронныеДокументыСлужебныйКлиент.ПарольКСертификатуПолучен2(ДоступныеСертификаты, ВидОперации) Тогда
			
			ДополнительныеПараметры.Вставить("СоотвСертификатовИИхСтруктур", ДоступныеСертификаты);
			ОООЗ = Новый ОписаниеОповещения(
					"ПродолжитьПодтверждениеПлатежаПослеВводаПароляКСертификатуiBank2", ЭтотОбъект);
			ДополнительныеПараметры.Вставить("ВызватьОповещение", ОООЗ);
			ЭлектронныеДокументыСлужебныйКлиент.ПолучитьПарольКСертификату(
				ДоступныеСертификаты, ВидОперации, , , ДополнительныеПараметры);
			Возврат;
		КонецЕсли;
		ДополнительныеПараметры.Вставить("СоотвСертификатовИИхСтруктур", ДоступныеСертификаты);
	КонецЕсли;

	ПродолжитьПодтверждениеПлатежаПослеВводаПароляКСертификатуiBank2(Неопределено, ДополнительныеПараметры)
	
КонецПроцедуры

&НаКлиенте
Процедура ПродолжитьПодтверждениеПлатежаПослеВводаПароляКСертификатуЧерезДополнительнуюОбработку(Результат, ДополнительныеПараметры) Экспорт
	
	ВыбранныйСертификат = Неопределено;
	Если ТипЗнч(Результат) = Тип("Структура") И Результат.Свойство("ВыбранныйСертификат", ВыбранныйСертификат)
		И ТипЗнч(ВыбранныйСертификат) = Тип("СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования") Тогда
		
		ДоступныеСертификаты = ДополнительныеПараметры.СоотвСертификатовИИхСтруктур;
		ПарольУстановленРанее = ДополнительныеПараметры.ПарольУстановленРанее;
		ВнешнийПодключаемыйМодуль = ДополнительныеПараметры.ВнешнийПодключаемыйМодуль;
		
		ПарольПользователя = Результат.ПарольПользователя;
		Если ДоступныеСертификаты.Количество() > 0 Тогда
			Для Каждого КлючИЗначение Из ДоступныеСертификаты Цикл
				Если КлючИЗначение.Ключ = ВыбранныйСертификат Тогда
					ПараметрыСертификата = КлючИЗначение.Значение;
					СертификатXML = ПараметрыСертификата.ДанныеСертификата;
					Прервать;
				КонецЕсли;
			КонецЦикла;
			ДанныеСертификата = ЭлектронныеДокументыСлужебныйКлиент.ДанныеСертификатаЧерезДополнительнуюОбработку(
																			ВнешнийПодключаемыйМодуль, СертификатXML);

			Если ДанныеСертификата <> Неопределено Тогда
				ПараметрыВыполнения = Новый Структура;
				ПараметрыВыполнения.Вставить("ИмяПроцедуры", "ВыполнитьПодтверждениеПлатежаЧерезДополнительнуюОбработку");
				ПараметрыВыполнения.Вставить("Модуль",                    ЭтотОбъект);
				ПараметрыВыполнения.Вставить("СертификатXML",             СертификатXML);
				ПараметрыВыполнения.Вставить("ПарольУстановленРанее",     ПарольУстановленРанее);
				ПараметрыВыполнения.Вставить("ПарольПользователя",        ПарольПользователя);
				ПараметрыВыполнения.Вставить("ВнешнийПодключаемыйМодуль", ВнешнийПодключаемыйМодуль);
				
				ТребуетсяУстановкаPINКода = ЭлектронныеДокументыСлужебныйКлиент.НеобходимаУстановкаPINКодаХранилищаЧерезДополнительнуюОбработку(
					ВнешнийПодключаемыйМодуль, ДанныеСертификата.ИдентификаторХранилища);
					
				Если ТребуетсяУстановкаPINКода = Ложь Тогда
					ПродолжитьПодтверждениеПлатежаПослеВводаPINКодаЧерезДополнительнуюОбработку(Истина, ПараметрыВыполнения);
				ИначеЕсли ТребуетсяУстановкаPINКода = Истина Тогда
					ОписаниеОповещенияОЗакрытии = Новый ОписаниеОповещения(
						"ПродолжитьПодтверждениеПлатежаПослеВводаPINКодаЧерезДополнительнуюОбработку", ЭтотОбъект, ПараметрыВыполнения);
					ЭлектронныеДокументыСлужебныйКлиент.НачатьУстановкуPINКодаХранилища(
						Объект.СоглашениеЭД, ДанныеСертификата.ИдентификаторХранилища, ОписаниеОповещенияОЗакрытии);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьЗапросСтатусаЭДВБанк(ДанныеАутентификации, Параметры = Неопределено) Экспорт
	
	Если НЕ ЗначениеЗаполнено(ДанныеАутентификации) Тогда
		Возврат;
	КонецЕсли;
	ПараметрыПолучения = Новый Структура;
	ПараметрыПолучения.Вставить("ДанныеАвторизации", ДанныеАутентификации);
	ПараметрыПолучения.Вставить("СоглашениеЭД", Объект.СоглашениеЭД);
	ПараметрыПолучения.Вставить("ЭлектронныйДокумент", Объект.Ссылка);
	РеквизитыНастройкиЭДО = ЭлектронныеДокументыСлужебныйВызовСервера.РеквизитыНастройкиЭДО(Объект.СоглашениеЭД);
	
	Обработчик = Новый ОписаниеОповещения("ОтправитьЗапросСтатусаЭДВБанкПослеПолученияМаркера", ЭтотОбъект, ПараметрыПолучения);
	ЭлектронныеДокументыСлужебныйКлиент.ПолучитьМаркерБанкаПоЛогинуИПаролю(Объект.СоглашениеЭД, Обработчик,
		РеквизитыНастройкиЭДО.АдресСервера, РеквизитыНастройкиЭДО.ИдентификаторОрганизации,
		ДанныеАутентификации.Пользователь, ДанныеАутентификации.ПарольПользователя);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьЗапросСтатусаЭДВБанкПослеПолученияМаркера(Маркер, Параметры) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Маркер) Тогда
		Возврат;
	КонецЕсли;

	Параметры.Вставить("ИдентификаторСессииБанка", Маркер);
	
	ОткрытьФорму("Обработка.ОбменЭлектроннымиДокументамиСБанком.Форма.ЗапросВБанк", Параметры);
	
КонецПроцедуры


&НаКлиенте
Процедура ВыполнитьПодтверждениеПлатежаiBank2(АутентификацияВыполнена, Параметры) Экспорт
	
	Если НЕ АутентификацияВыполнена = Истина Тогда
		Возврат;
	КонецЕсли;
	
	СертификатXML = Параметры.СертификатXML;
	
	ВнешниеИдентификаторы = Новый Массив;
	ВнешниеИдентификаторы.Добавить(УникальныйИДВнешний(Объект.Ссылка));
	ПараметрыЗапроса = Новый Структура("ИдентификаторыДокументов", ВнешниеИдентификаторы);
	
	Результат = ЭлектронныеДокументыСлужебныйКлиент.ОтправитьЗапросiBank2("4", ПараметрыЗапроса);
	
	Если НЕ ЗначениеЗаполнено(Результат) Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ Результат.Способы.Свойство("SMS") Тогда
		ТекстОшибки = НСтр("ru = 'Подтверждение платежа по SMS не поддерживается.'");
		Операция = НСтр("ru = 'Инициализация процедуры подтверждения платежа'");
		ЭлектронныеДокументыСлужебныйВызовСервера.ОбработатьИсключениеПоЭДНаСервере(
												Операция, ТекстОшибки, ТекстОшибки, 1);
		Возврат;
	КонецЕсли;
		
	Сессия = Результат.Сессия;
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("Сертификат", СертификатXML);
	ПараметрыФормы.Вставить("Сессия", Сессия);
	ПараметрыФормы.Вставить("ЭлектронныйДокумент", Объект.Ссылка);
	ПараметрыФормы.Вставить("СоглашениеЭД", Объект.СоглашениеЭД);
	ПараметрыФормы.Вставить("ПрограммаБанка", ПрограммаБанка);

	ДопДанные = Новый Структура("Сессия, СертификатXML", Сессия, СертификатXML);
	ОписаниеОповещения = Новый ОписаниеОповещения("ПодтвердитьПлатежЗавершитьiBank2", ЭтотОбъект, ДопДанные);
	ОткрытьФорму("Обработка.ОбменЭлектроннымиДокументамиСБанком.Форма.ПодтверждениеПлатежныхПорученийПоSMS",
					ПараметрыФормы, ЭтотОбъект, УникальныйИдентификатор, , , ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьПодтверждениеПлатежаЧерезДополнительнуюОбработку(АутентификацияВыполнена, Параметры) Экспорт
	
	ВнешнийПодключаемыйМодуль = Параметры.ВнешнийПодключаемыйМодуль;
	СертификатXML = Параметры.СертификатXML;
	
	ВнешниеИдентификаторы = Новый Массив;
	ВнешниеИдентификаторы.Добавить(УникальныйИДВнешний(Объект.Ссылка));
	ПараметрыЗапроса = Новый Структура("ИдентификаторыДокументов", ВнешниеИдентификаторы);
	
	Попытка
		Результат = ВнешнийПодключаемыйМодуль.ОтправитьЗапрос(СертификатXML, 4, ПараметрыЗапроса);
	Исключение
		ШаблонОшибки = НСтр("ru = 'Ошибка инициализации сессии подтверждения.
							|Код ошибки: %1
							|%2'");
		ДеталиОшибки = ВнешнийПодключаемыйМодуль.ДеталиОшибки();
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
															ШаблонОшибки,
															ДеталиОшибки.Код,
															ДеталиОшибки.Сообщение);
		Операция = НСтр("ru = 'Инициализация сессии подтверждения'");
		ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЭлектронныеДокументыСлужебныйВызовСервера.ОбработатьИсключениеПоЭДНаСервере(Операция,
			ПодробноеПредставлениеОшибки, ТекстСообщения, 1);
		Возврат;
	КонецПопытки;
	
	Сессия = Результат.Сессия;
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("Сертификат", СертификатXML);
	ПараметрыФормы.Вставить("Сессия", Сессия);
	ПараметрыФормы.Вставить("ЭлектронныйДокумент", Объект.Ссылка);
	ПараметрыФормы.Вставить("СоглашениеЭД", Объект.СоглашениеЭД);
	ПараметрыФормы.Вставить("ПрограммаБанка", ПрограммаБанка);
	
	ДопДанные = Новый Структура("Сессия, ВнешнийПодключаемыйМодуль, СертификатXML",
		Сессия, ВнешнийПодключаемыйМодуль, СертификатXML);
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ПодтвердитьПлатежЗавершитьЧерезДополнительнуюОбработку", ЭтотОбъект, ДопДанные);
	ОткрытьФорму("Обработка.ОбменЭлектроннымиДокументамиСБанком.Форма.ПодтверждениеПлатежныхПорученийПоSMS",
					ПараметрыФормы, ЭтотОбъект, УникальныйИдентификатор, , , ОписаниеОповещения);
	
КонецПроцедуры


&НаКлиенте
Процедура ПродолжитьПодтверждениеПлатежаПослеВводаPINКодаiBank2(PINКодУстановлен, ПараметрыВыполнения) Экспорт
	
	Если Не PINКодУстановлен = Истина Тогда
		Возврат;
	КонецЕсли;
	
	ПарольУстановленРанее = ПараметрыВыполнения.ПарольУстановленРанее;
	СертификатXML = ПараметрыВыполнения.СертификатXML;
	ПарольПользователя = ПараметрыВыполнения.ПарольПользователя;

	Если НЕ ПарольУстановленРанее Тогда
		ПарольУстановлен = ЭлектронныеДокументыСлужебныйКлиент.УстановитьПарольСертификатаiBank2(
																СертификатXML, ПарольПользователя);
		Если НЕ ПарольУстановлен Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	СоединениеУстановлено = Ложь;
	ЭлектронныеДокументыСлужебныйКлиент.УстановитьСоединениеiBank2(
		Объект.СоглашениеЭД, СертификатXML, ПараметрыВыполнения, СоединениеУстановлено);
	Если Не СоединениеУстановлено Тогда
		Возврат;
	КонецЕсли;
	
	ВыполнитьПодтверждениеПлатежаiBank2(СоединениеУстановлено, ПараметрыВыполнения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПродолжитьПодтверждениеПлатежаПослеВводаPINКодаЧерезДополнительнуюОбработку(PINКодУстановлен, ПараметрыВыполнения) Экспорт
	
	Если Не PINКодУстановлен = Истина Тогда
		Возврат;
	КонецЕсли;
	
	ПарольУстановленРанее = ПараметрыВыполнения.ПарольУстановленРанее;
	ВнешнийПодключаемыйМодуль = ПараметрыВыполнения.ВнешнийПодключаемыйМодуль;
	СертификатXML = ПараметрыВыполнения.СертификатXML;
	ПарольПользователя = ПараметрыВыполнения.ПарольПользователя;

	Если НЕ ПарольУстановленРанее Тогда
		ПарольУстановлен = ЭлектронныеДокументыСлужебныйКлиент.УстановитьПарольСертификатаЧерезДополнительнуюОбработку(
															ВнешнийПодключаемыйМодуль, СертификатXML, ПарольПользователя);
		Если НЕ ПарольУстановлен Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	СоединениеУстановлено = Ложь;
	ЭлектронныеДокументыСлужебныйКлиент.УстановитьСоединениеЧерезДополнительнуюОбработку(
		Объект.СоглашениеЭД, ВнешнийПодключаемыйМодуль, СертификатXML, ПараметрыВыполнения, СоединениеУстановлено);
	Если Не СоединениеУстановлено Тогда
		Возврат;
	КонецЕсли;
	
	ВыполнитьПодтверждениеПлатежаЧерезДополнительнуюОбработку(СоединениеУстановлено, ПараметрыВыполнения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПродолжитьПодтверждениеПлатежаПослеВводаПароляКСертификатуiBank2(Результат, ДополнительныеПараметры) Экспорт
	
	ДоступныеСертификаты = ДополнительныеПараметры.СоотвСертификатовИИхСтруктур;
	ПарольУстановленРанее = ДополнительныеПараметры.ПарольУстановленРанее;
	
	Если ДоступныеСертификаты.Количество() > 0 Тогда
		Для Каждого КлючИЗначение Из ДоступныеСертификаты Цикл
			ПараметрыСертификата = КлючИЗначение.Значение;
			ПарольПользователя = ПараметрыСертификата.ПарольПользователя;
			ВыбранныйСертификат = КлючИЗначение.Ключ;
			СертификатXML = ПараметрыСертификата.ДвоичныеДанныеСертификата;
			Прервать;
		КонецЦикла;
	Иначе
		Возврат;
	КонецЕсли;
	
		
	ДанныеСертификата = ЭлектронныеДокументыСлужебныйКлиент.ДанныеСертификатаiBank2(СертификатXML);

	Если ДанныеСертификата = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыВыполнения = Новый Структура;
	ПараметрыВыполнения.Вставить("ИмяПроцедуры", "ВыполнитьПодтверждениеПлатежаiBank2");
	ПараметрыВыполнения.Вставить("Модуль", ЭтотОбъект);
	ПараметрыВыполнения.Вставить("СертификатXML", СертификатXML);
	ПараметрыВыполнения.Вставить("ПарольУстановленРанее", ПарольУстановленРанее);
	ПараметрыВыполнения.Вставить("ПарольПользователя", ПарольПользователя);
	
	ТребуетсяУстановкаPINКода = ЭлектронныеДокументыСлужебныйКлиент.НеобходимоУстановитьPINКодХранилищаiBank2(
																		ДанныеСертификата.ИдентификаторХранилища);
		
	Если ТребуетсяУстановкаPINКода = Неопределено Тогда
		Возврат;
	ИначеЕсли ТребуетсяУстановкаPINКода Тогда
		ОписаниеОповещенияОЗакрытии = Новый ОписаниеОповещения("ПродолжитьПодтверждениеПлатежаПослеВводаPINКодаiBank2",
																				ЭтотОбъект, ПараметрыВыполнения);
		ЭлектронныеДокументыСлужебныйКлиент.НачатьУстановкуPINКодаХранилища(
			Объект.СоглашениеЭД, ДанныеСертификата.ИдентификаторХранилища, ОписаниеОповещенияОЗакрытии);
		Возврат;
	КонецЕсли;
	
	ПродолжитьПодтверждениеПлатежаПослеВводаPINКодаiBank2(Истина, ПараметрыВыполнения);
	
КонецПроцедуры

&НаСервере
Функция СпособОбменаТЧСоглашения(СоглашениеЭД, ВидЭД)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СоглашенияОбИспользованииЭДИсходящиеДокументы.СпособОбменаЭД Как СпособОбменаЭД
	|ИЗ
	|	Справочник.СоглашенияОбИспользованииЭД.ИсходящиеДокументы КАК СоглашенияОбИспользованииЭДИсходящиеДокументы
	|ГДЕ
	|	СоглашенияОбИспользованииЭДИсходящиеДокументы.Ссылка = &СоглашениеЭД
	|	И СоглашенияОбИспользованииЭДИсходящиеДокументы.ИсходящийДокумент = &ВидЭД";
	
	Запрос.УстановитьПараметр("СоглашениеЭД", СоглашениеЭД);
	Запрос.УстановитьПараметр("ВидЭД", ВидЭД);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	СпособОбмена = Неопределено;
	Если Выборка.Следующий() Тогда
	
		СпособОбмена = Выборка.СпособОбменаЭД;
	КонецЕсли;
	
	Возврат СпособОбмена;
	
КонецФункции

&НаКлиенте
Процедура ПослеПолученияОтпечатковЗапроситьСостояниеЭД(Отпечатки, Параметры = Неопределено) Экспорт
	
	МассивОтпечатковСертификов = Новый Массив;
	
	Если ТипЗнч(Отпечатки) = Тип("Соответствие") Тогда
		Для Каждого КлючЗначение Из Отпечатки Цикл
			МассивОтпечатковСертификов.Добавить(КлючЗначение.Ключ);
		КонецЦикла
	КонецЕсли;
	
	Соответствие = ЭлектронныеДокументыСлужебныйВызовСервера.СоответствиеДоступныхСертификатовИПараметров(
		МассивОтпечатковСертификов, Объект.СоглашениеЭД);
	
	Сертификат = Неопределено;
	МассивСертификатов = Новый Массив;
	ПарольПолучен = Ложь;
	Для Каждого КлючЗначение Из Соответствие Цикл
		МассивСертификатов.Добавить(КлючЗначение.Ключ);
		Если КлючЗначение.Значение.Свойство("ПарольПолучен", ПарольПолучен) И ПарольПолучен = Истина Тогда
			МассивСертификатов.Очистить();
			МассивСертификатов.Добавить(КлючЗначение.Ключ);
			Прервать;
		КонецЕсли;
	КонецЦикла;
	Если МассивСертификатов.Количество() Тогда
			
		ИД_Параметров = Строка(Новый УникальныйИдентификатор);
			
		СертификатыБанкаСМаркерами = ЭлектронныеДокументыСлужебныйВызовСервера.СертификатыБанкаСМаркерами(
			Объект.СоглашениеЭД, МассивСертификатов);
			
		Если СертификатыБанкаСМаркерами = Неопределено Тогда
			Возврат;
		КонецЕсли;
			
		ПараметрыПриложения.Вставить("ЭлектронноеВзаимодействие." + ИД_Параметров, СертификатыБанкаСМаркерами);
		
		ОписаниеДанных = Новый Структура;
		ОписаниеДанных.Вставить("ОтборСертификатов", МассивСертификатов);
		ОписаниеДанных.Вставить("БезПодтверждения",  Истина);
		ОписаниеДанных.Вставить("ЭтоАутентификация", Истина);
		ОписаниеДанных.Вставить("Операция", НСтр("ru = 'Аутентификация на сервере банка'"));
		ОписаниеДанных.Вставить("РазрешитьЗапоминатьПароль", Истина);
			
		ПараметрыЗапросаМеркера = Новый Структура("ИД_Параметров, ОписаниеДанных", ИД_Параметров, ОписаниеДанных);
		Маркер = Новый ОписаниеОповещения(
			"ПолучитьДанныеЗашифрованногоМаркераБанка", ЭлектронныеДокументыСлужебныйКлиент, ПараметрыЗапросаМеркера);
						
		ОписаниеДанных.Вставить("Данные", Маркер);
										
		ОписаниеОповещения = Новый ОписаниеОповещения("ПослеРасшифровкиМаркераБанкаОтправитьЗапросСостоянияЭД", ЭтотОбъект);
					
		ЭлектроннаяПодписьКлиент.Расшифровать(ОписаниеДанных, , ОписаниеОповещения);
			
	Иначе
		ТекстСообщения = НСтр("ru = 'Нет доступных сертификатов.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Функция МожноОтозватьЭД(ЭД)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СостоянияЭД.СсылкаНаОбъект
	|ИЗ
	|	РегистрСведений.СостоянияЭД КАК СостоянияЭД
	|ГДЕ
	|	СостоянияЭД.ЭлектронныйДокумент = &ЭлектронныйДокумент
	|	И СостоянияЭД.СостояниеВерсииЭД = ЗНАЧЕНИЕ(Перечисление.СостоянияВерсийЭД.ОжидаетсяИсполнение)
	|	И СостоянияЭД.ЭлектронныйДокумент.СоглашениеЭД.ИспользуетсяКриптография
	|	И СостоянияЭД.ЭлектронныйДокумент.СоглашениеЭД.ПрограммаБанка = ЗНАЧЕНИЕ(Перечисление.ПрограммыБанка.АсинхронныйОбмен)";
	Запрос.УстановитьПараметр("ЭлектронныйДокумент", ЭД);
	Результат = Запрос.Выполнить();
	Возврат НЕ Результат.Пустой();
	
КонецФункции

&НаКлиенте
Процедура НачатьОтзывЭД(Строка, Параметры) Экспорт
	
	Если Строка = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Отзыв = Неопределено;
	
	Отзыв = СформироватьЗапросНаОтзывЭД(Объект.СоглашениеЭД, Объект.Ссылка, Строка);
	
	МассивСсылок = Новый Массив;
	МассивСсылок.Добавить(Объект.ВладелецФайла);
	ЭлектронныеДокументыСлужебныйКлиент.ОбработатьЭД(МассивСсылок, "УтвердитьПодписатьОтправить", , Отзыв);

КонецПроцедуры

&НаКлиенте
Процедура ПослеРасшифровкиМаркераБанкаОтправитьЗапросСостоянияЭД(Результат, Параметры) Экспорт
	
	Если НЕ Результат.Успех Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыЗапроса = Новый Структура;
	ИдентификаторСессииБанка = ЭлектронныеДокументыСлужебныйВызовСервера.СтрокаИзДвоичныхДанных(
		Результат.РасшифрованныеДанные);
	ПараметрыЗапроса.Вставить("ЭлектронныйДокумент", Объект.Ссылка);
	ПараметрыЗапроса.Вставить("СоглашениеЭД", Объект.СоглашениеЭД);
		
	ОтправитьЗапросСтатусаЭДВБанкПослеПолученияМаркера(ИдентификаторСессииБанка, ПараметрыЗапроса);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СформироватьЗапросНаОтзывЭД(Знач НастройкаЭДО, Знач ЭДПлатежноеПоручение, Знач Причина)
	
	ТекстОшибки = "";
	ПространствоИмен = ЭлектронныеДокументыСлужебный.ПространствоИменАсинхронногоОбменаСБанками();
	ВерсияФормата = ЭлектронныеДокументыСлужебный.ВерсияСхемыАсинхронногоОбменаСБанками();
	ВерсияПрограммы = ЭлектронныеДокументыПовтИсп.ВерсияПрограммыКлиентаДляБанка();
	РеквизитыНастройкиЭДО = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(НастройкаЭДО, "Организация, ИдентификаторОрганизации, Контрагент");
	ИдентификаторОрганизации = РеквизитыНастройкиЭДО.ИдентификаторОрганизации;
	
	ОтправительНаименование = ЭлектронныеДокументыСлужебный.СокращенноеНаименованиеОрганизации(
															РеквизитыНастройкиЭДО.Организация);
	РеквизитыОрганизации = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(РеквизитыНастройкиЭДО.Организация, "ИНН, КПП");
	РеквизитыБанка = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(РеквизитыНастройкиЭДО.Контрагент, "Код, Наименование");
	
	Попытка
	
		ВремФайл = ЭлектронныеДокументыСлужебный.ТекущееИмяВременногоФайла("xml");
			
		УнИдЭД = Новый УникальныйИдентификатор;
			
		ЭД = ЭлектронныеДокументыВнутренний.ПолучитьОбъектТипаCML("CancelationRequest", ПространствоИмен);
		ЭлектронныеДокументыВнутренний.ЗаполнитьСвойствоXDTO(ЭД, "id", Строка(УнИдЭД), Истина, ТекстОшибки);
		ЭлектронныеДокументыВнутренний.ЗаполнитьСвойствоXDTO(ЭД, "formatVersion", ВерсияФормата, Истина, ТекстОшибки);
		ЭлектронныеДокументыВнутренний.ЗаполнитьСвойствоXDTO(ЭД, "creationDate", ТекущаяДатаСеанса(), Истина, ТекстОшибки);
		ЭлектронныеДокументыВнутренний.ЗаполнитьСвойствоXDTO(ЭД, "userAgent", ВерсияПрограммы, , ТекстОшибки);
			
		Отправитель = ЭлектронныеДокументыВнутренний.ПолучитьОбъектТипаCML("CustomerPartyType", ПространствоИмен);
		ЭлектронныеДокументыВнутренний.ЗаполнитьСвойствоXDTO(Отправитель, "id", ИдентификаторОрганизации, Истина, ТекстОшибки);
		ЭлектронныеДокументыВнутренний.ЗаполнитьСвойствоXDTO(Отправитель, "name", ОтправительНаименование, , ТекстОшибки);
		ЭлектронныеДокументыВнутренний.ЗаполнитьСвойствоXDTO(Отправитель, "inn", РеквизитыОрганизации.ИНН, , ТекстОшибки);
		ЭлектронныеДокументыВнутренний.ЗаполнитьСвойствоXDTO(Отправитель, "kpp", РеквизитыОрганизации.КПП, , ТекстОшибки);
		ЭлектронныеДокументыВнутренний.ЗаполнитьСвойствоXDTO(ЭД, "Sender", Отправитель, Истина, ТекстОшибки);
			
		Получатель = ЭлектронныеДокументыВнутренний.ПолучитьОбъектТипаCML("BankPartyType", ПространствоИмен);
		ЭлектронныеДокументыВнутренний.ЗаполнитьСвойствоXDTO(Получатель, "bic", РеквизитыБанка.Код, Истина, ТекстОшибки);
		ЭлектронныеДокументыВнутренний.ЗаполнитьСвойствоXDTO(Получатель, "name", РеквизитыБанка.Наименование, , ТекстОшибки);
		ЭлектронныеДокументыВнутренний.ЗаполнитьСвойствоXDTO(ЭД, "Recipient", Получатель, Истина, ТекстОшибки);
		ИДЭД = Строка(ЭДПлатежноеПоручение.УникальныйИдентификатор());
		
		ЭлектронныеДокументыВнутренний.ЗаполнитьСвойствоXDTO(ЭД, "ExtID", ИДЭД, Истина, ТекстОшибки);
		ЭлектронныеДокументыВнутренний.ЗаполнитьСвойствоXDTO(ЭД, "Reason", Причина, , ТекстОшибки);
		ЭД.Проверить();
		
		Если ЗначениеЗаполнено(ТекстОшибки) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
			Возврат Неопределено;
		КонецЕсли;
		
		ЭлектронныеДокументыВнутренний.ВыгрузитьЭДвФайл(ЭД, ВремФайл, Ложь, "UTF-8");
		
	Исключение
		КраткоеПредставлениеОшибки = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		ШаблонСообщения = НСтр("ru = '%1 (подробности см. в Журнале регистрации).'");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
										ШаблонСообщения, КраткоеПредставлениеОшибки);
		Операция = НСтр("ru = 'Формирование ЭД'");
		ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЭлектронныеДокументыСлужебныйВызовСервера.ОбработатьИсключениеПоЭДНаСервере(
							Операция, ПодробноеПредставлениеОшибки, ТекстСообщения, 1);
		Возврат Неопределено;
	КонецПопытки;

	ДвоичныеДанные = Новый ДвоичныеДанные(ВремФайл);
	АдресФайла = ПоместитьВоВременноеХранилище(ДвоичныеДанные);

	УдалитьФайлы(ВремФайл);
	
	НазваниеЭД = НСтр("ru = 'Запрос на отзыв ЭД'");
	ВладелецФайла = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЭДПлатежноеПоручение, "ВладелецФайла");

	ТекущаяДатаСеанса = ТекущаяДатаСеанса();
	ЭДЗапросНаОтзыв = ПрисоединенныеФайлы.ДобавитьФайл(ВладелецФайла, НазваниеЭД, "xml", ТекущаяДатаСеанса,
		ТекущаяДатаСеанса, АдресФайла, , , Справочники.ЭДПрисоединенныеФайлы.ПолучитьСсылку(УнИдЭД));

	Ответственный = ЭлектронныеДокументыПереопределяемый.ПолучитьОтветственногоПоЭД(
										РеквизитыНастройкиЭДО.Контрагент, НастройкаЭДО);

	СтруктураПараметров = Новый Структура();
	СтруктураПараметров.Вставить("Автор", Пользователи.АвторизованныйПользователь());
	СтруктураПараметров.Вставить("НаправлениеЭД", Перечисления.НаправленияЭД.Исходящий);
	СтруктураПараметров.Вставить("ВидЭД", Перечисления.ВидыЭД.ЗапросНаОтзывЭД);
	СтруктураПараметров.Вставить("СтатусЭД", Перечисления.СтатусыЭД.Сформирован);
	СтруктураПараметров.Вставить("Ответственный", Ответственный);
	СтруктураПараметров.Вставить("Организация", РеквизитыНастройкиЭДО.Организация);
	СтруктураПараметров.Вставить("Контрагент", РеквизитыНастройкиЭДО.Контрагент);
	СтруктураПараметров.Вставить("ЭлектронныйДокументВладелец", ЭДПлатежноеПоручение);
	СтруктураПараметров.Вставить("СоглашениеЭД", НастройкаЭДО);
	СтруктураПараметров.Вставить("ДатаДокументаОтправителя", ТекущаяДатаСеанса);
	СтруктураПараметров.Вставить("НаименованиеФайла", НазваниеЭД);
	
	ЭлектронныеДокументыСлужебный.ИзменитьПоСсылкеПрисоединенныйФайл(ЭДЗапросНаОтзыв, СтруктураПараметров, Ложь);

	Возврат ЭДЗапросНаОтзыв;
	
КонецФункции

#КонецОбласти
