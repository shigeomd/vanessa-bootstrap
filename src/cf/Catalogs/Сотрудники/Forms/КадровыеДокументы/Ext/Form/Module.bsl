#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ЭтаФорма, Параметры, "Заголовок,СотрудникСсылка");
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(КадровыеДокументы.Отбор, "Сотрудник", СотрудникСсылка);
	
	ЗарплатаКадры.ПриСозданииНаСервереФормыСДинамическимСписком(ЭтотОбъект, "КадровыеДокументы");
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	СотрудникиКлиент.ОбработкаОповещения(ЭтаФорма, ИмяСобытия, Параметр, Источник);
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовТаблицыФормыКадровыеДокументы

&НаКлиенте
Процедура КадровыеДокументыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	ПолноИмяТипаДокумента = ИмяТипаДокументаВводимогоНаОснованииСотрудника(Параметр);
	Если НЕ ПустаяСтрока(ПолноИмяТипаДокумента) Тогда
		
		Отказ = Истина;
		
		ПутьКФормеДокумента = ПолноИмяТипаДокумента + ".ФормаОбъекта";
		
		ПараметрыФормы = Новый Структура("Основание", СотрудникСсылка);
		ОткрытьФорму(ПутьКФормеДокумента, ПараметрыФормы, ЭтаФорма);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СписокПередЗагрузкойПользовательскихНастроекНаСервере(Элемент, Настройки)
	ЗарплатаКадры.ПроверитьПользовательскиеНастройкиДинамическогоСписка(ЭтотОбъект, Истина);
КонецПроцедуры

&НаСервере
Процедура СписокПриОбновленииСоставаПользовательскихНастроекНаСервере(СтандартнаяОбработка)
	ЗарплатаКадры.ПроверитьПользовательскиеНастройкиДинамическогоСписка(ЭтотОбъект);
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ИмяТипаДокументаВводимогоНаОснованииСотрудника(ТипДокумента)
	
	ИмяТипа = "";
	
	ОбъектМетаданных = Метаданные.НайтиПоТипу(ТипДокумента);
	Если ОбъектМетаданных <> Неопределено Тогда
		
		Если ОбъектМетаданных.ВводитсяНаОсновании.Содержит(Метаданные.Справочники.Сотрудники) Тогда
			ИмяТипа = ОбъектМетаданных.ПолноеИмя();
		КонецЕсли; 
		
	КонецЕсли; 
	
	Возврат ИмяТипа;
	
КонецФункции

#КонецОбласти
