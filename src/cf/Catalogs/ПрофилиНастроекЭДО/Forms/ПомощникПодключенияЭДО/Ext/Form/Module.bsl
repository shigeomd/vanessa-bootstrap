
&НаКлиенте
Перем ВнутренниеДанные, ЗначениеПароля, ОписаниеДанных, ФормаОбъекта, СписокПредставлений, ФормаДлительнойОперации;

&НаКлиенте
Перем ОбработкаПослеПредупреждения, ОбработкаПоясненияПароля, ПараметрыОбработчикаОжидания;

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Элементы.СтраницыПомощника.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
	
	Если Параметры.Свойство("Организация") Тогда
		Организация = Параметры.Организация;
		Если ОбщегоНазначения.ПодсистемаСуществует("РегламентированнаяОтчетность") Тогда
			МодульЭлектронныйДокументооборотСКонтролирующимиОрганами = ОбщегоНазначения.ОбщийМодуль("ЭлектронныйДокументооборотСКонтролирующимиОрганами");

			СтруктураПараметров = Новый Структура;
			МодульЭлектронныйДокументооборотСКонтролирующимиОрганами.ЗаполнитьПараметрыДляФормыПодключенияКСервисуЭлектронныхДокументов(
				Организация, СтруктураПараметров);
				
				Если ЗначениеЗаполнено(СтруктураПараметров) Тогда
					Если Не СтруктураПараметров.ЭлектроннаяПодписьВМоделиСервиса Тогда
						ОтпечатокСертификата = СтруктураПараметров.ОтпечатокСертификатаПодписи;
					КонецЕсли;
				КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если Параметры.Свойство("Контрагент") Тогда
		Контрагент = Параметры.Контрагент;
	КонецЕсли;
	
	СпособыОбменаЭД = "";
	Если Параметры.Свойство("СпособыОбменаЭД", СпособыОбменаЭД) И ЗначениеЗаполнено(СпособыОбменаЭД) Тогда
		Если СпособыОбменаЭД.Количество() = 1 Тогда
			СпособОбменаЭД = СпособыОбменаЭД[0];
			Элементы.СпособОбменаЭД.СписокВыбора.Добавить(СпособОбменаЭД);
			Если СпособОбменаЭД = Перечисления.СпособыОбменаЭД.ЧерезОператораЭДОТакском Тогда
				
				Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.СтраницаПодключенияК1СТакском;
				ЭтаФорма.Заголовок = Элементы.СтраницаПодключенияК1СТакском.Заголовок;
				
			ИначеЕсли СпособОбменаЭД = Перечисления.СпособыОбменаЭД.ЧерезСервис1СЭДО Тогда
				Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.СтраницаПодключенияК1СЭДО;
				ЭтаФорма.Заголовок = Элементы.СтраницаПодключенияК1СЭДО.Заголовок;
				
				ОператорЭДО = "ЗАО Калуга Астрал";
				ПодключенныеОператорыЭДО1СЭДО();
				
				ТипРегистрации = 2;
				ПараметрыАутентификацииНаСайте = СтандартныеПодсистемыСервер.ПараметрыАутентификацииНаСайте();
				
			КонецЕсли;
		Иначе
			Для каждого Строка Из СпособыОбменаЭД Цикл
				Элементы.СпособОбменаЭД.СписокВыбора.Добавить(Строка);
			КонецЦикла;
			Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.СтраницаНастройкиПрямогоОбмена;
			СпособОбменаЭД = Перечисления.СпособыОбменаЭД.ЧерезЭлектроннуюПочту;
			ЭтотОбъект.Заголовок = Элементы.СтраницаНастройкиПрямогоОбмена.Заголовок;
		КонецЕсли;
	КонецЕсли;
	
	УстановитьЗначенияПоУмолчанию();
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если СпособОбменаЭД = ПредопределенноеЗначение("Перечисление.СпособыОбменаЭД.ЧерезСервис1СЭДО")
		И ЗначениеЗаполнено(Организация) ТОгда
		
		ЗаполнитьРеквизитыПоИНННаКлиенте();
	КонецЕсли;
	
	Если СпособОбменаЭД = ПредопределенноеЗначение("Перечисление.СпособыОбменаЭД.ЧерезОператораЭДОТакском") Тогда
		
		СогласенСУсловиями = Истина;
		ЭлектронныеДокументыКлиентПереопределяемый.ЗапроситьСогласиеСУсловиямиЛицензионногоСоглашения(СогласенСУсловиями);
		Если СогласенСУсловиями <> Истина Тогда
			Отказ = Истина;
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если ЭлектронныеДокументыСлужебныйКлиент.ПроверитьИспользованиеИнтернетПоддержкаПользователей() Тогда
		Элементы.ГруппаЗаполненияИдентификатораБезИПП.Видимость = Ложь;
	Иначе
		Элементы.ГруппаЗаполненияИдентификатораСИПП.Видимость = Ложь;
	КонецЕсли;
	
	ЭлектронныеДокументыСлужебныйКлиент.ЗаполнитьДанныеСлужбыПоддержки(ТелефонСлужбыПоддержки, АдресЭлектроннойПочтыСлужбыПоддержки);
	
	#Если ВебКлиент Тогда
		Элементы.КаталогВходящихДокументов.КнопкаВыбора = Ложь;
	#КонецЕсли
	
	Если ЗначениеЗаполнено(ОтпечатокСертификата) Тогда
		Контекст = Новый Структура;
		Контекст.Вставить("Организация", Организация);
		
		Оповещение = Новый ОписаниеОповещения(
			"ПослеПолученияОтпечатковПолучитьМенеджерКриптографии", ЭтотОбъект, Контекст);
		ЭлектроннаяПодписьКлиент.ПолучитьОтпечаткиСертификатов(Оповещение, Истина, Ложь);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПолученияОтпечатковПолучитьМенеджерКриптографии(Отпечатки, Контекст = Неопределено) Экспорт
	
	Если ТипЗнч(Отпечатки) <> Тип("Соответствие") Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого КлючЗначение Из Отпечатки Цикл
		Если ОтпечатокСертификата = НРег(СТРЗаменить(Строка(Base64Значение(КлючЗначение.Ключ))," ","")) Тогда
			
			ОписаниеОповещения = Новый ОписаниеОповещения(
				"ПолучитьМенеджерКриптографииПослеПолученияСертификатаКриптографии", ЭтотОбъект, Контекст);
			ЭлектроннаяПодписьКлиент.ПолучитьСертификатПоОтпечатку(ОписаниеОповещения, КлючЗначение.Ключ, Истина, Ложь);
			Прервать;
		КонецЕсли;
	КонецЦикла
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьМенеджерКриптографииПослеПолученияСертификатаКриптографии(Сертификат, Контекст = Неопределено) Экспорт
	
	Если ТипЗнч(Сертификат) <> Тип("СертификатКриптографии") Тогда
		Возврат;
	КонецЕсли;
	
	ДвоичныеДанныеСертификата = Сертификат.Выгрузить();
	
	Контекст.Вставить("ДвоичныеДанныеСертификата", ДвоичныеДанныеСертификата);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыполнитьДействияПослеПолученияМенеджераКриптографии", ЭтотОбъект, Контекст);
	ЭлектроннаяПодписьКлиент.СоздатьМенеджерКриптографии(ОписаниеОповещения, "ПолучениеСертификатов", Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьДействияПослеПолученияМенеджераКриптографии(МенеджерКриптографии, Контекст = Неопределено) Экспорт
	
	Если НЕ ТипЗнч(МенеджерКриптографии) = Тип("МенеджерКриптографии") Тогда
		Возврат;
	КонецЕсли;
		
	ОписаниеОповещения = Новый ОписаниеОповещения("СоздатьНайтиСертификатКриптографииПользователя", ЭтотОбъект, Контекст);
	МенеджерКриптографии.НачатьПолучениеИнформацииМодуляКриптографии(ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьНайтиСертификатКриптографииПользователя(ИнформацияМодуля, Контекст = Неопределено) Экспорт
	
	СертификатКриптографии = ЭлектронныеДокументыСлужебныйВызовСервера.СоздатьНайтиСертификатЭП(
		Контекст.ДвоичныеДанныеСертификата, Контекст.Организация, ИнформацияМодуля.Имя);
		
	ПолучитьИдентификаторУчастникаОбменаЭДЧерез1СЭДО();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	// Задаем вопрос только в случае досрочного закрытия помощника.
	Если ЗакрытьФорму <> Истина И Не ЗначениеЗаполнено(СсылкаНаПрофильНастроек) Тогда
		ТекстВопроса = НСтр("ru = 'Введенные данные не будут сохранены.
							|Прервать работу помощника?'");
		ОписаниеОповещения = Новый ОписаниеОповещения("ПередЗакрытиемЗавершить", ЭтотОбъект);
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// ИнтернетПоддержкаПользователей
	
	// Механизм получения уникального идентификатора передает уникальный идентификатор
	// в виде строки в параметре оповещения с именем события
	// "ОповещениеОПолученииУникальногоИдентификатораУчастникаОбменаЭД"
	Если ИмяСобытия = "ОповещениеОПолученииУникальногоИдентификатораУчастникаОбменаЭД" Тогда
		ИдентификаторОрганизации = СокрЛП(Параметр);
		
		Элементы.НадписьИдентификаторУчастникаОбменаЭД.Заголовок = ИдентификаторОрганизации;
		Элементы.НадписьИдентификаторУчастникаОбменаЭД.Гиперссылка = Ложь;
		ШрифтЭлемента = Элементы.НадписьИдентификаторУчастникаОбменаЭД.Шрифт;
		Элементы.НадписьИдентификаторУчастникаОбменаЭД.Шрифт = Новый Шрифт(ШрифтЭлемента, , , Истина);
	КонецЕсли;
	// Конец ИнтернетПоддержкаПользователей
	
	Если ИмяСобытия = "ОповещениеОПолученииУникальногоИдентификатораУчастникаОбменаЭД1СЭДО" Тогда
		ИдентификаторОрганизации = СокрЛП(Параметр);
		Элементы.ИдентификаторОрганизацииЧерез1СЭДО.СписокВыбора.Добавить(ИдентификаторОрганизации);
		
		Элементы.НадписьИдентификаторУчастникаОбменаЭДЧерезЭДО.Заголовок = ИдентификаторОрганизации;
		Элементы.НадписьИдентификаторУчастникаОбменаЭДЧерезЭДО.Гиперссылка = Ложь;
		ШрифтЭлемента = Элементы.НадписьИдентификаторУчастникаОбменаЭДЧерезЭДО.Шрифт;
		Элементы.НадписьИдентификаторУчастникаОбменаЭДЧерезЭДО.Шрифт = Новый Шрифт(ШрифтЭлемента, , , Истина);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийПолейФормы

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	Если СпособОбменаЭД = ПредопределенноеЗначение("Перечисление.СпособыОбменаЭД.ЧерезОператораЭДОТакском") Тогда
		ИдентификаторОрганизации = "";
		Элементы.НадписьИдентификаторУчастникаОбменаЭД.Заголовок = НСтр("ru = 'Получить уникальный идентификатор участника обмена ЭД.'");
		ШрифтЭлемента = Элементы.НадписьИдентификаторУчастникаОбменаЭД.Шрифт;
		Элементы.НадписьИдентификаторУчастникаОбменаЭД.Шрифт = Новый Шрифт(ШрифтЭлемента, , , Ложь);
		Элементы.НадписьИдентификаторУчастникаОбменаЭД.Гиперссылка = Истина;
		
		ОрганизацияПриИзмененииЗавершить(Неопределено, Неопределено);
	ИначеЕсли СпособОбменаЭД = ПредопределенноеЗначение("Перечисление.СпособыОбменаЭД.ЧерезСервис1СЭДО") Тогда
		
		ИдентификаторОрганизации = "";
		ИдентификаторОрганизацииСуществующий = "";
		Элементы.ИдентификаторОрганизацииЧерез1СЭДО.СписокВыбора.Очистить();
		
		Элементы.НадписьИдентификаторУчастникаОбменаЭДЧерезЭДО.Заголовок = НСтр("ru = 'Получить уникальный идентификатор участника обмена ЭД.'");
		ШрифтЭлемента = Элементы.НадписьИдентификаторУчастникаОбменаЭДЧерезЭДО.Шрифт;
		Элементы.НадписьИдентификаторУчастникаОбменаЭДЧерезЭДО.Шрифт = Новый Шрифт(ШрифтЭлемента, , , Ложь);
		Элементы.НадписьИдентификаторУчастникаОбменаЭДЧерезЭДО.Гиперссылка = Истина;
		
		// Очистить адрес организации
		АдресОрганизации = "";
		Индекс           = "";
		Регион           = "";
		Район            = "";
		Город            = "";
		НаселенныйПункт  = "";
		Улица            = "";
		Дом              = "";
		Корпус           = "";
		Квартира         = "";
		КодРегиона       = "";
		КодНалоговогоОргана = "";

		ОрганизацияПриИзмененииЗавершить(Неопределено, Неопределено);
	Иначе
		Если ЗначениеЗаполнено(ИдентификаторОрганизации) Тогда
			ОписаниеОповещения = Новый ОписаниеОповещения("ОрганизацияПриИзмененииЗавершить", ЭтотОбъект);
			ТекстВопроса = НСтр("ru = 'Была изменена организация. Изменить идентификатор обмена организации?'");
			ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
		Иначе
			ОрганизацияПриИзмененииЗавершить(КодВозвратаДиалога.Да, Неопределено);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОператорЭДОПодключенныйК1СЭДОПриИзменении(Элемент)
	
	ПодключенныеОператорыЭДО1СЭДО();
	
КонецПроцедуры

&НаКлиенте
Процедура ТипРегистрацииПриИзменении(Элемент)
	
	ИдентификаторОрганизацииСуществующий = "";
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура СпособОбменаЭДПриИзменении(Элемент)
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьЭППриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(СертификатКриптографии) Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("ИспользоватьЭППриИзмененииЗавершить", ЭтотОбъект);
		ТекстВопроса = НСтр("ru = 'Данные по сертификату будут очищены. Продолжить?'");
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
	Иначе
		ИспользоватьЭППриИзмененииЗавершить(Неопределено, Неопределено);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СертификатЭПНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ЭлектроннаяПодписьКлиент.СертификатНачалоВыбораСПодтверждением(Элемент,
		СертификатКриптографии, СтандартнаяОбработка);
		
КонецПроцедуры

&НаКлиенте
Процедура НадписьИдентификаторУчастникаОбменаЭДНажатие(Элемент)
	
	Если Не ЗначениеЗаполнено(СертификатКриптографии) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			ЭлектронныеДокументыКлиентСервер.ПолучитьТекстСообщения("Поле", "Заполнение", "Сертификат ключа электронной подписи"),
			,
			"СертификатКриптографии");
		Возврат;
	КонецЕсли;
	ЭлектронныеДокументыКлиентПереопределяемый.СтартоватьМеханизмРаботыСОператоромЭДО(СертификатКриптографии,
																					  Организация,
																					  "taxcomGetID",
																					  ИдентификаторОрганизации,
																					  Неопределено,
																					  ЭтотОбъект.УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияСоздатьУчетнуюЗаписьНажатие(Элемент)
	
	ОткрытьФорму("Справочник.УчетныеЗаписиЭлектроннойПочты.Форма.ФормаЭлемента");
	
КонецПроцедуры

&НаКлиенте
Процедура КаталогВходящихДокументовНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
#Если НЕ ВебКлиент Тогда
	КаталогОбмена(КаталогВходящихДокументов);
#КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура FTPКаталогВходящихДокументовНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	КаталогОбмена(FTPКаталогВходящихДокументов);
КонецПроцедуры

&НаКлиенте
Процедура НадписьИдентификаторУчастникаОбменаЭДЧерез1СЭДОНажатие(Элемент)
	
	ОчиститьСообщения();
	ПолучитьИдентификаторУчастникаОбменаЭДЧерез1СЭДО();
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьИдентификаторУчастникаОбменаЭДЧерез1СЭДО()
	
	ВозниклиОшибки = Ложь;
	Если Не ЗначениеЗаполнено(СертификатКриптографии) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			ЭлектронныеДокументыКлиентСервер.ПолучитьТекстСообщения("Поле", "Заполнение", "Сертификат ключа электронной подписи"),
			,
			"СертификатКриптографии");
		ВозниклиОшибки = Истина;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(КодНалоговогоОргана) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			ЭлектронныеДокументыКлиентСервер.ПолучитьТекстСообщения("Поле", "Заполнение", "Код налогового органа"),
			,
			"КодНалоговогоОргана");
		ВозниклиОшибки = Истина;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(АдресОрганизации) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			ЭлектронныеДокументыКлиентСервер.ПолучитьТекстСообщения("Поле", "Заполнение", "Юридический адрес"),
			,
			"АдресОрганизации");
		ВозниклиОшибки = Истина;
	ИначеЕсли Не ЗначениеЗаполнено(КодРегиона) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			ЭлектронныеДокументыКлиентСервер.ПолучитьТекстСообщения("Поле", "Заполнение", """КодРегиона"" в юридическом адресе"),
			,
			"АдресОрганизации");
		ВозниклиОшибки = Истина;
	КонецЕсли;
		
	Если Не ВозниклиОшибки Тогда
		ТекстВопроса = НСтр("ru = 'Операция получения идентификатора участника ЭДО займет несколько минут.
									|Продолжить?'");
									
		ОписаниеОповещения = Новый ОписаниеОповещения("НадписьИдентификаторУчастникаОбменаЭДЧерез1СЭДОПродолжение", ЭтотОбъект);
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьИдентификаторУчастникаОбменаЭДЧерез1СЭДОПродолжение(Знач Результат, Знач ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		Если ТипЗнч(ПараметрыАутентификацииНаСайте) <> Тип("Структура") ИЛИ Не ЗначениеЗаполнено(ПараметрыАутентификацииНаСайте.Пароль) Тогда
			СтандартныеПодсистемыКлиент.АвторизоватьНаСайтеПоддержкиПользователей(ЭтотОбъект,
				Новый ОписаниеОповещения("НадписьИдентификаторУчастникаОбменаЭДЧерез1СЭДОЗавершение", ЭтотОбъект));
		Иначе
			НадписьИдентификаторУчастникаОбменаЭДЧерез1СЭДОЗавершение(ПараметрыАутентификацииНаСайте, Неопределено);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьИдентификаторУчастникаОбменаЭДЧерез1СЭДОЗавершение(ПараметрыАутентификации, Контекст) Экспорт
	
	Если ТипЗнч(ПараметрыАутентификации) <> Тип("Структура") Тогда
		ПараметрыАутентификацииНаСайте = Неопределено;
		// Пользователь отказался от ввода логина и пароля.
		Возврат;
	КонецЕсли;
	
	ПараметрыАутентификацииНаСайте = ПараметрыАутентификации;
	ПодготовитьДанныеДляРегистрационногоПакета1СЭДОНачало();
	
КонецПроцедуры

&НаКлиенте
Процедура АдресНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	// Подготовка данных и открытие формы для ввода адреса
	СтандартнаяОбработка = Ложь;
	ВыбратьАдрес(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура АдресОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ВыбратьАдрес(Истина);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Готово(Команда)
	
	ОчиститьСообщения();
	
	ТестПрофиляНастроекЭДО();
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьПодсказкуПоЭП(Команда)
	
	ОткрытьФорму("Справочник.ПрофилиНастроекЭДО.Форма.ФормаПодсказкиПоЭП");
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСсылкуНаСтатьюПо1СБухфон(Команда)
	
	ЭлектронныеДокументыСлужебныйКлиент.ОткрытьИнструкциюПо1СБухфон();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	
	ЗначениеФОИспользоватьЭП = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьЗначениеФункциональнойОпции(
		"ИспользоватьЭлектронныеПодписиЭД");
	Элементы.ГруппаЧерезЭлектроннуюПочтуНастройкаКриптографии.Видимость = ЗначениеФОИспользоватьЭП;
	Элементы.ГруппаЧерезКаталогНастройкаКриптографии.Видимость          = ЗначениеФОИспользоватьЭП;
	Элементы.ГруппаЧерезFTPНастройкаКриптографии.Видимость              = ЗначениеФОИспользоватьЭП;
	
	Элементы.СертификатЭПЧерезЭлектроннуюПочту.Доступность = Форма.ИспользоватьЭП;
	Элементы.СертификатЭПЧерезКаталог.Доступность          = Форма.ИспользоватьЭП;
	Элементы.СертификатЭПЧерезFTP.Доступность              = Форма.ИспользоватьЭП;
	
	Если Форма.СпособОбменаЭД = ПредопределенноеЗначение("Перечисление.СпособыОбменаЭД.ЧерезЭлектроннуюПочту") Тогда
		Элементы.ГруппаНастройкиУчастника.ТекущаяСтраница = Элементы.ГруппаНастроекУчастникаЧерезЭлектроннуюПочту;
	ИначеЕсли Форма.СпособОбменаЭД = ПредопределенноеЗначение("Перечисление.СпособыОбменаЭД.ЧерезКаталог") Тогда
		Элементы.ГруппаНастройкиУчастника.ТекущаяСтраница = Элементы.ГруппаНастроекУчастникаЧерезКаталог;
	ИначеЕсли Форма.СпособОбменаЭД = ПредопределенноеЗначение("Перечисление.СпособыОбменаЭД.ЧерезFTP") Тогда
		Элементы.ГруппаНастройкиУчастника.ТекущаяСтраница = Элементы.ГруппаНастроекУчастникаЧерезFTP;
	ИначеЕсли Форма.СпособОбменаЭД = ПредопределенноеЗначение("Перечисление.СпособыОбменаЭД.ЧерезСервис1СЭДО") Тогда
		Элементы.ИдентификаторОрганизацииЧерез1СЭДО.Доступность = Форма.ТипРегистрации = 1;
		Элементы.НадписьИдентификаторУчастникаОбменаЭДЧерезЭДО.Доступность = Форма.ТипРегистрации = 2;
		Элементы.ГруппаРегистрацииНовойУчетнойЗаписиДанные.Доступность = Форма.ТипРегистрации = 2;
	КонецЕсли;
	
	Если ОбщегоНазначенияКлиентСервер.ЭтоПлатформа83() Тогда
		Элементы.СертификатЭПЧерезОператора.КнопкаСоздания = Ложь;
		Элементы.СертификатЭПЧерезЭлектроннуюПочту.КнопкаСоздания = Ложь;
		Элементы.СертификатЭПЧерезКаталог.КнопкаСоздания = Ложь;
		Элементы.СертификатЭПЧерезFTP.КнопкаСоздания = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьЗначенияПоУмолчанию()
	
	ИспользоватьЭП = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьЗначениеФункциональнойОпции(
		"ИспользоватьЭлектронныеПодписиЭД");
	СертификатКриптографии = Справочники.СертификатыКлючейЭлектроннойПодписиИШифрования.ПустаяСсылка();
	
	Если Не ЗначениеЗаполнено(Организация) Тогда
		ЭлектронныеДокументыСлужебныйВызовСервера.ОпределитьОрганизацию(Организация);
	КонецЕсли;

	Если СпособОбменаЭД = Перечисления.СпособыОбменаЭД.ЧерезСервис1СЭДО
		И ЗначениеЗаполнено(Организация) ТОгда
		
		СуществующиеИдентификаторыНаСервсие1СЭДО();
		ТипРегистрации = ?(ЗначениеЗаполнено(ИдентификаторОрганизацииСуществующий), 1, 2);
	КонецЕсли;

	Если СпособОбменаЭД = Перечисления.СпособыОбменаЭД.ЧерезЭлектроннуюПочту
		И НЕ ЗначениеЗаполнено(ЭлектроннаяПочтаОрганизации) Тогда
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 2
		|	УчетныеЗаписиЭлектроннойПочты.Ссылка
		|ИЗ
		|	Справочник.УчетныеЗаписиЭлектроннойПочты КАК УчетныеЗаписиЭлектроннойПочты
		|ГДЕ
		|	НЕ УчетныеЗаписиЭлектроннойПочты.ПометкаУдаления
		|	И УчетныеЗаписиЭлектроннойПочты.ИспользоватьДляОтправки
		|	И УчетныеЗаписиЭлектроннойПочты.ИспользоватьДляПолучения");
		Выборка = Запрос.Выполнить().Выбрать();
		
		Если Выборка.Количество() = 1 И Выборка.Следующий() Тогда
			ЭлектроннаяПочтаОрганизации = Выборка.Ссылка;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ИспользоватьЭП И ЗначениеЗаполнено(Организация) Тогда
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	Сертификаты.Ссылка
		|ИЗ
		|	Справочник.СертификатыКлючейЭлектроннойПодписиИШифрования КАК Сертификаты
		|ГДЕ
		|	Сертификаты.Организация = &Организация
		|	И НЕ Сертификаты.ПометкаУдаления
		|	И НЕ Сертификаты.Отозван";
		Запрос.УстановитьПараметр("Организация", Организация);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Количество() = 1 И Выборка.Следующий() Тогда
			СертификатКриптографии = Выборка.Ссылка;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КаталогОбмена(ПутьККаталогу)
	
	ПараметрыВыполнения = Новый Структура;
	ПараметрыВыполнения.Вставить("ПутьККаталогу", ПутьККаталогу);
	
	ДиалогКаталога = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
	ДиалогКаталога.Заголовок = НСтр("ru = 'Выберите сетевой каталог для обмена'");
	ДиалогКаталога.Каталог = ПутьККаталогу;
	ОписаниеВыбораКаталога = Новый ОписаниеОповещения("ПослеВыбораКаталога", ЭтотОбъект);
		
	ДиалогКаталога.Показать(ОписаниеВыбораКаталога);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьИдентификатор(ИмяСправочника, СсылкаНаИсточникИдентификатора)
	
	Если ИмяСправочника = "Организации" Тогда
		ИмяРеквизитаИННОрганизации = ЭлектронныеДокументыПовтИсп.ИмяНаличиеОбъектаРеквизитаВПрикладномРешении("ИННОрганизации");
		ИмяРеквизитаКППОрганизации = ЭлектронныеДокументыПовтИсп.ИмяНаличиеОбъектаРеквизитаВПрикладномРешении("КППОрганизации");
		
		ПараметрыОрганиазции = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СсылкаНаИсточникИдентификатора,
			ИмяРеквизитаИННОрганизации + ", " + ИмяРеквизитаКППОрганизации);
		
		СтрокаЗаполнения = Строка(ПараметрыОрганиазции[ИмяРеквизитаИННОрганизации])
			+ "_" + Строка(ПараметрыОрганиазции[ИмяРеквизитаКППОрганизации]);
		Если Прав(СтрокаЗаполнения, 1) = "_" Тогда
			СтрокаЗаполнения = СтрЗаменить(СтрокаЗаполнения, "_", "");
		КонецЕсли;
		ИдентификаторОрганизации = СокрЛП(СтрокаЗаполнения);
		
	ИначеЕсли ИмяСправочника = "Контрагенты" Тогда
		ИмяРеквизитаИННКонтрагента = ЭлектронныеДокументыПовтИсп.ИмяНаличиеОбъектаРеквизитаВПрикладномРешении("ИННКонтрагента");
		ИмяРеквизитаКППКонтрагента = ЭлектронныеДокументыПовтИсп.ИмяНаличиеОбъектаРеквизитаВПрикладномРешении("КППКонтрагента");
		
		ПараметрыКонтрагента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СсылкаНаИсточникИдентификатора,
			ИмяРеквизитаИННКонтрагента + ", " + ИмяРеквизитаКППКонтрагента);
		
		СтрокаЗаполнения = Строка(ПараметрыКонтрагента[ИмяРеквизитаИННКонтрагента])
			+ "_" + Строка(ПараметрыКонтрагента[ИмяРеквизитаКППКонтрагента]);
		Если Прав(СтрокаЗаполнения, 1) = "_" Тогда
			СтрокаЗаполнения = СтрЗаменить(СтрокаЗаполнения, "_", "");
		КонецЕсли;
		ИдентификаторКонтрагента = СокрЛП(СтрокаЗаполнения);
	КонецЕсли;
	
КонецПроцедуры

// Открывает форму выбора адреса в модальном режиме и возвращает
// реквизиты адреса в виде структуры с соответствующими полями
//
// Параметры:
// - ТолькоДляПросмотра (Булево): Истина - открыть форму выбора адреса только для просмотра
//
// Возвращаемое значение: Структура с полями - реквизитыми адреса;
//						  Неопределено, если на форме адреса при закрытии не была нажата кнопка "ОК"
// 
&НаКлиенте
Процедура ВыбратьАдрес(ТолькоДляПросмотра = Ложь)
	
	ПараметрыФормы = Новый Структура("ТолькоПросмотр", ТолькоДляПросмотра);
	
	Если ТолькоДляПросмотра Тогда
		ОповещениеОЗакрытии = Неопределено;
	Иначе
		ОповещениеОЗакрытии = Новый ОписаниеОповещения("ПриВыбореАдреса", ЭтотОбъект);
	КонецЕсли;
	
	ПараметрыФормы.Вставить("Индекс"    , Индекс);
	ПараметрыФормы.Вставить("Регион"    , Регион);
	ПараметрыФормы.Вставить("Район"     , Район);
	ПараметрыФормы.Вставить("Город"     , Город);
	ПараметрыФормы.Вставить("НасПункт"  , НаселенныйПункт);
	ПараметрыФормы.Вставить("Улица"     , Улица);
	ПараметрыФормы.Вставить("Дом"       , Дом);
	ПараметрыФормы.Вставить("Корпус"    , Корпус);
	ПараметрыФормы.Вставить("Квартира"  , Квартира);
	ПараметрыФормы.Вставить("КодРегиона", КодРегиона);
	
	
	ФормаВводаАдреса = ОткрытьФорму("ОбщаяФорма.АдресУчастникаОбменаЭД",
		ПараметрыФормы,
		,
		,
		,
		,
		ОповещениеОЗакрытии);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриВыбореАдреса(ПараметрыАдреса, ДопПараметры) Экспорт
	
	Если ТипЗнч(ПараметрыАдреса) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	// Если адрес изменен, то применение изменений
	Индекс          = ПараметрыАдреса.Индекс;
	Регион          = ПараметрыАдреса.Регион;
	Район           = ПараметрыАдреса.Район;
	Город           = ПараметрыАдреса.Город;
	НаселенныйПункт = ПараметрыАдреса.НаселенныйПункт;
	Улица           = ПараметрыАдреса.Улица;
	Дом             = ПараметрыАдреса.Дом;
	Корпус          = ПараметрыАдреса.Корпус;
	Квартира        = ПараметрыАдреса.Квартира;
	КодРегиона      = ПараметрыАдреса.КодРегиона;
	
	СформироватьАдрес();
	
КонецПроцедуры

// Выполняет формирование строки адреса по реквизитам адреса
&НаСервере
Процедура СформироватьАдрес() Экспорт
	
	Адр = "";
	
	ДобавитьПодстроку(Адр, Индекс);
	ДобавитьПодстроку(Адр, Регион);
	ДобавитьПодстроку(Адр, КодРегиона, "регион ");
	ДобавитьПодстроку(Адр, Район);
	ДобавитьПодстроку(Адр, Город);
	ДобавитьПодстроку(Адр, НаселенныйПункт);
	ДобавитьПодстроку(Адр, Улица);
	ДобавитьПодстроку(Адр, Дом     , "д. ");
	ДобавитьПодстроку(Адр, Корпус  , "корп. ");
	ДобавитьПодстроку(Адр, Квартира, "кв. ");
	
	АдресОрганизации = Адр;
	
КонецПроцедуры

// Процедура добавления подстроки к строке
// Параметры:
// - ИсходнаяСтрока - Строка - исходная строка;
// - Подстрока      - Строка - строка, которая должна быть добавлена в конец исходной строки;
// - Префикс        - Строка - строка, которая добавляется перед подстрокой;
// - Разделитель    - строка - строка, которая служит разделителем между строкой и подстрокой.
//
&НаСервере
Процедура ДобавитьПодстроку(ИсходнаяСтрока, Знач Подстрока, Префикс = "", Разделитель = ", ")
	
	Если НЕ ПУстаяСтрока(ИсходнаяСтрока) И НЕ ПустаяСтрока(Подстрока) Тогда
		ИсходнаяСтрока = ИсходнаяСтрока + Разделитель;
	КонецЕсли;
	
	Если НЕ ПустаяСтрока(Подстрока) Тогда
		ИсходнаяСтрока = ИсходнаяСтрока + Префикс + Подстрока;
	КонецЕсли;
	
КонецПроцедуры

// Получение данных по организации из сервиса ВЕБ ИТС(данные ЕГРИП)

&НаКлиенте
Процедура ЗаполнитьРеквизитыПоИНННаКлиенте()
	
	Если ТипЗнч(ПараметрыАутентификацииНаСайте) <> Тип("Структура") ИЛИ Не ЗначениеЗаполнено(ПараметрыАутентификацииНаСайте.Пароль) Тогда
		СтандартныеПодсистемыКлиент.АвторизоватьНаСайтеПоддержкиПользователей(ЭтотОбъект,
			Новый ОписаниеОповещения("ЗаполнитьРеквизитыПоИННПродолжить", ЭтотОбъект));
	Иначе
		ЗаполнитьРеквизитыПоИННПродолжить(ПараметрыАутентификацииНаСайте, Неопределено);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьРеквизитыПоИННПродолжить(ПараметрыАутентификации, Контекст) Экспорт
	
	Если ТипЗнч(ПараметрыАутентификации) <> Тип("Структура") Тогда
		ПараметрыАутентификацииНаСайте = Неопределено;
		// Пользователь отказался от ввода логина и пароля.
		Возврат;
	КонецЕсли;
	
	ПараметрыАутентификацииНаСайте = ПараметрыАутентификации;
	ЗаполнитьРеквизитыПоИНННаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРеквизитыПоИНННаСервере()
	
	ИмяРеквизитаИННОрганизации = ЭлектронныеДокументыПовтИсп.ИмяНаличиеОбъектаРеквизитаВПрикладномРешении("ИННОрганизации");
	ЗначениеРеквизитаИННОрганизации = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Организация, ИмяРеквизитаИННОрганизации);
	
	Если Не ЭлектронныеДокументыПереопределяемый.ЭтоФизЛицо(Организация) Тогда
		РеквизитыЮрЛицаПоИНН(ЗначениеРеквизитаИННОрганизации);
	Иначе
		РеквизитыПредпринимателяПоИНН(ЗначениеРеквизитаИННОрганизации);
	КонецЕсли;
	
КонецПроцедуры

// Возвращаются реквизиты юридического лица по данным ЕГРЮЛ (наименование, адрес, коды и т.д.)
//
// Параметры:
//  ИНН  - Строка - ИНН юридического лица, реквизиты которого надо получить
//
// Возвращаемое значение:
//   Структура   - реквизиты юридического лица. 
//                 Содержание структуры описано в функции НовыеРеквизитыЮрЛица()
//
&НаСервере
Процедура РеквизитыЮрЛицаПоИНН(Знач ИНН) Экспорт
	
	ОписаниеОшибки = "";
	Прокси = ПроксиСервиса(ОписаниеОшибки);
	Если Прокси <> Неопределено Тогда
		ВходныеПараметры = Прокси.ФабрикаXDTO.Создать(
			Прокси.ФабрикаXDTO.Тип(ПространствоИмен(), "getCorporationRequisitesByINN"));
		ВходныеПараметры.INN = ИНН;
		ВходныеПараметры.configurationName = Метаданные.Имя;
		Попытка
			Ответ = Прокси.getCorporationRequisitesByINN(ВходныеПараметры);
		Исключение
			ИнформацияОбОшибке = ИнформацияОбОшибке();
			ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='ИНН %1:'"), ИНН)
				+ Символы.ПС + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
		КонецПопытки;
	КонецЕсли;

	Если ЗначениеЗаполнено(ОписаниеОшибки) Тогда
		ОбработатьОшибкуСервиса(ОписаниеОшибки, ИНН);
		Возврат;
	КонецЕсли;
	
	ЗаполнитьРегистрациюВНалоговомОргане(Ответ.РеквизитыЮрЛица);
	
	ЗаполнитьЮридическийАдрес(Ответ.РеквизитыЮрЛица);
	
КонецПроцедуры

// Возвращаются реквизиты индивидуального предпринимателя по данным ЕГРИП (ФИО, свидетельство о регистрации, коды и т.д.)
//
// Параметры:
//  ИНН  - Строка - ИНН индивидуального предпринимателя, реквизиты которого надо получить
//
// Возвращаемое значение:
//   Структура   - реквизиты индивидуального предпринимателя. 
//                 Содержание структуры описано в функции НовыеРеквизитыПредпринимателя()
//
&НаСервере
Процедура РеквизитыПредпринимателяПоИНН(Знач ИНН) Экспорт
	
	ОписаниеОшибки = "";
	Прокси = ПроксиСервиса(ОписаниеОшибки);
	Если Прокси <> Неопределено Тогда
		ВходныеПараметры = Прокси.ФабрикаXDTO.Создать(
			Прокси.ФабрикаXDTO.Тип(ПространствоИмен(), "getEntrepreneurRequisitesByINN"));
		ВходныеПараметры.INN = ИНН;
		ВходныеПараметры.configurationName = Метаданные.Имя;
		Попытка
			Ответ = Прокси.getEntrepreneurRequisitesByINN(ВходныеПараметры);
		Исключение
			ИнформацияОбОшибке = ИнформацияОбОшибке();
			ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='ИНН %1:'"), ИНН)
				+ Символы.ПС + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
		КонецПопытки;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ОписаниеОшибки) Тогда
		ОбработатьОшибкуСервиса(ОписаниеОшибки, ИНН);
		Возврат;
	КонецЕсли;
	
	ЗаполнитьРегистрациюВНалоговомОргане(Ответ.РеквизитыИП);
	
КонецПроцедуры

&НаСервере
Функция ПроксиСервиса(ОписаниеОшибки)
	
	Прокси = Неопределено;
	
	Если ПараметрыАутентификацииНаСайте = Неопределено Тогда
		// Служебный текст. Должен быть обработан на клиенте.
		ОписаниеОшибки = "НеУказаныПараметрыАутентификацииНаПользовательсомСайте";
		
	Иначе
		
		Попытка
			Прокси = ОбщегоНазначения.WSПрокси(
				"https://api.orgregister.1c.ru/orgregister/v1?wsdl",  // АдресWSDL
				ПространствоИмен(),                                   // URIПространстваИмен
				"RequisitesWebServiceEndpointImplService",            // ИмяСервиса
				"RequisitesWebServiceEndpointImplPort",               // ИмяТочкиПодключения
				ПараметрыАутентификацииНаСайте.Логин,                        // ИмяПользователя
				ПараметрыАутентификацииНаСайте.Пароль,                       // Пароль
				30);                                                  //Таймаут
		Исключение
			ИнформацияОбОшибке = ИнформацияОбОшибке();
			ОписаниеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
			
		КонецПопытки; 
		
	КонецЕсли;
	
	Возврат Прокси;
	
КонецФункции

&НаСервере
Функция ПространствоИмен()

	Возврат "http://ws.orgregister.company1c.com/";

КонецФункции

&НаСервере
Процедура ЗаполнитьРегистрациюВНалоговомОргане(ОбъектXDTO)
	
	Если ОбъектXDTO.СвУчетНО <> Неопределено
		И ОбъектXDTO.СвУчетНО.СвНО <> Неопределено Тогда
		
		КодНалоговогоОргана = ОбъектXDTO.СвУчетНО.СвНО.КодНО
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьЮридическийАдрес(ОбъектXDTO)
	
	Если ОбъектXDTO.СвАдрес <> Неопределено
		И ОбъектXDTO.СвАдрес.Адрес <> Неопределено
		И ОбъектXDTO.СвАдрес.Адрес.Состав <> Неопределено Тогда
		
		Если ОбъектXDTO.СвАдрес.Адрес.Состав.СубъектРФ <> Неопределено Тогда
			Регион = ОбъектXDTO.СвАдрес.Адрес.Состав.СубъектРФ;
		КонецЕсли;
		Если ОбъектXDTO.СвАдрес.Адрес.Состав.ВнутригРайон <> Неопределено Тогда
			Район = ОбъектXDTO.СвАдрес.Адрес.Состав.ВнутригРайон;
		КонецЕсли;
		Если ОбъектXDTO.СвАдрес.Адрес.Состав.Город <> Неопределено Тогда
			Город = ОбъектXDTO.СвАдрес.Адрес.Состав.Город;
		КонецЕсли;
		Если ОбъектXDTO.СвАдрес.Адрес.Состав.НаселПункт <> Неопределено Тогда
			НаселенныйПункт = ОбъектXDTO.СвАдрес.Адрес.Состав.НаселПункт;
		КонецЕсли;
		Если ОбъектXDTO.СвАдрес.Адрес.Состав.Улица <> Неопределено Тогда
			Улица = ОбъектXDTO.СвАдрес.Адрес.Состав.Улица;
		КонецЕсли;
		
		// Чтение
		Если ОбъектXDTO.СвАдрес.Адрес.Состав.ДопАдрЭл <> Неопределено Тогда
			Для Каждого ДопЭлемент Из ОбъектXDTO.СвАдрес.Адрес.Состав.ДопАдрЭл Цикл
				Если ДопЭлемент.Номер <> Неопределено Тогда
					КодОбъекта = ДопЭлемент.Номер.Тип;
					ТипОбъекта = ТипОбъектаПоКодуСериализации(КодОбъекта);
					Если ТипОбъекта <> Неопределено Тогда
						Вид = ТипОбъекта.Тип;
						Если Вид = 1 Тогда
							Дом = ДопЭлемент.Номер.Значение;
						ИначеЕсли Вид = 2 Тогда
							Корпус = ДопЭлемент.Номер.Значение;
						ИначеЕсли Вид = 3 Тогда
							Квартира = ДопЭлемент.Номер.Значение;
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
				Если ДопЭлемент.ТипАдрЭл = "10100000" Тогда
					Индекс = ДопЭлемент.Значение;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		СформироватьАдрес();
	КонецЕсли;
	
КонецПроцедуры

// Возвращает строку с описанием типа по коду части адреса.
//  Противоположность функции "КодСериализацииОбъектаАдресации".
//
// Параметры:
//      Код - Строка - код
//
// Возвращаемое значение:
//      Число - Тип
//
&НаСервере
Функция ТипОбъектаПоКодуСериализации(Код) Экспорт
	Для Каждого Элемент Из ТипыОбъектовАдресацииАдресаРФ() Цикл
		Если Элемент.Код = Код Тогда
			Возврат Элемент;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Неопределено;
КонецФункции

// Возвращает массив структур с информацией о частях адреса согласно приказу ФНС ММВ-7-1/525 от 31.08.2011.
//
// Возвращаемое значение:
//      Массив - содержит структуры - описания.
//
&НаСервере
Функция ТипыОбъектовАдресацииАдресаРФ() Экспорт
	
	Результат = Новый Массив;
	
	// Код, Наименование, Тип, Порядок, КодФИАС
	// Тип: 1 - владение, 2 - здание, 3 - помещение.
	
	Результат.Добавить(СтрокаОбъектаАдресации("1010", НСтр("ru = 'Дом'"),          1, 1, 2));
	Результат.Добавить(СтрокаОбъектаАдресации("1020", НСтр("ru = 'Владение'"),     1, 2, 1));
	Результат.Добавить(СтрокаОбъектаАдресации("1030", НСтр("ru = 'Домовладение'"), 1, 3, 3));
	
	Результат.Добавить(СтрокаОбъектаАдресации("1050", НСтр("ru = 'Корпус'"),     2, 1));
	Результат.Добавить(СтрокаОбъектаАдресации("1060", НСтр("ru = 'Строение'"),   2, 2, 1));
	Результат.Добавить(СтрокаОбъектаАдресации("1080", НСтр("ru = 'Литера'"),     2, 3, 3));
	Результат.Добавить(СтрокаОбъектаАдресации("1070", НСтр("ru = 'Сооружение'"), 2, 4, 2));
	Результат.Добавить(СтрокаОбъектаАдресации("1040", НСтр("ru = 'Участок'"),    2, 5));
	
	Результат.Добавить(СтрокаОбъектаАдресации("2010", НСтр("ru = 'Квартира'"),  3, 1));
	Результат.Добавить(СтрокаОбъектаАдресации("2030", НСтр("ru = 'Офис'"),      3, 2));
	Результат.Добавить(СтрокаОбъектаАдресации("2040", НСтр("ru = 'Бокс'"),      3, 3));
	Результат.Добавить(СтрокаОбъектаАдресации("2020", НСтр("ru = 'Помещение'"), 3, 4));
	Результат.Добавить(СтрокаОбъектаАдресации("2050", НСтр("ru = 'Комната'"),   3, 5));
	//  Наши сокращения для поддержки обратной совместимости при парсинге.
	Результат.Добавить(СтрокаОбъектаАдресации("2010", НСтр("ru = 'кв.'"),       3, 6));
	Результат.Добавить(СтрокаОбъектаАдресации("2030", НСтр("ru = 'оф.'"),       3, 7));
	
	// Уточняющие объекты
	Результат.Добавить(СтрокаОбъектаАдресации("10100000", НСтр("ru = 'Почтовый индекс'")));
	Результат.Добавить(СтрокаОбъектаАдресации("10200000", НСтр("ru = 'Адресная точка'")));
	Результат.Добавить(СтрокаОбъектаАдресации("10300000", НСтр("ru = 'Садовое товарищество'")));
	Результат.Добавить(СтрокаОбъектаАдресации("10400000", НСтр("ru = 'Элемент улично-дорожной сети, планировочной структуры дополнительного адресного элемента'")));
	Результат.Добавить(СтрокаОбъектаАдресации("10500000", НСтр("ru = 'Промышленная зона'")));
	Результат.Добавить(СтрокаОбъектаАдресации("10600000", НСтр("ru = 'Гаражно-строительный кооператив'")));
	Результат.Добавить(СтрокаОбъектаАдресации("10700000", НСтр("ru = 'Территория'")));
	
	Возврат Результат;
КонецФункции

&НаСервере
Функция СтрокаОбъектаАдресации(Код, Наименование, Тип = 0, Порядок = 0, КодФИАС = 0)
	Возврат Новый Структура("Код, Наименование, Тип, Порядок, КодФИАС, Сокращение, Ключ",
		Код, Наименование, Тип, Порядок, КодФИАС, НРег(Наименование), ВРег(Наименование));
КонецФункции

&НаСервере
Процедура ОбработатьОшибкуСервиса(ОписаниеОшибки, ИНН)
	
	КодОсновногоЯзыка = ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка(); // Для записи события в журнал регистраации
	
	Если ОписаниеОшибки = "НеУказаныПараметрыАутентификации" Тогда
		ТекстОшибки    = "НеУказаныПараметрыАутентификации"; // Служебный текст. Должен быть обработан на клиенте.
		ОписаниеОшибки = НСтр("ru='Не указаны логин и пароль для доступа к интернет-поддержке'");
		ТекстСобытия   = НСтр("ru='Ошибка доступа'", КодОсновногоЯзыка);
		
	ИначеЕсли Найти(ОписаниеОшибки, """status"":401") > 0 Тогда
		ТекстОшибки  = НСтр("ru='Неверно указаны логин и пароль для доступа к интернет-поддержке'");
		ТекстСобытия = НСтр("ru='Ошибка доступа'", КодОсновногоЯзыка);
		
	ИначеЕсли Найти(ОписаниеОшибки, "SERVER-1") > 0 Тогда
		ТекстОшибки  = НСтр("ru='Не указан ИНН юридического лица'");
		ТекстСобытия = НСтр("ru='Ошибка получения данных'", КодОсновногоЯзыка);
		
	ИначеЕсли Найти(ОписаниеОшибки, "SERVER-2") > 0 Тогда
		ТекстОшибки  = НСтр("ru='ИНН юридического лица должен состоять из 10 цифр'");
		ТекстСобытия = НСтр("ru='Ошибка получения данных'", КодОсновногоЯзыка);
		
	ИначеЕсли Найти(ОписаниеОшибки, "SERVER-3") > 0 Тогда
		ТекстОшибки  = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Не удалось найти данные для заполнения реквизитов по ИНН %1'"), ИНН);
		ТекстСобытия = НСтр("ru='Ошибка получения данных'", КодОсновногоЯзыка);
		
	ИначеЕсли Найти(ОписаниеОшибки, "SERVER-4") > 0 Тогда
		ТекстОшибки  = НСтр("ru='Не указан ИНН предпринимателя'");
		ТекстСобытия = НСтр("ru='Ошибка получения данных'", КодОсновногоЯзыка);
		
	ИначеЕсли Найти(ОписаниеОшибки, "SERVER-5") > 0 Тогда
		ТекстОшибки  = НСтр("ru='ИНН предпринимателя должен состоять из 12 цифр'");
		ТекстСобытия = НСтр("ru='Ошибка получения данных'", КодОсновногоЯзыка);
		
	ИначеЕсли Найти(ОписаниеОшибки, "SERVER-6") > 0 Тогда
		ТекстОшибки  = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Не удалось найти данные для заполнения реквизитов по ИНН %1'"), ИНН);
		ТекстСобытия = НСтр("ru='Ошибка получения данных'", КодОсновногоЯзыка);
		
	ИначеЕсли Найти(ОписаниеОшибки, "SERVER-7") > 0 Тогда
		ТекстОшибки  = НСтр("ru='Превышен лимит количества вызовов сервиса за один день'");
		ТекстСобытия = НСтр("ru='Ошибка доступа'", КодОсновногоЯзыка);
		
	ИначеЕсли Найти(ОписаниеОшибки, "SERVER-8") > 0 Тогда
		ТекстОшибки  = НСтр("ru='Отсутствует действующий договор ИТС'");
		ТекстСобытия = НСтр("ru='Ошибка доступа'", КодОсновногоЯзыка);
		
	Иначе
		ТекстОшибки  = НСтр("ru='Ошибка при работе с сервисом (подробнее см. Журнал регистрации)'");
		ТекстСобытия = НСтр("ru='Ошибка при работе с сервисом'", КодОсновногоЯзыка);
	КонецЕсли;
	
	//СтруктураРеквизитов.ОписаниеОшибки = ТекстОшибки;
	
	ИмяСобытия = НСтр("ru = 'Сервис данных единых гос_реестров.'", КодОсновногоЯзыка) + " " + ТекстСобытия;
	ЗаписьЖурналаРегистрации(ИмяСобытия, УровеньЖурналаРегистрации.Ошибка, , , ОписаниеОшибки);
	
КонецПроцедуры

// Методы работы с сервисом 1СЭДО

&НаКлиенте
Процедура Подключаемый_ПроверитьВыполнениеЗаданияДляЭД()

	Попытка
		Если ФормаДлительнойОперации.Открыта() Тогда
			ОбменССерверомПолучитьОтветЭДО(УникальныйИдентификаторЗаявки1СЭДО);
			Если ЗначениеЗаполнено(ИдентификаторОрганизации) Тогда
				Оповестить("ОповещениеОПолученииУникальногоИдентификатораУчастникаОбменаЭД1СЭДО", ИдентификаторОрганизации);
				ДлительныеОперацииКлиент.ЗакрытьФормуДлительнойОперации(ФормаДлительнойОперации);
			Иначе
				ДлительныеОперацииКлиент.ОбновитьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
				ПодключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеЗаданияДляЭД",
				ПараметрыОбработчикаОжидания.ТекущийИнтервал, Истина);
			КонецЕсли;
		КонецЕсли;
	Исключение
		ДлительныеОперацииКлиент.ЗакрытьФормуДлительнойОперации(ФормаДлительнойОперации);
		ВызватьИсключение ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
	КонецПопытки;

КонецПроцедуры

&НаСервере
Процедура ОбменССерверомПолучитьОтветЭДО(ИдентификаторЗапросаНаРегистрацию)
	
	ИмяФайлаОтвета = ЭлектронныеДокументыСлужебный.ПолучитьФайлОтветаОтСервераЭДО("GetInfo", ИдентификаторЗапросаНаРегистрацию);
	ДанныеФайла = ПрочитатьТекстИзФайла(ИмяФайлаОтвета, , Истина);
	МассивИдентификаторов = ПолучитьИдентификаторАбонента(ДанныеФайла);
	Если ЗначениеЗаполнено(МассивИдентификаторов) Тогда
		ИдентификаторОрганизации = МассивИдентификаторов[0];
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура СуществующиеИдентификаторыНаСервсие1СЭДО()
	
	ИмяРеквизитаИННОрганизации = ЭлектронныеДокументыПовтИсп.ИмяНаличиеОбъектаРеквизитаВПрикладномРешении("ИННОрганизации");
	ИмяРеквизитаКППОрганизации = ЭлектронныеДокументыПовтИсп.ИмяНаличиеОбъектаРеквизитаВПрикладномРешении("КППОрганизации");
	
	ПараметрыОрганиазции = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Организация,
		ИмяРеквизитаИННОрганизации + ", " + ИмяРеквизитаКППОрганизации);
		
	ПараметрРесурса = ПараметрыОрганиазции[ИмяРеквизитаИННОрганизации];
	Если ЗначениеЗаполнено(ПараметрыОрганиазции[ИмяРеквизитаКППОрганизации]) Тогда
		ПараметрРесурса = ПараметрРесурса + "_" + ПараметрыОрганиазции[ИмяРеквизитаКППОрганизации];
	КонецЕсли;
	
	ИмяФайлаОтвета = ЭлектронныеДокументыСлужебный.ПолучитьФайлОтветаОтСервераЭДО("GetInfo", ПараметрРесурса);
	ДанныеФайла = ПрочитатьТекстИзФайла(ИмяФайлаОтвета, , Истина);
	МассивИдентификтаторов = ПолучитьИдентификаторАбонента(ДанныеФайла);
	Если ЗначениеЗаполнено(МассивИдентификтаторов) Тогда
		ИдентификаторОрганизацииСуществующий = МассивИдентификтаторов[0];
		Элементы.ИдентификаторОрганизацииЧерез1СЭДО.СписокВыбора.ЗагрузитьЗначения(МассивИдентификтаторов);
	КонецЕсли;
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура ПодключенныеОператорыЭДО1СЭДО()
	
	ИмяФайлаОтвета = ЭлектронныеДокументыСлужебный.ПолучитьФайлОтветаОтСервераЭДО("GetOperators");
	ДанныеФайла = ПрочитатьТекстИзФайла(ИмяФайлаОтвета, , Истина);
	МассивОператоровЭДО = ОператорыЭДОПодключенныеК1СЭДО(ДанныеФайла);
	
	Если ЗначениеЗаполнено(МассивОператоровЭДО) Тогда
		Элементы.ОператорЭДОПодключенныйК1СЭДО.СписокВыбора.ЗагрузитьЗначения(МассивОператоровЭДО);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьИдентификаторАбонента(ДанныеФайла)
	
	МассивИдентификаторов = Новый Массив;
	Если НЕ ПустаяСтрока(ДанныеФайла) Тогда
		
		ДеревоXML = ЗагрузитьСтрокуXMLВДеревоЗначений(ДанныеФайла);
		Если ДеревоXML <> Неопределено Тогда
			
			УзелАбоненты = ДеревоXML.Строки.Найти("Abonents", "Имя");
			
			Если УзелАбоненты <> Неопределено Тогда
				Для каждого УзелАбонент Из УзелАбоненты.Строки Цикл
					УзелИдентификаторАбонента = УзелАбонент.Строки.Найти("Identifier", "Имя");
					МассивИдентификаторов.Добавить(УзелИдентификаторАбонента.Значение);
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат МассивИдентификаторов;
	
КонецФункции

&НаСервере
Функция ОператорыЭДОПодключенныеК1СЭДО(ДанныеФайла)
	
	МассивОператоровЭДО = Новый Массив;
	
	Если НЕ ПустаяСтрока(ДанныеФайла) Тогда
		ДеревоXML = ЗагрузитьСтрокуXMLВДеревоЗначений(ДанныеФайла);
		
		Если ДеревоXML <> Неопределено Тогда
			
			УзелОператорыЭДО = ДеревоXML.Строки.Найти("Operators", "Имя");
			Если УзелОператорыЭДО <> Неопределено Тогда
				Для Каждого УзелОператорЭДО Из УзелОператорыЭДО.Строки Цикл
					
					УзелОператорЭДОНаимОрг = УзелОператорЭДО.Строки.Найти("НаимОрг", "Имя");
					МассивОператоровЭДО.Добавить(УзелОператорЭДОНаимОрг.Значение);
					
					Если УзелОператорЭДОНаимОрг.Значение = ОператорЭДО Тогда
						УзелОператорЭДОИд = УзелОператорЭДО.Строки.Найти("ИдОперЭДО", "Имя");
						ОператорЭДОИд = УзелОператорЭДОИд.Значение;
					
						УзелОператорЭДОИНН = УзелОператорЭДО.Строки.Найти("ИННЮЛ", "Имя");
						ОператорЭДОИНН = УзелОператорЭДОИНН.Значение;
						
						УзелОператорЭДОКПП = УзелОператорЭДО.Строки.Найти("КПП", "Имя");
						ОператорЭДОКПП = УзелОператорЭДОКПП.Значение;
						
						УзелОператорЭДООГРН = УзелОператорЭДО.Строки.Найти("ОГРН", "Имя");
						Если УзелОператорЭДООГРН <> Неопределено Тогда
							ОператорЭДООГРН = УзелОператорЭДООГРН.Значение;
						КонецЕсли;
						
						УзелОператорЭДОСертификат = УзелОператорЭДО.Строки.Найти("Сертификат", "Имя");
						Если УзелОператорЭДОСертификат <> Неопределено Тогда
							ОператорЭДОСертификат = УзелОператорЭДОСертификат.Значение;
						КонецЕсли;
					КонецЕсли;
					
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат МассивОператоровЭДО;
	
КонецФункции

&НаСервереБезКонтекста
Функция ЗагрузитьСтрокуXMLВДеревоЗначений(СтрокаXML, ОписаниеОшибки = Неопределено, ЧтениеXML = Неопределено, Знач ТекУзел = Неопределено) Экспорт
	
	ПерваяИтерация = (ТекУзел = Неопределено);
	Если ПерваяИтерация Тогда
		ТекУзел = СоздатьДеревоСтруктурыXML();
		Попытка
			ЧтениеXML = Новый ЧтениеXML;
			ЧтениеXML.УстановитьСтроку(СтрокаXML);
		Исключение
			ОписаниеОшибки = "Ошибка разбора XML: " + ИнформацияОбОшибке().Описание + ".";
			Возврат Неопределено;
		КонецПопытки;
	КонецЕсли;
	
	Попытка
		Пока ЧтениеXML.Прочитать() Цикл
			ТипУзла = ЧтениеXML.ТипУзла;
			Если ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
				НовУзел = ТекУзел.Строки.Добавить();
				НовУзел.Имя = ЧтениеXML.Имя;
				НовУзел.Тип = "Э";
				НовУзел.Значение = ЧтениеXML.Значение;
				Пока ЧтениеXML.ПрочитатьАтрибут() Цикл
					НовАтрибут = НовУзел.Строки.Добавить();
					НовАтрибут.Имя = ЧтениеXML.Имя;
					НовАтрибут.Тип = "А";
					НовАтрибут.Значение = ЧтениеXML.Значение;
				КонецЦикла;
				ЗагрузитьСтрокуXMLВДеревоЗначений(СтрокаXML, ОписаниеОшибки, ЧтениеXML, НовУзел);
				Если ЗначениеЗаполнено(ОписаниеОшибки) Тогда
					Возврат Неопределено;
				КонецЕсли;
			ИначеЕсли ТипУзла = ТипУзлаXML.КонецЭлемента Тогда
				Прервать;
			ИначеЕсли ТипУзла = ТипУзлаXML.Текст И ТипЗнч(ТекУзел) = Тип("СтрокаДереваЗначений") И ТекУзел.Тип = "Э" Тогда
				ТекУзел.Значение = ЧтениеXML.Значение;
			КонецЕсли;
		КонецЦикла;
	Исключение
		ОписаниеОшибки = "Ошибка разбора XML: " + ИнформацияОбОшибке().Описание + ".";
		Возврат Неопределено;
	КонецПопытки;
	
	Если ПерваяИтерация Тогда
		Возврат ТекУзел;
	КонецЕсли;
	
КонецФункции

&НаСервереБезКонтекста
Функция СоздатьДеревоСтруктурыXML() Экспорт
	
	ДеревоСтруктуры = Новый ДеревоЗначений;
	ДеревоСтруктуры.Колонки.Добавить("Имя");
	ДеревоСтруктуры.Колонки.Добавить("Тип");
	ДеревоСтруктуры.Колонки.Добавить("Значение");
	Возврат ДеревоСтруктуры;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПрочитатьТекстИзФайла(ИмяФайла, _КодировкаТекста = Неопределено, АвтоопределениеКодировкиXML = Ложь) Экспорт
	
	Если ЭтоАдресВременногоХранилища(ИмяФайла) Тогда
		ИмяФайлаНаДиске = ПолучитьИмяВременногоФайла();
		ПолучитьИзВременногоХранилища(ИмяФайла).Записать(ИмяФайлаНаДиске);
	Иначе
		ИмяФайлаНаДиске = ИмяФайла;
	КонецЕсли;
	
	ОбъектЧтение = Новый ЧтениеТекста(ИмяФайлаНаДиске, _КодировкаТекста);
	ТекстИзФайла = ОбъектЧтение.Прочитать();
	ОбъектЧтение.Закрыть();
	
	Если АвтоопределениеКодировкиXML И ЗначениеЗаполнено(ТекстИзФайла) Тогда
		ПерваяСтрокаФайла = ВРЕГ(Лев(СтрПолучитьСтроку(ТекстИзФайла, 1), 100));
		Если Найти(ПерваяСтрокаФайла, ВРЕГ("encoding=""UTF-8""")) <> 0 Тогда
			ТекстИзФайла = ПрочитатьТекстИзФайла(ИмяФайла, КодировкаТекста.UTF8);
		ИначеЕсли Найти(ПерваяСтрокаФайла, ВРЕГ("encoding=""UTF-16""")) <> 0 Тогда
			ТекстИзФайла = ПрочитатьТекстИзФайла(ИмяФайла, КодировкаТекста.UTF16);
		КонецЕсли;
	КонецЕсли;
	
	Возврат ТекстИзФайла;
	
КонецФункции

// Методы создания и отправки рег. пакета 1СЭДО

&НаКлиенте
Процедура ПодготовитьДанныеДляРегистрационногоПакета1СЭДОНачало()
	
	Контекст = Новый Структура;
	
	РеквизитыПакета = Новый Структура;
	ДвоичныеДанныеДляОператораЭДО = Неопределено;
	ДвоичныеДанныеСоглашенияНаПодключениеЭДО = Неопределено;
	
	ПодготовитьДанныеПоСоглашениюНаПодключениеКЭДО(ДвоичныеДанныеСоглашенияНаПодключениеЭДО);
	ПодготовитьДанныеПоРегистрационнойИнформации(РеквизитыПакета, ДвоичныеДанныеДляОператораЭДО);
	
	Если Не ЗначениеЗаполнено(ДвоичныеДанныеДляОператораЭДО) ИЛИ Не ЗначениеЗаполнено(ДвоичныеДанныеДляОператораЭДО) Тогда
		Возврат;
	КонецЕсли;
	
	Контекст.Вставить("ДвоичныеДанныеСоглашенияНаПодключениеЭДО", ДвоичныеДанныеСоглашенияНаПодключениеЭДО);
	Контекст.Вставить("РеквизитыПакета", РеквизитыПакета);
	Контекст.Вставить("ДвоичныеДанныеДляОператораЭДО", ДвоичныеДанныеДляОператораЭДО);
	ОписаниеПодписатьЭД = Новый ОписаниеОповещения("ПодготовитьДанныеДляРегистрационногоПакета1СЭДОПродолжение2", ЭтотОбъект, Контекст);
	
	
	ОписаниеДанных = Новый Структура;
	
	Операция = НСтр("ru = 'Подписание регистрационных данных на подключение к сервису 1С-ЭДО'");
	ОписаниеДанных.Вставить("Операция",            Операция);
	
	МассивСертификатов = Новый Массив;
	МассивСертификатов.Добавить(СертификатКриптографии);
	ОписаниеДанных.Вставить("ОтборСертификатов",   МассивСертификатов);
	
	
	ОписаниеДанных.Вставить("НаборДанных",         Новый Массив);
	ОписаниеДанных.НаборДанных.Добавить(Новый Структура("Данные", ДвоичныеДанныеСоглашенияНаПодключениеЭДО));
	ОписаниеДанных.НаборДанных.Добавить(Новый Структура("Данные", ДвоичныеДанныеДляОператораЭДО));
	
	ОписаниеДанных.Вставить("ПоказатьКомментарий", Ложь);
	ОписаниеДанных.Вставить("ЗаголовокДанных",     "");
	ОписаниеДанных.Вставить("БезПодтверждения",    Истина);
	
	СписокПредставлений = Новый СписокЗначений;
	
	Представление = НСтр("ru = 'Пользовательское соглашение на подключение к сервису 1С-ЭДО'");
	ОписаниеПредставленияЗаявления = Новый ОписаниеОповещения("ОбработатьПредставлениеЗаявленияНаПодключениеКЭДО", ЭтотОбъект, Контекст);
	СписокПредставлений.Добавить(ОписаниеПредставленияЗаявления, Представление);
	
	Представление = НСтр("ru = 'Данные для регистрации учетной записи у оператора ЭДО'");
	ОписаниеПредставленияДанных = Новый ОписаниеОповещения("ОбработатьПредставлениеДанныхДляОператораЭДО", ЭтотОбъект, Контекст);
	СписокПредставлений.Добавить(ОписаниеПредставленияДанных, Представление);
	
	ОписаниеДанных.Вставить("СписокПредставлений", СписокПредставлений);
	ОписаниеДанных.Вставить("ПредставлениеНабора", НСтр("ru = 'Пользовательское соглашение, Данные для регистрации у оператора ЭДО'"));
	
	ЭлектроннаяПодписьКлиент.Подписать(ОписаниеДанных, , ОписаниеПодписатьЭД);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьПредставлениеЗаявленияНаПодключениеКЭДО(РезультатВыполнения = Неопределено, Контекст) Экспорт
	
	ПерейтиПоНавигационнойСсылке("https://portal.1c.ru/applications/30/licenseAgreement");
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьПредставлениеДанныхДляОператораЭДО(РезультатВыполнения = Неопределено, Контекст) Экспорт
	
	ШаблонИмениВременногоФайла = НСтр("ru = 'Данные для регистрации у оператора ЭДО_%1'");
	ИмяВременногоФайла = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонИмениВременногоФайла, УникальныйИдентификаторЗаявки1СЭДО);
	ИмяВременногоФайлаСРасширением = ОбщегоНазначенияКлиентСервер.ПолучитьИмяСРасширением(ИмяВременногоФайла, "xml");
	
	СсылкаНаДвоичныеДанныеФайла = ПоместитьВоВременноеХранилище(Контекст.ДвоичныеДанныеДляОператораЭДО, УникальныйИдентификатор);
	
	Результат = Новый Структура;
	Результат.Вставить("СсылкаНаДвоичныеДанныеФайла",  СсылкаНаДвоичныеДанныеФайла);
	Результат.Вставить("ОтносительныйПуть",            УникальныйИдентификаторЗаявки1СЭДО + "\");
	Результат.Вставить("ИмяФайла",                     ИмяВременногоФайлаСРасширением);
	Результат.Вставить("Наименование",                 ИмяВременногоФайла);
	Результат.Вставить("Расширение",                   "xml");
	Результат.Вставить("Редактирует",                  "");
	Результат.Вставить("Размер",                       "");
	Результат.Вставить("ДатаМодификацииУниверсальная", ТекущаяДата());
	
	ПрисоединенныеФайлыКлиент.ОткрытьФайл(Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодготовитьДанныеДляРегистрационногоПакета1СЭДОПродолжение2(РезультатВыполнения = Неопределено, Контекст) Экспорт
	
	Если ТипЗнч(РезультатВыполнения) = Тип("Структура") Тогда
		// Оповещение произошло из процедуры БСП
		Если РезультатВыполнения.Свойство("НаборДанных") Тогда
			// Если Успех, необходимо перебрать элементы массива Набор данных
			// в подписанных эд в элементе массива являющимся структурой будет свойство "Свойства подписи"
			// такие ЭД надо добавить в массив "МассивЭД" для обновления их статусов.
			Для Каждого ПодписываемыеДанные Из РезультатВыполнения.НаборДанных Цикл
				Если Не ПодписываемыеДанные.Свойство("СвойстваПодписи") Тогда
					Возврат;
				КонецЕсли;
				
				СтурктураПодписи = ПодписываемыеДанные.СвойстваПодписи;
				Если ЭтоАдресВременногоХранилища(ПодписываемыеДанные.СвойстваПодписи) Тогда
					СтурктураПодписи = ПолучитьИзВременногоХранилища(СтурктураПодписи);
				КонецЕсли;
				Если Контекст.ДвоичныеДанныеСоглашенияНаПодключениеЭДО = ПодписываемыеДанные.Данные Тогда
				
					Контекст.Вставить("ПодписанныеДвоичныеДанныеСоглашенияНаПодключениеЭДО", СтурктураПодписи.Подпись);
				КонецЕсли;
				Если Контекст.ДвоичныеДанныеДляОператораЭДО = ПодписываемыеДанные.Данные Тогда
				
					Контекст.Вставить("ПодписанныеДвоичныеДанныеДляОператораЭДО", СтурктураПодписи.Подпись);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
	КонецЕсли;
	
	// Подготовим сертификат оператора ЭДО для шифрования информации
	Если Не ЗначениеЗаполнено(ОператорЭДОСертификат) Тогда
		ПодключенныеОператорыЭДО1СЭДО();
		
		ТекстСообщения = НСтр("ru = 'Ошибка получения сертификата оператора ЭДО.
									|Необходимо повторить получение идентификатора участника ЭДО.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;

	// Зашифровать
	ОписаниеПодписатьЭД = Новый ОписаниеОповещения("ПодготовитьДанныеДляРегистрационногоПакета1СЭДОЗавершение", ЭтотОбъект, Контекст);
	
	ТекстСертификата = ОператорЭДОСертификат;
	Если Найти(ТекстСертификата, "-----BEGIN CERTIFICATE-----") > 0 Тогда
		ТекстСертификата = СтрЗаменить(ТекстСертификата, "-----BEGIN CERTIFICATE-----", "");
		ТекстСертификата = СтрЗаменить(ТекстСертификата, "-----END CERTIFICATE-----", "");
	КонецЕсли;
	ДвоичныеДанныеСертификатаОператораЭДО = Base64Значение(ТекстСертификата);
	
	ОписаниеДанных = Новый Структура;
	ОписаниеДанных.Вставить("Операция",            НСтр("ru = 'Шифрование данных для регистрации у оператора ЭДО'"));
	
	МассивСертификатов = Новый Массив;
	МассивСертификатов.Добавить(ДвоичныеДанныеСертификатаОператораЭДО);
	ОписаниеДанных.Вставить("НаборСертификатов",   МассивСертификатов);
	
	ОписаниеДанных.Вставить("ПоказатьКомментарий", Ложь);
	ОписаниеДанных.Вставить("ИзменятьНабор",       Ложь);
	ОписаниеДанных.Вставить("Данные",              Контекст.ДвоичныеДанныеДляОператораЭДО);
	ОписаниеДанных.Вставить("ЗаголовокДанных",     НСтр("ru = 'Файл'"));
	ОписаниеДанных.Вставить("БезПодтверждения",    Истина);
	
	Представление = Новый Структура;
	Представление.Вставить("Представление", НСтр("ru = 'Данные для регистрации у оператора ЭДО'"));
	ОписаниеПредставленияЗаявления = Новый ОписаниеОповещения("ОбработатьПредставлениеДанныхДляОператораЭДО", ЭтотОбъект, Контекст);
	Представление.Вставить("Значение",      ОписаниеПредставленияЗаявления);
	
	ОписаниеДанных.Вставить("Представление",       Представление);
	
	ЭлектроннаяПодписьКлиент.Зашифровать(ОписаниеДанных, , ОписаниеПодписатьЭД);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодготовитьДанныеДляРегистрационногоПакета1СЭДОЗавершение(РезультатВыполнения = Неопределено, Контекст) Экспорт
	
	Если ТипЗнч(РезультатВыполнения) = Тип("Структура") Тогда
		Если Не РезультатВыполнения.Свойство("ЗашифрованныеДанные") Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ЗашифрованныеДвоичныеДанныеОператораЭДО = РезультатВыполнения.ЗашифрованныеДанные;
	
	ЕстьОшибки = Ложь;
	СформироватьИОтправитьРегистрационныйПакет1СЭДО(Контекст.РеквизитыПакета, ЗашифрованныеДвоичныеДанныеОператораЭДО, Контекст.ПодписанныеДвоичныеДанныеДляОператораЭДО,
		Контекст.ДвоичныеДанныеСоглашенияНаПодключениеЭДО, Контекст.ПодписанныеДвоичныеДанныеСоглашенияНаПодключениеЭДО, ПараметрыАутентификацииНаСайте, ЕстьОшибки);
		
	Если Не ЕстьОшибки Тогда
		// Запустим обработчик ожидания резальтата регистрации органиазации у оператора.
		ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
		ПодключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеЗаданияДляЭД", ПараметрыОбработчикаОжидания.ТекущийИнтервал, Истина);
		ФормаДлительнойОперации = ДлительныеОперацииКлиент.ОткрытьФормуДлительнойОперации(ЭтаФорма, УникальныйИдентификаторЗаявки1СЭДО);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПодготовитьДанныеПоСоглашениюНаПодключениеКЭДО(ДвоичныеДанныеСоглашенияНаПодключениеЭДО)
	
	// Подготовим данные для соглашения с оператором ЭДО
	ИмяВременногоФайлаСоглашениеЭДО = ЭлектронныеДокументыСлужебный.ТекущееИмяВременногоФайла("xml");
	
	// LicenseCard
	СоглашениеЭДО = Новый ЗаписьXML;
	СоглашениеЭДО.ОткрытьФайл(ИмяВременногоФайлаСоглашениеЭДО, "windows-1251");
	СоглашениеЭДО.ЗаписатьОбъявлениеXML();
	
	СоглашениеЭДО.ЗаписатьНачалоЭлемента("LicenseCard");
		СоглашениеЭДО.ЗаписатьНачалоЭлемента("info");
			СоглашениеЭДО.ЗаписатьАтрибут("EDOAgreed", XMLСтрока(Истина));
		СоглашениеЭДО.ЗаписатьКонецЭлемента(); // info
	СоглашениеЭДО.ЗаписатьКонецЭлемента(); // LicenseCard
	
	СоглашениеЭДО.Закрыть();
	
	ДвоичныеДанныеСоглашенияНаПодключениеЭДО = Новый ДвоичныеДанные(ИмяВременногоФайлаСоглашениеЭДО);
	УдалитьФайлы(ИмяВременногоФайлаСоглашениеЭДО);
	
КонецПроцедуры

&НаСервере
Процедура ПодготовитьДанныеПоРегистрационнойИнформации(РеквизитыПакета, ДвоичныеДанныеДляОператораЭДО)
	
	УникальныйИдентификаторЗаявки1СЭДО = ЭлектронныеДокументыСлужебный.СгенерироватьUUID();
	
	// Подготовим данные для 1СЭДО
	ИмяРеквизитаИННОрганизации = ЭлектронныеДокументыПовтИсп.ИмяНаличиеОбъектаРеквизитаВПрикладномРешении("ИННОрганизации");
	ИмяРеквизитаКППОрганизации = ЭлектронныеДокументыПовтИсп.ИмяНаличиеОбъектаРеквизитаВПрикладномРешении("КППОрганизации");
	ИмяРеквизитаОГРНОрганизации = ЭлектронныеДокументыПовтИсп.ИмяНаличиеОбъектаРеквизитаВПрикладномРешении("ОГРНОрганизации");
	ИмяРеквизитаНаименованиеОрганизации = ЭлектронныеДокументыПовтИсп.ИмяНаличиеОбъектаРеквизитаВПрикладномРешении("НаименованиеОрганизации");
	
	ПараметрыОрганиазции = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Организация,
		ИмяРеквизитаИННОрганизации + ", " + ИмяРеквизитаКППОрганизации + ", " + ИмяРеквизитаНаименованиеОрганизации + ", " + ИмяРеквизитаОГРНОрганизации);

	ПараметрыСертификата = ЭлектронныеДокументыСлужебныйВызовСервера.РеквизитыСертификата(СертификатКриптографии);
	
	РеквизитыПакета.Вставить("УникальныйИдентификаторЗаявки1СЭДО", УникальныйИдентификаторЗаявки1СЭДО);
	РеквизитыПакета.Вставить("НаименованиеОрганизации", ПараметрыОрганиазции[ИмяРеквизитаНаименованиеОрганизации]);
	РеквизитыПакета.Вставить("ИННОрганизации",  ПараметрыОрганиазции[ИмяРеквизитаИННОрганизации]);
	РеквизитыПакета.Вставить("КППОрганизации",  ПараметрыОрганиазции[ИмяРеквизитаКППОрганизации]);
	РеквизитыПакета.Вставить("ОператорЭДО",     ОператорЭДО);
	РеквизитыПакета.Вставить("ОператорЭДОИд",   ОператорЭДОИд);
	РеквизитыПакета.Вставить("Отпечаток",       НРег(СТРЗаменить(Строка(Base64Значение(ПараметрыСертификата.Отпечаток))," ","")));
	
	// Подготовим данные для оператора ЭДО
	ДеревоРегистрационнойИнформации = ОбщегоНазначенияЭД.ДеревоДокумента("РегистрационнаяИнформация");
	
	// Служебные данные
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "ИдФайл",   "registration_" + УникальныйИдентификаторЗаявки1СЭДО);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "ВерсПрог", "1С:Предприятие 8");
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "ВерсФорм", "5.02");
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "КНД",      "1115109");
	
	// Данные по налоговому органу
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "КодНО", КодНалоговогоОргана);
	
	// Данные по оператору ЭДО
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "ОперЭДО.НаимОрг",   ОператорЭДО);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "ОперЭДО.ИдОперЭДО", ОператорЭДОИд);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "ОперЭДО.ИННЮЛ",     ОператорЭДОИНН);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "ОперЭДО.КПП",       ОператорЭДОКПП);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "ОперЭДО.ОГРН",      ОператорЭДООГРН);
	
	// Данные по абоненту
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипЗаявления", "1");
		
	Если Не ЭлектронныеДокументыПереопределяемый.ЭтоФизЛицо(Организация) Тогда
		
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ЮЛ.НаимОрг", ПараметрыОрганиазции[ИмяРеквизитаНаименованиеОрганизации]);
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ЮЛ.ИННЮЛ",   ПараметрыОрганиазции[ИмяРеквизитаИННОрганизации]);
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ЮЛ.КПП",     ПараметрыОрганиазции[ИмяРеквизитаКППОрганизации]);
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ЮЛ.ОГРН",    ПараметрыОрганиазции[ИмяРеквизитаОГРНОрганизации]);
		
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ЮЛ.ДатаПодклЭДО", ТекущаяДатаСеанса());
		
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ЮЛ.АдрРФ.Индекс",     Индекс);
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ЮЛ.АдрРФ.КодРегион",  КодРегиона);
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ЮЛ.АдрРФ.Район",      Район);
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ЮЛ.АдрРФ.Город",      Город);
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ЮЛ.АдрРФ.НаселПункт", НаселенныйПункт);
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ЮЛ.АдрРФ.Улица",      Улица);
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ЮЛ.АдрРФ.Дом",        Дом);
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ЮЛ.АдрРФ.Корпус",     Корпус);
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ЮЛ.АдрРФ.Кварт",      Квартира);
		
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ЮЛ.ФИООтв.Фамилия",  ПараметрыСертификата.Фамилия);
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ЮЛ.ФИООтв.Имя",      ПараметрыСертификата.Имя);
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ЮЛ.ФИООтв.Отчество", ПараметрыСертификата.Отчество);
		
	Иначе
		
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ИП.ИННФЛ",  ПараметрыОрганиазции[ИмяРеквизитаИННОрганизации]);
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ИП.ОГРНИП", ПараметрыОрганиазции[ИмяРеквизитаОГРНОрганизации]);
		
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ИП.ДатаПодклЭДО", ТекущаяДатаСеанса());
		
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ИП.АдрРФ.Индекс",     Индекс);
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ИП.АдрРФ.КодРегион",  КодРегиона);
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ИП.АдрРФ.Район",      Район);
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ИП.АдрРФ.Город",      Город);
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ИП.АдрРФ.НаселПункт", НаселенныйПункт);
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ИП.АдрРФ.Улица",      Улица);
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ИП.АдрРФ.Дом",        Дом);
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ИП.АдрРФ.Корпус",     Корпус);
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ИП.АдрРФ.Кварт",      Квартира);
		
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ИП.ФИО.Фамилия",  СертификатКриптографии.Фамилия);
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ИП.ФИО.Имя",      СертификатКриптографии.Имя);
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ИП.ФИО.Отчество", СертификатКриптографии.Отчество);
		
	КонецЕсли;
	
	СертификатыПользователя = Новый ТаблицаЗначений;
	СертификатыПользователя.Колонки.Добавить("ДатаНачСертиф");
	СертификатыПользователя.Колонки.Добавить("ДатаКонСертиф");
	СертификатыПользователя.Колонки.Добавить("ОтпСертиф");
	СертификатыПользователя.Колонки.Добавить("Сертификат");
	СертификатыПользователя.Колонки.Добавить("Фамилия");
	СертификатыПользователя.Колонки.Добавить("Имя");
	СертификатыПользователя.Колонки.Добавить("Отчество");
	
	НоваяСтрока = СертификатыПользователя.Добавить();
	
	ТекущийСертификат = Новый СертификатКриптографии(ПараметрыСертификата.ДвоичныеДанныеСертификата);
	НоваяСтрока.ДатаНачСертиф = ТекущийСертификат.ДатаНачала;
	НоваяСтрока.ДатаКонСертиф = ПараметрыСертификата.ДействителенДо;
	
	НоваяСтрока.ОтпСертиф = НРег(СТРЗаменить(Строка(Base64Значение(ПараметрыСертификата.Отпечаток))," ",""));
	
	СертификатТекст = Base64Строка(ПараметрыСертификата.ДвоичныеДанныеСертификата);
	НоваяСтрока.Сертификат = "-----BEGIN CERTIFICATE-----" + символы.пс
							+ СертификатТекст + символы.пс
							+ "-----END CERTIFICATE-----";

	НоваяСтрока.Фамилия  = ПараметрыСертификата.Фамилия;
	НоваяСтрока.Имя      = ПараметрыСертификата.Имя;
	НоваяСтрока.Отчество = ПараметрыСертификата.Отчество;
	
	ОбщегоНазначенияЭД.ЗагрузитьТаблицуВДерево(ДеревоРегистрационнойИнформации, СертификатыПользователя, "УчастЭДО.СертифДолжн");
	
	//Данные по подписанту
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "Подписант.Должность",    ПараметрыСертификата.Должность);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "Подписант.ФИО.Фамилия",  ПараметрыСертификата.Фамилия);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "Подписант.ФИО.Имя",      ПараметрыСертификата.Имя);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "Подписант.ФИО.Отчество", ПараметрыСертификата.Отчество);
	
	ДвоичныеДанныеДляОператораЭДО = ЭлектронныеДокументыВнутренний.РегистрационныеДанныеДляОператораЭДО(ДеревоРегистрационнойИнформации);
	
КонецПроцедуры

&НаСервере
Процедура СформироватьИОтправитьРегистрационныйПакет1СЭДО(РеквизитыПакета, ЗашифрованныеДвоичныеДанныеОператораЭДО, ПодписанныеДвоичныеДанныеДляОператораЭДО,
		ДвоичныеДанныеСоглашенияНаПодключениеЭДО, ПодписанныеДвоичныеДанныеСоглашенияНаПодключениеЭДО, ПараметрыАутентификации, ЕстьОшибки)
	
	ИмяФайлаПакетаДля1СЭДО = ЭлектронныеДокументыСлужебный.РегистрационныйПакетДляОператораЭДО(РеквизитыПакета,
		ЗашифрованныеДвоичныеДанныеОператораЭДО, ПодписанныеДвоичныеДанныеДляОператораЭДО,
		ДвоичныеДанныеСоглашенияНаПодключениеЭДО, ПодписанныеДвоичныеДанныеСоглашенияНаПодключениеЭДО);
		
	// Отправка заявления на сервис 1С-ЭДО
	ТекстЗаголовкаСообщения = НСтр("ru = 'Отправка заявления о регистрации на сервисе 1С-ЭДО'");
	
	ШаблонСообщения = НСтр("ru = 'Заявления № %1'");
	ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения, УникальныйИдентификаторЗаявки1СЭДО);
	ЭлектронныеДокументыСлужебныйВызовСервера.ОбработатьИсключениеПоЭДНаСервере(ТекстЗаголовкаСообщения,
		ТекстСообщения);
		
	ЭлектронныеДокументыСлужебный.ОтправитьРегистрационныйПакет1СЭДО(ИмяФайлаПакетаДля1СЭДО, ПараметрыАутентификации, ЕстьОшибки);
	
	УдалитьФайлы(ИмяФайлаПакетаДля1СЭДО);
	
КонецПроцедуры

// Профиль настроек ЭДО

&НаКлиенте
Процедура ТестПрофиляНастроекЭДО()
	
	Отказ = Ложь;
	// Тестируем заполненность формы
	Если Не ЗначениеЗаполнено(Организация) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
		ЭлектронныеДокументыКлиентСервер.ПолучитьТекстСообщения("Поле", "Заполнение", "Организация"),
		,
		"Организация",
		,
		Отказ);
	КонецЕсли;
	Если Не ЗначениеЗаполнено(ИдентификаторОрганизации)
		И ЭлектронныеДокументыСлужебныйВызовСервера.ЭтоПрямойОбмен(СпособОбменаЭД) Тогда
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			ЭлектронныеДокументыКлиентСервер.ПолучитьТекстСообщения("Поле", "Заполнение", "Идентификатор организации"),
			,
			"ИдентификаторОрганизации",
			,
			Отказ);
	КонецЕсли;

	Если Не ЗначениеЗаполнено(СпособОбменаЭД) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			ЭлектронныеДокументыКлиентСервер.ПолучитьТекстСообщения("Поле", "Заполнение", "Способ Обмена ЭД"),
			,
			"СпособОбменаЭД",
			,
			Отказ);
	КонецЕсли;
	
	Если СпособОбменаЭД = ПредопределенноеЗначение("Перечисление.СпособыОбменаЭД.ЧерезЭлектроннуюПочту") Тогда
		Если Не ЗначениеЗаполнено(ЭлектроннаяПочтаОрганизации) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			ЭлектронныеДокументыКлиентСервер.ПолучитьТекстСообщения("Поле", "Заполнение", "Электронная почта организации"),
			,
			"ЭлектроннаяПочтаОрганизации",
			,
			Отказ);
		КонецЕсли;
		Если Не ЗначениеЗаполнено(СертификатКриптографии) И ИспользоватьЭП Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				ЭлектронныеДокументыКлиентСервер.ПолучитьТекстСообщения("Поле", "Заполнение", "Сертификат ключа электронной подписи"),
				,
				"СертификатКриптографии",
				,
				Отказ);
		КонецЕсли;

	ИначеЕсли СпособОбменаЭД = ПредопределенноеЗначение("Перечисление.СпособыОбменаЭД.ЧерезКаталог") Тогда
		Если Не ЗначениеЗаполнено(КаталогВходящихДокументов) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			ЭлектронныеДокументыКлиентСервер.ПолучитьТекстСообщения("Поле", "Заполнение", "Каталог входящих документов"),
			,
			"КаталогВходящихДокументов",
			,
			Отказ);
		КонецЕсли;
		Если Не ЗначениеЗаполнено(СертификатКриптографии) И ИспользоватьЭП Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				ЭлектронныеДокументыКлиентСервер.ПолучитьТекстСообщения("Поле", "Заполнение", "Сертификат ключа электронной подписи"),
				,
				"СертификатКриптографии",
				,
				Отказ);
		КонецЕсли;

	ИначеЕсли СпособОбменаЭД = ПредопределенноеЗначение("Перечисление.СпособыОбменаЭД.ЧерезFTP") Тогда
		Если Не ЗначениеЗаполнено(АдресСервераFTP) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			ЭлектронныеДокументыКлиентСервер.ПолучитьТекстСообщения("Поле", "Заполнение", "Адрес сервера"),
			,
			"АдресСервераFTP",
			,
			Отказ);
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(FTPКаталогВходящихДокументов) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			ЭлектронныеДокументыКлиентСервер.ПолучитьТекстСообщения("Поле", "Заполнение", "Каталог входящих документов"),
			,
			"FTPКаталогВходящихДокументов",
			,
			Отказ);
		КонецЕсли;
		Если Не ЗначениеЗаполнено(СертификатКриптографии) И ИспользоватьЭП Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				ЭлектронныеДокументыКлиентСервер.ПолучитьТекстСообщения("Поле", "Заполнение", "Сертификат ключа электронной подписи"),
				,
				"СертификатКриптографии",
				,
				Отказ);
		КонецЕсли;
		
	КонецЕсли;
	
	Если Не ЭлектронныеДокументыСлужебныйВызовСервера.ЭтоПрямойОбмен(СпособОбменаЭД) Тогда

		Если Не ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьЗначениеФункциональнойОпции(
				"ИспользоватьЭлектронныеПодписиЭД") Тогда
		
			ТекстСообщения = ЭлектронныеДокументыСлужебныйВызовСервера.ТекстСообщенияОНеобходимостиНастройкиСистемы(
				"НАСТРОЙКАКРИПТОГРАФИИ");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , , , Отказ);
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(СертификатКриптографии) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				ЭлектронныеДокументыКлиентСервер.ПолучитьТекстСообщения("Поле", "Заполнение", "Сертификат ключа электронной подписи"),
				,
				"СертификатКриптографии",
				,
				Отказ);
		КонецЕсли;
		
		Если СпособОбменаЭД = ПредопределенноеЗначение("Перечисление.СпособыОбменаЭД.ЧерезСервис1СЭДО") Тогда
			
			Если ТипРегистрации = 1 И Не ЗначениеЗаполнено(ИдентификаторОрганизацииСуществующий) Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					ЭлектронныеДокументыКлиентСервер.ПолучитьТекстСообщения("Поле", "Заполнение", "Идентификатор организации"),
					,
					"ИдентификаторОрганизацииСуществующий",
					,
					Отказ);
					
			ИначеЕсли ТипРегистрации = 2 И Не ЗначениеЗаполнено(ИдентификаторОрганизации) Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					Нстр("ru = 'Необходимо получить уникальный идентификатор участника обмена ЭД.'"), , , , Отказ);
			КонецЕсли;
		ИначеЕсли СпособОбменаЭД = ПредопределенноеЗначение("Перечисление.СпособыОбменаЭД.ЧерезОператораЭДОТакском") Тогда
			
			Если Не ЗначениеЗаполнено(ИдентификаторОрганизации) Тогда
				Если ЭлектронныеДокументыСлужебныйКлиент.ПроверитьИспользованиеИнтернетПоддержкаПользователей() Тогда
					
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
						Нстр("ru = 'Необходимо получить уникальный идентификатор участника обмена ЭД.'"), , , , Отказ);
				Иначе
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
						ЭлектронныеДокументыКлиентСервер.ПолучитьТекстСообщения("Поле", "Заполнение", "Идентификатор организации"),
							,
							"ИдентификаторОрганизации",
							,
							Отказ);
				КонецЕсли;
			Иначе
				
				ИдентификаторОрганизации = СокрЛП(ИдентификаторОрганизации);
				ДлинаИдентификатора = СтрДлина(ИдентификаторОрганизации);
				Если ДлинаИдентификатора <> 46 Тогда
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
						ЭлектронныеДокументыКлиентСервер.ПолучитьТекстСообщения("Поле", "КОРРЕКТНОСТЬ", "Идентификатор организации", , ,
						Нстр("ru = 'Длина поля не равна 46.'")),
						,
						"ИдентификаторОрганизации",
						,
						Отказ);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЭлектронныеДокументыСлужебныйВызовСервера.ЭтоПрямойОбмен(СпособОбменаЭД) Тогда
		
		Состояние(НСтр("ru = 'Создание профиля настроек ЭДО.'"),
			,
			НСтр("ru = 'Выполняется тестирование связи с оператором. Подождите...'"));
			
		ТестСвязиСервисаЭДО();
		
	Иначе
		ТестСвязиПройден = Истина;
		
		Если СпособОбменаЭД = ПредопределенноеЗначение("Перечисление.СпособыОбменаЭД.ЧерезЭлектроннуюПочту") Тогда
			
			Состояние(НСтр("ru = 'Создание профиля настроек ЭДО.'"),
				,
				НСтр("ru = 'Выполняется тестирование обмена ЭД через электронную почту. Подождите...'"));
			
			СообщениеОбОшибке = "";
			ДополнительноеСообщение = "";
			РаботаСПочтовымиСообщениямиВызовСервера.ПроверитьВозможностьОтправкиИПолученияЭлектроннойПочты(
					ЭлектроннаяПочтаОрганизации, Неопределено, СообщениеОбОшибке, ДополнительноеСообщение);
			
			Если ЗначениеЗаполнено(СообщениеОбОшибке) Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Проверка параметров учетной записи завершилась с ошибками:
								|%1'"), СообщениеОбОшибке ),,
					НСтр("ru = 'Проверка учетной записи'"));
				ТестСвязиПройден = Ложь;
			КонецЕсли;
			
		ИначеЕсли СпособОбменаЭД = ПредопределенноеЗначение("Перечисление.СпособыОбменаЭД.ЧерезКаталог") Тогда
			
			Состояние(НСтр("ru = 'Создание профиля настроек ЭДО.'"),
				,
				НСтр("ru = 'Выполняется тестирование обмена ЭД через каталог. Подождите...'"));
			ТестСвязиПрямогоОбменаНаСервере(КаталогВходящихДокументов, ТестСвязиПройден);
			
		ИначеЕсли СпособОбменаЭД = ПредопределенноеЗначение("Перечисление.СпособыОбменаЭД.ЧерезFTP") Тогда
			
			Состояние(НСтр("ru = 'Создание профиля настроек ЭДО.'"),
				,
				НСтр("ru = 'Выполняется тестирование обмена ЭД через FTP. Подождите...'"));
			ТестСвязиОбменаЧерезFTPНаСервере(ТестСвязиПройден);
			
		КонецЕсли;
		
		Если ТестСвязиПройден Тогда
			СоздатьНовыйПрофиль();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СохранитьПараметры(ДанныеСохранены)
	
	Попытка
		НачатьТранзакцию();
		НовыйПрофильНастроек = Справочники.ПрофилиНастроекЭДО.СоздатьЭлемент();
		
		ШаблонНаименование = НСтр("ru = '%1, %2'");
		НовыйПрофильНастроек.Наименование = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонНаименование,
			Организация, СпособОбменаЭД);
			
		НовыйПрофильНастроек.Организация              = Организация;
		НовыйПрофильНастроек.ИдентификаторОрганизации = СокрЛП(ИдентификаторОрганизации);
		НовыйПрофильНастроек.СпособОбменаЭД           = СпособОбменаЭД;
		
		// Настройки сертификатов
		Если ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьЗначениеФункциональнойОпции("ИспользоватьЭлектронныеПодписиЭД") Тогда
			Если Не ЭлектронныеДокументыСлужебныйВызовСервера.ЭтоПрямойОбмен(СпособОбменаЭД) ИЛИ ИспользоватьЭП Тогда
				
				НоваяСтрока = НовыйПрофильНастроек.СертификатыПодписейОрганизации.Добавить();
				НоваяСтрока.Сертификат = СертификатКриптографии;
			КонецЕсли;
		КонецЕсли;
		
		// Загружаем ТЧ из профиля настроек ЭДО.
		АктуальныеВидыЭД = ЭлектронныеДокументыПовтИсп.ПолучитьАктуальныеВидыЭД();
		БанковскиеВидыЭД = ЭлектронныеДокументыСлужебный.БанковскиеВидыЭД();
		Для Каждого ЗначениеПеречисления Из АктуальныеВидыЭД Цикл
			Если ЗначениеПеречисления <> Перечисления.ВидыЭД.Подтверждение
				И ЗначениеПеречисления <> Перечисления.ВидыЭД.ВозвратТоваровМеждуОрганизациями
				И ЗначениеПеречисления <> Перечисления.ВидыЭД.ПередачаТоваровМеждуОрганизациями
				И ЗначениеПеречисления <> Перечисления.ВидыЭД.УведомлениеОбУточнении
				И ЗначениеПеречисления <> Перечисления.ВидыЭД.Ошибка
				И ЗначениеПеречисления <> Перечисления.ВидыЭД.ИзвещениеОПолучении
				И ЗначениеПеречисления <> Перечисления.ВидыЭД.ПредложениеОбАннулировании
				И БанковскиеВидыЭД.Найти(ЗначениеПеречисления) = Неопределено Тогда
				
				НоваяСтрока = НовыйПрофильНастроек.ИсходящиеДокументы.Добавить();
				НоваяСтрока.Формировать = Истина;
				НоваяСтрока.ИсходящийДокумент = ЗначениеПеречисления;
				
				Если ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьЗначениеФункциональнойОпции(
					"ИспользоватьЭлектронныеПодписиЭД") Тогда
					Если Не ЭлектронныеДокументыСлужебныйВызовСервера.ЭтоПрямойОбмен(СпособОбменаЭД) ИЛИ ИспользоватьЭП Тогда
						НоваяСтрока.ИспользоватьЭП = Истина;
					КонецЕсли;
				КонецЕсли;
				
				Если (ЗначениеПеречисления = Перечисления.ВидыЭД.СчетФактура
					ИЛИ ЗначениеПеречисления = Перечисления.ВидыЭД.КорректировочныйСчетФактура)
					И ЭлектронныеДокументыСлужебныйВызовСервера.ЭтоПрямойОбмен(СпособОбменаЭД) Тогда
					
					НоваяСтрока.Формировать = Ложь;
					НоваяСтрока.ИспользоватьЭП = Ложь;
					
				КонецЕсли;
				// Проставим в новые настройки ЭДО версию формата обмена.
				ВерсияФормата = "CML 2.08";
				Если ЗначениеПеречисления = Перечисления.ВидыЭД.ПроизвольныйЭД Тогда
					ВерсияФормата = "";
				ИначеЕсли ЗначениеПеречисления = Перечисления.ВидыЭД.АктЗаказчик
					ИЛИ ЗначениеПеречисления = Перечисления.ВидыЭД.АктИсполнитель
					ИЛИ ЗначениеПеречисления = Перечисления.ВидыЭД.ТОРГ12Покупатель
					ИЛИ ЗначениеПеречисления = Перечисления.ВидыЭД.ТОРГ12Продавец
					ИЛИ ЗначениеПеречисления = Перечисления.ВидыЭД.СоглашениеОбИзмененииСтоимостиОтправитель
					ИЛИ ЗначениеПеречисления = Перечисления.ВидыЭД.СоглашениеОбИзмененииСтоимостиПолучатель
					ИЛИ ЗначениеПеречисления = Перечисления.ВидыЭД.СчетФактура
					ИЛИ ЗначениеПеречисления = Перечисления.ВидыЭД.КорректировочныйСчетФактура Тогда
					ВерсияФормата = "ФНС 5.01";
				КонецЕсли;
				НоваяСтрока.ВерсияФормата = ВерсияФормата;
			КонецЕсли;
		КонецЦикла;
		
		НовыйПрофильНастроек.ИсходящиеДокументы.Сортировать("ИсходящийДокумент");
		
		// Настройки обмена ЭД
		Если СпособОбменаЭД = Перечисления.СпособыОбменаЭД.ЧерезКаталог Тогда
			НовыйПрофильНастроек.РесурсВходящихДокументов = КаталогВходящихДокументов;
			
		ИначеЕсли СпособОбменаЭД = Перечисления.СпособыОбменаЭД.ЧерезЭлектроннуюПочту Тогда
			НовыйПрофильНастроек.РесурсВходящихДокументов = ЭлектроннаяПочтаОрганизации;
		
		ИначеЕсли СпособОбменаЭД = Перечисления.СпособыОбменаЭД.ЧерезFTP Тогда
			НовыйПрофильНастроек.АдресСервера             = АдресСервераFTP;
			НовыйПрофильНастроек.Порт                     = ПортFTP;
			НовыйПрофильНастроек.ПассивноеСоединение      = ПассивноеСоединениеFTP;
			НовыйПрофильНастроек.Логин                    = ПользовательFTP;
			НовыйПрофильНастроек.Пароль                   = ПарольFTP;
			НовыйПрофильНастроек.РесурсВходящихДокументов = FTPКаталогВходящихДокументов;
			
		ИначеЕсли СпособОбменаЭД = Перечисления.СпособыОбменаЭД.ЧерезСервис1СЭДО Тогда
			Если ТипРегистрации = 1 Тогда
				НовыйПрофильНастроек.ИдентификаторОрганизации = СокрЛП(ИдентификаторОрганизацииСуществующий);
			КонецЕсли;
			
			// Заполним данные по адресу организации
			НовыйПрофильНастроек.Индекс          = Индекс;
			НовыйПрофильНастроек.КодРегиона      = КодРегиона;
			НовыйПрофильНастроек.Регион          = Регион;
			НовыйПрофильНастроек.Район           = Район;
			НовыйПрофильНастроек.Город           = Город;
			НовыйПрофильНастроек.НаселенныйПункт = НаселенныйПункт;
			НовыйПрофильНастроек.Улица           = Улица;
			НовыйПрофильНастроек.Дом             = Дом;
			НовыйПрофильНастроек.Корпус          = Корпус;
			НовыйПрофильНастроек.Квартира        = Квартира;
			
			// Заполним данные код налогового органа организации
			НовыйПрофильНастроек.КодНалоговогоОргана = КодНалоговогоОргана;
			
			// Заполним данные по оператору ЭДО
			НовыйПрофильНастроек.ОператорЭДО     = ОператорЭДО;
			НовыйПрофильНастроек.ОператорЭДОИд   = ОператорЭДОИд;
			НовыйПрофильНастроек.ОператорЭДОИНН  = ОператорЭДОИНН;
			НовыйПрофильНастроек.ОператорЭДОКПП  = ОператорЭДОКПП;
			НовыйПрофильНастроек.ОператорЭДООГРН = ОператорЭДООГРН;
		КонецЕсли;
		
		Если НовыйПрофильНастроек.ПрофильНастроекЭДОУникален() Тогда
			НовыйПрофильНастроек.Записать();
		Иначе
			ОтменитьТранзакцию();
			ДанныеСохранены = Ложь;
			Возврат;
		КонецЕсли;
		СсылкаНаПрофильНастроек = НовыйПрофильНастроек.Ссылка;
		ЗафиксироватьТранзакцию();
		
		УстановитьДатуСостоянияОбмена(СсылкаНаПрофильНастроек);
		
		ОбновитьПовторноИспользуемыеЗначения();
	Исключение
		ОтменитьТранзакцию();
		ДанныеСохранены = Ложь;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОписаниеОшибки());
	КонецПопытки
	
КонецПроцедуры

&НаСервере
Процедура УстановитьДатуСостоянияОбмена(СсылкаНаПрофильНастроек)
	
	Если ЭлектронныеДокументыСлужебныйВызовСервера.ЭтоПрямойОбмен(СпособОбменаЭД) Тогда
		Возврат;
	КонецЕсли;
	
	НаборЗаписей = РегистрыСведений.СостоянияОбменовЭДЧерезОператоровЭДО.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ПрофильНастроекЭДО.Установить(СсылкаНаПрофильНастроек);
	НаборЗаписей.Прочитать();
	
	НоваяЗапись = НаборЗаписей.Добавить();
	НоваяЗапись.ДатаПолученияЭД = НачалоДня(ТекущаяДатаСеанса());
	НоваяЗапись.ПрофильНастроекЭДО = СсылкаНаПрофильНастроек;
	НаборЗаписей.Записать();
	
КонецПроцедуры

// Обмен через сервис ЭДО

&НаКлиенте
Процедура ТестСвязиСервисаЭДО()
	
	СтруктураПараметровЗапросаМаркера = Новый Структура;
	СтруктураПараметровЗапросаМаркера.Вставить("СертификатПодписи", СертификатКриптографии);
	СтруктураПараметровЗапросаМаркера.Вставить("СпособОбменаЭД", СпособОбменаЭД);
	
	Если ТипРегистрации = 1 Тогда
		
		СтруктураПараметровЗапросаМаркера.Вставить("ИдентификаторОрганизации", ИдентификаторОрганизацииСуществующий);
	Иначе
		СтруктураПараметровЗапросаМаркера.Вставить("ИдентификаторОрганизации", ИдентификаторОрганизации);
	КонецЕсли;
	
	ЗашифрованныеДанные = ЭлектронныеДокументыСлужебныйВызовСервера.ЗашифрованныйМаркер(СтруктураПараметровЗапросаМаркера,
		ПараметрыАутентификацииНаСайте);
	Если Не ЗначениеЗаполнено(ЗашифрованныеДанные) Тогда
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура;
	ОбработкаЗавершения = Новый ОписаниеОповещения("ДействияПослеТестаСвязиЭДО", ЭтотОбъект, ДополнительныеПараметры);
	
	ОписаниеДанных = Новый Структура;
	ОписаниеДанных.Вставить("Операция",            НСтр("ru = 'Тест аутентификации в сервисе ЭДО'"));
	
	МассивСертификатов = Новый Массив;
	МассивСертификатов.Добавить(СертификатКриптографии);
	ОписаниеДанных.Вставить("ОтборСертификатов",   МассивСертификатов);
	
	ОписаниеДанных.Вставить("ПоказатьКомментарий", Ложь);
	ОписаниеДанных.Вставить("ИзменятьНабор",       Ложь);
	ОписаниеДанных.Вставить("ЭтоАутентификация",   Истина);
	ОписаниеДанных.Вставить("Данные",              ЗашифрованныеДанные);
	
	ЭлектроннаяПодписьКлиент.Расшифровать(ОписаниеДанных, , ОбработкаЗавершения);
	
КонецПроцедуры

&НаКлиенте
Процедура ДействияПослеТестаСвязиЭДО(РезультатВыполнения, Контекст = Неопределено) Экспорт
	
	Если ТипЗнч(РезультатВыполнения) = Тип("Структура") Тогда
		Если РезультатВыполнения.Успех = Истина Тогда
			СоздатьНовыйПрофиль();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Обмен через каталог

&НаСервереБезКонтекста
Процедура ТестСвязиПрямогоОбменаНаСервере(КаталогВходящихДокументов, Отказ)
	
	// Блок проверки доступа к каталогам.
	Попытка
		Если Не ЭлектронныеДокументыСлужебныйВызовСервера.ПроверитьДоступностьКаталогаДляПрямогоОбмена(КаталогВходящихДокументов) Тогда
			ТекстОшибки = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьСообщениеОбОшибке("107");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
			Отказ = Истина;
		КонецЕсли;
	Исключение
		ТекстОшибки = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьСообщениеОбОшибке("107");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
		Отказ = Истина;
	КонецПопытки;
	
КонецПроцедуры

// Обмен через FTP

&НаСервере
Процедура ТестСвязиОбменаЧерезFTPНаСервере(Отказ)
	
	ИспользоватьПрокси = Ложь;
	
	НастройкаПроксиСервера = ПолучениеФайловИзИнтернета.НастройкиПроксиНаСервере();
	Если НастройкаПроксиСервера <> Неопределено Тогда
		ПараметрИспользоватьПрокси = НастройкаПроксиСервера.Получить("ИспользоватьПрокси");
		Если НЕ ПараметрИспользоватьПрокси=Неопределено Тогда
			ИспользоватьПрокси = ПараметрИспользоватьПрокси;
		КонецЕсли;
	КонецЕсли;
	
	Если ИспользоватьПрокси Тогда
		Если НастройкаПроксиСервера.Получить("ИспользоватьСистемныеНастройки") Тогда
			// Системные настройки прокси-сервера.
			Прокси = Новый ИнтернетПрокси(Истина);
		Иначе
			// Ручные настройки прокси-сервера.
			Прокси = Новый ИнтернетПрокси;
			Прокси.Установить("ftp", НастройкаПроксиСервера["Сервер"], НастройкаПроксиСервера["Порт"]);
			Прокси.НеИспользоватьПроксиДляЛокальныхАдресов = НастройкаПроксиСервера["НеИспользоватьПроксиДляЛокальныхАдресов"];
			Прокси.Пользователь = НастройкаПроксиСервера["Пользователь"];
			Прокси.Пароль = НастройкаПроксиСервера["Пароль"];
		КонецЕсли;
	Иначе
		Прокси = Новый ИнтернетПрокси(Ложь);
	КонецЕсли;
	
	Попытка
		FTPСоединение = Новый FTPСоединение(АдресСервераFTP,
											ПортFTP,
											ПользовательFTP,
											ПарольFTP,
											Прокси,
											ПассивноеСоединениеFTP);
	Исключение
		ТекстОшибки = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьСообщениеОбОшибке("121");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
		Отказ = Истина;
		Возврат;
	КонецПопытки;
	
	Попытка
		ЭлектронныеДокументыСлужебный.ПодготовитьПутьFTP(FTPКаталогВходящихДокументов);
		FTPСоединение.УстановитьТекущийКаталог(FTPКаталогВходящихДокументов);
	Исключение
		СоздатьКаталогиFTP(FTPСоединение, FTPКаталогВходящихДокументов, Истина, ТекстОшибки);
	КонецПопытки;
	
	Если ЗначениеЗаполнено(ТекстОшибки) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ПроверитьФайл(FTPСоединение, ТекстОшибки);
	Если ЗначениеЗаполнено(ТекстОшибки) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьФайл(FTPСоединение, ТекстОшибки)
	
	ВремФайл = ПолучитьИмяВременногоФайла();
	ТекстовыйДокумент = Новый ТекстовыйДокумент();
	ТестоваяСтрока = "Тестовая строка 1С: Предприятие";
	ТекстовыйДокумент.УстановитьТекст(ТестоваяСтрока);
	ТекстовыйДокумент.Записать(ВремФайл);
	ФайлТест = Новый Файл(ВремФайл);
		
	ЗаписатьФайлНаFTP(FTPСоединение, ВремФайл, ФайлТест.Имя, Истина, ТекстОшибки);
	УдалитьФайлы(ВремФайл);
	Если ЗначениеЗаполнено(ТекстОшибки) Тогда
		Возврат;
	КонецЕсли;
	
	ФайлПолучатель = ПолучитьИмяВременногоФайла();
	
	ПолучитьФайлСFTP(FTPСоединение, ФайлТест.Имя, ФайлПолучатель, Истина, ТекстОшибки);
	
	Если ЗначениеЗаполнено(ТекстОшибки) Тогда
		УдалитьФайлы(ФайлПолучатель);
		Возврат;
	КонецЕсли;
		
	ТекстовыйДокумент = Новый ТекстовыйДокумент;
	ТекстовыйДокумент.Прочитать(ФайлПолучатель);
	СтрокаРезультата = ТекстовыйДокумент.ПолучитьТекст();
	УдалитьФайлы(ФайлПолучатель);
	Если НЕ СтрокаРезультата = ТестоваяСтрока Тогда
		ШаблонСообщения = НСтр("ru = '%1 %2.'");
		ТекстСообщения = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьСообщениеОбОшибке("126");
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, ТекстСообщения,
			FTPСоединение.ТекущийКаталог());
		Возврат;
	КонецЕсли;
	
	УдалитьФайлFTP(FTPСоединение, ФайлТест.Имя, Истина, ТекстОшибки);
	
КонецПроцедуры

&НаСервере
Процедура СоздатьКаталогиFTP(FTPСоединение, ПолныйПуть, ЭтоТест = Ложь, ТекстОшибки = Неопределено) Экспорт
	
	ПолныйПуть = СтрЗаменить(ПолныйПуть, "\", "/");
	МассивКаталогов = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ПолныйПуть, "/", Истина);
	ТекущийПуть = "/";
	FTPСоединение.УстановитьТекущийКаталог(ТекущийПуть);
	Для Каждого Элемент ИЗ МассивКаталогов Цикл
		
		мКаталог = Новый Массив;
		
		НайтиФайлыВКаталогеFTP(FTPСоединение, Элемент, Неопределено, Истина, ТекстОшибки, мКаталог);
		
		Если ЗначениеЗаполнено(ТекстОшибки) Тогда
			Возврат;
		КонецЕсли;
		
		Если мКаталог.Количество() = 1 Тогда 
			Если мКаталог[0].ЭтоФайл() Тогда 
				ТекстОшибки = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьСообщениеОбОшибке("122");
				Если НЕ ЭтоТест Тогда
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
				КонецЕсли;
				Возврат;
			КонецЕсли;
			СоздаватьКаталог = Ложь;
		Иначе
			СоздаватьКаталог = Истина;
		КонецЕсли;

		Если СоздаватьКаталог Тогда
			Попытка
				FTPСоединение.СоздатьКаталог(Элемент);
				ТекущийПуть = ТекущийПуть + Элемент + "/";
			Исключение
				ТекстОшибки = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьСообщениеОбОшибке("123");
				Если НЕ ЭтоТест Тогда
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
				КонецЕсли;
				Возврат;
			КонецПопытки
		КонецЕсли;
		
		Попытка
			FTPСоединение.УстановитьТекущийКаталог(ТекущийПуть);
		Исключение
			ТекстОшибки = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьСообщениеОбОшибке("124");
			Если НЕ ЭтоТест Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
			КонецЕсли;
		КонецПопытки
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьФайлНаFTP(FTPСоединение,
							Источник,
							ИмяВыходногоФайла,
							ЭтоТест = Ложь,
							РезультатТеста = Неопределено) Экспорт
	
	Попытка
		FTPСоединение.Записать(Источник, ИмяВыходногоФайла);
	Исключение
		РезультатТеста = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьСообщениеОбОшибке("127");
		
		Если НЕ ЭтоТест Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(РезультатТеста);
		КонецЕсли;
	КонецПопытки;

КонецПроцедуры

&НаСервере
Процедура ПолучитьФайлСFTP(FTPСоединение, Источник, ИмяВыходногоФайла, ЭтоТест = Ложь, РезультатТеста = Неопределено)
	
	Попытка
		FTPСоединение.Получить(Источник, ИмяВыходногоФайла);
	Исключение
		РезультатТеста = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьСообщениеОбОшибке("128");
		
		Если Не ЭтоТест Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(РезультатТеста);
		КонецЕсли;
	КонецПопытки;
	
КонецПроцедуры

&НаСервере
Процедура НайтиФайлыВКаталогеFTP(FTPСоединение, Путь, Маска, ЭтоТест, РезультатТеста, МассивФайлов)
	
	Попытка
		Если Маска = Неопределено Тогда
			МассивФайлов = FTPСоединение.НайтиФайлы(Путь);
		Иначе
			МассивФайлов = FTPСоединение.НайтиФайлы(Путь, Маска);
		КонецЕсли;
	Исключение
		РезультатТеста = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьСообщениеОбОшибке("125");
		
		Если НЕ ЭтоТест = Истина Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(РезультатТеста);
		КонецЕсли;
	КонецПопытки;
	
КонецПроцедуры

&НаСервере
Процедура УдалитьФайлFTP(FTPСоединение, Путь, РезультатТеста = Неопределено, ЭтоТест = Ложь)
		
	Попытка
		FTPСоединение.Удалить(Путь);
	Исключение
		РезультатТеста = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьСообщениеОбОшибке("129");
		
		Если Не ЭтоТест Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(РезультатТеста);
		КонецЕсли;
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьНовыйПрофиль()
	
	ДанныеСохранены = Истина;
	СохранитьПараметры(ДанныеСохранены);
	Если ДанныеСохранены Тогда
		Если ЗначениеЗаполнено(Контрагент) Тогда
			
			ПараметрыФормы = Новый Структура;
			ЗначенияЗаполнения = Новый Структура;
			ЗначенияЗаполнения.Вставить("Контрагент", Контрагент);
			ЗначенияЗаполнения.Вставить("ПрофильНастроекЭДО", СсылкаНаПрофильНастроек);
			
			ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
			ОткрытьФорму("Справочник.СоглашенияОбИспользованииЭД.Форма.ФормаЭлемента", ПараметрыФормы, ЭтотОбъект);
			
		Иначе
			ПоказатьОповещениеПользователя("Создание",
				ПолучитьНавигационнуюСсылку(СсылкаНаПрофильНастроек), СсылкаНаПрофильНастроек);
				
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("Ключ", СсылкаНаПрофильНастроек);
			ОткрытьФорму("Справочник.ПрофилиНастроекЭДО.Форма.ФормаЭлемента", ПараметрыФормы);
		КонецЕсли;
		
		Оповестить("ОбновитьСостояниеЭД");
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиАсинхронныхДиалогов

&НаКлиенте
Процедура ПередЗакрытиемЗавершить(Знач Результат, Знач ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ЗакрытьФорму = Истина;
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзмененииЗавершить(Знач Результат, Знач ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(Результат) Тогда
		Если Результат = КодВозвратаДиалога.Да Тогда
			УстановитьИдентификатор("Организации", Организация);
		КонецЕсли;
	КонецЕсли;
	
	Если СпособОбменаЭД = ПредопределенноеЗначение("Перечисление.СпособыОбменаЭД.ЧерезСервис1СЭДО")
		И ЗначениеЗаполнено(Организация) ТОгда
		
		ЗаполнитьРеквизитыПоИНННаКлиенте();
	КонецЕсли;
	
	УстановитьЗначенияПоУмолчанию();
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьЭППриИзмененииЗавершить(Знач Результат, Знач ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(Результат) Тогда
		Если Результат = КодВозвратаДиалога.Да Тогда
			СертификатКриптографии = ПредопределенноеЗначение("Справочник.СертификатыКлючейЭлектроннойПодписиИШифрования.ПустаяСсылка");
		Иначе
			// Вернем все как было.
			ИспользоватьЭП = Не ИспользоватьЭП;
		КонецЕсли;
	КонецЕсли;
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораКаталога(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныеФайлы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если СпособОбменаЭД = ПредопределенноеЗначение("Перечисление.СпособыОбменаЭД.ЧерезКаталог") Тогда
		КаталогВходящихДокументов = ВыбранныеФайлы[0];
	ИначеЕсли СпособОбменаЭД = ПредопределенноеЗначение("Перечисление.СпособыОбменаЭД.ЧерезFTP") Тогда
		FTPКаталогВходящихДокументов = ВыбранныеФайлы[0];
	КонецЕсли;
	ЭлектронныеДокументыСлужебныйВызовСервера.ПроверитьДоступностьКаталогаДляПрямогоОбмена(ВыбранныеФайлы[0]);

КонецПроцедуры

#КонецОбласти