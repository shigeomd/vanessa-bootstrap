
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	Если НЕ Параметры.Свойство("ИсторияКПП")
		ИЛИ НЕ Параметры.Свойство("ТекущийКПП") Тогда 
		Возврат;
	КонецЕсли;
	
	ИсторияКПП.Загрузить(Параметры.ИсторияКПП.Выгрузить());
	Если ИсторияКПП.Количество() = 0 Тогда
		ИсторияКПП.Добавить().КПП = Параметры.ТекущийКПП;
	КонецЕсли;
	
	// Если объект еще не заблокирован для изменений и есть права на изменение набора
	// попытаемся установить блокировку.
	Если НЕ ПравоДоступа("Изменение", Метаданные.Справочники.Контрагенты) Тогда
		
		ТолькоПросмотр = Истина;
		
	КонецЕсли; 
	
	Если ТолькоПросмотр Тогда
		
		Элементы.ИсторияКПП.ТолькоПросмотр = Истина;
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы, 
			"ФормаКомандаОК",
			"Доступность",
			Ложь);
			
		Элементы.ФормаКомандаОтмена.КнопкаПоУмолчанию = Истина;
		
	КонецЕсли;
	
	ИсторияКПП.Сортировать("Период");
	
	Если ИсторияКПП.Количество() > 0 Тогда
		Элементы.ИсторияКПП.ТекущаяСтрока = ИсторияКПП[ИсторияКПП.Количество()-1].ПолучитьИдентификатор();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТаблицыФормыИсторияКПП

&НаКлиенте
Процедура ИсторияКПППриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока Тогда
		
		Если Элемент.ТекущиеДанные <> Неопределено Тогда
			
			НовыйПериод = НачалоДня(ОбщегоНазначенияКлиент.ДатаСеанса());
			
			// Получим максимальный период в таблице
			ПоследнийПериод = '00010101000000';
			Для Каждого ЗаписьИстории ИЗ ИсторияКПП Цикл
				Если ЗаписьИстории.Период > ПоследнийПериод Тогда
					ПоследнийПериод = ЗаписьИстории.Период;
				КонецЕсли;
			КонецЦикла;
			
			Если НовыйПериод <= ПоследнийПериод Тогда
				НовыйПериод = НачалоДня(ПоследнийПериод + 60*60*24);
			КонецЕсли;
			
			Элемент.ТекущиеДанные.Период = НовыйПериод;
			
			ЭтотОбъект.ТекущийЭлемент = Элементы.ИсторияКППКПП;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИсторияКПППередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	
	Если Элемент.ТекущиеДанные <> Неопределено Тогда
		
		Если НЕ ОтменаРедактирования Тогда
			
			Если НЕ ЗначениеЗаполнено(Элемент.ТекущиеДанные.Период)
				И НЕ (ИсторияКПП.Индекс(Элемент.ТекущиеДанные) = 0) Тогда
				
				СообщениеОбОшибке = НСтр("ru = 'Нужно указать дату начала действия'");
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СообщениеОбОшибке, , "ИсторияКПППериод", , Отказ);
				Возврат;
				
			КонецЕсли;
			
			НайденныеСтроки = ИсторияКПП.НайтиСтроки(Новый Структура("Период", Элемент.ТекущиеДанные.Период));
			Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
				
				Если НайденнаяСтрока <> Элемент.ТекущиеДанные Тогда
					СообщениеОбОшибке = НСтр("ru = 'Уже есть запись с указанной датой сведений'");
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СообщениеОбОшибке, , "ИсторияКПППериод", , Отказ);
					Прервать;
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИсторияКПППриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если ОтменаРедактирования Тогда
		Возврат;
	КонецЕсли;
	
	ИсторияКПП.Сортировать("Период");
	
КонецПроцедуры

&НаКлиенте
Процедура ИсторияКПППередУдалением(Элемент, Отказ)
	
	Если ИсторияКПП.Индекс(Элемент.ТекущиеДанные) = 0 Тогда
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОК(Команда)
	
	Отказ = Ложь;
	Для Каждого Запись Из ИсторияКПП Цикл
		Если НЕ ЗначениеЗаполнено(Запись.КПП) Тогда
			СообщениеОбОшибке = НСтр("ru = 'Поле ""КПП"" не заполнено'");
			НомерСтроки = Формат(ИсторияКПП.Индекс(Запись), "ЧЦ=; ЧДЦ=; ЧГ=");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СообщениеОбОшибке, , "ИсторияКПП["+НомерСтроки+"].КПП",  , Отказ);
		КонецЕсли;
	КонецЦикла;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	РезультатВыбора = Новый Структура;
	РезультатВыбора.Вставить("ИсторияКПП", ИсторияКПП);
	
	ОповеститьОВыборе(РезультатВыбора);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОтмена(Команда)
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти