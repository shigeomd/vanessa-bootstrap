#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Наименование = Параметры.Наименование;
	Если НЕ ПустаяСтрока(Параметры.Заголовок) Тогда
		Заголовок = Параметры.Заголовок;
	КонецЕсли;
	
	ТаблицаРегионов = РегистрыСведений.АдресныеОбъекты.КлассификаторСубъектовРФ();
	Элементы.Регион.СписокВыбора.Очистить();
	Для каждого СтрокаТаблицы Из ТаблицаРегионов Цикл
		Элементы.Регион.СписокВыбора.Добавить(
			Формат(СтрокаТаблицы.КодСубъектаРФ, "ЧЦ=2; ЧДЦ=; ЧВН="), СтрокаТаблицы.Наименование + " " + СтрокаТаблицы.Сокращение);
	КонецЦикла;
	Элементы.Регион.СписокВыбора.СортироватьПоПредставлению();
	Элементы.Регион.СписокВыбора.Вставить(0, "", НСтр("ru='Все регионы'"));
	Регион = "";
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ПодключитьОбработчикОжидания("ВыполнитьПоискКонтрагентов", 0.1, Истина);
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	Регион = Настройки["Регион"];
	Если Элементы.Регион.СписокВыбора.НайтиПоЗначению(Регион) = Неопределено Тогда
		Регион = "";
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура РегионПриИзменении(Элемент)
	
	Если Элементы.Регион.СписокВыбора.НайтиПоЗначению(Регион) = Неопределено Тогда
		Регион = "";
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыКонтрагенты

&НаКлиенте
Процедура КонтрагентыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ВыполнитьВыборКонтрагента();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура НайтиКонтрагентов(Команда)
	
	ВыполнитьПоискКонтрагентов();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьКонтрагента(Команда)
	
	ВыполнитьВыборКонтрагента();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВыполнитьПоискКонтрагентов() Экспорт

	ОписаниеОшибки = "";
	НайтиРеквизитыНаСервере(ОписаниеОшибки);
	
	// Обработка ошибок
	Если ЗначениеЗаполнено(ОписаниеОшибки) Тогда
		Если ОписаниеОшибки = "НеУказаныПараметрыАутентификации" Тогда
			ТекстВопроса = НСтр("ru='Для автоматического заполнения реквизитов контрагентов
				|необходимо подключиться к интернет-поддержке пользователей.
				|Подключиться сейчас?'");
			ОписаниеОповещения = Новый ОписаниеОповещения("ПодключитьИнтернетПоддержку", ЭтотОбъект);
			ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		Иначе
			ПоказатьПредупреждение(, ОписаниеОшибки);
		КонецЕсли;
	ИначеЕсли КоличествоНайденных > 20 Тогда
		ТекстПредупреждения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Найдено слишком много похожих контрагентов (%1). Уточните реквизиты для поиска.'"),
			КоличествоНайденных);
		ПоказатьПредупреждение(, ТекстПредупреждения);
	ИначеЕсли НЕ ПустаяСтрока(Наименование) И КоличествоНайденных = 0 Тогда
		ПоказатьПредупреждение(, НСтр("ru='Ничего не найдено. Уточните реквизиты для поиска.'"));
	КонецЕсли;

КонецПроцедуры 

&НаКлиенте
Процедура ВыполнитьВыборКонтрагента()

	ТекДанные = Элементы.Контрагенты.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда
		Если Контрагенты.Количество() = 0 Тогда
			ТекстПредупреждения = НСтр("ru='Ничего не найдено. Уточните реквизиты и нажмите ""Найти"".'");
			ПоказатьПредупреждение(, ТекстПредупреждения);
			КонецЕсли;
		Возврат;
	КонецЕсли;
	
	Если Параметры.РеквизитыЗаполнены Тогда
		ТекстВопроса = НСтр("ru='Перезаполнить текущие реквизиты?'");
		ДопПараметры = Новый Структура("ИНН", ТекДанные.ИНН);
		ОписаниеОповещения = Новый ОписаниеОповещения("ВыполнитьВыборКонтрагентаЗавершение", ЭтотОбъект, ДопПараметры);
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	Иначе
		ОповеститьОВыборе(ТекДанные.ИНН);
	КонецЕсли;
	
КонецПроцедуры 

&НаКлиенте
Процедура ВыполнитьВыборКонтрагентаЗавершение(Ответ, ДопПараметры) Экспорт

	Если Ответ = КодВозвратаДиалога.Да Тогда
		ОповеститьОВыборе(ДопПараметры.ИНН);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура НайтиРеквизитыНаСервере(ОписаниеОшибки)
	
	Если НЕ ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	Информация = "";
	Контрагенты.Очистить();
	КоличествоНайденных = 0;
	
	РеквизитыКонтрагентов = СервисДанныхЕдиныхГосРеестров.РеквизитыЮрЛицПоНаименованию(Наименование, Регион, Адрес);
	Если ЗначениеЗаполнено(РеквизитыКонтрагентов.ОписаниеОшибки) Тогда
		ОписаниеОшибки = РеквизитыКонтрагентов.ОписаниеОшибки;
		Возврат;
	КонецЕсли;
	
	Для каждого РеквизитыЮрЛица Из РеквизитыКонтрагентов.РеквизитыЮрЛиц Цикл
	
		НоваяСтрока = Контрагенты.Добавить();
		НоваяСтрока.ИНН = РеквизитыЮрЛица.ИНН;
		НоваяСтрока.Наименование = РеквизитыЮрЛица.НаименованиеПолное;
		Если РеквизитыЮрЛица.ЮридическийАдрес <> Неопределено Тогда
			НоваяСтрока.ЮридическийАдрес = РеквизитыЮрЛица.ЮридическийАдрес.Представление;
		КонецЕсли;
		Если РеквизитыЮрЛица.Руководитель <> Неопределено Тогда
			НоваяСтрока.Руководитель = РеквизитыЮрЛица.Руководитель.Фамилия
				+ " " + РеквизитыЮрЛица.Руководитель.Имя
				+ " " + РеквизитыЮрЛица.Руководитель.Отчество;
		КонецЕсли;
	
	КонецЦикла;
	
	КоличествоНайденных = РеквизитыКонтрагентов.КоличествоНайденных;
	
	Если КоличествоНайденных > 20 Тогда
		ТекущийЭлемент = Элементы.Наименование;
		Информация = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Показаны первые 20 контрагентов из %1 найденных. Уточните реквизиты для поиска.'"), 
			КоличествоНайденных);
	ИначеЕсли КоличествоНайденных > 0 Тогда
		ТекущийЭлемент = Элементы.Контрагенты;
		Элементы.Контрагенты.ТекущаяСтрока = Контрагенты[0].ПолучитьИдентификатор();
	Иначе
		ТекущийЭлемент = Элементы.Наименование;
	КонецЕсли;
	
КонецПроцедуры 

&НаКлиенте
Процедура ПодключитьИнтернетПоддержку(Ответ, ДопПараметры) Экспорт

	Если Ответ = КодВозвратаДиалога.Да Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("ПодключитьИнтернетПоддержкуЗавершение", ЭтотОбъект, ДопПараметры);
		ИнтернетПоддержкаПользователейКлиент.ПодключитьИнтернетПоддержкуПользователей(ОписаниеОповещения);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПодключитьИнтернетПоддержкуЗавершение(Результат, ДопПараметры) Экспорт

	Если Результат <> Неопределено Тогда
		ВыполнитьПоискКонтрагентов();
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

