#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОсновнаяСистемаНалогообложения = Константы.ОсновнаяСистемаНалогообложения.Получить();
	ОсновнойВидОрганизации         = Константы.ОсновнойВидОрганизации.Получить();
	
	ВидОрганизации = ?(ОсновнойВидОрганизации = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо,
		"ИндивидуальныйПредприниматель", "ЮридическоеЛицо");
	
	ЗаполнитьСписокВыбораВидаОрганизации();
	
	Если ОсновнаяСистемаНалогообложения = Перечисления.СистемыНалогообложения.Упрощенная Тогда
		СистемаНалогообложения = "УпрощеннаяДоходы";
	ИначеЕсли ОсновнаяСистемаНалогообложения = Перечисления.СистемыНалогообложения.ОсобыйПорядок
		И ОсновнойВидОрганизации = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо Тогда
		СистемаНалогообложения = "Патентная";
	Иначе
		СистемаНалогообложения = "Общая";
	КонецЕсли;
	
	Элементы.СтраницыФормы.ТекущаяСтраница = Элементы.СтраницаВидОрганизации;
	Элементы.СтраницыКоманднойПанели.ТекущаяСтраница = Элементы.СтраницаКоманднойПанелиВидОрганизации;
	Элементы.Далее.КнопкаПоУмолчанию = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	Если ВидОрганизации <> "ОбособленноеПодразделение" Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ГоловнаяОрганизация");
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Заголовок = ПолучитьЗаголовокФормы();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВидОрганизацииПриИзменении(Элемент)
	
	Если ВидОрганизации <> "ИндивидуальныйПредприниматель" 
		И СистемаНалогообложения = "Патентная" ИЛИ СистемаНалогообложения = "ЕНВД" Тогда
		СистемаНалогообложения = "Общая";
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Далее(Команда)
	
	Если ВидОрганизации = "ЮридическоеЛицо" Тогда
		Элементы.СтраницыФормы.ТекущаяСтраница = Элементы.СтраницаСистемаНалогообложенияЮрЛица;
	ИначеЕсли ВидОрганизации = "ОбособленноеПодразделение" Тогда
		Элементы.СтраницыФормы.ТекущаяСтраница = Элементы.СтраницаСистемаНалогообложенияОП;
	ИначеЕсли ВидОрганизации = "ИндивидуальныйПредприниматель" Тогда
		Элементы.СтраницыФормы.ТекущаяСтраница = Элементы.СтраницаСистемаНалогообложенияИП;
	КонецЕсли;
	Элементы.СтраницыКоманднойПанели.ТекущаяСтраница = Элементы.СтраницаКоманднойПанелиСистемаНалогообложения;
	Элементы.СоздатьОрганизацию.КнопкаПоУмолчанию = Истина;
	
	Заголовок = ПолучитьЗаголовокФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура Назад(Команда)
	
	Элементы.СтраницыФормы.ТекущаяСтраница = Элементы.СтраницаВидОрганизации;
	Элементы.СтраницыКоманднойПанели.ТекущаяСтраница = Элементы.СтраницаКоманднойПанелиВидОрганизации;
	Элементы.Далее.КнопкаПоУмолчанию = Истина;
	
	Заголовок = ПолучитьЗаголовокФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьОрганизацию(Команда)
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураУчетнойПолитики = Новый Структура;
	СтруктураУчетнойПолитики.Вставить("ПлательщикНалогаНаПрибыль",        Ложь);
	СтруктураУчетнойПолитики.Вставить("ПлательщикНДС",                    Ложь);
	СтруктураУчетнойПолитики.Вставить("ПлательщикНДФЛ",                   Ложь);
	СтруктураУчетнойПолитики.Вставить("ПрименяетсяУСН",                   Ложь);
	СтруктураУчетнойПолитики.Вставить("ПрименяетсяУСНДоходы",             Ложь);
	СтруктураУчетнойПолитики.Вставить("ПрименяетсяУСНДоходыМинусРасходы", Ложь);
	СтруктураУчетнойПолитики.Вставить("ПрименяетсяУСНПатент",             Ложь);
	СтруктураУчетнойПолитики.Вставить("ПлательщикЕНВД",                   Ложь);
	
	Если СистемаНалогообложения = "УпрощеннаяДоходы" Тогда
		
		СтруктураУчетнойПолитики.Вставить("СистемаНалогообложения",
			ПредопределенноеЗначение("Перечисление.СистемыНалогообложения.Упрощенная"));
		СтруктураУчетнойПолитики.Вставить("ОбъектНалогообложенияУСН",
			ПредопределенноеЗначение("Перечисление.ОбъектыНалогообложенияПоУСН.Доходы"));
		
		СтруктураУчетнойПолитики.Вставить("ПрименяетсяУСН",       Истина);
		СтруктураУчетнойПолитики.Вставить("ПрименяетсяУСНДоходы", Истина);
		
	ИначеЕсли СистемаНалогообложения = "УпрощеннаяДоходыМинусРасходы" Тогда
		
		СтруктураУчетнойПолитики.Вставить("СистемаНалогообложения",
			ПредопределенноеЗначение("Перечисление.СистемыНалогообложения.Упрощенная"));
		СтруктураУчетнойПолитики.Вставить("ОбъектНалогообложенияУСН",
			ПредопределенноеЗначение("Перечисление.ОбъектыНалогообложенияПоУСН.ДоходыМинусРасходы"));
		
		СтруктураУчетнойПолитики.Вставить("ПрименяетсяУСН",                   Истина);
		СтруктураУчетнойПолитики.Вставить("ПрименяетсяУСНДоходыМинусРасходы", Истина);
		СтруктураУчетнойПолитики.Вставить("СпособОценкиМПЗ", ПредопределенноеЗначение("Перечисление.СпособыОценки.ФИФО"));
		
	ИначеЕсли СистемаНалогообложения = "Патентная" Тогда
		
		СтруктураУчетнойПолитики.Вставить("СистемаНалогообложения",
			ПредопределенноеЗначение("Перечисление.СистемыНалогообложения.ОсобыйПорядок"));
		
		СтруктураУчетнойПолитики.Вставить("ПрименяетсяУСНПатент", Истина);
		
	ИначеЕсли СистемаНалогообложения = "ЕНВД" Тогда
		
		СтруктураУчетнойПолитики.Вставить("СистемаНалогообложения",
			ПредопределенноеЗначение("Перечисление.СистемыНалогообложения.ОсобыйПорядок"));
		
		СтруктураУчетнойПолитики.Вставить("ПлательщикЕНВД", Истина);
		
	Иначе
		
		СтруктураУчетнойПолитики.Вставить("СистемаНалогообложения",
			ПредопределенноеЗначение("Перечисление.СистемыНалогообложения.Общая"));
		
		СтруктураУчетнойПолитики.Вставить("ПлательщикНДС", Истина);
		Если ВидОрганизации = "ИндивидуальныйПредприниматель" Тогда
			СтруктураУчетнойПолитики.Вставить("СпособОценкиМПЗ", ПредопределенноеЗначение("Перечисление.СпособыОценки.ФИФО"));
		Иначе
			СтруктураУчетнойПолитики.Вставить("ПлательщикНалогаНаПрибыль", Истина);
		КонецЕсли;
		
	КонецЕсли;
	
	ЗначенияЗаполнения = Новый Структура;
	Если ВидОрганизации = "ИндивидуальныйПредприниматель" Тогда
		ЗначенияЗаполнения.Вставить("ЮридическоеФизическоеЛицо",
			ПредопределенноеЗначение("Перечисление.ЮридическоеФизическоеЛицо.ФизическоеЛицо"));
	Иначе
		ЗначенияЗаполнения.Вставить("ЮридическоеФизическоеЛицо",
			ПредопределенноеЗначение("Перечисление.ЮридическоеФизическоеЛицо.ЮридическоеЛицо"));
	КонецЕсли;
	
	ЗначенияЗаполнения.Вставить("ОбособленноеПодразделение", (ВидОрганизации = "ОбособленноеПодразделение"));
	Если ВидОрганизации = "ОбособленноеПодразделение" Тогда
		ЗначенияЗаполнения.Вставить("ГоловнаяОрганизация", ГоловнаяОрганизация);
		ЗначенияЗаполнения.Вставить("ИНН", ПолучитьИННГоловнойОрганизации(ГоловнаяОрганизация));
	КонецЕсли;
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("ЗначенияЗаполнения",       ЗначенияЗаполнения);
	СтруктураПараметров.Вставить("СтруктураУчетнойПолитики", СтруктураУчетнойПолитики);
	
	Модифицированность = Ложь;
	
	Закрыть();
	
	ОткрытьФорму("Справочник.Организации.Форма.ФормаОрганизации", СтруктураПараметров, ВладелецФормы);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьСписокВыбораВидаОрганизации()
	
	СписокВыбора = Элементы.ВидОрганизации.СписокВыбора;
	
	СписокВыбора.Очистить();
	СписокВыбора.Добавить("ЮридическоеЛицо", НСтр("ru = 'Юридическое лицо'"));
	СписокВыбора.Добавить("ИндивидуальныйПредприниматель", НСтр("ru = 'Индивидуальный предприниматель'"));
	Если ПолучитьФункциональнуюОпцию("ВестиУчетПоПодразделениям") Тогда
		СписокВыбора.Добавить("ОбособленноеПодразделение", НСтр("ru = 'Обособленное подразделение'"));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ПолучитьЗаголовокФормы()
	
	ЗаголовокФормы = НСтр("ru = 'Создание организации'");
	
	Если Элементы.СтраницыФормы.ТекущаяСтраница <> Элементы.СтраницаВидОрганизации Тогда
		ВыбранноеЗначение = Элементы.ВидОрганизации.СписокВыбора.НайтиПоЗначению(ВидОрганизации);
		Если ВыбранноеЗначение <> Неопределено Тогда
			ЗаголовокФормы = ВыбранноеЗначение.Представление;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ЗаголовокФормы;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьИННГоловнойОрганизации(Организация)
	
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Организация, "ИНН")	
	
КонецФункции

#КонецОбласти
