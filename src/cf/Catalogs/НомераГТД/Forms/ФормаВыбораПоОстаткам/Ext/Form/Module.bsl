////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПоказыватьТолькоОстатки = Истина;
	Период = Неопределено;
	
	Если Параметры.Свойство("Дата", Период) Тогда
		Период = Новый Граница(КонецДня(Период), ВидГраницы.Включая);
	КонецЕсли;
		
	Список.Параметры.УстановитьЗначениеПараметра("ПоказыватьТолькоОстатки", ПоказыватьТолькоОстатки);
	Список.Параметры.УстановитьЗначениеПараметра("Период", Период);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбновитьИнформационнуюНадписьПараметрыВыбора(ЭтаФорма);
	
КонецПроцедуры  


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура СписокВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	Если ВладелецФормы <> Неопределено Тогда
		
		ТекущиеДанные = ВладелецФормы.ТекущийЭлемент.ТекущиеДанные;
		
		Если ТекущиеДанные <> Неопределено Тогда
			ТекущиеДанные.НомерГТД = Значение;
			ТекущиеДанные.СтранаПроисхождения = Элемент.ТекущиеДанные.СтранаПроисхождения;
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПоказыватьТолькоОстаткиПриИзменении(Элемент)
	
	Список.Параметры.УстановитьЗначениеПараметра("ПоказыватьТолькоОстатки", ПоказыватьТолькоОстатки);
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьИнформационнуюНадписьПараметрыВыбора(Форма)

	ЭлементыОтбора = Форма.Список.Отбор.Элементы;

	ТекстНадписи = "";
	Для Каждого Элемент Из ЭлементыОтбора Цикл
		Если ЗначениеЗаполнено(Элемент.ПравоеЗначение) и Элемент.Использование Тогда
			ТекстНадписи = ТекстНадписи + Элемент.ПравоеЗначение + "; ";
		КонецЕсли;
	КонецЦикла;

	ТекстНадписи = Лев(ТекстНадписи, СтрДлина(ТекстНадписи) - 2);

	Форма.ИнформационнаяНадписьПараметровВыбора = ТекстНадписи;

КонецПроцедуры
