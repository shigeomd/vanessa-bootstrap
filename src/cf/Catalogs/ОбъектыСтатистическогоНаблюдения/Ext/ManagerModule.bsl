#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

// Возвращает реквизиты справочника, которые образуют естественный ключ
//  для элементов справочника.
//
// Возвращаемое значение: Массив(Строка) - массив имен реквизитов, образующих
//  естественный ключ.
//
Функция ПоляЕстественногоКлюча() Экспорт
	
	Результат = Новый Массив();
	
	Результат.Добавить("Код");
	
	Возврат Результат;
	
КонецФункции

// Обновление информационной базы

// При заполнении справочника поставляемыми данными в версиях ранее 3.0.35, 
// в поле Настройка записывались ссылки на разделенные элементы.
// Для того, чтобы исправить эту проблему, следует перезаполнить справочник.
// При этом не следует изменять элементы, номер версии которых указывает на то, 
// что пользователь (администратор) информационной базы хотел бы оградить их от изменения.
//
Процедура ПерезаполнитьПоставляемымиДанными() Экспорт
	
	Если ПланыОбмена.ГлавныйУзел() <> Неопределено Тогда
		// Поставляемые элементы не следует создавать в подчиненных узлах РИБ - 
		// они приедут туда из головного узла
 		Возврат;
	КонецЕсли;
	
	// Сначала пометим для обновления все объекты, за исключением тех,
	// номер версии которых указывает на то, что пользователь (администратор) информационной базы 
	// хотел бы оградить их от изменения.
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДата());
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОбъектыСтатистическогоНаблюдения.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ОбъектыСтатистическогоНаблюдения КАК ОбъектыСтатистическогоНаблюдения
	|ГДЕ
	|	ОбъектыСтатистическогоНаблюдения.ДатаИзменения <= &ТекущаяДата
	|
	|УПОРЯДОЧИТЬ ПО
	|	ОбъектыСтатистическогоНаблюдения.Ссылка";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Объект = Выборка.Ссылка.ПолучитьОбъект();
		Объект.ДатаИзменения = '0001-01-01';
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(Объект);
		
	КонецЦикла;
	
	// Затем запустим штатное заполнение, 
	// в ходе которого в справочник будут записаны имена предопределенных элементов, 
	// а не ссылки на них.
	
	Справочники.СтатистическиеПоказатели.ЗаполнитьПоставляемымиПравилами();
	
КонецПроцедуры

#КонецЕсли