////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.Печать
	УправлениеПечатью.ПриСозданииНаСервере(ЭтаФорма, Элементы.ГруппаКоманднаяПанель);
	// Конец СтандартныеПодсистемы.Печать
	
	// ДополнительныеОтчетыИОбработки
	ДополнительныеОтчетыИОбработки.ПриСозданииНаСервере(ЭтаФорма);
	// Конец ДополнительныеОтчетыИОбработки
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	ОсновнаяОрганизация		= БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновнаяОрганизация");
	ОсновноеПодразделение	= БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновноеПодразделениеОрганизации");
	
	Если ОтборОрганизация.Пустая() Тогда
		ОтборОрганизация	= ОсновнаяОрганизация;
	КонецЕсли;
	
	Если Параметры.Отбор.Свойство("Владелец") Тогда
		ЕстьОтборПоВладельцу = Истина;
		ОтборОрганизация     = Параметры.Отбор.Владелец;
		Элементы.СтраницыОтборОрганизация.ТекущаяСтраница = Элементы.ГруппаОтображениеОрганизация;
	Иначе
		ЕстьОтборПоВладельцу = Ложь;
		ОтборОрганизацияИспользование = ЗначениеЗаполнено(ОтборОрганизация);
		Элементы.СтраницыОтборОрганизация.ТекущаяСтраница = Элементы.ГруппаОтборОрганизация;
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
			Список.КомпоновщикНастроек.ФиксированныеНастройки.Отбор,
			"Владелец",
			ОтборОрганизация,
			,
			,
			ОтборОрганизацияИспользование);
	КонецЕсли;
	
	ИспользуетсяНесколькоОрганизаций = Справочники.Организации.ИспользуетсяНесколькоОрганизаций();
	Элементы.Организация.Видимость                   = ИспользуетсяНесколькоОрганизаций;
	Элементы.ОтборОрганизация.Видимость              = ИспользуетсяНесколькоОрганизаций;
	Элементы.ОтборОрганизацияИспользование.Видимость = ИспользуетсяНесколькоОрганизаций;
	
	Список.Параметры.УстановитьЗначениеПараметра("ОсновноеПодразделение", ОсновноеПодразделение);
	
	ОрганизацияЯвляетсяЮрЛицом = НЕ ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОтборОрганизация, "ЮридическоеФизическоеЛицо") 
		= Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо;
	
	НастройкиПредупреждений = ОбщегоНазначенияБП.НастройкиПредупрежденийОбИзменениях("УчетЗатратПоПодразделениям");
	
	МожноРедактировать = ПравоДоступа("Редактирование", Метаданные.Справочники.ПодразделенияОрганизаций);
	Элементы.СписокКонтекстноеМенюИзменитьВыделенные.Видимость = МожноРедактировать;
	
	УстановитьУсловноеОформление();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если Источник = ЭтаФорма Тогда
		Возврат;
	КонецЕсли;
	
	Если ИмяСобытия = "ИзменениеОсновнойОрганизации" И НЕ ЕстьОтборПоВладельцу Тогда
		
		ОтборОрганизация = Параметр;
		ОтборОрганизацияИспользование = ЗначениеЗаполнено(ОтборОрганизация);
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
			Список.КомпоновщикНастроек.ФиксированныеНастройки.Отбор,
			"Владелец",
			ОтборОрганизация,
			,
			,
			ОтборОрганизацияИспользование);
		
		ОсновнаяОрганизация = Параметр;
		
		ОрганизацияЯвляетсяЮрЛицом	= ОрганизацияЯвляетсяЮрЛицом(ОтборОрганизация);
	
		УправлениеФормойКлиент();
		
	ИначеЕсли ИмяСобытия = "ИзменениеОсновногоПодразделенияОрганизации" Тогда
		
		ОсновноеПодразделение = Параметр;
		Список.Параметры.УстановитьЗначениеПараметра("ОсновноеПодразделение", ОсновноеПодразделение);
		
		УправлениеФормойКлиент();
		
	ИначеЕсли ИмяСобытия = "Запись_Подразделения" Тогда
		
		ПодключитьОбработчикОжидания("ПоказатьБыстроеОсвоение", 0.5, Истина);
		
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

&НаКлиенте
Процедура ОтборОрганизацияИспользованиеПриИзменении(Элемент)
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
		Список.КомпоновщикНастроек.ФиксированныеНастройки.Отбор,
		"Владелец",
		ОтборОрганизация,
		,
		,
		ОтборОрганизацияИспользование);

КонецПроцедуры

&НаКлиенте
Процедура ОтборОрганизацияПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(ОтборОрганизация) Тогда
		ОтборОрганизацияИспользование = Истина;
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
		Список.КомпоновщикНастроек.ФиксированныеНастройки.Отбор,
		"Владелец",
		ОтборОрганизация,
		,
		,
		ОтборОрганизацияИспользование);
	
	ОрганизацияЯвляетсяЮрЛицом	= ОрганизацияЯвляетсяЮрЛицом(ОтборОрганизация);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	УправлениеФормойКлиент();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ИспользоватьОсновным(Команда)
	
	Если Элементы.Список.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Элементы.Список.ТекущиеДанные.Ссылка	= ОсновноеПодразделение Тогда
		ОсновноеПодразделение	= Неопределено;
	Иначе
		ОсновноеПодразделение	= Элементы.Список.ТекущиеДанные.Ссылка;
	КонецЕсли;
	
	ВремОсновнаяОрганизация	= ОсновнаяОрганизация;
	
	УстановитьОсновноеПодразделение(ОсновнаяОрганизация, ОсновноеПодразделение);
	
	Если НЕ ВремОсновнаяОрганизация	= ОсновнаяОрганизация Тогда
		ПоказатьОповещениеПользователя(НСтр("ru = 'Установлена основная организация:'"),
			ПолучитьНавигационнуюСсылку(ОсновнаяОрганизация), ОсновнаяОрганизация);
		Оповестить("ИзменениеОсновнойОрганизации", ОсновнаяОрганизация);
	КонецЕсли;
	
	Оповестить("ИзменениеОсновногоПодразделенияОрганизации", ОсновноеПодразделение);
	
	Список.Параметры.УстановитьЗначениеПараметра("ОсновноеПодразделение", ОсновноеПодразделение);
	
	УправлениеФормойКлиент();
	
КонецПроцедуры

&НаКлиенте
Процедура ПереместитьЭлементВверх(Команда)

	НастройкаПорядкаЭлементовКлиент.ПереместитьЭлементВверхВыполнить(Список, Элементы.Список);

КонецПроцедуры

&НаКлиенте
Процедура ПереместитьЭлементВниз(Команда)

	НастройкаПорядкаЭлементовКлиент.ПереместитьЭлементВнизВыполнить(Список, Элементы.Список);

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();


	// КПП

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "КПП");

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ОрганизацияЯвляетсяЮрЛицом", ВидСравненияКомпоновкиДанных.Равно, Ложь);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);

КонецПроцедуры

&НаКлиенте
Процедура УправлениеФормойКлиент()
	
	Если НЕ Элементы.Список.ТекущиеДанные = Неопределено Тогда
		Элементы.ФормаИспользоватьОсновным.Пометка	=
			Элементы.Список.ТекущиеДанные.Ссылка	= ОсновноеПодразделение;
	КонецЕсли;
		
	Элементы.ФормаИспользоватьОсновным.Доступность	= НЕ Элементы.Список.ТекущиеДанные = Неопределено;
		
КонецПроцедуры

&НаСервереБезКонтекста
Процедура УстановитьОсновноеПодразделение(ОсновнаяОрганизация, ОсновноеПодразделение)

	Если ЗначениеЗаполнено(ОсновноеПодразделение) Тогда
		
		ОсновнаяОрганизация			= БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновнаяОрганизация");
		ОрганизацияПодразделения	= ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОсновноеПодразделение, "Владелец");
		
		Если НЕ ОсновнаяОрганизация = ОрганизацияПодразделения Тогда
			ОбщегоНазначенияБПВызовСервера.УстановитьЗначениеПоУмолчанию("ОсновнаяОрганизация",	ОрганизацияПодразделения);
			ОсновнаяОрганизация	= ОрганизацияПодразделения;
		КонецЕсли;
		
	КонецЕсли;
	
	ОбщегоНазначенияБПВызовСервера.УстановитьЗначениеПоУмолчанию("ОсновноеПодразделениеОрганизации",	ОсновноеПодразделение);

КонецПроцедуры

&НаКлиенте
Процедура ИзменитьВыделенные(Команда)
	
	ГрупповоеИзменениеОбъектовКлиент.ИзменитьВыделенные(Элементы.Список);

КонецПроцедуры

&НаКлиенте
Процедура ПоказатьБыстроеОсвоение()
	
	ОбщегоНазначенияБПКлиент.ПоказатьПредупреждениеОбИзменениях("УчетЗатратПоПодразделениям", , НастройкиПредупреждений);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ БСП

&НаСервереБезКонтекста
Функция ОрганизацияЯвляетсяЮрЛицом(Организация)
	
	Возврат НЕ ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Организация, "ЮридическоеФизическоеЛицо") 
		= ПредопределенноеЗначение("Перечисление.ЮридическоеФизическоеЛицо.ФизическоеЛицо");
	
КонецФункции

// СтандартныеПодсистемы.Печать
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуПечати(Команда)
	
	УправлениеПечатьюКлиент.ВыполнитьПодключаемуюКомандуПечати(Команда, ЭтаФорма, Элементы.Список);
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.Печать
