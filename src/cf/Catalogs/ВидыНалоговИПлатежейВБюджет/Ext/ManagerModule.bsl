#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает дату вступления в силу актуального классификатора кодов бюджетной классификации
//
Функция ДатаАктуальностиКлассификатора() Экспорт
	
	Возврат '20150101'
	
КонецФункции

// Проверяет актуальность реквизитов при подготовке платежных документов
//
// Параметры:
//   Налог - СправочникСсылка.ВидыНалоговИПлатежейВБюджет
//   Период - Дата - дата платежного документа
//
// Возвращаемое значение:
//  Булево
//
Функция РеквизитыАктуальны(Налог, Знач Период = Неопределено) Экспорт
	
	Если Не ЗначениеЗаполнено(Налог) Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Период) Тогда
		Период = ТекущаяДатаСеанса();
	КонецЕсли;
	
	Если ТекущаяДатаСеанса() < ДатаАктуальностиКлассификатора() Тогда
		// Новый классификатор еще не вступил в силу
		Возврат Истина;
	ИначеЕсли НачалоДня(Период) < ДатаАктуальностиКлассификатора() Тогда
		// Платеж относится к периоду действия старого классификатора
		Возврат Истина;
	КонецЕсли;
	
	ЗначениеРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Налог, "ВидНалога, ДатаАктуальности");
	
	Если ЗначениеЗаполнено(ЗначениеРеквизитов.ВидНалога) И ЗначениеЗаполнено(ЗначениеРеквизитов.ДатаАктуальности) Тогда
		// Проверка актуальности выполняется только для поставляемых элементов
		РеквизитыАктуальны = (НачалоДня(ЗначениеРеквизитов.ДатаАктуальности) >= ДатаАктуальностиКлассификатора());
	Иначе
		РеквизитыАктуальны = Истина;
	КонецЕсли;
	
	Возврат РеквизитыАктуальны;
	
КонецФункции

// Ищет налог по виду и создает поставляемый элемент при необходимости
//
Функция НалогПоВиду(ВидНалога, СоздаватьНовый = Истина) Экспорт
	
	Если Не ЗначениеЗаполнено(ВидНалога) Тогда
		Возврат Справочники.ВидыНалоговИПлатежейВБюджет.ПустаяСсылка();
	КонецЕсли;
	
	Ссылка = Справочники.ВидыНалоговИПлатежейВБюджет.НайтиПоРеквизиту("ВидНалога", ВидНалога);
	
	Если Ссылка.Пустая() И СоздаватьНовый Тогда
		ТаблицаПоставляемыхДанных = ПрочитатьПоставляемыеДанныеКлассификатора(, Новый Структура("ВидНалога", ВидНалога));
		Если ТаблицаПоставляемыхДанных.Количество() > 0 Тогда
			Ссылка = СоздатьПоставляемыйЭлемент(ТаблицаПоставляемыхДанных[0]);
		КонецЕсли;
	КонецЕсли;
	
	Если Ссылка.Пустая() Тогда
		Ссылка = ПрочиеНалогиИСборы;
	КонецЕсли;
	
	Возврат Ссылка;
	
КонецФункции

// Ищет налог по КБК и создает поставляемый элемент при необходимости
//
Функция НалогПоКБК(Знач КодБК, СоздаватьНовый = Истина) Экспорт
	
	Если ПустаяСтрока(КодБК) Тогда
		Возврат ПрочиеНалогиИСборы;
	КонецЕсли;
	
	Ссылка = Справочники.ВидыНалоговИПлатежейВБюджет.НайтиПоРеквизиту("КодБК", КодБК);
	Если Ссылка.Пустая() Тогда
		КодБК = ПлатежиВБюджетКлиентСервер.КодГлавногоАдминистратора(КодБК) + ПлатежиВБюджетКлиентСервер.ШаблонКБК(КодБК, Истина);
		Ссылка = Справочники.ВидыНалоговИПлатежейВБюджет.НайтиПоРеквизиту("КодБК", КодБК);
	КонецЕсли;
	
	Если Ссылка.Пустая() И СоздаватьНовый Тогда
		ТаблицаПоставляемыхДанных = ПрочитатьПоставляемыеДанныеКлассификатора(, Новый Структура("КодБК", КодБК));
		Если ТаблицаПоставляемыхДанных.Количество() > 0 Тогда
			Ссылка = СоздатьПоставляемыйЭлемент(ТаблицаПоставляемыхДанных[0]);
		КонецЕсли;
	КонецЕсли;
	
	Если Ссылка.Пустая() Тогда
		Ссылка = ПрочиеНалогиИСборы;
	КонецЕсли;
	
	Возврат Ссылка;
	
КонецФункции

// Возвращает КБК налога
//
// Параметры:
//   Налог                      - СправочникСсылка.ВидыНалоговИПлатежейВБюджет
//   ВидНалоговогоОбязательства - ПеречислениеСсылка.ВидыПлатежейВГосБюджет
//   Период                     - Дата - период действия классификатора КБК
//
// Возвращаемое значение:
//   Строка
//
Функция КБК(Налог, Знач ВидНалоговогоОбязательства = Неопределено, Знач Период = Неопределено) Экспорт
	
	Если Не ЗначениеЗаполнено(Налог) Тогда
		Возврат "";
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ВидНалоговогоОбязательства) Тогда
		ВидНалоговогоОбязательства = Перечисления.ВидыПлатежейВГосБюджет.Налог;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Период) Тогда
		Период = ТекущаяДатаСеанса();
	КонецЕсли;
	
	ЗначениеРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Налог, "ВидНалога, КодБК, ДатаАктуальности");
	ВидНалога        = ЗначениеРеквизитов.ВидНалога;
	КодБК            = ЗначениеРеквизитов.КодБК;
	ДатаАктуальности = ЗначениеРеквизитов.ДатаАктуальности;
	
	// Если требуется устаревшее значение или новое значение до вступления классификатора в силу, читаем данные из макета
	Если ЗначениеЗаполнено(ВидНалога) И ЗначениеЗаполнено(ДатаАктуальности) Тогда
		
		Если (НачалоДня(Период) >= ДатаАктуальностиКлассификатора() И ДатаАктуальности < ДатаАктуальностиКлассификатора())
			Или (НачалоДня(Период) < ДатаАктуальностиКлассификатора() И ДатаАктуальности >= ДатаАктуальностиКлассификатора()) Тогда
			
			ТаблицаПоставляемыхДанных = ПрочитатьПоставляемыеДанныеКлассификатора(Период, Новый Структура("ВидНалога", ВидНалога));
			Если ТаблицаПоставляемыхДанных.Количество() > 0 Тогда
				КодБК = ТаблицаПоставляемыхДанных[0].КодБК;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ПлатежиВБюджетКлиентСервер.КодПодвидаДоходов(КодБК) = ПлатежиВБюджетКлиентСервер.ПустойКодПодвидаДоходов() Тогда
		КодБК = КБКПоВидуНалоговогоОбязательства(КодБК, ВидНалога, ВидНалоговогоОбязательства, Период);
	ИначеЕсли ПлатежиВБюджетКлиентСервер.ЭтоКБКНалогиВзносы(КодБК) И ВидНалоговогоОбязательства <> Перечисления.ВидыПлатежейВГосБюджет.Налог Тогда
		ШаблонКБК = ПлатежиВБюджетКлиентСервер.КодГлавногоАдминистратора(КодБК) + ПлатежиВБюджетКлиентСервер.ШаблонКБК(КодБК, Истина);
		КодБК = КБКПоВидуНалоговогоОбязательства(ШаблонКБК, ВидНалога, ВидНалоговогоОбязательства, Период);
	КонецЕсли;
	
	Возврат КодБК;
	
КонецФункции

// Возвращает КБК с учетом вида налогового обязательства
//
// Параметры:
//   КБК                        - Строка
//   ВидНалоговогоОбязательства - ПеречислениеСсылка.ВидыПлатежейВГосБюджет
//
// Возвращаемое значение:
//   Строка
//
Функция КБКПоВидуНалоговогоОбязательства(КБК, ВидНалога, ВидНалоговогоОбязательства, Знач Период = Неопределено) Экспорт
	
	Если Не ЗначениеЗаполнено(ВидНалоговогоОбязательства) Тогда
		Возврат КБК;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Период) Тогда
		Период = ТекущаяДатаСеанса();
	КонецЕсли;
	
	КодГлавногоАдминистратора    = ПлатежиВБюджетКлиентСервер.КодГлавногоАдминистратора(КБК);
	КодВидаДоходов               = ПлатежиВБюджетКлиентСервер.КодВидаДоходов(КБК);
	КодПодвидаДоходов            = ПлатежиВБюджетКлиентСервер.КодПодвидаДоходов(КБК);
	КодОперацииСектораУправления = ПлатежиВБюджетКлиентСервер.КодОперацииСектораУправления(КБК);
	
	ПлатежАдминистрируетсяНалоговымиОрганами = ПлатежиВБюджетКлиентСервер.ПлатежАдминистрируетсяНалоговымиОрганами(КБК);
	
	ЭтоПени     = Перечисления.ВидыПлатежейВГосБюджет.ЭтоПени(ВидНалоговогоОбязательства);
	ЭтоПроценты = Перечисления.ВидыПлатежейВГосБюджет.ЭтоПроценты(ВидНалоговогоОбязательства);
	ЭтоШтраф    = Перечисления.ВидыПлатежейВГосБюджет.ЭтоШтраф(ВидНалоговогоОбязательства);
	
	Если ПлатежАдминистрируетсяНалоговымиОрганами Тогда
		КодПодвидаДоходов = "1";
		Если (ЭтоПени Или ЭтоПроценты) И НачалоДня(Период) < '20150101' Тогда
			КодПодвидаДоходов = "2";
		ИначеЕсли ЭтоПени Тогда
			КодПодвидаДоходов = "21";
		ИначеЕсли ЭтоПроценты Тогда
			КодПодвидаДоходов = "22";
		ИначеЕсли ЭтоШтраф Тогда
			КодПодвидаДоходов = "3";
		КонецЕсли;
	Иначе
		Если ВидНалога = Перечисления.ВидыНалогов.СтраховыеВзносы_ПФР_Добровольные Тогда
			КодПодвидаДоходов = "12";
		ИначеЕсли ВидНалога = Перечисления.ВидыНалогов.ФиксированныеВзносы_ФСС Тогда
			КодПодвидаДоходов = "6";
		ИначеЕсли ВидНалога = Перечисления.ВидыНалогов.СтраховыеВзносы_ФФОМС Тогда
			КодПодвидаДоходов = "1011";
			Если ЭтоПени Или ЭтоПроценты Тогда
				КодПодвидаДоходов = "2011";
			ИначеЕсли ЭтоШтраф Тогда
				КодПодвидаДоходов = "3011";
			КонецЕсли;
		Иначе
			КодПодвидаДоходов = "1";
			Если ЭтоПени Или ЭтоПроценты Тогда
				КодПодвидаДоходов = "2";
			ИначеЕсли ЭтоШтраф Тогда
				КодПодвидаДоходов = "3";
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	РасположениеЭлемента = ПлатежиВБюджетКлиентСервер.РасположениеЭлементаКБК("КодПодвидаДоходов");
	Для Счетчик = СтрДлина(КодПодвидаДоходов) + 1 По РасположениеЭлемента.Длина Цикл
		КодПодвидаДоходов = КодПодвидаДоходов + "0";
	КонецЦикла;
	
	Возврат КодГлавногоАдминистратора + КодВидаДоходов + КодПодвидаДоходов + КодОперацииСектораУправления;
	
КонецФункции

Функция ВидНалоговогоОбязательстваПоКБК(КБК, Знач Период = Неопределено) Экспорт
	
	Если ПустаяСтрока(КБК) Тогда
		Возврат Перечисления.ВидыПлатежейВГосБюджет.Налог;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Период) Тогда
		Период = ТекущаяДатаСеанса();
	КонецЕсли;
	
	КодПодвидаДоходов = ПлатежиВБюджетКлиентСервер.КодПодвидаДоходов(КБК);
	
	Если Лев(КодПодвидаДоходов, 1) = "3" Тогда
		
		ВидНалоговогоОбязательства = Перечисления.ВидыПлатежейВГосБюджет.Штраф;
		
	ИначеЕсли Лев(КодПодвидаДоходов, 2) = "22"
		И НачалоДня(Период) >= '20150101'
		И ПлатежиВБюджетКлиентСервер.ПлатежАдминистрируетсяНалоговымиОрганами(КБК) Тогда
		
		ВидНалоговогоОбязательства = Перечисления.ВидыПлатежейВГосБюджет.Проценты;
		
	ИначеЕсли Лев(КодПодвидаДоходов, 1) = "2" Тогда
		
		ВидНалоговогоОбязательства = Перечисления.ВидыПлатежейВГосБюджет.ПениСам;
		
	Иначе
		
		ВидНалоговогоОбязательства = Перечисления.ВидыПлатежейВГосБюджет.Налог;
		
	КонецЕсли;
	
	Возврат ВидНалоговогоОбязательства;
	
КонецФункции

// Формирует назначение платежа для уплаты налогов
//
// Параметры:
//   Налог                      - СправочникСсылка.ВидыНалоговИПлатежейВБюджет
//   ВидНалоговогоОбязательства - ПеречислениеСсылка.ВидыПлатежейВГосБюджет
//   Организация                - СправочникСсылка.Организации
//
// Возвращаемое значение:
//  Строка - назначение платежа
//
Функция НазначениеПлатежа(Налог, ВидНалоговогоОбязательства = Неопределено, Организация = Неопределено, Знач Период = Неопределено, ПредставлениеНалоговогоПериода = "") Экспорт
	
	Если Не ЗначениеЗаполнено(Период) Тогда
		Период = ТекущаяДатаСеанса();
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Налог) И Налог <> ПрочиеНалогиИСборы Тогда
		ЗначениеРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Налог, "ВидНалога, НазначениеПлатежа, ДатаАктуальности");
		ВидНалога         = ЗначениеРеквизитов.ВидНалога;
		НазначениеПлатежа = ЗначениеРеквизитов.НазначениеПлатежа;
		ДатаАктуальности  = ЗначениеРеквизитов.ДатаАктуальности;
	Иначе
		НазначениеПлатежа = НСтр("ru = 'Уплата налога'");
	КонецЕсли;
	
	// Если требуется устаревшее значение или новое значение до вступления классификатора в силу, читаем данные из макета
	Если ЗначениеЗаполнено(ВидНалога) И ЗначениеЗаполнено(ДатаАктуальности) Тогда
		
		Если (НачалоДня(Период) >= ДатаАктуальностиКлассификатора() И ДатаАктуальности < ДатаАктуальностиКлассификатора())
			Или (НачалоДня(Период) < ДатаАктуальностиКлассификатора() И ДатаАктуальности >= ДатаАктуальностиКлассификатора()) Тогда
			
			ТаблицаПоставляемыхДанных = ПрочитатьПоставляемыеДанныеКлассификатора(, Новый Структура("ВидНалога", ВидНалога));
			Если ТаблицаПоставляемыхДанных.Количество() > 0 Тогда
				НазначениеПлатежа = ТаблицаПоставляемыхДанных[0].НазначениеПлатежа;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	// Заменяем при необходимости в назначении платежа "Налог" на "Штраф по налогу", "Процент по налогу" и т.п
	Если ЗначениеЗаполнено(ВидНалоговогоОбязательства) И ВидНалоговогоОбязательства <> Перечисления.ВидыПлатежейВГосБюджет.Налог Тогда
		
		ПредставлениеНалоговогоОбязательства = Перечисления.ВидыПлатежейВГосБюджет.ПредставлениеНалоговогоОбязательства(Налог, ВидНалоговогоОбязательства);
		ПредставлениеПоУмолчанию = Перечисления.ВидыПлатежейВГосБюджет.ПредставлениеНалоговогоОбязательства(Налог, Перечисления.ВидыПлатежейВГосБюджет.Налог);
		
		Если ПредставлениеНалоговогоОбязательства <> ПредставлениеПоУмолчанию Тогда
			ДлинаПредставленияПоУмолчанию = СтрДлина(ПредставлениеПоУмолчанию);
			Если ВРег(Лев(НазначениеПлатежа, ДлинаПредставленияПоУмолчанию)) = ВРег(ПредставлениеПоУмолчанию) Тогда
				НазначениеПлатежа = ПредставлениеНалоговогоОбязательства + Сред(НазначениеПлатежа, ДлинаПредставленияПоУмолчанию + 1);
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Если Не ПустаяСтрока(ПредставлениеНалоговогоПериода) И ВидНалоговогоОбязательства <> Перечисления.ВидыПлатежейВГосБюджет.Штраф Тогда
		НазначениеПлатежа = СтрЗаменить(НазначениеПлатежа, "<Период>", ПредставлениеНалоговогоПериода);
	Иначе
		НазначениеПлатежа = СтрЗаменить(НазначениеПлатежа, "<Период>", "");
	КонецЕсли;
	
	// При уплате страховых взносов, как правило, требуется указывать регистрационный номер
	ВидГосударственногоОргана = Справочники.ВидыНалоговИПлатежейВБюджет.ВидГосударственногоОргана(Налог);
	Если ВидГосударственногоОргана = Перечисления.ВидыГосударственныхОрганов.ОрганПФР
		Или ВидГосударственногоОргана = Перечисления.ВидыГосударственныхОрганов.ОрганФСС Тогда
		
		КлючиРеквизитов = "РегистрационныйНомерПФР, ИПРегистрационныйНомерПФР, РегистрационныйНомерФСС, ИПРегистрационныйНомерФСС";
		РеквизитыОрганизации = ?(ЗначениеЗаполнено(Организация), ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Организация, КлючиРеквизитов), Новый Структура(КлючиРеквизитов));
		
		НазначениеПлатежа = СтрЗаменить(НазначениеПлатежа, "<ПФР>",    РеквизитыОрганизации.РегистрационныйНомерПФР);
		НазначениеПлатежа = СтрЗаменить(НазначениеПлатежа, "<ПФР_ИП>", РеквизитыОрганизации.ИПРегистрационныйНомерПФР);
		НазначениеПлатежа = СтрЗаменить(НазначениеПлатежа, "<ФСС>",    РеквизитыОрганизации.РегистрационныйНомерФСС);
		НазначениеПлатежа = СтрЗаменить(НазначениеПлатежа, "<ФСС_ИП>", РеквизитыОрганизации.ИПРегистрационныйНомерФСС);
		
	КонецЕсли;
	
	НазначениеПлатежа = СтрЗаменить(НазначениеПлатежа, "  ", " ");
	
	Возврат НазначениеПлатежа;
	
КонецФункции

// Возвращает Счет учета для налога
//
// Параметры:
//   Налог  - СправочникСсылка.ВидыНалоговИПлатежейВБюджет
//   Период - Дата
//
// Возвращаемое значение:
//  ПланСчетовСсылка.Хозрасчетный
//
Функция СчетУчета(Налог, Знач Период = Неопределено) Экспорт
	
	Если Не ЗначениеЗаполнено(Налог) Тогда
		Возврат ПланыСчетов.Хозрасчетный.ПустаяСсылка();
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Период) Тогда
		Период = ТекущаяДатаСеанса();
	КонецЕсли;
	
	ЗначениеРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Налог, "ВидНалога, СчетУчета, ДатаАктуальности");
	ВидНалога        = ЗначениеРеквизитов.ВидНалога;
	СчетУчета        = ЗначениеРеквизитов.СчетУчета;
	ДатаАктуальности = ЗначениеРеквизитов.ДатаАктуальности;
	
	// Если требуется устаревшее значение или новое значение до вступления классификатора в силу, читаем данные из макета
	Если ЗначениеЗаполнено(ВидНалога) И ЗначениеЗаполнено(ДатаАктуальности) Тогда
		
		Если (НачалоДня(Период) >= ДатаАктуальностиКлассификатора() И ДатаАктуальности < ДатаАктуальностиКлассификатора())
			Или (НачалоДня(Период) < ДатаАктуальностиКлассификатора() И ДатаАктуальности >= ДатаАктуальностиКлассификатора()) Тогда
			
			СчетУчета =  РасчетыСБюджетом.СчетУчета(ВидНалога, Период);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат СчетУчета
	
КонецФункции

// Возвращает уровень бюджета по налогу
//
Функция УровеньБюджета(Налог) Экспорт
	
	Если ЗначениеЗаполнено(Налог) Тогда
		ВидНалога = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Налог, "ВидНалога");
	Иначе
		ВидНалога = Перечисления.ВидыНалогов.ПустаяСсылка();
	КонецЕсли;
	
	Возврат РасчетыСБюджетом.УровеньБюджета(ВидНалога);
	
КонецФункции

// Возвращает администратора налога по КБК
//
// Параметры:
//   Налог - СправочникСсылка.ВидыНалоговИПлатежейВБюджет
//
// Возвращаемое значение:
//  ПеречислениеСсылка.ВидыГосударственныхОрганов
//
Функция ВидГосударственногоОргана(Налог) Экспорт
	
	Если ЗначениеЗаполнено(Налог) Тогда
		КБК = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Налог, "КодБК");
	Иначе
		КБК = "";
	КонецЕсли;
	
	Возврат ПлатежиВБюджетПереопределяемый.ВидГосударственногоОргана(КБК);
	
КонецФункции

// Создает поставляемые элементы справочника в зависимости от включеных функциональных опций
//
Функция СоздатьПоставляемыеЭлементы() Экспорт
	
	НеобходимыеВидыНалогов = РасчетыСБюджетом.НеобходимыеВидыНалогов();
	
	СуществующиеНалоги = НайтиНалоги(НеобходимыеВидыНалогов);
	
	Если НеобходимыеВидыНалогов.Количество() > 0 Тогда
		ТаблицаПоставляемыхДанных = ПрочитатьПоставляемыеДанныеКлассификатора(, Новый Структура("ВидНалога", НеобходимыеВидыНалогов));
		ТаблицаПоставляемыхДанных.Индексы.Добавить("ВидНалога");
	КонецЕсли;
	
	Для Каждого ВидНалога Из НеобходимыеВидыНалогов Цикл
		
		Если СуществующиеНалоги[ВидНалога] = Неопределено Тогда
			СоздатьПоставляемыйЭлемент(ТаблицаПоставляемыхДанных.Найти(ВидНалога, "ВидНалога"));
		КонецЕсли;
		
	КонецЦикла;
	
КонецФункции

// Обновляет поставляемые данные справочника
//
// Параметры:
//   Период - Дата - период действия классификатора КБК
//
Процедура ОбновитьПоставляемыеДанныеИзКлассификатора(Период = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	СуществующиеНалоги = НайтиНалоги();
	
	ВидыНалогов = Новый Массив;
	Для Каждого КлючИЗначение Из СуществующиеНалоги Цикл
		ВидыНалогов.Добавить(КлючИЗначение.Ключ);
	КонецЦикла;
	
	ТаблицаПоставляемыхДанных = ПрочитатьПоставляемыеДанныеКлассификатора(Период, Новый Структура("ВидНалога", ВидыНалогов));
	ТаблицаПоставляемыхДанных.Индексы.Добавить("ВидНалога");
	
	НачатьТранзакцию();
	
	Для Каждого КлючИЗначение Из СуществующиеНалоги Цикл
		
		СтрокаДанныхКлассификатора = ТаблицаПоставляемыхДанных.Найти(КлючИЗначение.Ключ, "ВидНалога");
		
		Если СтрокаДанныхКлассификатора <> Неопределено Тогда
			Объект = КлючИЗначение.Значение.ПолучитьОбъект();
			ЗаполнитьЗначенияСвойств(Объект, СтрокаДанныхКлассификатора);
			Объект.СчетУчета        = РасчетыСБюджетом.СчетУчета(Объект.ВидНалога, Период);
			Объект.ДатаАктуальности = ДатаАктуальностиКлассификатора();
			Объект.Записать();
		КонецЕсли;
		
	КонецЦикла;
	
	ЗафиксироватьТранзакцию();
	
КонецПроцедуры

Функция НаименованиеГлавногоАдминистратора(КБК) Экспорт
	
	Если НЕ ПлатежиВБюджетКлиентСервер.КБКЗадан(КБК) Тогда
		Возврат "";
	КонецЕсли;
	
	ИсточникДанных = Справочники.ВидыНалоговИПлатежейВБюджет.ПолучитьМакет("АдминистраторыДоходовБюджетов");
	ОбластьПоиска  = ИсточникДанных.Область(2, 1, ИсточникДанных.ВысотаТаблицы, 1);
	
	ОбластьДанных  = ИсточникДанных.НайтиТекст(
		ПлатежиВБюджетКлиентСервер.КодГлавногоАдминистратора(КБК),
		,
		ОбластьПоиска);
	
	Если ОбластьДанных = Неопределено Тогда
		Возврат "";
	КонецЕсли;
	
	Возврат ЗначениеИзИсточникаДанных(ИсточникДанных, ОбластьДанных.Верх, 2);
	
КонецФункции

Функция ДанныеНалоговогоОрганаПоКБК(КБК) Экспорт
	
	ВидГосоргана = ПлатежиВБюджетПереопределяемый.ВидГосударственногоОргана(КБК);
	
	ДанныеНалоговогоОргана = Новый Структура;
	ДанныеНалоговогоОргана.Вставить("ВидГосоргана",        ВидГосоргана);
	ДанныеНалоговогоОргана.Вставить("ВидНалоговогоОргана", ПлатежиВБюджетКлиентСервер.ВидНалоговогоОргана(ВидГосоргана));
	
	Возврат ДанныеНалоговогоОргана;
	
КонецФункции

Функция ДанныеВыбораПоКБК(КодБК) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ШаблонКБК", Лев(КодБК, 13) + "%");
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВидыНалоговИПлатежейВБюджет.Ссылка,
	|	ВидыНалоговИПлатежейВБюджет.ПометкаУдаления
	|ИЗ
	|	Справочник.ВидыНалоговИПлатежейВБюджет КАК ВидыНалоговИПлатежейВБюджет
	|ГДЕ
	|	ВидыНалоговИПлатежейВБюджет.КодБК ПОДОБНО &ШаблонКБК
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВидыНалоговИПлатежейВБюджет.КодБК";
	
	ДанныеВыбора = Новый СписокЗначений;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ДанныеВыбора.Добавить(
			Новый Структура("Значение, ПометкаУдаления",
			Выборка.Ссылка, Выборка.ПометкаУдаления));
	КонецЦикла;
	
	Возврат ДанныеВыбора;
	
КонецФункции

#Область УстаревшиеПроцедурыИФункции

// Ищет КБК в макете (указанной версии) по переданному Коду
//
// Параметры:
//
//	- Код - Строка
//	- ВерсияКлассификатора - Число - Значение по умолчанию: 0
//	- КБКИсходный - Строка
//
Функция НайтиКодДоходаБюджета(Код, ВерсияКлассификатора = 0, КБКИсходный = "", ОпределитьКОСГУ = Ложь) Экспорт
	
	// Функция оставлена для совместимости.
	// Следует использовать ОписаниеКБК()
	// В качестве Код и КБКИсходный передается шаблон кода (17 знаков)
	
	ВидДохода = Лев(Код, 10);
	КОСГУ    = Сред(Код, 15);
	
	КодДоходаБюджета = Новый Структура();
	КодДоходаБюджета.Вставить("ВидДохода",           ВидДохода);
	КодДоходаБюджета.Вставить("КОСГУ",               КОСГУ);
	КодДоходаБюджета.Вставить("Наименование",        "");
	КодДоходаБюджета.Вставить("КраткоеНаименование", "");
	КодДоходаБюджета.Вставить("ПодвидДохода",        "");
	
	Период = ДатаАктуальностиКлассификатора();
	Если ЗначениеЗаполнено(ВерсияКлассификатора) Тогда
		Период = Дата(ВерсияКлассификатора, 1, 1);
	КонецЕсли;
	
	Если Период < ДатаАктуальностиКлассификатора() Тогда
		Период = НачалоГода(ДатаАктуальностиКлассификатора() - 1);
	КонецЕсли;
	
	ОписаниеКБК = Неопределено;
	Если ЗначениеЗаполнено(КБКИсходный) Тогда
		ОписаниеКБК = ОписаниеКБК("000" + СокрЛП(КБКИсходный), Период, ОпределитьКОСГУ);
	КонецЕсли;
	
	Если ОписаниеКБК = Неопределено Тогда
		ОписаниеКБК = ОписаниеКБК("000" + СокрЛП(Код), Период, ОпределитьКОСГУ);
	КонецЕсли;
	
	Если ОписаниеКБК = Неопределено Тогда
		Возврат КодДоходаБюджета;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(КодДоходаБюджета, ОписаниеКБК);
	
	НайденныйШаблон = ОписаниеКБК.НайденныйШаблон;
	
	КодДоходаБюджета.ВидДохода = Лев(НайденныйШаблон,  10);
	КодДоходаБюджета.КОСГУ     = Сред(НайденныйШаблон, 15);
	
	ВыбраныйПодвидДохода = Сред(НайденныйШаблон, 11, 4);
	Если НЕ ПустаяСтрока(ВыбраныйПодвидДохода) И ВыбраныйПодвидДохода <> "0000"
		И СтрДлина(ВыбраныйПодвидДохода) = 4 И СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(ВыбраныйПодвидДохода) Тогда
		КодДоходаБюджета.ПодвидДохода = ВыбраныйПодвидДохода;
	КонецЕсли;
	
	Возврат КодДоходаБюджета;
	
КонецФункции

Функция ОписаниеКБК(КБК, Период, ОпределитьКОСГУ = Ложь) Экспорт
	
	Если НЕ ПлатежиВБюджетКлиентСервер.КБКЗадан(КБК) Тогда
		ДлинаКБК = СтрДлина(КБК);
		Если НЕ (ОпределитьКОСГУ И (ДлинаКБК = 13 ИЛИ ДлинаКБК = 17)) Тогда
			Возврат Неопределено;
		КонецЕсли;
	КонецЕсли;
	
	ИмяМакета = ИмяМакетаКБК(Период);
	
	ИсточникДанных = Справочники.ВидыНалоговИПлатежейВБюджет.ПолучитьМакет(ИмяМакета);
	ОбластьПоиска  = ИсточникДанных.Область(2, 1, ИсточникДанных.ВысотаТаблицы, 1);
	
	ШаблонКБК      = ПлатежиВБюджетКлиентСервер.ШаблонКБК(КБК, Ложь);
	
	ОбластьДанных = Неопределено;
	Если ШаблонКБК <> "" Тогда
		ОбластьДанных = ИсточникДанных.НайтиТекст(ШаблонКБК,, ОбластьПоиска);
		Если ОбластьДанных = Неопределено Тогда
			ШаблонКБК     = ПлатежиВБюджетКлиентСервер.ШаблонКБК(КБК, Истина);
			ОбластьДанных = ИсточникДанных.НайтиТекст(ШаблонКБК,, ОбластьПоиска);
		КонецЕсли;
	КонецЕсли;
	
	Если ОбластьДанных = Неопределено И ОпределитьКОСГУ Тогда
		ШаблонКБКБезКОСГУ = ПлатежиВБюджетКлиентСервер.ЭлементКБК(КБК, "КодВидаДоходов");
		ОбластьДанных     = ИсточникДанных.НайтиТекст(ШаблонКБКБезКОСГУ,, ОбластьПоиска);
		
		Если ОбластьДанных <> Неопределено Тогда
			ШаблонКБК = ШаблонКБКБезКОСГУ + ПлатежиВБюджетКлиентСервер.ПустойКодПодвидаДоходов();
		КонецЕсли;
	КонецЕсли;
	
	Если ОбластьДанных = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	НомерСтроки                     = ОбластьДанных.Верх;
	НомерКолонкиКод                 = 1;
	НомерКолонкиНаименование        = 2;
	НомерКолонкиКраткоеНаименование = 3;
	
	Описание = Новый Структура();
	
	Если ОпределитьКОСГУ Тогда
		НайденныйКБК = ЗначениеИзИсточникаДанных(ИсточникДанных, НомерСтроки, НомерКолонкиКод);
		Описание.Вставить("КОСГУ",           Прав(НайденныйКБК, 3));
		Описание.Вставить("НайденныйШаблон", НайденныйКБК); // ШаблонКБК + Описание.КОСГУ);
	Иначе
		Описание.Вставить("НайденныйШаблон", ШаблонКБК);
	КонецЕсли;
	
	Описание.Вставить("Наименование",        ЗначениеИзИсточникаДанных(ИсточникДанных, НомерСтроки, НомерКолонкиНаименование));
	Описание.Вставить("КраткоеНаименование", ЗначениеИзИсточникаДанных(ИсточникДанных, НомерСтроки, НомерКолонкиКраткоеНаименование));
	
	Возврат Описание;
	
КонецФункции

Функция СчетУчетаРасчетовПоКБК(КБК) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("КодБК_1_13", Лев(КБК, 13));
	Запрос.УстановитьПараметр("КОСГУ", ПлатежиВБюджетКлиентСервер.ЭлементКБК(КБК, "КодОперацииСектораУправления"));
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 2
	|	ВидыНалогов.СчетУчета КАК Счет
	|ИЗ
	|	Справочник.ВидыНалоговИПлатежейВБюджет КАК ВидыНалогов
	|ГДЕ
	|	ПОДСТРОКА(ВидыНалогов.КодБК, 1, 13) = &КодБК_1_13
	|	И ПОДСТРОКА(ВидыНалогов.КодБК, 18, 3) = &КОСГУ
	|	И ВидыНалогов.СчетУчета <> ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Счет;
	Иначе
		Возврат ПланыСчетов.Хозрасчетный.ПустаяСсылка();
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ИмяМакетаКБК(Период = Неопределено)
	
	Если Период = Неопределено Или НачалоДня(Период) >= ДатаАктуальностиКлассификатора() Тогда
		Возврат "КлассификацияДоходовБюджетов2015";
	Иначе
		Возврат "КлассификацияДоходовБюджетов";
	КонецЕсли;
	
КонецФункции

Функция НайтиПоставляемыеДанные(ВидНалога = Неопределено, КБК = Неопределено, СчетУчета = Неопределено, Период = Неопределено) Экспорт
	
	Если ЗначениеЗаполнено(ВидНалога) Тогда
		
		ТаблицаПоставляемыхДанных = ПрочитатьПоставляемыеДанныеКлассификатора(Период, Новый Структура("ВидНалога", ВидНалога));
		
	ИначеЕсли Не ПустаяСтрока(КБК) Тогда
		
		КодБК = Лев(СокрЛ(КБК), 13) + ПлатежиВБюджетКлиентСервер.ПустойКодПодвидаДоходов() + Сред(СокрЛП(КБК), 18);
		ТаблицаПоставляемыхДанных = ПрочитатьПоставляемыеДанныеКлассификатора(Период, Новый Структура("КодБК", КодБК));
		
	ИначеЕсли ЗначениеЗаполнено(СчетУчета) Тогда
		
		ВидНалога = РасчетыСБюджетом.ВидНалогаПоСчетуУчета(СчетУчета);
		Если Не ЗначениеЗаполнено(ВидНалога) Тогда
			Возврат Неопределено
		КонецЕсли;
		
		ТаблицаПоставляемыхДанных = ПрочитатьПоставляемыеДанныеКлассификатора(Период, Новый Структура("ВидНалога", ВидНалога));
		
	Иначе
		
		Возврат Неопределено;
		
	КонецЕсли;
	
	Если ТаблицаПоставляемыхДанных.Количество() > 0 Тогда
		
		СтруктураДанных = НоваяСтруктураПоставляемыхДанных();
		ЗаполнитьЗначенияСвойств(СтруктураДанных, ТаблицаПоставляемыхДанных[0]);
		
		Если ЗначениеЗаполнено(СтруктураДанных.ВидНалога) Тогда
			СтруктураДанных.СчетУчета = РасчетыСБюджетом.СчетУчета(СтруктураДанных.ВидНалога, Период);
			Если ЗначениеЗаполнено(Период) И Период < ДатаАктуальностиКлассификатора() Тогда
				СтруктураДанных.ДатаАктуальности = Период;
			Иначе
				СтруктураДанных.ДатаАктуальности = ДатаАктуальностиКлассификатора();
			КонецЕсли;
			ПодменитьРеквизитыПриСовпаденииКБК(СтруктураДанных);
		КонецЕсли;
		
	Иначе
		
		СтруктураДанных = Неопределено;
		
	КонецЕсли;
	
	Возврат СтруктураДанных;
	
КонецФункции

Процедура ПодменитьРеквизитыПриСовпаденииКБК(Объект) Экспорт
	
	Если ЗначениеЗаполнено(Объект.ВидНалога) И Объект.ВидНалога = Перечисления.ВидыНалогов.ФиксированныеВзносы_ФФОМС Тогда
		// Фиксированные взносы в ФФОМС уплачиваются по тому же КБК, что и взносы за сотрудников.
		// По умолчанию реквизиты содержат наименование и назначение для взносов с ФОТ.
		Объект.Наименование      = НСтр("ru = 'Фиксированные страховые взносы в ФФОМС'");
		Объект.НазначениеПлатежа = НСтр("ru = 'Страховые взносы на ОМС, зачисляемые в бюджет ФФОМС. Регистрационный номер в ПФР <ПФР_ИП>'");
	КонецЕсли;
	
КонецПроцедуры

Функция СоздатьПоставляемыйЭлемент(СтрокаДанныхКлассификатора)
	
	Если СтрокаДанныхКлассификатора = Неопределено Или Не ЗначениеЗаполнено(СтрокаДанныхКлассификатора.ВидНалога) Тогда
		Возврат Справочники.ВидыНалоговИПлатежейВБюджет.ПустаяСсылка();
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Объект = Справочники.ВидыНалоговИПлатежейВБюджет.СоздатьЭлемент();
	ЗаполнитьЗначенияСвойств(Объект, СтрокаДанныхКлассификатора);
	Объект.СчетУчета        = РасчетыСБюджетом.СчетУчета(Объект.ВидНалога);
	Объект.ДатаАктуальности = ДатаАктуальностиКлассификатора();
	Объект.Записать();
	
	Возврат Объект.Ссылка;
	
КонецФункции

Функция НайтиНалоги(ВидыНалогов = Неопределено)
	
	Налоги = Новый Соответствие();
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ВидыНалоговИПлатежейВБюджет.Ссылка,
		|	ВидыНалоговИПлатежейВБюджет.ВидНалога
		|ИЗ
		|	Справочник.ВидыНалоговИПлатежейВБюджет КАК ВидыНалоговИПлатежейВБюджет
		|ГДЕ
		|	&УсловиеПоВидамНалогов
		|	И ВидыНалоговИПлатежейВБюджет.ВидНалога <> ЗНАЧЕНИЕ(Перечисление.ВидыНалогов.ПрочиеНалогиИСборы)
		|	И ВидыНалоговИПлатежейВБюджет.ВидНалога <> ЗНАЧЕНИЕ(Перечисление.ВидыНалогов.ПустаяСсылка)";
		
	Если ВидыНалогов <> Неопределено Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеПоВидамНалогов", "ВидыНалоговИПлатежейВБюджет.ВидНалога В(&ВидыНалогов)");
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеПоВидамНалогов", "ИСТИНА");
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ВидыНалогов", ВидыНалогов);
	Запрос.Текст = ТекстЗапроса;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Налоги.Вставить(Выборка.ВидНалога, Выборка.Ссылка);
	КонецЦикла;
	
	Возврат Налоги;
	
КонецФункции

Функция НоваяСтруктураПоставляемыхДанных()
	
	ТаблицаПоставляемыхДанных = НоваяТаблицаПоставляемыхДанных();
	
	СтруктураПоставляемыхДанных = ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(ТаблицаПоставляемыхДанных.Добавить());
	СтруктураПоставляемыхДанных.Вставить("СчетУчета",        ПланыСчетов.Хозрасчетный.ПустаяСсылка());
	СтруктураПоставляемыхДанных.Вставить("ДатаАктуальности", Дата(1, 1, 1));
	
	Возврат СтруктураПоставляемыхДанных;
	
КонецФункции

Функция НоваяТаблицаПоставляемыхДанных()
	
	ТаблицаПоставляемыхДанных = Новый ТаблицаЗначений;
	ТаблицаПоставляемыхДанных.Колонки.Добавить("ВидНалога",          Новый ОписаниеТипов("ПеречислениеСсылка.ВидыНалогов"));
	ТаблицаПоставляемыхДанных.Колонки.Добавить("КодБК",              ОбщегоНазначения.ОписаниеТипаСтрока(20));
	ТаблицаПоставляемыхДанных.Колонки.Добавить("Наименование",       ОбщегоНазначения.ОписаниеТипаСтрока(100));
	ТаблицаПоставляемыхДанных.Колонки.Добавить("НаименованиеПолное", ОбщегоНазначения.ОписаниеТипаСтрока(1000));
	ТаблицаПоставляемыхДанных.Колонки.Добавить("НазначениеПлатежа",  ОбщегоНазначения.ОписаниеТипаСтрока(210));
	
	Возврат ТаблицаПоставляемыхДанных;
	
КонецФункции

Функция ПрочитатьПоставляемыеДанныеКлассификатора(Период = Неопределено, Отбор = Неопределено)
	Перем ВидНалога;
	Перем ОтборПоВидуНалога, ОтборПоКБК, ОтборПоШаблонуКБК;
	
	УстановленОтбор = (Отбор <> Неопределено);
	
	Если УстановленОтбор И ТипЗнч(Отбор) = Тип("Структура") Тогда
		Отбор.Свойство("ВидНалога", ОтборПоВидуНалога);
		Отбор.Свойство("КодБК",     ОтборПоКБК);
		Если ОтборПоКБК <> Неопределено Тогда
			ОтборПоШаблонуКБК = ПлатежиВБюджетКлиентСервер.ПустойКодГлавногоАдминистратора() + ПлатежиВБюджетКлиентСервер.ШаблонКБК(ОтборПоКБК, Истина);
		КонецЕсли;
	КонецЕсли;
	
	ТипОтбораПоВидуНалога = ТипЗнч(ОтборПоВидуНалога);
	
	УстановленМножественныйОтбор = (ТипОтбораПоВидуНалога = Тип("Массив"));
	Если УстановленМножественныйОтбор Тогда
		ОтборПоВидуНалога = ОбщегоНазначенияБПВызовСервера.УдалитьПовторяющиесяЭлементыМассива(ОтборПоВидуНалога);
	КонецЕсли;
	
	ТаблицаПоставляемыхДанных = НоваяТаблицаПоставляемыхДанных();
	
	НомерКолонкиКБК                = 1; // Код дохода бюджета
	НомерКолонкиНаименованиеПолное = 2; // Полное наименование
	НомерКолонкиНаименование       = 3; // Краткое наименование
	НомерКолонкиКодАдминистратора  = 4; // Код главного администратора доходов бюджета
	НомерКолонкиНазначениеПлатежа  = 5; // Шаблон назначения платежа
	НомерКолонкиИдентификатор      = 6; // Идентификатор
	
	ИсточникДанных = Справочники.ВидыНалоговИПлатежейВБюджет.ПолучитьМакет(ИмяМакетаКБК(Период));
	
	ПредопределенныеВидовНалогов = РасчетыСБюджетом.ВидыНалогов();
	
	ПустойКодГлавногоАдминистратора = ПлатежиВБюджетКлиентСервер.ПустойКодГлавногоАдминистратора();
	
	Для НомерСтроки = 2 По ИсточникДанных.ВысотаТаблицы Цикл
		
		КБК = ЗначениеИзИсточникаДанных(ИсточникДанных, НомерСтроки, НомерКолонкиКБК);
		Если ПустаяСтрока(КБК) Тогда
			// Конец таблицы
			Прервать;
		КонецЕсли;
		
		КодАдминистратора = ЗначениеИзИсточникаДанных(ИсточникДанных, НомерСтроки, НомерКолонкиКодАдминистратора);
		
		КодБК = ?(Не ПустаяСтрока(КодАдминистратора), КодАдминистратора + КБК, ПустойКодГлавногоАдминистратора + КБК);
		
		Если УстановленОтбор И ОтборПоКБК <> Неопределено Тогда
			Если Не ПустаяСтрока(КодАдминистратора) Тогда
				Если ОтборПоКБК <> КодБК Тогда
					Продолжить;
				КонецЕсли;
			Иначе
				Если ОтборПоШаблонуКБК <> КодБК Тогда
					Продолжить;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		СтрокаИдентификаторов = ЗначениеИзИсточникаДанных(ИсточникДанных, НомерСтроки, НомерКолонкиИдентификатор);
		Если УстановленОтбор И ОтборПоВидуНалога <> Неопределено И ПустаяСтрока(СтрокаИдентификаторов) Тогда
			// Непоставляемые данные
			Продолжить;
		КонецЕсли;
		
		Если Не ПустаяСтрока(СтрокаИдентификаторов) Тогда
			МассивИдентификаторов = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(СтрокаИдентификаторов);
		Иначе
			МассивИдентификаторов = Новый Массив;
			МассивИдентификаторов.Добавить("");
		КонецЕсли;
		
		НаименованиеПолное    = ЗначениеИзИсточникаДанных(ИсточникДанных, НомерСтроки, НомерКолонкиНаименованиеПолное);
		Наименование          = ЗначениеИзИсточникаДанных(ИсточникДанных, НомерСтроки, НомерКолонкиНаименование);
		НазначениеПлатежа     = ЗначениеИзИсточникаДанных(ИсточникДанных, НомерСтроки, НомерКолонкиНазначениеПлатежа);
		
		Для Индекс = 0 По МассивИдентификаторов.ВГраница() Цикл
			
			Идентификатор = СокрЛП(МассивИдентификаторов[Индекс]);
			
			Если ПустаяСтрока(Идентификатор) Или Не ПредопределенныеВидовНалогов.Свойство(Идентификатор, ВидНалога) Тогда
				ВидНалога = Перечисления.ВидыНалогов.ПустаяСсылка();
			КонецЕсли;
			
			Если УстановленОтбор И ОтборПоВидуНалога <> Неопределено Тогда
				Если ТипОтбораПоВидуНалога = Тип("Массив") Тогда
					Если ОтборПоВидуНалога.Найти(ВидНалога) = Неопределено Тогда
						Продолжить;
					КонецЕсли;
				ИначеЕсли ОтборПоВидуНалога <> ВидНалога Тогда
					Продолжить;
				КонецЕсли;
			КонецЕсли;
			
			НоваяСтрока = ТаблицаПоставляемыхДанных.Добавить();
			НоваяСтрока.ВидНалога          = ВидНалога;
			НоваяСтрока.КодБК              = КодБК;
			НоваяСтрока.НаименованиеПолное = НаименованиеПолное;
			НоваяСтрока.Наименование       = Наименование;
			НоваяСтрока.НазначениеПлатежа  = НазначениеПлатежа;
			
			Если УстановленОтбор Тогда
				Если УстановленМножественныйОтбор Тогда
					Если ОтборПоВидуНалога <> Неопределено И ТаблицаПоставляемыхДанных.Количество() = ОтборПоВидуНалога.Количество() Тогда
						// Все значения отбора уже найдены
						Возврат ТаблицаПоставляемыхДанных;
					КонецЕсли;
				Иначе
					// Возвращаем первую найденную запись
					Возврат ТаблицаПоставляемыхДанных;
				КонецЕсли;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат ТаблицаПоставляемыхДанных;
	
КонецФункции

Функция ЗначениеИзИсточникаДанных(ИсточникДанных, НомерСтроки, НомерКолонки)
	
	ОбластьДанных = ИсточникДанных.Область(НомерСтроки, НомерКолонки);
	Если ОбластьДанных = Неопределено Тогда
		Возврат "";
	Иначе
		Возврат ОбластьДанных.Текст;
	КонецЕсли;
	
КонецФункции

#Область ОбработчикиОбновления

// Заполняет реквизиты предопределенного элемента "ПрочиеНалогиИСборы"
// и создает поставляемые элементы справочника в зависимости от включеных функциональных опций
//
Процедура ЗаполнитьПоставляемыеДанные() Экспорт
	
	// Предопределенный элемент заполняем во всех узлах РИБ
	
	Объект = ПрочиеНалогиИСборы.ПолучитьОбъект();
	Объект.ВидНалога    = Перечисления.ВидыНалогов.ПрочиеНалогиИСборы;
	Объект.Наименование = НСтр("ru = 'Прочие налоги и сборы'");
	
	ОбновлениеИнформационнойБазы.ЗаписатьОбъект(Объект, Ложь);
	
	Если ПланыОбмена.ГлавныйУзел() = Неопределено Тогда
		
		// Поставляемые элементы создаем в главном узле и регистрируем изменения
		
		ВидыНалогов = Новый Массив;
		КоллекцияМетаданных = Перечисления.ВидыНалогов.Получить(0).Метаданные().ЗначенияПеречисления;
		Для Индекс = 0 По КоллекцияМетаданных.Количество() - 1 Цикл
			ЗначениеПеречисления = Перечисления.ВидыНалогов.Получить(Индекс);
			Если ЗначениеПеречисления <> Перечисления.ВидыНалогов.ПрочиеНалогиИСборы Тогда
				ВидыНалогов.Добавить(ЗначениеПеречисления);
			КонецЕсли;
		КонецЦикла;
		
		ТаблицаПоставляемыхДанных = ПрочитатьПоставляемыеДанныеКлассификатора(, Новый Структура("ВидНалога", ВидыНалогов));
		ТаблицаПоставляемыхДанных.Индексы.Добавить("ВидНалога");
		ТаблицаПоставляемыхДанных.Индексы.Добавить("КодБК");
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ВидыНалоговИПлатежейВБюджет.Ссылка,
			|	ВидыНалоговИПлатежейВБюджет.ВидНалога,
			|	ВидыНалоговИПлатежейВБюджет.КодБК,
			|	ВидыНалоговИПлатежейВБюджет.СчетУчета,
			|	ВидыНалоговИПлатежейВБюджет.ПометкаУдаления
			|ИЗ
			|	Справочник.ВидыНалоговИПлатежейВБюджет КАК ВидыНалоговИПлатежейВБюджет";
		
		СуществующиеНалоги = Запрос.Выполнить().Выгрузить();
		СуществующиеНалоги.Индексы.Добавить("ВидНалога");
		
		// Обновляем реквизиты существующих налогов, если обновление ИБ выполняется повторно
		
		Для Каждого СтрокаДанных Из СуществующиеНалоги Цикл
			
			Если ЗначениеЗаполнено(СтрокаДанных.ВидНалога) Тогда
				
				СтрокаДанныхКлассификатора = ТаблицаПоставляемыхДанных.Найти(СтрокаДанных.ВидНалога, "ВидНалога");
				
				Если СтрокаДанныхКлассификатора <> Неопределено Тогда
					
					Объект = СтрокаДанных.Ссылка.ПолучитьОбъект();
					ЗаполнитьЗначенияСвойств(Объект, СтрокаДанныхКлассификатора);
					Объект.СчетУчета        = РасчетыСБюджетом.СчетУчета(Объект.ВидНалога);
					Объект.ДатаАктуальности = ДатаАктуальностиКлассификатора();
					
					ОбновлениеИнформационнойБазы.ЗаписатьОбъект(Объект, Истина, Истина);
					
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
		
		// По возможности ставим на поддержку пользовательские данные
		
		ПользовательскиеДанные = СуществующиеНалоги.НайтиСтроки(Новый Структура("ВидНалога, ПометкаУдаления", Перечисления.ВидыНалогов.ПустаяСсылка(), Ложь));
		Для Каждого СтрокаДанных Из ПользовательскиеДанные Цикл
			
			СтрокаДанныхКлассификатора = Неопределено;
			Если Не ПустаяСтрока(СтрокаДанных.КодБК) Тогда
				Если ПлатежиВБюджетКлиентСервер.КодПодвидаДоходов(СтрокаДанных.КодБК) = ПлатежиВБюджетКлиентСервер.ПустойКодПодвидаДоходов() Тогда
					СтрокаДанныхКлассификатора = ТаблицаПоставляемыхДанных.Найти(СтрокаДанных.КодБК, "КодБК");
				ИначеЕсли ПлатежиВБюджетКлиентСервер.ЭтоКБКНалогиВзносы(СтрокаДанных.КодБК) Тогда
					ШаблонКБК = ПлатежиВБюджетКлиентСервер.КодГлавногоАдминистратора(СтрокаДанных.КодБК) + ПлатежиВБюджетКлиентСервер.ШаблонКБК(СтрокаДанных.КодБК, Истина);
					СтрокаДанныхКлассификатора = ТаблицаПоставляемыхДанных.Найти(ШаблонКБК, "КодБК");
				Иначе
					Продолжить;
				КонецЕсли;
			КонецЕсли;
			
			Если СтрокаДанныхКлассификатора <> Неопределено
				И ЗначениеЗаполнено(СтрокаДанныхКлассификатора.ВидНалога)
				И СуществующиеНалоги.Найти(СтрокаДанныхКлассификатора.ВидНалога, "ВидНалога") = Неопределено Тогда
				
				// Ставим на поддержку только один элемент
				
				СчетУчета = РасчетыСБюджетом.СчетУчета(СтрокаДанныхКлассификатора.ВидНалога);
				Если ЗначениеЗаполнено(СтрокаДанных.СчетУчета) И СтрокаДанных.СчетУчета <> СчетУчета Тогда
					// В классификаторе есть соответствующая строка, но пользователь решил учитывать данные на другом счете учета
					Продолжить;
				КонецЕсли;
				
				Объект = СтрокаДанных.Ссылка.ПолучитьОбъект();
				ЗаполнитьЗначенияСвойств(Объект, СтрокаДанныхКлассификатора);
				Объект.СчетУчета        = СчетУчета;
				Объект.ДатаАктуальности = ДатаАктуальностиКлассификатора();
				
				ОбновлениеИнформационнойБазы.ЗаписатьОбъект(Объект, Истина, Истина);
				
				// Обновление данных таблицы СуществующиеНалоги для исключения дублирования элементов
				ЗаполнитьЗначенияСвойств(СтрокаДанных, Объект);
				
			КонецЕсли;
			
		КонецЦикла;
		
		// Создаем необходимые налоги
		
		НеобходимыеВидыНалогов = РасчетыСБюджетом.НеобходимыеВидыНалогов();
		
		Для Каждого ВидНалога Из НеобходимыеВидыНалогов Цикл
			
			Если СуществующиеНалоги.Найти(ВидНалога, "ВидНалога") = Неопределено Тогда
				
				СтрокаДанныхКлассификатора = ТаблицаПоставляемыхДанных.Найти(ВидНалога, "ВидНалога");
				
				Если СтрокаДанныхКлассификатора <> Неопределено Тогда
					
					Объект = Справочники.ВидыНалоговИПлатежейВБюджет.СоздатьЭлемент();
					ЗаполнитьЗначенияСвойств(Объект, СтрокаДанныхКлассификатора);
					Объект.СчетУчета        = РасчетыСБюджетом.СчетУчета(Объект.ВидНалога);
					Объект.ДатаАктуальности = ДатаАктуальностиКлассификатора();
					
					ОбновлениеИнформационнойБазы.ЗаписатьОбъект(Объект, Истина, Истина);
					
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбновитьПоставляемыеДанныеПриАктуализацииКлассификатора() Экспорт
	
	Период = ДатаАктуальностиКлассификатора();
	
	СуществующиеНалоги = НайтиНалоги();
	
	ВидыНалогов = Новый Массив;
	Для Каждого КлючИЗначение Из СуществующиеНалоги Цикл
		ВидыНалогов.Добавить(КлючИЗначение.Ключ);
	КонецЦикла;
	
	ТаблицаПоставляемыхДанных = ПрочитатьПоставляемыеДанныеКлассификатора(Период, Новый Структура("ВидНалога", ВидыНалогов));
	ТаблицаПоставляемыхДанных.Индексы.Добавить("ВидНалога");
	
	Для Каждого КлючИЗначение Из СуществующиеНалоги Цикл
		
		СтрокаДанныхКлассификатора = ТаблицаПоставляемыхДанных.Найти(КлючИЗначение.Ключ, "ВидНалога");
		
		Если СтрокаДанныхКлассификатора <> Неопределено Тогда
			Объект = КлючИЗначение.Значение.ПолучитьОбъект();
			ЗаполнитьЗначенияСвойств(Объект, СтрокаДанныхКлассификатора);
			Объект.СчетУчета        = РасчетыСБюджетом.СчетУчета(Объект.ВидНалога, Период);
			Объект.ДатаАктуальности = ДатаАктуальностиКлассификатора();
			ОбновлениеИнформационнойБазы.ЗаписатьОбъект(Объект, Ложь);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
