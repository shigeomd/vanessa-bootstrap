#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Функция СоздатьПатентПоУмолчанию(Организация, Дата = Неопределено) Экспорт
	Перем ПатентПоУмолчанию;
	
	ПатентыОрганизации	= ПолучитьПатентыОрганизации(Организация, Дата);
	Если ПатентыОрганизации.Количество() > 0 Тогда
		ПатентПоУмолчанию	= ПатентыОрганизации[0];
	Иначе
		
		НовыйЭлемент	= СоздатьЭлемент();
		НовыйЭлемент.Владелец		= Организация;
		НовыйЭлемент.Наименование	= НСтр("ru = 'Патент'");
		НовыйЭлемент.ДатаНачала		= НачалоГода(?(ЗначениеЗаполнено(Дата), Дата, ТекущаяДатаСеанса()));
		НовыйЭлемент.ДатаОкончания	= КонецГода(?(ЗначениеЗаполнено(Дата), Дата, ТекущаяДатаСеанса()));
		
		Попытка
			НовыйЭлемент.Записать();
			ПатентПоУмолчанию	= НовыйЭлемент.Ссылка;
		Исключение
			
			ИнформацияОбОшибке = ИнформацияОбОшибке();
			Если ИнформацияОбОшибке.Причина = Неопределено Тогда
				ОписаниеОшибки = ИнформацияОбОшибке.Описание;
			Иначе
				ОписаниеОшибки = ИнформацияОбОшибке.Причина.Описание;
			КонецЕсли;
			ОписаниеОшибки = НСтр("ru = 'Ошибка при записи патента:'") + Символы.ПС + ОписаниеОшибки;
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОписаниеОшибки);
			
		КонецПопытки;
		
	КонецЕсли;
	
	Возврат ПатентПоУмолчанию;
	
КонецФункции

Функция ПолучитьПатентыОрганизации(Организация, Дата = Неопределено) Экспорт
	
	МассивПатентов = Новый Массив;
	
	Запрос = Новый Запрос;
	
	Если Дата = Неопределено Тогда
		
		ТекстЗапроса = 
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	Патенты.Ссылка КАК Ссылка
			|ИЗ
			|	Справочник.Патенты КАК Патенты
			|ГДЕ
			|	Патенты.Владелец = &Владелец
			|	И НЕ Патенты.ПометкаУдаления";
		
	Иначе
		
		ТекстЗапроса = 
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	Патенты.Ссылка КАК Ссылка
			|ИЗ
			|	Справочник.Патенты КАК Патенты
			|ГДЕ
			|	Патенты.ДатаНачала <= &КонецДняДокумента
			|	И Патенты.ДатаОкончания >= &НачалоДняДокумента
			|	И Патенты.Владелец = &Владелец
			|	И НЕ Патенты.ПометкаУдаления";
		
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Владелец", Организация);
	Если Дата <> Неопределено Тогда
		Запрос.УстановитьПараметр("НачалоДняДокумента", НачалоДня(Дата));
		Запрос.УстановитьПараметр("КонецДняДокумента", КонецДня(Дата));
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	
	Результат = Запрос.Выполнить();
	Если НЕ Результат.Пустой() Тогда
		МассивПатентов = Результат.Выгрузить().ВыгрузитьКолонку("Ссылка");
	КонецЕсли;
	
	Возврат МассивПатентов;
	
КонецФункции

#КонецЕсли
