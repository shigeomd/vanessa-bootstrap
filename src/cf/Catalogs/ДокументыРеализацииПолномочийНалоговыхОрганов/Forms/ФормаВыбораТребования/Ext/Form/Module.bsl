
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Отбор.Свойство("Организация") Тогда
		ОтборОрганизация = Параметры.Отбор.Организация;
	КонецЕсли;
	
	Если Параметры.Отбор.Свойство("НалоговыйОрган") Тогда
		ОтборНалоговыйОрган = Параметры.Отбор.НалоговыйОрган;
	КонецЕсли;
	
	Если ОбщегоНазначенияКлиентСервер.ЭтоПлатформа83БезРежимаСовместимости() Тогда
		
		Если Параметры.Отбор.Свойство("Организация") Тогда
			Организация = Параметры.Отбор.Организация;
			
			// Установка значения отбора в компоновщике настроек.
			ЭлементОтбораДанных = Список.КомпоновщикНастроек.Настройки.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));	
			ЭлементОтбораДанных.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Организация");
			ЭлементОтбораДанных.ПравоеЗначение = Организация;
			ЭлементОтбораДанных.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
			ЭлементОтбораДанных.Использование = Истина;
			
			// Удаление отбора из параметров.
			Параметры.Отбор.Удалить("Организация"); 
		КонецЕсли;
		
		Если Параметры.Отбор.Свойство("НалоговыйОрган") Тогда
			НалоговыйОрган = Параметры.Отбор.НалоговыйОрган;
			
			// Установка значения отбора в компоновщике настроек.
			ЭлементОтбораДанных = Список.КомпоновщикНастроек.Настройки.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));	
			ЭлементОтбораДанных.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("НалоговыйОрган");
			ЭлементОтбораДанных.ПравоеЗначение = НалоговыйОрган;
			ЭлементОтбораДанных.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
			ЭлементОтбораДанных.Использование = Истина;
			
			// Удаление отбора из параметров.
			Параметры.Отбор.Удалить("НалоговыйОрган"); 
		КонецЕсли;
		
		Список.КомпоновщикНастроек.Настройки.Отбор.ИдентификаторПользовательскойНастройки = Строка(Новый УникальныйИдентификатор);
		
	КонецЕсли;

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОтборСписка(ОтборСписка, СтрокаЛевоеЗначение, ПравоеЗначение, ВидСравнения = Неопределено)
	
	ЛевоеЗначение = Новый ПолеКомпоновкиДанных(СтрокаЛевоеЗначение);
	
    Если ВидСравнения = Неопределено Тогда
        ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
    КонецЕсли;
    
    ОтборУстановлен = Ложь;
    Для Каждого ЭлементОтбора Из ОтборСписка.Элементы Цикл
        Если ЭлементОтбора.ЛевоеЗначение = ЛевоеЗначение Тогда
            ЭлементОтбора.Использование = Истина;
            ЭлементОтбора.ВидСравнения = ВидСравнения;
            ЭлементОтбора.ПравоеЗначение = ПравоеЗначение;
            ОтборУстановлен = Истина;
        КонецЕсли;
    КонецЦикла;    
    
    Если Не ОтборУстановлен Тогда
        ЭлементОтбора = ОтборСписка.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
        ЭлементОтбора.ЛевоеЗначение = ЛевоеЗначение;
        ЭлементОтбора.Использование = Истина;
        ЭлементОтбора.ВидСравнения = ВидСравнения;
        ЭлементОтбора.ПравоеЗначение = ПравоеЗначение;
    КонецЕсли;
        
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОтключитьОтборСписка(ОтборСписка, СтрокаЛевоеЗначение)
	
	ЛевоеЗначение = Новый ПолеКомпоновкиДанных(СтрокаЛевоеЗначение);
	
    Для Каждого ЭлементОтбора Из ОтборСписка.Элементы Цикл
        Если ЭлементОтбора.ЛевоеЗначение = ЛевоеЗначение Тогда
            ЭлементОтбора.Использование = Ложь;
            Возврат;
		КонецЕсли;
    КонецЦикла;    
        
КонецПроцедуры

&НаКлиенте
Процедура ОтборОрганизацияПриИзменении(Элемент)
	
	Если ОбщегоНазначенияКлиентСервер.ЭтоПлатформа83БезРежимаСовместимости() Тогда
		СписокОтбор = Список.КомпоновщикНастроек.Настройки.Отбор;
	Иначе
		СписокОтбор = Список.Отбор;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ОтборОрганизация) Тогда
		УстановитьОтборСписка(СписокОтбор, "Организация", ОтборОрганизация); 
	Иначе
		ОтключитьОтборСписка(СписокОтбор, "Организация"); 
	КонецЕсли;
	
	Если ОбщегоНазначенияКлиентСервер.ЭтоПлатформа83БезРежимаСовместимости() Тогда
		СписокОтбор.ИдентификаторПользовательскойНастройки = Строка(Новый УникальныйИдентификатор);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОтборНалоговыйОрганПриИзменении(Элемент)
	
	Если ОбщегоНазначенияКлиентСервер.ЭтоПлатформа83БезРежимаСовместимости() Тогда
		СписокОтбор = Список.КомпоновщикНастроек.Настройки.Отбор;
	Иначе
		СписокОтбор = Список.Отбор;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ОтборНалоговыйОрган) Тогда
		УстановитьОтборСписка(СписокОтбор, "НалоговыйОрган", ОтборНалоговыйОрган);
	Иначе
		ОтключитьОтборСписка(СписокОтбор, "НалоговыйОрган"); 
	КонецЕсли;
	
	Если ОбщегоНазначенияКлиентСервер.ЭтоПлатформа83БезРежимаСовместимости() Тогда
		СписокОтбор.ИдентификаторПользовательскойНастройки = Строка(Новый УникальныйИдентификатор);
	КонецЕсли;

КонецПроцедуры
