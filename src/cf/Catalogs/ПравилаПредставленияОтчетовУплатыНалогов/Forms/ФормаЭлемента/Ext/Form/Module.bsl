////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПриЧтенииСозданииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ПриЧтенииСозданииНаСервере();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура ДействиеПриИзменении(Элемент)
	НастроитьДоступностьПериодаДекларации(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ПовторятьНесколькоРазПриИзменении(Элемент)
	
	НастроитьДоступностьПериодичности(ЭтаФорма);
	Если Не ПовторятьНесколькоРаз Тогда
		Объект.Периодичность = Объект.ФинансовыйПериод;
		Объект.ОграничениеПериода = "";
		ЗаданоОграничениеПериода = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ФинансовыйПериодПриИзменении(Элемент)
	
	Если Не ЗначениеЗаполнено(Объект.Периодичность) Тогда
		Объект.Периодичность = Объект.ФинансовыйПериод;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаданоОграничениеПериодаПриИзменении(Элемент)
	
	НастроитьДоступностьОграничениеПериода(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОграничениеПериодаПриИзменении(Элемент)
	
	Если ПустаяСтрока(Объект.ОграничениеПериода) Тогда
		ЗаданоОграничениеПериода = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СтандартныйБазовыйПериодПриИзменении(Элемент)
	
	НастроитьДоступностьВариантовБазовогоПериода(ЭтаФорма);
	Если СтандартныйБазовыйПериод = 1 Тогда
		Объект.ОтставаниеБазовогоПериода = 0;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НестандартныйБазовыйПериодРанееПриИзменении(Элемент)
	
	НастроитьДоступностьВариантовБазовогоПериода(ЭтаФорма);
	Если СтандартныйБазовыйПериод = 0 Тогда
		Объект.ОтставаниеБазовогоПериода = 1;
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

////////////////////////////////////////////////////////////////////////////////
// Настройка формы

&НаКлиентеНаСервереБезКонтекста
Процедура НастроитьДоступностьПериодаДекларации(Форма)
	
	УплатаНалога = ПредопределенноеЗначение("Перечисление.ВидыДействийКалендаряБухгалтера.УплатаНалога");
	Форма.Элементы.ПериодДекларации.Доступность = (Форма.Объект.Действие = УплатаНалога);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура НастроитьДоступностьПериодичности(Форма)
	
	Форма.Элементы.ПериодичностьГруппа.Доступность = Форма.ПовторятьНесколькоРаз;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура НастроитьДоступностьОграничениеПериода(Форма)
	
	Форма.Элементы.ОграничениеПериода.Доступность = Форма.ЗаданоОграничениеПериода;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура НастроитьДоступностьВариантовБазовогоПериода(Форма)
	
	СтандартныйБазовыйПериод = (Форма.СтандартныйБазовыйПериод = 1);
	Форма.Элементы.БазовыйПериод.Доступность             = СтандартныйБазовыйПериод;
	Форма.Элементы.БазовыйПериодРанее.Доступность        = Не СтандартныйБазовыйПериод;
	Форма.Элементы.ОтставаниеБазовогоПериода.Доступность = Не СтандартныйБазовыйПериод;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Прочее

&НаСервере
Процедура ПриЧтенииСозданииНаСервере()
	
	// Заполним параметры формы
	ПовторятьНесколькоРаз    = (Объект.Периодичность <> Объект.ФинансовыйПериод);
	ЗаданоОграничениеПериода = Не ПустаяСтрока(Объект.ОграничениеПериода);
	СтандартныйБазовыйПериод = (Объект.ОтставаниеБазовогоПериода = 0);
	
	// Настроим форму
	НастроитьДоступностьПериодаДекларации(ЭтаФорма);
	НастроитьДоступностьПериодичности(ЭтаФорма);
	НастроитьДоступностьОграничениеПериода(ЭтаФорма);
	НастроитьДоступностьВариантовБазовогоПериода(ЭтаФорма);

КонецПроцедуры
