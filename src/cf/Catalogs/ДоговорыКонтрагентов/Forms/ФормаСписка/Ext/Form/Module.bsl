&НаКлиенте
Перем УстановкаОсновногоДоговораКонтрагентаВыполнена;

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.Печать
	УправлениеПечатью.ПриСозданииНаСервере(ЭтаФорма);
	// Конец СтандартныеПодсистемы.Печать
	
	// ДополнительныеОтчетыИОбработки
	ДополнительныеОтчетыИОбработки.ПриСозданииНаСервере(ЭтаФорма);
	// Конец ДополнительныеОтчетыИОбработки
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	ОбщегоНазначенияБПВызовСервера.УстановитьОтборПоОсновнойОрганизации(ЭтаФорма);
	ОткрытИзПлатежки = Параметры.Свойство("ОткрытИзПлатежки");
	ДоступноИспользоватьОсновным = РольДоступна("ДобавлениеИзменениеДанныхБухгалтерии") ИЛИ РольДоступна("ПолныеПрава");

	Если Параметры.Отбор.Свойство("Владелец") И ЗначениеЗаполнено(Параметры.Отбор.Владелец) Тогда
		ОсновнойДоговор = Параметры.Отбор.Владелец.ОсновнойДоговорКонтрагента;
		Список.Параметры.УстановитьЗначениеПараметра("ОсновнойДоговор", ОсновнойДоговор);
	ИначеЕсли ДоступноИспользоватьОсновным Тогда
		Элементы.ФормаИспользоватьОсновным.Видимость = Ложь;
		Список.Параметры.УстановитьЗначениеПараметра("ОсновнойДоговор", Неопределено);
	Иначе
		Список.Параметры.УстановитьЗначениеПараметра("ОсновнойДоговор", Неопределено);
	КонецЕсли;
	
	МожноРедактировать = ПравоДоступа("Редактирование", Метаданные.Справочники.ДоговорыКонтрагентов);
	Элементы.СписокКонтекстноеМенюИзменитьВыделенные.Видимость = МожноРедактировать;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ИзменениеОсновнойОрганизации" Тогда
		ОбщегоНазначенияБПКлиент.ИзменитьОтборПоОсновнойОрганизации(Список,, Параметр);
	ИначеЕсли ИмяСобытия = "УстановкаОсновногоДоговораКонтрагентаВыполнена" Тогда
		УстановкаОсновногоДоговораКонтрагентаВыполнена = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТаблицыФормыСписок

&НаКлиенте
Процедура СписокПриИзменении(Элемент)
	
	ДанныеСтроки = Элемент.ТекущиеДанные;
	
	Если ДанныеСтроки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Оповестить("Запись_ДоговорыКонтрагентов", , ДанныеСтроки.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Если ОткрытИзПлатежки И НЕ Группа И НЕ Копирование Тогда
		Отказ = Истина;
		
		ПараметрыФормы = Новый Структура;

		ЗначенияЗаполнения = Новый Структура;
		ЗначенияЗаполнения.Вставить("Родитель", Родитель);
		ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
		
		Для каждого ЭлементОтбора Из Список.Отбор.Элементы Цикл
			
			Если НЕ ЭлементОтбора.Использование Тогда
				Продолжить;
			КонецЕсли;
			
			Если ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно Тогда
				ЗначенияЗаполнения.Вставить(Строка(ЭлементОтбора.ЛевоеЗначение), ЭлементОтбора.ПравоеЗначение);
			КонецЕсли;
			
			Если ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке Тогда
				ЗначенияЗаполнения.Вставить(Строка(ЭлементОтбора.ЛевоеЗначение), ЭлементОтбора.ПравоеЗначение[0].Значение);
			КонецЕсли;
			
		КонецЦикла;
		
		ПараметрыФормы.Вставить("ОткрытИзПлатежки");
		ОткрытьФорму("Справочник.ДоговорыКонтрагентов.ФормаОбъекта", ПараметрыФормы, ЭтаФорма);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СписокПередЗагрузкойПользовательскихНастроекНаСервере(Элемент, Настройки)
	
	ОбщегоНазначенияБП.ВосстановитьОтборСписка(Список, Настройки, "Организация");
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	УправлениеФормойКлиент();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ИспользоватьОсновным(Команда)
	
	Если Элементы.Список.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Элементы.Список.ТекущиеДанные.Ссылка = ОсновнойДоговор Тогда
		ОсновнойДоговор = ПредопределенноеЗначение("Справочник.ДоговорыКонтрагентов.ПустаяСсылка");
	Иначе
		ОсновнойДоговор = Элементы.Список.ТекущиеДанные.Ссылка;
	КонецЕсли;
	
	Список.Параметры.УстановитьЗначениеПараметра("ОсновнойДоговор", ОсновнойДоговор);
	
	УстановкаОсновногоДоговораКонтрагентаВыполнена = Ложь;
	
	СтруктураПараметров = Новый Структура();
	СтруктураПараметров.Вставить("Контрагент", Элементы.Список.ТекущиеДанные.Владелец);
	СтруктураПараметров.Вставить("ОсновнойДоговор", ОсновнойДоговор);
	
	Оповестить("УстановкаОсновногоДоговораКонтрагента", СтруктураПараметров);
	
	// Если форма владельца закрыта, то запишем основной договор контрагента самостоятельно.
	Если НЕ УстановкаОсновногоДоговораКонтрагентаВыполнена Тогда
		ЗаписатьОсновнойДоговорКонтрагента(СтруктураПараметров);
	КонецЕсли;
	
	УправлениеФормойКлиент();
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьВыделенные(Команда)
	
	ГрупповоеИзменениеОбъектовКлиент.ИзменитьВыделенные(Элементы.Список);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Процедура ЗаписатьОсновнойДоговорКонтрагента(СтруктураПараметров)
	
	Контрагент = СтруктураПараметров.Контрагент;
	КонтрагентОбъект = Контрагент.ПолучитьОбъект();
	
	УстановитьОсновнойДоговор = Истина;
	
	Попытка
		КонтрагентОбъект.Заблокировать();
	Исключение
		// в случае блокировки - не выполнять изменение объекта
		УстановитьОсновнойДоговор = Ложь;
		// записать предупреждение в журнал регистрации
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Не удалось заблокировать объект Контрагент.'", Метаданные.ОсновнойЯзык.КодЯзыка),
			УровеньЖурналаРегистрации.Предупреждение,, КонтрагентОбъект, ОписаниеОшибки());
	КонецПопытки;
	
	Если УстановитьОсновнойДоговор Тогда
		КонтрагентОбъект.ОсновнойДоговорКонтрагента = СтруктураПараметров.ОсновнойДоговор;
		КонтрагентОбъект.Записать();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УправлениеФормойКлиент()
	
	Если ОсновнойДоговор = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ДоступноИспользоватьОсновным И НЕ Элементы.Список.ТекущиеДанные = Неопределено Тогда
		Элементы.ФормаИспользоватьОсновным.Пометка	=
			Элементы.Список.ТекущиеДанные.Ссылка	= ОсновнойДоговор;
	КонецЕсли;
	
	Если ДоступноИспользоватьОсновным Тогда
		Элементы.ФормаИспользоватьОсновным.Доступность	= НЕ Элементы.Список.ТекущиеДанные = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункцииБСП

// СтандартныеПодсистемы.Печать
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуПечати(Команда)
	
	УправлениеПечатьюКлиент.ВыполнитьПодключаемуюКомандуПечати(Команда, ЭтаФорма, Элементы.Список);
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.Печать

#КонецОбласти


