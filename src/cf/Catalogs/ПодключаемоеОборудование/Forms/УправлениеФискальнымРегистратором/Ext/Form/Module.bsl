
#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПечатьОтчетаБезГашенияВыполнить()
	
	Контекст = Новый Структура("Действие", "PrintXReport");
	ОписаниеОповещения = Новый ОписаниеОповещения("ПечатьОтчетаЗавершение", ЭтотОбъект, Контекст);
	МенеджерОборудованияКлиент.ПредложитьВыбратьУстройство(ОписаниеОповещения, "ФискальныйРегистратор",
		НСтр("ru='Выберите фискальный регистратор'"), НСтр("ru='Фискальный регистратор не подключен.'"));
	
КонецПроцедуры

&НаКлиенте
Процедура ПечатьОтчетаСГашениемВыполнить()
	
	Контекст = Новый Структура("Действие", "PrintZReport");
	ОписаниеОповещения = Новый ОписаниеОповещения("ПечатьОтчетаЗавершение", ЭтотОбъект, Контекст);
	МенеджерОборудованияКлиент.ПредложитьВыбратьУстройство(ОписаниеОповещения, "ФискальныйРегистратор",
		НСтр("ru='Выберите фискальный регистратор'"), НСтр("ru='Фискальный регистратор не подключен.'"));
	
КонецПроцедуры

&НаКлиенте
Процедура ПечатьОтчетаЗавершение(ИдентификаторУстройства, Параметры) Экспорт
	
	Если НЕ Параметры.Свойство("Действие") Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеОшибки = "";
	
	Результат = МенеджерОборудованияКлиент.ПодключитьОборудованиеПоИдентификатору(УникальныйИдентификатор, ИдентификаторУстройства, ОписаниеОшибки);
	Если Результат Тогда
		ВходныеПараметры  = Неопределено;
		ВыходныеПараметры = Неопределено;
		Результат = МенеджерОборудованияКлиент.ВыполнитьКоманду(ИдентификаторУстройства, Параметры.Действие, ВходныеПараметры, ВыходныеПараметры);
		
		Если Не Результат Тогда
			ТекстСообщения = НСтр("ru = 'При снятии отчета на фискальном регистраторе произошла ошибка.
				|%ОписаниеОшибки%
				|Отчет на фискальном регистраторе не сформирован.'");
				ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ОписаниеОшибки%", ВыходныеПараметры[1]);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		КонецЕсли;
		
		МенеджерОборудованияКлиент.ОтключитьОборудованиеПоИдентификатору(УникальныйИдентификатор, ИдентификаторУстройства);
		
	Иначе
		ТекстСообщения = НСтр("ru = 'При подключении устройства произошла ошибка.'") + Символы.ПС + ОписаниеОшибки;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти