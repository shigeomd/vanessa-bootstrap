#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
// СЛУЖЕБНЫЙ ПРОГРАММНЫЙ ИНТЕРФЕЙС

// Возвращает реквизиты справочника, которые образуют естественный ключ
//  для элементов справочника.
Функция ПоляЕстественногоКлюча() Экспорт
	
	Результат = Новый Массив();
	Результат.Добавить("Владелец");
	Результат.Добавить("Код");
	
	Возврат Результат;
	
КонецФункции

Процедура ИсправитьНарушенияЕстественногоКлюча() Экспорт
	
	// Исправляет нарушения в данных справочника: наличие одинаковых элементов с одинаковым ествественным ключем.
	// Такие нарушения могли получиться в результате того, что значения полей естественного ключа разных поколений данных 
	// отличались только регистром букв.
	
	Запрос = Новый Запрос();
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПоляФормСтатистики.Владелец КАК Владелец,
	|	ПоляФормСтатистики.Код КАК Код
	|ПОМЕСТИТЬ Ошибки
	|ИЗ
	|	Справочник.ПоляФормСтатистики КАК ПоляФормСтатистики
	|
	|СГРУППИРОВАТЬ ПО
	|	ПоляФормСтатистики.Владелец,
	|	ПоляФормСтатистики.Код
	|
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ПоляФормСтатистики.Ссылка) > 1
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Владелец,
	|	Код
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПоляФормСтатистики.Владелец КАК Владелец,
	|	ПоляФормСтатистики.Код КАК Код,
	|	ПоляФормСтатистики.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ПоляФормСтатистики КАК ПоляФормСтатистики
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Ошибки КАК Ошибки
	|		ПО ПоляФормСтатистики.Владелец = Ошибки.Владелец
	|			И ПоляФормСтатистики.Код = Ошибки.Код
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПоляФормСтатистики.ПометкаУдаления,
	|	ПоляФормСтатистики.ЭтоГруппа
	|ИТОГИ ПО
	|	Владелец,
	|	Код";
	
	Формы = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока Формы.Следующий() Цикл
		
		Поля = Формы.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока Поля.Следующий() Цикл
			
			Дубли = Поля.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			ЭтоПервый = Истина;
			
			Пока Дубли.Следующий() Цикл
				
				Если ЭтоПервый Тогда
					ЭтоПервый = Ложь;
					Продолжить;
				КонецЕсли;
				
				Объект = Дубли.Ссылка.ПолучитьОбъект();
				Объект.Код             = Строка(Новый УникальныйИдентификатор) + Объект.Код;
				Объект.ПометкаУдаления = Истина;
				Справочники.СтатистическиеПоказатели.ЗаписатьПоставляемыеДанные(Объект);
				
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецЕсли
